
Процедура РассчитатьДанныеИнформационныхБаз() Экспорт
	Граница = НачалоДня(Константы.ГраницаАктуальностиРасчетаДанныхИнформационныхБаз.Получить());
	Если Не ЗначениеЗаполнено(Граница) Тогда
		Граница = НачалоДня(ТекущаяДатаСеанса());
	КонецЕсли;
	Глубина = Константы.ГлубинаРасчетаДанныхИнформационныхБаз.Получить();
	Задержка = Константы.ЗадержкаПолученияДанных.Получить();
	НачалоПериода = Граница - Глубина * 86400;
	ДатаОкончанияПересчета = НачалоДня(ТекущаяДата()) - Задержка * 86400;
	Пока НачалоПериода < ДатаОкончанияПересчета Цикл
		КонецПериода = НачалоДня(НачалоПериода+86400);
		РассчитатьДанныеИнформационныхБазЗаПериод(НачалоПериода, КонецПериода);
		Константы.ГраницаАктуальностиРасчетаДанныхИнформационныхБаз.Установить(КонецПериода);
		НачалоПериода = КонецПериода;
	КонецЦикла;
КонецПроцедуры

Процедура РассчитатьДанныеИнформационныхБазЗаПериод(НачалоПериода, КонецПериода)
	
	ДокументРегистраторСсылка = Документы.РегистраторДанныхИнформационнойБазы.ПолучитьДокументРегистратор(КонецПериода);
	
	Запрос = Новый Запрос(ТекстЗапроса());
	Запрос.УстановитьПараметр("НачалоПериода", НачалоПериода);
	Запрос.УстановитьПараметр("КонецПериода", КонецПериода);
	ТаблицаДвижений = Запрос.Выполнить().Выгрузить();
	Если ТаблицаДвижений.Количество() Тогда
		Если ДокументРегистраторСсылка.Пустая() Тогда
			ДокументРегистратор = Документы.РегистраторДанныхИнформационнойБазы.СоздатьДокумент();
			ДокументРегистратор.Дата = КонецПериода;
		Иначе
			ДокументРегистратор = ДокументРегистраторСсылка.ПолучитьОбъект();
		КонецЕсли;
		ДокументРегистратор.Движения.ДанныеИнформационныхБаз.Загрузить(ТаблицаДвижений);
		ДокументРегистратор.Движения.ДанныеИнформационныхБаз.Записывать = Истина;
		Попытка 
			ДокументРегистратор.Записать(РежимЗаписиДокумента.Проведение);
		Исключение
			ОписаниеОшибки = ОписаниеОшибки();
		КонецПопытки;
	КонецЕсли;
	
КонецПроцедуры

Функция ТекстЗапроса()
	Возврат "ВЫБРАТЬ РАЗЛИЧНЫЕ
	        |	ИнформацияИсторияПодробно.ИнформационнаяБаза КАК ИнформационнаяБаза,
	        |	ИнформацияИсторияПодробно.Конфигурация КАК Конфигурация,
	        |	ИнформацияИсторияПодробно.ВерсияПлатформы КАК ВерсияПлатформы,
	        |	ИнформацияИсторияПодробно.ВерсияКонфигурации.Релизная КАК РелизнаяВерсияКонфигурации,
	        |	ИнформацияИсторияПодробно.РежимРаботыКонфигурации КАК РежимРаботыКонфигурации,
	        |	ЕСТЬNULL(РезультатАнализаИнформационныхБаз.Статус, ЗНАЧЕНИЕ(перечисление.СтатусыИнформационнойБазы.ПустаяСсылка)) КАК Статус
	        |ПОМЕСТИТЬ ИБНаНачало
	        |ИЗ
	        |	РегистрСведений.ИнформацияИсторияПодробно КАК ИнформацияИсторияПодробно
	        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РезультатАнализаИнформационныхБаз КАК РезультатАнализаИнформационныхБаз
	        |		ПО ИнформацияИсторияПодробно.ИнформационнаяБаза = РезультатАнализаИнформационныхБаз.ИнформационнаяБаза
	        |ГДЕ
	        |	ИнформацияИсторияПодробно.ПериодЗаписи = &НачалоПериода
	        |
	        |ИНДЕКСИРОВАТЬ ПО
	        |	ИнформационнаяБаза,
	        |	Конфигурация,
	        |	ВерсияПлатформы,
	        |	РелизнаяВерсияКонфигурации,
	        |	РежимРаботыКонфигурации,
	        |	Статус
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ РАЗЛИЧНЫЕ
	        |	ИнформацияИсторияПодробно.ИнформационнаяБаза КАК ИнформационнаяБаза,
	        |	ИнформацияИсторияПодробно.Конфигурация КАК Конфигурация,
	        |	ИнформацияИсторияПодробно.ВерсияПлатформы КАК ВерсияПлатформы,
	        |	ИнформацияИсторияПодробно.ВерсияКонфигурации.Релизная КАК РелизнаяВерсияКонфигурации,
	        |	ИнформацияИсторияПодробно.РежимРаботыКонфигурации КАК РежимРаботыКонфигурации,
	        |	ЕСТЬNULL(РезультатАнализаИнформационныхБаз.Статус, ЗНАЧЕНИЕ(перечисление.СтатусыИнформационнойБазы.ПустаяСсылка)) КАК Статус
	        |ПОМЕСТИТЬ ИБНаКонец
	        |ИЗ
	        |	РегистрСведений.ИнформацияИсторияПодробно КАК ИнформацияИсторияПодробно
	        |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РезультатАнализаИнформационныхБаз КАК РезультатАнализаИнформационныхБаз
	        |		ПО ИнформацияИсторияПодробно.ИнформационнаяБаза = РезультатАнализаИнформационныхБаз.ИнформационнаяБаза
	        |ГДЕ
	        |	ИнформацияИсторияПодробно.ПериодЗаписи = &КонецПериода
	        |
	        |ИНДЕКСИРОВАТЬ ПО
	        |	ИнформационнаяБаза,
	        |	Конфигурация,
	        |	ВерсияПлатформы,
	        |	РелизнаяВерсияКонфигурации,
	        |	РежимРаботыКонфигурации,
	        |	Статус
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ
	        |	&КонецПериода КАК Период,
	        |	ВЫБОР
	        |		КОГДА НЕ ИБНаКонец.ИнформационнаяБаза ЕСТЬ NULL
	        |				И ИБНаНачало.ИнформационнаяБаза ЕСТЬ NULL
	        |			ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	        |		КОГДА ИБНаКонец.ИнформационнаяБаза ЕСТЬ NULL
	        |				И НЕ ИБНаНачало.ИнформационнаяБаза ЕСТЬ NULL
	        |			ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	        |	КОНЕЦ КАК ВидДвижения,
	        |	ЕСТЬNULL(ИБНаКонец.Конфигурация, ИБНаНачало.Конфигурация) КАК Конфигурация,
	        |	ЕСТЬNULL(ИБНаКонец.ВерсияПлатформы, ИБНаНачало.ВерсияПлатформы) КАК ВерсияПлатформы,
			|	ВЫРАЗИТЬ(ЕСТЬNULL(ИБНаКонец.ВерсияПлатформы.ВерсияЧисло, ИБНаНачало.ВерсияПлатформы.ВерсияЧисло) / 10000 - 0.5 КАК ЧИСЛО(12, 0)) КАК Сборка,			
	        |	ЕСТЬNULL(ИБНаКонец.РелизнаяВерсияКонфигурации, ИБНаНачало.РелизнаяВерсияКонфигурации) КАК РелизнаяВерсияКонфигурации,
	        |	ЕСТЬNULL(ИБНаКонец.РежимРаботыКонфигурации, ИБНаНачало.РежимРаботыКонфигурации) КАК РежимРаботыКонфигурации,
	        |	ЕСТЬNULL(ИБНаКонец.Статус, ИБНаНачало.Статус) КАК Статус,
	        |	ЕСТЬNULL(СУММА(ВЫБОР
	        |				КОГДА НЕ ИБНаКонец.ИнформационнаяБаза ЕСТЬ NULL
	        |						И ИБНаНачало.ИнформационнаяБаза ЕСТЬ NULL
	        |					ТОГДА 1
	        |				КОГДА ИБНаКонец.ИнформационнаяБаза ЕСТЬ NULL
	        |						И НЕ ИБНаНачало.ИнформационнаяБаза ЕСТЬ NULL
	        |					ТОГДА 1
	        |				ИНАЧЕ 0
	        |			КОНЕЦ), 0) КАК Количество,
	        |	ЕСТЬNULL(СУММА(ВЫБОР
	        |				КОГДА НЕ ИБНаКонец.ИнформационнаяБаза ЕСТЬ NULL
	        |						И ИБНаНачало.ИнформационнаяБаза ЕСТЬ NULL
	        |					ТОГДА 1
	        |				КОГДА ИБНаКонец.ИнформационнаяБаза ЕСТЬ NULL
	        |						И НЕ ИБНаНачало.ИнформационнаяБаза ЕСТЬ NULL
	        |					ТОГДА -1
	        |				ИНАЧЕ 0
	        |			КОНЕЦ), 0) КАК КоличествоСоЗнаком
	        |ИЗ
	        |	ИБНаКонец КАК ИБНаКонец
	        |		ПОЛНОЕ СОЕДИНЕНИЕ ИБНаНачало КАК ИБНаНачало
	        |		ПО ИБНаКонец.ИнформационнаяБаза = ИБНаНачало.ИнформационнаяБаза
	        |			И ИБНаКонец.Конфигурация = ИБНаНачало.Конфигурация
	        |			И ИБНаКонец.ВерсияПлатформы = ИБНаНачало.ВерсияПлатформы
	        |			И ИБНаКонец.РелизнаяВерсияКонфигурации = ИБНаНачало.РелизнаяВерсияКонфигурации
	        |			И ИБНаКонец.РежимРаботыКонфигурации = ИБНаНачало.РежимРаботыКонфигурации
	        |			И ИБНаКонец.Статус = ИБНаНачало.Статус
	        |
	        |СГРУППИРОВАТЬ ПО
	        |	ЕСТЬNULL(ИБНаКонец.Конфигурация, ИБНаНачало.Конфигурация),
	        |	ЕСТЬNULL(ИБНаКонец.ВерсияПлатформы, ИБНаНачало.ВерсияПлатформы),
	        |	ЕСТЬNULL(ИБНаКонец.РелизнаяВерсияКонфигурации, ИБНаНачало.РелизнаяВерсияКонфигурации),
	        |	ЕСТЬNULL(ИБНаКонец.РежимРаботыКонфигурации, ИБНаНачало.РежимРаботыКонфигурации),
	        |	ЕСТЬNULL(ИБНаКонец.Статус, ИБНаНачало.Статус),
			|	ВЫРАЗИТЬ(ЕСТЬNULL(ИБНаКонец.ВерсияПлатформы.ВерсияЧисло, ИБНаНачало.ВерсияПлатформы.ВерсияЧисло) / 10000 - 0.5 КАК ЧИСЛО(12, 0)),
	        |	ВЫБОР
	        |		КОГДА НЕ ИБНаКонец.ИнформационнаяБаза ЕСТЬ NULL
	        |				И ИБНаНачало.ИнформационнаяБаза ЕСТЬ NULL
	        |			ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	        |		КОГДА ИБНаКонец.ИнформационнаяБаза ЕСТЬ NULL
	        |				И НЕ ИБНаНачало.ИнформационнаяБаза ЕСТЬ NULL
	        |			ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	        |	КОНЕЦ
	        |
	        |ИМЕЮЩИЕ
	        |	ЕСТЬNULL(СУММА(ВЫБОР
	        |				КОГДА НЕ ИБНаКонец.ИнформационнаяБаза ЕСТЬ NULL
	        |						И ИБНаНачало.ИнформационнаяБаза ЕСТЬ NULL
	        |					ТОГДА 1
	        |				КОГДА ИБНаКонец.ИнформационнаяБаза ЕСТЬ NULL
	        |						И НЕ ИБНаНачало.ИнформационнаяБаза ЕСТЬ NULL
	        |					ТОГДА -1
	        |				ИНАЧЕ 0
	        |			КОНЕЦ), 0) <> 0"
КонецФункции