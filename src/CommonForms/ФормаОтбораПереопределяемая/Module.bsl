
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
    
    Если Параметры.Свойство("Компоновщик") Тогда
        
        Компоновщик.Инициализировать(Параметры.Компоновщик.ПолучитьИсточникДоступныхНастроек());
        Компоновщик.ЗагрузитьНастройки(Параметры.Компоновщик.ПолучитьНастройки());
            
    КонецЕсли;
    
    Если Параметры.Свойство("Расписание") Тогда
        Расписание = Параметры.Расписание;
    КонецЕсли;
        
    ЭлементЗначение = ЭтотОбъект.Элементы.Найти("КомпоновщикНастройкиОтборПравоеЗначение");
	ЭлементЗначение.УстановитьДействие("НачалоВыбора", "ЗначениеНачалоВыбора");
    ЭлементЗначение.УстановитьДействие("ОбработкаВыбора", "ЗначениеОбработкаВыбора");
    
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
    
    ДоступныеВидыСравнения = Компоновщик.Настройки.Отбор.ДоступныеПоляОтбора.Элементы.Найти("РасписаниеПредставление").ДоступныеВидыСравнения;
    ДоступныеВидыСравнения.Очистить();
    ДоступныеВидыСравнения.Добавить(ВидСравненияКомпоновкиДанных.Равно);
    ДоступныеВидыСравнения.Добавить(ВидСравненияКомпоновкиДанных.НеРавно);
               
КонецПроцедуры

&НаКлиенте
Процедура КомпоновщикНастройкиОтборПриАктивизацииСтроки(Элемент)
    
    ЭлементПравоеЗначение = Элементы.КомпоновщикНастройкиОтборГруппаКолонокПравоеЗначение.ПодчиненныеЭлементы.КомпоновщикНастройкиОтборПравоеЗначение;
    
    Если Строка(Элемент.ТекущиеДанные.ЛевоеЗначение) = "РасписаниеПредставление" Тогда
        
        ЭлементПравоеЗначение.КнопкаВыбора = Истина;
        ЭлементПравоеЗначение.РедактированиеТекста = Ложь;
        
    Иначе
        
        ЭлементПравоеЗначение.КнопкаВыбора = Ложь;
        ЭлементПравоеЗначение.РедактированиеТекста = Истина;
    КонецЕсли;
    
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
    Закрыть(Компоновщик);
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
    Закрыть();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗначениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
    
    Если Строка(Элементы.КомпоновщикНастройкиОтбор.ТекущиеДанные.ЛевоеЗначение) = "РасписаниеПредставление" Тогда
        
        Если Расписание = Неопределено Тогда
            Расписание = Новый РасписаниеРегламентногоЗадания;
        КонецЕсли;
    
        Диалог = Новый ДиалогРасписанияРегламентногоЗадания(Расписание);
    
        ОписаниеОповещения = Новый ОписаниеОповещения("РасписаниеЗавершениеВыбора", ЭтотОбъект);
        Диалог.Показать(ОписаниеОповещения);
        
        СтандартнаяОбработка = Ложь;
        
    КонецЕсли;
    
КонецПроцедуры

&НаКлиенте
Процедура РасписаниеЗавершениеВыбора(ВыбранноеРасписание, ДополнительныеПараметры) Экспорт
    
    Если ВыбранноеРасписание <> Неопределено Тогда
        
        Расписание = ВыбранноеРасписание;
        Компоновщик.Настройки.Отбор.ПолучитьОбъектПоИдентификатору(Элементы.КомпоновщикНастройкиОтбор.ТекущаяСтрока).ПравоеЗначение =  Строка(ВыбранноеРасписание); 
        Элементы.КомпоновщикНастройкиОтбор.ЗакончитьРедактированиеСтроки(Ложь);
        
    КонецЕсли;
        
КонецПроцедуры

#КонецОбласти
