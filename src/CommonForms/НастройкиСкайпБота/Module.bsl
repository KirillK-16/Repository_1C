
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
    
    УстановитьПривилегированныйРежим(Истина);
    НастройкиСкайпБота = СкайпБот.ПрочитатьНастройки();
    УстановитьПривилегированныйРежим(Ложь);
    
    ЭтотОбъект.АдресПолученияТокена = НастройкиСкайпБота["АдресПолученияТокена"];
    ЭтотОбъект.АдресОтправкиСообщения = НастройкиСкайпБота["АдресОтправкиСообщения"];
    ЭтотОбъект.client_id = НастройкиСкайпБота["client_id"];
    ЭтотОбъект.client_secret = НастройкиСкайпБота["client_secret"];
    ЭтотОбъект.token_type = НастройкиСкайпБота["token_type"];
    ЭтотОбъект.access_token = НастройкиСкайпБота["access_token"];
    ЭтотОбъект.valid_until = НастройкиСкайпБота["valid_until"];
    
    ЭтотОбъект.ИспользоватьПрокси = НастройкиСкайпБота["ИспользоватьПрокси"];
    ЭтотОбъект.СерверПрокси = НастройкиСкайпБота["СерверПрокси"];
    ЭтотОбъект.ПортПрокси = НастройкиСкайпБота["ПортПрокси"];
    ЭтотОбъект.ПользовательПрокси = НастройкиСкайпБота["ПользовательПрокси"];
    ЭтотОбъект.ПарольПрокси = НастройкиСкайпБота["ПарольПрокси"];

    
    Если ЗначениеЗаполнено(ЭтотОбъект.client_id) Тогда
        ЭтотОбъект.СсылкаДобавитьБота = "https://join.skype.com/bot/" + ЭтотОбъект.client_id;
        ЭтотОбъект.Элементы.ДобавитьБотаВКонтакты.Заголовок = ЭтотОбъект.СсылкаДобавитьБота;
        ЭтотОбъект.Элементы.ДобавитьБотаВКонтакты.Видимость = Истина;
    Иначе
        ЭтотОбъект.Элементы.ДобавитьБотаВКонтакты.Видимость = Ложь;
    КонецЕсли;
    
    ЭтотОбъект.Элементы.ГруппаПроксиПараметры.Доступность = ЭтотОбъект.ИспользоватьПрокси;
    
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ИспользоватьПроксиПриИзменении(Элемент)
    ЭтотОбъект.Элементы.ГруппаПроксиПараметры.Доступность = ЭтотОбъект.ИспользоватьПрокси;
КонецПроцедуры

&НаКлиенте
Процедура client_idПриИзменении(Элемент)
    
    Если ЗначениеЗаполнено(ЭтотОбъект.client_id) Тогда
        ЭтотОбъект.СсылкаДобавитьБота = "https://join.skype.com/bot/" + ЭтотОбъект.client_id;
        ЭтотОбъект.Элементы.ДобавитьБотаВКонтакты.Заголовок = ЭтотОбъект.СсылкаДобавитьБота;
        ЭтотОбъект.Элементы.ДобавитьБотаВКонтакты.Видимость = Истина;
    Иначе
        ЭтотОбъект.Элементы.ДобавитьБотаВКонтакты.Видимость = Ложь;
    КонецЕсли;
    
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Записать(Команда)
    ЗаписатьНастройки();
КонецПроцедуры


&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
    
    ЗаписатьНастройки();    
    ЭтотОбъект.Закрыть();
    
КонецПроцедуры

&НаКлиенте
Процедура Тест(Команда)
    
    Токен = ТестНаСервере(ЭтотОбъект.АдресПолученияТокена, ЭтотОбъект.client_id, ЭтотОбъект.client_secret);
    
    ЭтотОбъект.token_type = Токен["token_type"];
    ЭтотОбъект.access_token = Токен["access_token"];
    ЭтотОбъект.valid_until = Токен["valid_until"];
    
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьСообщение(Команда)
    ОтправитьСообщениеНаСервере(ЭтотОбъект.Контакт, ЭтотОбъект.Текст);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ЗаписатьНастройки()
    
    НастройкиСкайпБота = Новый Соответствие;
    
    НастройкиСкайпБота.Вставить("АдресПолученияТокена", ЭтотОбъект.АдресПолученияТокена);
    НастройкиСкайпБота.Вставить("АдресОтправкиСообщения", ЭтотОбъект.АдресОтправкиСообщения);
    НастройкиСкайпБота.Вставить("client_id", ЭтотОбъект.client_id);
    НастройкиСкайпБота.Вставить("client_secret", ЭтотОбъект.client_secret);
    НастройкиСкайпБота.Вставить("token_type", ЭтотОбъект.token_type);
    НастройкиСкайпБота.Вставить("access_token", ЭтотОбъект.access_token);
    НастройкиСкайпБота.Вставить("valid_until", ЭтотОбъект.valid_until);
    НастройкиСкайпБота.Вставить("ИспользоватьПрокси", ЭтотОбъект.ИспользоватьПрокси);
    НастройкиСкайпБота.Вставить("СерверПрокси", ЭтотОбъект.СерверПрокси);
    НастройкиСкайпБота.Вставить("ПортПрокси", ЭтотОбъект.ПортПрокси);
    НастройкиСкайпБота.Вставить("ПользовательПрокси", ЭтотОбъект.ПользовательПрокси);
    НастройкиСкайпБота.Вставить("ПарольПрокси", ЭтотОбъект.ПарольПрокси);
    
    ЗаписатьНаСервере(НастройкиСкайпБота);
    
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаписатьНаСервере(Настройки)
    
    УстановитьПривилегированныйРежим(Истина);
    СкайпБот.ЗаписатьНастройки(Настройки);
    УстановитьПривилегированныйРежим(Ложь);
    
КонецПроцедуры

&НаСервереБезКонтекста
Функция ТестНаСервере(Адрес, client_id, client_secret)
	
	УстановитьПривилегированныйРежим(Истина);
	Настройки = СкайпБот.ПрочитатьНастройки();
	УстановитьПривилегированныйРежим(Ложь);
	
    Возврат СкайпБот.ПолучитьТокен(Настройки);
    
КонецФункции

&НаСервереБезКонтекста
Процедура ОтправитьСообщениеНаСервере(Контакт, Текст)
    СкайпБот.ОтправитьСообщение(Контакт, Текст);
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьБотаВКонтакты(Команда)
    
    ОписаниеОповещения = Новый ОписаниеОповещения("ЗавершениеНачатьЗапускПриложения", ЭтотОбъект);
    НачатьЗапускПриложения(ОписаниеОповещения, ЭтотОбъект.СсылкаДобавитьБота);
            
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеНачатьЗапускПриложения(КодВозврата, ДополнительныеПараметры) Экспорт
    
    // Данный код нужен для обхода поиска пустых обработчиков при проверке конфигурации
    Если Истина = Ложь Тогда
    КонецЕсли;
    
КонецПроцедуры

#КонецОбласти
