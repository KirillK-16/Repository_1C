
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура ДлительнаяОперацияВФоне(ПараметрыЗапуска) Экспорт
    
    КлючОбъекта = ПараметрыЗапуска.ПараметрыХранилищаНастроек.КлючОбъекта;
    КлючНастроек = ПараметрыЗапуска.ПараметрыХранилищаНастроек.КлючНастроек;
    Пользователь = ПараметрыЗапуска.ПараметрыХранилищаНастроек.Пользователь;
    
    ДанныеХранилища = Новый Соответствие;
    ДанныеХранилища.Вставить("ВыполнятьДо", ТекущаяУниверсальнаяДата());
    ДанныеХранилища.Вставить("Данные", Неопределено);
    
    СохранитьВХранилище(КлючОбъекта, КлючНастроек, ДанныеХранилища, Пользователь);
    
    Запрос = Новый Запрос;
    
    Запрос.Текст = "
    |ВЫБРАТЬ
    |   ИнцидентыАктивные.ТипИнцидента КАК ТипИнцидента,
    |   Инциденты.УровеньИнцидента КАК УровеньИнцидента,
    |   КОЛИЧЕСТВО(*) КАК КоличествоИнцидентов
    |ИЗ
    |   РегистрСведений.ИнцидентыАктивные КАК ИнцидентыАктивные
    |ВНУТРЕННЕЕ СОЕДИНЕНИЕ
    |   РегистрСведений.Инциденты КАК Инциденты
    |ПО
    |   Инциденты.ТипИнцидента = ИнцидентыАктивные.ТипИнцидента
    |   И Инциденты.Инцидент = ИнцидентыАктивные.Инцидент
    |СГРУППИРОВАТЬ ПО
    |   ИнцидентыАктивные.ТипИнцидента,
    |   Инциденты.УровеньИнцидента
    |";
    
    Выполнять = Истина;
    
    Пока Выполнять Цикл
        
        Результат = Запрос.Выполнить();
        
        Данные = Новый Соответствие;
        Данные.Вставить(Перечисления.УровниИнцидентов.КритическаяОшибка, Новый Соответствие);
        Данные.Вставить(Перечисления.УровниИнцидентов.Ошибка, Новый Соответствие);
        Данные.Вставить(Перечисления.УровниИнцидентов.Предупреждение, Новый Соответствие);
                
        Выборка = Результат.Выбрать();
        Пока Выборка.Следующий() Цикл
            
            ДанныеУровень = Данные[Выборка.УровеньИнцидента];
            
            ДанныеТипаИнцидента = ДанныеУровень[Выборка.ТипИнцидента];
            Если ДанныеТипаИнцидента = Неопределено Тогда
                ДанныеТипаИнцидента = Новый Соответствие;
                ДанныеУровень.Вставить(Выборка.ТипИнцидента, ДанныеТипаИнцидента);
            КонецЕсли;
            
            ДанныеТипаИнцидента.Вставить("Количество", Выборка.КоличествоИнцидентов);
            
        КонецЦикла;
        
        
        ДанныеХранилища.Вставить("Данные", Данные);
        СохранитьВХранилище(КлючОбъекта, КлючНастроек, ДанныеХранилища, Пользователь);
        
        КипВнешнийКомпонент.Пауза(5000);
        
        ДанныеХранилища = ЗагрузитьИзХранилища(КлючОбъекта, КлючНастроек, Пользователь);
        Выполнять = ДанныеХранилища["ВыполнятьДо"] > ТекущаяУниверсальнаяДата(); 
        
    КонецЦикла;
    
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СохранитьВХранилище(КлючОбъекта, КлючНастроек, ДанныеХранилища,Пользователь)
    
    Попытка
        
        НачатьТранзакцию();
        
        БлокировкаДанных = Новый БлокировкаДанных;
        ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.ОбъектыБлокировок");
        ЭлементБлокировки.УстановитьЗначение("Объект", КлючОбъекта+КлючНастроек+Пользователь);
        БлокировкаДанных.Заблокировать();
        
        ХранилищеОбщихНастроек.Сохранить(КлючОбъекта, КлючНастроек, ДанныеХранилища,,Пользователь);
        
        ЗафиксироватьТранзакцию();
        
    Исключение
        
        ОтменитьТранзакцию();
        ВызватьИсключение ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
        
    КонецПопытки;
    
КонецПроцедуры

Функция ЗагрузитьИзХранилища(КлючОбъекта, КлючНастроек, Пользователь)
    
    ДанныеХранилища = Неопределено;
    
    Попытка
        
        НачатьТранзакцию();
        
        БлокировкаДанных = Новый БлокировкаДанных;
        ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.ОбъектыБлокировок");
        ЭлементБлокировки.УстановитьЗначение("Объект", КлючОбъекта+КлючНастроек+Пользователь);
        БлокировкаДанных.Заблокировать();
        
        ДанныеХранилища = ХранилищеОбщихНастроек.Загрузить(КлючОбъекта, КлючНастроек,,Пользователь);
        
        ЗафиксироватьТранзакцию();
        
    Исключение
        
        ОтменитьТранзакцию();
        ВызватьИсключение ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
        
    КонецПопытки;
    
    Возврат ДанныеХранилища;
    
КонецФункции


#КонецОбласти

#КонецЕсли