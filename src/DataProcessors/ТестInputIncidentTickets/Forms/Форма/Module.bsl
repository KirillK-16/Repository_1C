
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(Объект.АдресЦКК) Тогда
		Объект.АдресЦКК = "http://[host]/[base]/hs/InputIncidentTickets/";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если НЕ ЗначениеЗаполнено(Объект.АдресЦКК) Тогда
		Объект.АдресЦКК = "http://[host]/[base]/hs/InputIncidentTickets/";
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СоздатьТипИнцидента(Команда)
	СоздатьТипИнцидентаНаСервере(
		"Тестовый тип",
		"Предупреждение",
		"Тестовая подсистема",
		"ТестовыйТег_1 ТестовыйТег_2",
		Объект.АдресЦКК,
		Объект.Пользователь,
		Объект.Пароль
	);
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьИнцидент(Команда)
	ОткрытьИнцидентНаСервере(
		"Тестовый тип",
		"Тестовый код",
		"Тестовое сообщение открытия инцидента",
		Объект.АдресЦКК,
		Объект.Пользователь,
		Объект.Пароль
	);
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьИнцидент(Команда)
    
    ЗакрытьИнцдиентНаСервере(
        "Тестовый тип",
        "Тестовый код",
        Объект.АдресЦКК,
        Объект.Пользователь,
        Объект.Пароль
    );
		
КонецПроцедуры

&НаКлиенте
Процедура СоздатьТипИнцидентаПросмотр(Команда)
	
	ЭтотОбъект.ХмлПакет.Удалить();
	ХмлПакетБуфер = СоздатьТипИнцидентаСформироватьХМЛ("Тестовый тип", "Предупреждение", "Тест", "ТестовыйТег_1 ТестовыйТег_2");
	ОбщийКлиентСервер.ФорматированныйДокументПодсветкаXML(ЭтотОбъект.ХмлПакет, ХмлПакетБуфер);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьИнцидентПросмотр(Команда)
	
	ЭтотОбъект.ХмлПакет.Удалить();
	ХмлПакетБуфер = ОткрытьИнцидентСформироватьХМЛ("Тестовый тип", "Тестовый код", "Тестовое сообщение открытия инцидента", Объект.АдресЦКК);
	ОбщийКлиентСервер.ФорматированныйДокументПодсветкаXML(ЭтотОбъект.ХмлПакет, ХмлПакетБуфер);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьИнцидентПросмотр(Команда)
    
    ЭтотОбъект.ХмлПакет.Удалить();
	ХмлПакетБуфер = ЗакрытьИнцидентСформироватьХМЛ("Тестовый тип", "Тестовый код");
	ОбщийКлиентСервер.ФорматированныйДокументПодсветкаXML(ЭтотОбъект.ХмлПакет, ХмлПакетБуфер);
    
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция СоздатьТипИнцидентаСформироватьХМЛ(Тип, Уровень, Подсистема, Теги)
	
	Запись = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип("http://www.1c.ru/1cFresh/Incidents/1_0_1_1", "IncidentType"));
	Запись.Type			= Тип;
	Запись.Level		= Уровень;
	Запись.SubSystem	= Подсистема;
	Запись.Tags			= Теги;	
		
	Поток = Новый ЗаписьXML(); 
	Поток.УстановитьСтроку();
	ФабрикаXDTO.ЗаписатьXML(Поток, Запись);
	ХмлПакет = Поток.Закрыть();
	
	Возврат ХмлПакет;
	
КонецФункции

&НаСервереБезКонтекста
Функция ОткрытьИнцидентСформироватьХМЛ(ТипИнцидента, КодИнцидента, ТекстСообщения, АдресЦКК)
	
	Запись = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип("http://www.1c.ru/1cFresh/Incidents/1_0_1_1", "Incident"));
	Запись.Type		= ТипИнцидента;
	Запись.Id 		= КодИнцидента;
	Запись.Message	= ТекстСообщения;
	Запись.Count		= 1;
	Запись.Infobase	= "";
	Запись.Cluster	= АдресЦКК;
	
	Поток = Новый ЗаписьXML(); 
	Поток.УстановитьСтроку();
	ФабрикаXDTO.ЗаписатьXML(Поток, Запись);
	ХмлПакет = Поток.Закрыть();
	
	Возврат ХмлПакет;
	
КонецФункции

&НаСервереБезКонтекста
Функция ЗакрытьИнцидентСформироватьХМЛ(ТипИнцидента, КодИнцидента)
	
	Запись = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип("http://www.1c.ru/1cFresh/Incidents/1_0_1_1", "IncidentClose"));
	Запись.Type		= ТипИнцидента;
	Запись.Id 		= КодИнцидента;
	
	Поток = Новый ЗаписьXML(); 
	Поток.УстановитьСтроку();
	ФабрикаXDTO.ЗаписатьXML(Поток, Запись);
	ХмлПакет = Поток.Закрыть();
	
	Возврат ХмлПакет;
	
КонецФункции

// Процедура - Отправляет http запрос в ЦКК на создание типа инцидента
//
// Параметры:
//  Тип			 - 	Строка(150), произвольное наименование типа инцидента
//  Уровень		 - 	Строка, возможные значения: "Информация", "Предупреждение", "Ошибка", "Критическая ошибка" 
//  Подсистема	 - 	Строка(100), произвольное наименование подсистемы, к которой относиться данный тип инцидента
//  Теги		 - 	Строка, набор тегов инцидента, разделенных пробелами, по которым можно будет группировать
//					инциденты
//	АдресЦКК	 -	Строка, адрес опубликованной информационной базы ЦКК
//	Пользователь - 	Строка, имя пользователя информационной базы ЦКК
//	Пароль		 - 	Строка, пароль пользователя информационной базы ЦКК
//
&НаСервереБезКонтекста
Процедура СоздатьТипИнцидентаНаСервере(Знач Тип, Знач Уровень, Знач Подсистема, Знач Теги, Знач АдресЦКК, Знач Пользователь, Знач Пароль)
	
	Запись = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип("http://www.1c.ru/1cFresh/Incidents/1_0_1_1", "IncidentType"));
	Запись.Type			= Тип;
	Запись.Level		= "Предупреждение";
	Запись.SubSystem	= "Тест";
	Запись.Tags			= Теги;
		
	КодСостояния = ВыполнитьЗапрос("DefineType", Запись, АдресЦКК, Пользователь, Пароль);
	
	Если КодСостояния = 200 Тогда
		Сообщить("Тип инцидента """ + Тип + """ создан успешно.");
	КонецЕсли;
		
КонецПроцедуры

// Процедура - Отправляет http запрос в ЦКК на открытие инцидента.
//
// Параметры:
//  ТипИнцидента		- Строка(150) - Идентификатор типа инцидента. Должен быть среди зарегистрированных типов.
//  КодИнцидента		- Строка(150) - Строковый идентификатор инцидента. Должен быть уникален внутри типа.
//						  Если повторяется, считается, что инцидент возник еще раз и счетчик срабатываний его в ЦКК увеличится.
//  ТекстСообщения		- Строка - текст сообщения инцидента
//	АдресЦКК	 -	Строка, адрес опубликованной информационной базы ЦКК
//	Пользователь - 	Строка, имя пользователя информационной базы ЦКК
//	Пароль		 - 	Строка, пароль пользователя информационной базы ЦКК
//
&НаСервереБезКонтекста
Процедура ОткрытьИнцидентНаСервере(Знач ТипИнцидента, Знач КодИнцидента, Знач ТекстСообщения, Знач АдресЦКК, Знач Пользователь, Знач Пароль)
	
	Если ПустаяСтрока(ТипИнцидента) Тогда	
		ВызватьИсключение НСтр("ru = 'Не указан тип инцидента'"); 
    КонецЕсли;
    
	Если ПустаяСтрока(КодИнцидента) Тогда	
		ВызватьИсключение НСтр("ru = 'Не указан код инцидента'"); 
    КонецЕсли;
    
	Если ПустаяСтрока(ТекстСообщения) Тогда	
		ВызватьИсключение НСтр("ru = 'Не указано сообщение инцидента'"); 
	КонецЕсли;
	
	Инцидент = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип("http://www.1c.ru/1cFresh/Incidents/1_0_1_1", "Incident"));
	Инцидент.Type		= ТипИнцидента;
	Инцидент.Id 		= КодИнцидента;
	Инцидент.Message	= ТекстСообщения;
	Инцидент.Count		= 1;
	Инцидент.Infobase	= "";
	Инцидент.Cluster	= АдресЦКК;
	
	КодСостояния = ВыполнитьЗапрос("Open", Инцидент, АдресЦКК, Пользователь, Пароль);
	
	Если КодСостояния = 200 Тогда
		Сообщить("Тип инцидента """ + ТипИнцидента + """, код инцидента """ + КодИнцидента + """ - открыт успешно.");
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗакрытьИнцдиентНаСервере(Знач ТипИнцидента, Знач КодИнцидента, Знач АдресЦКК, Знач Пользователь, Знач Пароль)
    
    Если ПустаяСтрока(ТипИнцидента) Тогда	
		ВызватьИсключение НСтр("ru = 'Не указан тип инцидента'"); 
    КонецЕсли;
    
	Если ПустаяСтрока(КодИнцидента) Тогда	
		ВызватьИсключение НСтр("ru = 'Не указан код инцидента'"); 
	КонецЕсли;
    
    Инцидент = ФабрикаXDTO.Создать(ФабрикаXDTO.Тип("http://www.1c.ru/1cFresh/Incidents/1_0_1_1", "IncidentClose"));
	Инцидент.Type		= ТипИнцидента;
	Инцидент.Id 		= КодИнцидента;
    
    КодСостояния = ВыполнитьЗапрос("Close", Инцидент, АдресЦКК, Пользователь, Пароль);
	
	Если КодСостояния = 200 Тогда
		Сообщить("Тип инцидента """ + ТипИнцидента + """, код инцидента """ + КодИнцидента + """ - закрыт успешно.");
	КонецЕсли;
    
КонецПроцедуры

&НаСервереБезКонтекста
Функция ВыполнитьЗапрос(КомандаЗапроса, Данные, АдресЦКК, Пользователь, Пароль)
	
	СтруктурированныйАдрес = ОбщийКлиентСервер.СтруктураURI(АдресЦКК);
	Запрос = Новый HTTPЗапрос();
	Запрос.АдресРесурса = СтруктурированныйАдрес.ПутьНаСервере + КомандаЗапроса;
	
	Запрос.Заголовки["Accept-Charset"] = "utf-8";
	Запрос.Заголовки["Accept"] = "application/xml";
	Запрос.Заголовки["Content-Type"] = "application/xml;charset=utf-8";
	Если ТипЗнч(Данные)=Тип("ОбъектXDTO") Тогда
		Поток = Новый ЗаписьXML(); 
		Поток.УстановитьСтроку();
		ФабрикаXDTO.ЗаписатьXML(Поток, Данные);
		ДанныеКакСтрока = Поток.Закрыть();
		Запрос.УстановитьТелоИзСтроки(ДанныеКакСтрока);
	Иначе
		Запрос.УстановитьТелоИзСтроки(Данные);
	КонецЕсли;
	
	Соединение = Новый HTTPСоединение(
		СтруктурированныйАдрес.Хост, 
		СтруктурированныйАдрес.Порт, 
		Пользователь,
		Пароль
	);
	
	Ответ = Соединение.ОтправитьДляОбработки(Запрос);
	
	Если Ответ.КодСостояния <> 200 Тогда
		ОтветСтрока = "Код состояния=" + Ответ.КодСостояния +", " + Ответ.ПолучитьТелоКакСтроку();
		Сообщить(ОтветСтрока);
	КонецЕсли;
	
	Возврат Ответ.КодСостояния;
	
КонецФункции

#КонецОбласти













