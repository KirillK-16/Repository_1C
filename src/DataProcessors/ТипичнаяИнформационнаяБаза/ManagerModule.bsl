Процедура СформироватьРезультатНаСервере(ИмяСтраницы, ПараметрыФормирования, АдресВременногоХранилища) Экспорт
	ПоИБ = ПараметрыФормирования.ПоИБ;
	ИнформационнаяБаза = ПараметрыФормирования.ИнформационнаяБаза;
	Конфигурация = ПараметрыФормирования.Конфигурация;
	Период = ПараметрыФормирования.Период;
	ПользовательскиеНастройки = ПараметрыФормирования.ПользовательскиеНастройки;
	
	Суффикс = ?(ПоИБ, "ПоИнфоБазе", "Общий");
	ТабличныйДокумент = Новый ТабличныйДокумент;
	СхемаКомпоновкиДанных = Обработки.ТипичнаяИнформационнаяБаза.ПолучитьМакет(ИмяСтраницы + Суффикс);
	
	КомпоновщикНастроекКомпоновкиДанных = Новый КомпоновщикНастроекКомпоновкиДанных;
	КомпоновщикНастроекКомпоновкиДанных.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаКомпоновкиДанных));
	КомпоновщикНастроекКомпоновкиДанных.ЗагрузитьНастройки(СхемаКомпоновкиДанных.НастройкиПоУмолчанию);
	
	// Загрузка настроек из дополнительных параметров
	ДатаНачала = ЗначениеДатыДляСтраницы(ИмяСтраницы, "НачалоПериода", Период.ДатаНачала); 
	ДатаОкончания = ЗначениеДатыДляСтраницы(ИмяСтраницы, "КонецПериода", Период.ДатаОкончания); 
	КомпоновщикНастроекКомпоновкиДанных.ЗагрузитьПользовательскиеНастройки(ПользовательскиеНастройки);
	УстановитьЗначениеПараметраКомпоновщика(КомпоновщикНастроекКомпоновкиДанных, "ИнформационнаяБаза", ИнформационнаяБаза);
	УстановитьЗначениеПараметраКомпоновщика(КомпоновщикНастроекКомпоновкиДанных, "Конфигурация", Конфигурация);
	УстановитьЗначениеПараметраКомпоновщика(КомпоновщикНастроекКомпоновкиДанных, "НачалоПериода", ДатаНачала);
	УстановитьЗначениеПараметраКомпоновщика(КомпоновщикНастроекКомпоновкиДанных, "КонецПериода", ДатаОкончания);
	
	//3. готовим макет 
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	Макет = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, КомпоновщикНастроекКомпоновкиДанных.ПолучитьНастройки());
	
	//4. исполняем макет 
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(Макет,,, Истина);
	ПроцессорКомпоновки.Сбросить();
	
	//5. выводим результат 
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ТабличныйДокумент);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	ПоместитьВоВременноеХранилище(ТабличныйДокумент, АдресВременногоХранилища);
КонецПроцедуры

Процедура УстановитьЗначениеПараметраКомпоновщика(Компоновщик, ИмяПараметра, Значение)
	Параметр = Компоновщик.Настройки.ПараметрыДанных.Элементы.Найти(ИмяПараметра);
	Если Параметр <> Неопределено Тогда
		Параметр.Использование = ЗначениеЗаполнено(Значение);
		Параметр.Значение = Значение;	
	КонецЕсли;
КонецПроцедуры

Функция ЗначениеДатыДляСтраницы(ИмяСтраницы, ИмяПараметра, ЗначениеДаты)
	СтраницыСОбязательнымиДатами = Новый Соответствие;
	СтраницыСОбязательнымиДатами.Вставить("Производительность", Истина);
	СтраницыСОбязательнымиДатами.Вставить("АктивностьПользователей", Истина);
	Если СтраницыСОбязательнымиДатами[ИмяСтраницы] = Истина Тогда
		Если ИмяПараметра = "НачалоПериода" И Не ЗначениеЗаполнено(ЗначениеДаты) Тогда
			Возврат НачалоДня(ТекущаяДатаСеанса());
		КонецЕсли;	
		Если ИмяПараметра = "КонецПериода" И Не ЗначениеЗаполнено(ЗначениеДаты) Тогда
			Возврат КонецДня(ТекущаяДатаСеанса());
		КонецЕсли;
	КонецЕсли;
	Возврат ЗначениеДаты; 	
КонецФункции