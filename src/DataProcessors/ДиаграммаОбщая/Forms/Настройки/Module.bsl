
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
    
    ИдентификаторНастройки = ИдентификаторНастройки();
    НастройкиСохраненные = ПрочитатьНастройки(ИдентификаторНастройки);
    ЭтотОбъект.Настройки.Загрузить(НастройкиСохраненные);
    
    Если ЭтотОбъект.Параметры.Свойство("Настройка") Тогда
        ЭтотОбъект.Наименование = ЭтотОбъект.Параметры.Настройка.Наименование;
        ЭтотОбъект.НастройкаТекущая = ЭтотОбъект.Параметры.Настройка;
        Элементы.ФормаВыбрать.Видимость = Ложь;
    Иначе
        Элементы.Наименование.Видимость = Ложь;
    КонецЕсли;
        
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Записать(Команда)
    
    Если ЗначениеЗаполнено(ЭтотОбъект.Наименование) Тогда
        Настройка = НайтиНастройку(ЭтотОбъект.Наименование);
        Настройка.Наименование = ЭтотОбъект.Наименование;
        Настройка.Настройка = ЭтотОбъект.НастройкаТекущая;
    КонецЕсли;
    
    ЗаписатьНаСервере();
    
    ЭтотОбъект.Закрыть();
    
КонецПроцедуры

&НаКлиенте
Процедура Выбрать(Команда)
    ЭтотОбъект.Закрыть(Элементы.Настройки.ТекущиеДанные);
КонецПроцедуры

&НаКлиенте
Процедура Изменить(Команда)
    
    Если Элементы.Настройки.ТекущиеДанные <> Неопределено Тогда
        ОписаниеОповещения = Новый ОписаниеОповещения("ИзменитьЗавершение", ЭтотОбъект);
        ПоказатьВводСтроки(ОписаниеОповещения, Элементы.Настройки.ТекущиеДанные.Наименование);
    КонецЕсли;
    
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура НастройкиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
    ЭтотОбъект.Закрыть(Элементы.Настройки.ТекущиеДанные);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ИдентификаторНастройки()
    
    ИдентификаторНастройки =
        "e99fc93d-5fa8-46bc-81dc-c2170268d578:" + 
        ПараметрыСеанса.ТекущийПользователь.УникальныйИдентификатор + 
        "(" + ПараметрыСеанса.ТекущийПользователь + " для 'Обработка.ДиаграммаОбщая')";
        
    Возврат ИдентификаторНастройки;
    
КонецФункции

&НаСервере
Функция ПрочитатьНастройки(ИдентификаторНастройки)
    
    УстановитьПривилегированныйРежим(Истина);
    НастройкиСохраненные = РегистрыСведений.БезопасноеХранилище.ПолучитьДанные(ИдентификаторНастройки);
    УстановитьПривилегированныйРежим(Ложь);
    
    Если НастройкиСохраненные = Неопределено Тогда
        НастройкиСохраненные = ЭтотОбъект.Настройки.Выгрузить();    
    КонецЕсли;
    
    Возврат НастройкиСохраненные;
        
КонецФункции

&НаКлиенте
Функция НайтиНастройку(Настройка)
    
    ПараметрыОтбора = Новый Структура("Наименование", Наименование);
    Строки = ЭтотОбъект.Настройки.НайтиСтроки(ПараметрыОтбора);
    Если Строки.Количество() = 0 Тогда
        Возврат ЭтотОбъект.Настройки.Добавить();
    Иначе
        Возврат Строки[0];
    КонецЕсли;
    
КонецФункции

&НаСервере
Процедура ЗаписатьНаСервере()
    
    ИдентификаторНастройки = ИдентификаторНастройки();
    УстановитьПривилегированныйРежим(Истина);
    РегистрыСведений.БезопасноеХранилище.ЗаписатьДанные(ИдентификаторНастройки, ЭтотОбъект.Настройки.Выгрузить());
    УстановитьПривилегированныйРежим(Ложь);
    
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьЗавершение(Строка, ДополнительныеПараметры) Экспорт
    
    Если ЗначениеЗаполнено(Строка) Тогда
        Элементы.Настройки.ТекущиеДанные.Наименование = Строка;
    КонецЕсли;
    
КонецПроцедуры

#КонецОбласти
