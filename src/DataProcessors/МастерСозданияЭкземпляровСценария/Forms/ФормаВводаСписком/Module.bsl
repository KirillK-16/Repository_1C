
&НаКлиенте
Процедура ЗакрытьФорму(Команда)
	ПараметрыЗакрытия = Новый Структура("ПараметрыСтрокой, ПараметрыСписком, ИмяШаблона", ПараметрыСтрокой, ПараметрыСписком, ИмяШаблона);
	Закрыть(ПараметрыЗакрытия);
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("ОбъектыЗаполненияПараметров") Тогда
		ОбъектыЗаполненияПараметров = Параметры.ОбъектыЗаполненияПараметров.Выгрузить();
	КонецЕсли;	
	Если Параметры.Свойство("ЗаготовкиЭкземпляровСценариев") Тогда
		ЗаготовкиЭкземпляровСценариев.Загрузить(Параметры.ЗаготовкиЭкземпляровСценариев.Выгрузить());
	КонецЕсли;	

	Если Параметры.Свойство("ИмяШаблона") Тогда
		ИмяШаблона = Параметры.ИмяШаблона;
	КонецЕсли;	
	
	МассивТипов = Новый Массив;
	ИспользоватьПодборСписком = Ложь;
	Если ИмяШаблона = "# Все машины" Тогда
		МассивТипов.Добавить(Тип("СправочникСсылка.ГруппыЕдиницАвтоматизации"));
		ИспользоватьПодборСписком = Истина;
	ИначеЕсли ТипЗнч(ИмяШаблона) = Тип("СправочникСсылка.ШаблоныЕдиницАвтоматизации") Тогда
		МассивТипов.Добавить(Тип("СправочникСсылка.АгентыКИП"));
		ИспользоватьПодборСписком = Истина;
	Иначе
		СтрокиПоШаблону = ОбъектыЗаполненияПараметров.НайтиСтроки(Новый Структура("ИмяШаблона", ИмяШаблона));
		
		Если СтрокиПоШаблону.Количество() > 0 Тогда
			Для Каждого Строка Из СтрокиПоШаблону Цикл
				ТипЗначения = ТипЗнч(Строка.Объект);
				Если ТипЗначения <> Тип("Строка") Тогда
					ИспользоватьПодборСписком = Истина;	
				КонецЕсли;
					
				МассивТипов.Добавить(ТипЗнч(Строка.Объект));
			КонецЦикла;
		Иначе // это реквизит
			Колонка = ЗаготовкиЭкземпляровСценариев.Выгрузить().Колонки.Найти(ИмяШаблона);
			Если Колонка <> Неопределено Тогда
				МассивТипов = Колонка.ТипЗначения.Типы();
				ИспользоватьПодборСписком = Истина;
			КонецЕсли;
		КонецЕсли;	
	КонецЕсли;
	
	ПараметрыСписком.ТипЗначения = Новый ОписаниеТипов(МассивТипов);
	
	Если ИспользоватьПодборСписком Тогда
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ВводПараметровСписком;
	Иначе
		Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ВводПараметровСтрокой;
	КонецЕсли;	
КонецПроцедуры
