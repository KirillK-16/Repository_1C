Функция OpenPOST(Запрос)
	
	Попытка
		ТелоЗапроса = Запрос.ПолучитьТелоКакСтроку();
	
		Поток = Новый ЧтениеXML();
		Поток.УстановитьСтроку(ТелоЗапроса);
		Инцидент = ФабрикаXDTO.ПрочитатьXML(Поток);
		Поток.Закрыть();
		
		Если Инцидент.Свойства().Получить("Type")=неопределено Тогда
			ВызватьИсключение "Не указан тип (Type) инцидента";
		КонецЕсли;
		
		Если Инцидент.Свойства().Получить("Id")=неопределено Тогда
			ВызватьИсключение "Не указан код (Id) инцидента";
		КонецЕсли;
		
		Если Инцидент.Свойства().Получить("Message")=неопределено Тогда
			ВызватьИсключение "Не указано сообщение (Message) инцидента";
		КонецЕсли;
        
        Если Инцидент.Свойства().Получить("Infobase") = Неопределено Тогда
            ИнформационнаяБаза = "";
        Иначе
            ИнформационнаяБаза =  Инцидент["Infobase"];
        КонецЕсли;
        
		Если ТипЗнч(ИнформационнаяБаза) = Тип("ОбъектXDTO") Тогда
			ИнформационнаяБаза = Неопределено;
		КонецЕсли;
        
        Если Инцидент.Свойства().Получить("Cluster") = Неопределено Тогда
            Кластер = "";
        Иначе
            Кластер =  Инцидент["Cluster"];
        КонецЕсли;
        
        Если Инцидент.Свойства().Получить("Count") = Неопределено Тогда
            ЧислоСрабатываний = 1;
        Иначе
            ЧислоСрабатываний = Число(Инцидент.Count);
        КонецЕсли;
        
		ИнцидентыСервер.ОткрытьИнцидент(Инцидент.Type, Инцидент.Id, ИнформационнаяБаза, Кластер, Инцидент.Message, ЧислоСрабатываний);
				
		Ответ = Новый HTTPСервисОтвет(200);
		Возврат Ответ;
		
	Исключение
		
		ОбъектОшибки = ИнформацияОбОшибке();
		
		ЗаписьЖурналаРегистрации("InputIncidentTickets.Open", УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки(ОбъектОшибки));
		
		Ответ = Новый HTTPСервисОтвет(500);
		Ответ.УстановитьТелоИзСтроки("<html><body>" + КраткоеПредставлениеОшибки(ОбъектОшибки) + "</body><html>"); //!! экранировать символы
        
		Возврат Ответ;
		
	КонецПопытки;
	
КонецФункции

Функция ClosePOST(Запрос)
	
	Попытка
		ТелоЗапроса = Запрос.ПолучитьТелоКакСтроку();
        
        Если СтрНайти(ТелоЗапроса, "<IncidentClose xmlns=""http://www.1c.ru/1cFresh/Incidents/") = 1 Тогда
            
            Поток = Новый ЧтениеXML();
            Поток.УстановитьСтроку(ТелоЗапроса);
            Инцидент = ФабрикаXDTO.ПрочитатьXML(Поток);
            Поток.Закрыть();
            
            Если Инцидент.Свойства().Получить("Type")=неопределено Тогда
                ВызватьИсключение "Не указан тип (Type) инцидента";
            КонецЕсли;
            
            Если Инцидент.Свойства().Получить("Id")=неопределено Тогда
                ВызватьИсключение "Не указан код (Id) инцидента";
            КонецЕсли;
            
            ИнцидентыСервер.ЗакрытьИнцидент(Инцидент.Type, Инцидент.Id, Истина);
            
            Ответ = Новый HTTPСервисОтвет(200);
        Иначе
            CloseByCode(ТелоЗапроса);
            Ответ = Новый HTTPСервисОтвет(200);
        КонецЕсли;
        
		Возврат Ответ;
	
	Исключение
		
		ОбъектОшибки = ИнформацияОбОшибке();
		ЗаписьЖурналаРегистрации("InputIncidentTickets.Close", 
			УровеньЖурналаРегистрации.Ошибка,,,
			ПодробноеПредставлениеОшибки(ОбъектОшибки)
		);
		
		Ответ = Новый HTTPСервисОтвет(500);
		Ответ.УстановитьТелоИзСтроки("<html><body>" + КраткоеПредставлениеОшибки(ОбъектОшибки) + "</body><html>"); //!! экранировать символы
		Возврат Ответ;
		
	КонецПопытки;
	
КонецФункции

Функция DefineTypePOST(Запрос)
	
	Попытка
		ТелоЗапроса = Запрос.ПолучитьТелоКакСтроку();
	
		Поток = Новый ЧтениеXML();
		Поток.УстановитьСтроку(ТелоЗапроса);
		ТипИнцидента = ФабрикаXDTO.ПрочитатьXML(Поток);
		Поток.Закрыть();
		
		_УровеньИнцидента = ПредопределенноеЗначение("Перечисление.УровниИнцидентов." + ТипИнцидента.Level);
		_Теги = ?(ТипИнцидента.Свойства().Получить("Tags")=неопределено, "", ТипИнцидента["Tags"]);
		Результат = ИнцидентыСервер.ОпределитьТипИнцидента(ТипИнцидента.Type, _УровеньИнцидента, ТипИнцидента.Subsystem);
		
		Ответ = Новый HTTPСервисОтвет(200);
		Возврат Ответ;
	
	Исключение
		
		ОбъектОшибки = ИнформацияОбОшибке();
		ЗаписьЖурналаРегистрации("InputIncidentTickets.DefineType", УровеньЖурналаРегистрации.Ошибка,,,ПодробноеПредставлениеОшибки(ОбъектОшибки));
		
		Ответ = Новый HTTPСервисОтвет(500);
		Ответ.УстановитьТелоИзСтроки("<html><body>" + КраткоеПредставлениеОшибки(ОбъектОшибки) + "</body><html>"); //!! экранировать символы
		Возврат Ответ;
		
	КонецПопытки;
		
КонецФункции

Процедура CloseByCode(КодИнцидента)
    
    Запрос = Новый Запрос;
    Запрос.Текст = "
    |ВЫБРАТЬ
    |   РегИнциденты.Инцидент,
    |   РегИнциденты.ТипИнцидента
    |ИЗ
    |   РегистрСведений.Инциденты КАК РегИнциденты
    |ВНУТРЕННЕЕ СОЕДИНЕНИЕ
    |   Справочник.Инциденты КАК СпрИнциденты
    |ПО
    |   СпрИнциденты.Ссылка = РегИнциденты.Инцидент
    |   И СпрИнциденты.Наименование = &КодИнцидента
    |";
    
    Запрос.УстановитьПараметр("КодИнцидента", КодИнцидента);
    
    Результат = Запрос.Выполнить();
    Выборка = Результат.Выбрать();
    Пока Выборка.Следующий() Цикл
        ИнцидентыСервер.ЗакрытьИнцидент(Выборка.ТипИнцидента, Выборка.Инцидент, Истина);
    КонецЦикла;
    
КонецПроцедуры
