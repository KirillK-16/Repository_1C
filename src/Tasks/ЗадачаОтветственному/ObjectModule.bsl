#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Процедура ПриЗаписи(Отказ)
	Если ОбменДанными.Загрузка Тогда 
		Возврат; 
	КонецЕсли; 

	Источник = ЭтотОбъект.ИсточникЗадачи;
	Если ТипЗнч(Источник) = Тип("СправочникСсылка.КонтрольныеПроцедуры")
		И ЭтотОбъект.ТипЗадачи <> Справочники.ТипыЗадач.ПерезапускКонтрольнойПроцедуры
	Тогда
				
		Если ЭтотОбъект.СтатусЗадачиИзменился И ЭтотОбъект.Выполнена Тогда
			
			Попытка
				ИсточникЗадачи.ПолучитьОбъект().ПриВыполненииЗадачи(ЭтотОбъект.Ссылка);
			Исключение
				Сообщение = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				Отладка.Ошибка(Сообщение);
			КонецПопытки;
			
			Общий.ЗаписатьВЖурналКонтроля(
				0,
				Источник, 
				Перечисления.ЗаголовкиЖурналаКонтроля.ВыполненаЗадача, 
				"Пользователь '" + Строка(СловарьСервер.ТекущийПользователь()) + "' выполнил задачу """ 
					+ Строка(ЭтотОбъект.ТипЗадачи.Наименование) + """",
				ЭтотОбъект.Ссылка,
				Ложь
			);
			
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда 
		Возврат; 
	КонецЕсли;
	
	Если Исполнитель = Неопределено Тогда
		ВызватьИсключение "Задача ответственному. Не указан исполнитель или ошибочный тип исполнителя.";
	КонецЕсли;
		
	ЭтотОбъект.СтатусЗадачиИзменился = (ЭтотОбъект.Выполнена <> ЭтотОбъект.Ссылка.Выполнена) ИЛИ Общий.ЭтоНовыйОбъект(ЭтотОбъект);
КонецПроцедуры

Процедура ПередУдалением(Отказ)
    
    Если ЗначениеЗаполнено(ЭтотОбъект.ИзменяемыеПараметрыЗадачи) Тогда
		ОписаниеЗадачи = ЭтотОбъект.ИзменяемыеПараметрыЗадачи.ПолучитьОбъект();
		ОписаниеЗадачи.Удалить();
	КонецЕсли;
	
	//проверка, что нет записи в регистре ЖурналКонтроля
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЖурналКонтроля.Период,
	|	ЖурналКонтроля.КонтрольнаяПроцедура,
	|	ЖурналКонтроля.НомерСообщения,
	|	ЖурналКонтроля.ЗаписьБизнесПроцесса,
	|	ЖурналКонтроля.Описание,
	|	ЖурналКонтроля.Результат,
	|	ЖурналКонтроля.ЗадачаСсылка
	|ИЗ
	|	РегистрСведений.ЖурналКонтроля КАК ЖурналКонтроля
	|ГДЕ
	|	ЖурналКонтроля.ЗадачаСсылка = &Задача";
	
	Запрос.УстановитьПараметр("Задача", ЭтотОбъект.Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		МенеджерЗаписи = РегистрыСведений.ЖурналКонтроля.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, ВыборкаДетальныеЗаписи);
		Попытка
			МенеджерЗаписи.Прочитать();
			Если МенеджерЗаписи.Выбран() Тогда 
				МенеджерЗаписи.Удалить();
			КонецЕсли;
		Исключение
			Инфо = ИнформацияОбОшибке();
			Комментарий =
			"Описание = '" +Инфо.Описание + "', " +
			"ИмяМодуля = '" + Инфо.ИмяМодуля + "', " +
			"НомерСтроки = '" + Инфо.НомерСтроки + "', " +
			"ИсходнаяСтрока = '" + Инфо.ИсходнаяСтрока + "', " +
			"Описание ошибки = '" + ОписаниеОшибки() + "'.";
			
			ЗаписьЖурналаРегистрации(
			"Процедура Задача.ЗадачаОтветственному.МодульОбъекта.ПередУдалением(Отказ)",
			УровеньЖурналаРегистрации.Ошибка,
			Метаданные.Задачи.ЗадачаОтветственному,
			,
			Комментарий);;
		КонецПопытки;		
	КонецЦикла

КонецПроцедуры

#КонецЕсли
