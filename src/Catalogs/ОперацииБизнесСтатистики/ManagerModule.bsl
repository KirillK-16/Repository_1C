#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Функция СоздатьЭлементПоНаименованиюПолное(НаименованиеПолное, ХешСумма = Неопределено) Экспорт
	Если ХешСумма = Неопределено Тогда
		ХешированиеДанных = Новый ХешированиеДанных(ХешФункция.MD5);
		ХешированиеДанных.Добавить(НаименованиеПолное);
		ХешСумма = СтрЗаменить(Строка(ХешированиеДанных.ХешСумма), " ", "");
	КонецЕсли;
	
	ПозицияНедопустимогоСимвола = НайтиНедопустимыеСимволыXML(НаименованиеПолное);
	Если ПозицияНедопустимогоСимвола > 0 Тогда
		ВызватьИсключение "Обнаружен недопустимый символ XML, позиция: " + Формат(ПозицияНедопустимогоСимвола) + ", хэш: " + Строка(ХешСумма);
	КонецЕсли;
	
	Результат = РезультатЗапросаПоХешСумма(ХешСумма);
	Если Результат.Пустой() Тогда
		Попытка
			БлокировкаДанных = Новый БлокировкаДанных;
			ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.ОбъектыБлокировок");
			ЭлементБлокировки.УстановитьЗначение("Объект", "Справочник.ОперацииБизнесСтатистики." + НаименованиеПолное);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			БлокировкаДанных.Заблокировать();
		Исключение
			ВызватьИсключение "Центр мониторинга. Ожидаемая управляемая блокировка при создании элемента справочника 'ОперацииБизнесСтатистики'";
		КонецПопытки;
		
		Результат = РезультатЗапросаПоХешСумма(ХешСумма);
		Если Результат.Пустой() Тогда
			НаименованияМассив = СтрРазделить(НаименованиеПолное, ".", Истина);
			
			ТаблицаОбъектов = Новый ТаблицаЗначений;
			ТаблицаОбъектов.Колонки.Добавить("Наименование", Новый ОписаниеТипов("Строка", Новый КвалификаторыСтроки(150)));
			ТаблицаОбъектов.Колонки.Добавить("НаименованиеПолное", Новый ОписаниеТипов("Строка", Новый КвалификаторыСтроки(1000)));
			ТаблицаОбъектов.Колонки.Добавить("ХешСумма", Новый ОписаниеТипов("Строка", Новый КвалификаторыСтроки(32)));
			
			ХешированиеДанных = Новый ХешированиеДанных(ХешФункция.MD5);
			Для ТекИндекс = 0 По НаименованияМассив.ВГраница() Цикл
				ХешированиеДанных.Добавить(НаименованияМассив[ТекИндекс]);
				ХешСумма = СтрЗаменить(Строка(ХешированиеДанных.ХешСумма), " ", "");
				
				НоваяСтрока = ТаблицаОбъектов.Добавить();
				НоваяСтрока.Наименование = НаименованияМассив[ТекИндекс];
				НоваяСтрока.НаименованиеПолное = ?(ТекИндекс = 0, НаименованияМассив[ТекИндекс], ТаблицаОбъектов[ТекИндекс - 1].НаименованиеПолное + "." + НаименованияМассив[ТекИндекс]);
				НоваяСтрока.ХешСумма = ХешСумма;
				
				ХешированиеДанных.Добавить(".");
			КонецЦикла;
			
			Запрос = Новый Запрос;
			Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
			Запрос.Текст = "
			|ВЫБРАТЬ
			|	ТаблицаОбъектов.Наименование КАК НаименованиеПакет,
			|	ТаблицаОбъектов.НаименованиеПолное КАК НаименованиеПолноеПакет,
			|	ТаблицаОбъектов.ХешСумма КАК ХешСумма
			|ПОМЕСТИТЬ
			|	ТаблицаОбъектов
			|ИЗ
			|	&ТаблицаОбъектов КАК ТаблицаОбъектов
			|;
			|ВЫБРАТЬ
			|	ТаблицаОбъектов.НаименованиеПакет КАК НаименованиеПакет,
			|   ТаблицаОбъектов.НаименованиеПолноеПакет КАК НаименованиеПолноеПакет,
			|	ТаблицаОбъектов.ХешСумма КАК ХешСумма,
			|	СпрОперацииБизнесСтатистики.Ссылка КАК СпрОперацииБизнесСтатистикиСсылка
			|ИЗ
			|	ТаблицаОбъектов
			|ЛЕВОЕ СОЕДИНЕНИЕ
			|	Справочник.ОперацииБизнесСтатистики КАК СпрОперацииБизнесСтатистики
			|ПО
			|	СпрОперацииБизнесСтатистики.ХешПолноеНаименование = ТаблицаОбъектов.ХешСумма
			|";
			Запрос.УстановитьПараметр("ТаблицаОбъектов", ТаблицаОбъектов);
			
			ТаблицаОбъектов = Запрос.Выполнить().Выгрузить();
			
			МаксимальныйИндекс = ТаблицаОбъектов.Количество() - 1;
			Для ТекИндекс = 0 По МаксимальныйИндекс Цикл
				ТекОбъект = ТаблицаОбъектов[ТекИндекс];
				Если НЕ ЗначениеЗаполнено(ТекОбъект.СпрОперацииБизнесСтатистикиСсылка) Тогда
					ТекОбъект.СпрОперацииБизнесСтатистикиСсылка =
					СоздатьЭлементПоНаименованию(
					ТекОбъект.НаименованиеПакет,
					ТекОбъект.НаименованиеПолноеПакет,
					ТекОбъект.ХешСумма,
					?(ТекИндекс = 0, Неопределено, ТаблицаОбъектов[ТекИндекс - 1].СпрОперацииБизнесСтатистикиСсылка));
				КонецЕсли;
			КонецЦикла;
			
			РезультатВыполнения = ТаблицаОбъектов[МаксимальныйИндекс].СпрОперацииБизнесСтатистикиСсылка;
		Иначе
			Выборка = Результат.Выбрать();
			Выборка.Следующий();
			РезультатВыполнения = Выборка.Ссылка;
		КонецЕсли;
	Иначе
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		РезультатВыполнения = Выборка.Ссылка;
	КонецЕсли;
	
	Возврат РезультатВыполнения;
КонецФункции

Функция СоздатьЭлементПоНаименованию(Наименование, НаименованиеПолное, ХешСумма, Родитель)
	Результат = РезультатЗапросаПоХешСумма(ХешСумма);
	Если Результат.Пустой() Тогда
		Попытка
			БлокировкаДанных = Новый БлокировкаДанных;
			ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.ОбъектыБлокировок");
			ЭлементБлокировки.УстановитьЗначение("Объект", "Справочник.ОперацииБизнесСтатистики." + НаименованиеПолное);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			БлокировкаДанных.Заблокировать();
		Исключение
			ВызватьИсключение "Центр мониторинга. Ожидаемая управляемая блокировка при создании элемента справочника 'ОперацииБизнесСтатистики'";
		КонецПопытки;
		
		Результат = РезультатЗапросаПоХешСумма(ХешСумма);
		Если Результат.Пустой() Тогда
			НовыйЭлемент = Справочники.ОперацииБизнесСтатистики.СоздатьЭлемент();
			НовыйЭлемент.Наименование = Наименование;
			НовыйЭлемент.НаименованиеПолное = НаименованиеПолное;
			НовыйЭлемент.ХешПолноеНаименование = ХешСумма;
			НовыйЭлемент.Родитель = Родитель;
			НовыйЭлемент.Записать();
			
			РезультатВыполнения = НовыйЭлемент.Ссылка;
		Иначе
			Выборка = Результат.Выбрать();
			Выборка.Следующий();
			РезультатВыполнения = Выборка.Ссылка;
		КонецЕсли;
	Иначе
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		РезультатВыполнения = Выборка.Ссылка;
	КонецЕсли;
	
	Возврат РезультатВыполнения;
	
КонецФункции

Функция РезультатЗапросаПоХешСумма(ХешСумма)
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	Ссылка
	|ИЗ
	|	Справочник.ОперацииБизнесСтатистики
	|ГДЕ
	|	ХешПолноеНаименование = &ХешСумма
	|";
	Запрос.УстановитьПараметр("ХешСумма", ХешСумма);
	Результат = Запрос.Выполнить();
	
	Возврат Результат;
КонецФункции

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	НаименованиеПолное
	|ИЗ
	|	Справочник.ОперацииБизнесСтатистики
	|ГДЕ
	|	Ссылка = &Ссылка
	|";
	Запрос.УстановитьПараметр("Ссылка", Данные.Ссылка);
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		
		Представление = Выборка.НаименованиеПолное;
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецЕсли
