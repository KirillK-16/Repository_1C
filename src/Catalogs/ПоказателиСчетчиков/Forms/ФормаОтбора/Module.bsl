
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИнициализироватьОтбор();
	ОбщийКлиентСервер.КопироватьОтбор(Компоновщик.ФиксированныеНастройки.Отбор, ЭтотОбъект.Параметры.Отбор);
	
КонецПроцедуры

&НаСервере
Процедура ИнициализироватьОтбор()
	
	СКД = Новый СхемаКомпоновкиДанных();
	ИсточникСКД = СКД.ИсточникиДанных.Добавить();
	ИсточникСКД.Имя = "ИсточникДанных1";
	ИсточникСКД.ТипИсточникаДанных = "local";
	НаборДанных = СКД.НаборыДанных.Добавить(Тип("НаборДанныхЗапросСхемыКомпоновкиДанных"));
	НаборДанных.Запрос = "
	|ВЫБРАТЬ
	|	ПолныйКодДляПоиска КАК Наименование,
	|	Ссылка
	|ИЗ
	|	Справочник.ГруппыСчетчиков
	|"; 
	НаборДанных.ИсточникДанных = ИсточникСКД.Имя;
	
	URLСхемы = ПоместитьВоВременноеХранилище(СКД, Новый УникальныйИдентификатор());
	Компоновщик.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(URLСхемы));
	
КонецПроцедуры

&НаКлиенте
Процедура Выбрать(Команда)
	
	ЭтотОбъект.ОповеститьОВыборе(Компоновщик.ФиксированныеНастройки.Отбор);
	
КонецПроцедуры

