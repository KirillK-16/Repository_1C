#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
    
#Область ПрограммныйИнтерфейс

Функция ПолучитьДанные
	(
		Знач ОпорнаяДата,
		Знач НачальноеСмещение,
		Знач ЧислоТочек,
		Знач Шаг, 
		Статистика = Неопределено, 
		ВычислитьАгрегатныеЗначения = Истина, 
		ВычислитьГоризонтАктуальности = Ложь,
		РежимСдвига = Ложь
	) Экспорт
	
	ДатаНачала = ОпорнаяДата + НачальноеСмещение * Шаг;
	ДатаОкончания = ОпорнаяДата + (НачальноеСмещение + ЧислоТочек - 1) * Шаг;
		
	Данные = СтатистикаГруппыСчетчиков.ПолучитьДанные(
			ЭтотОбъект.Ссылка,
			ОпорнаяДата,
			НачальноеСмещение,
			ЧислоТочек,
			Шаг,
			Неопределено,
			ЭтотОбъект.Аналитика,
			ЭтотОбъект.Периодичность
		);
		
	Если Статистика <> Неопределено Тогда
		Если ВычислитьАгрегатныеЗначения Тогда
			Статистика = МониторингКлиентСервер.ВычислитьСредниеЗначения(Данные);
			Статистика.Вставить("ПоТочкам");
			// вычислим параметр "Текущее"
			Попытка
				ТекущееЗначение = Данные[Данные.Количество() - 1];
			Исключение
				Инфо = ИнформацияОбОшибке();
				Комментарий =
					"Описание = '" +Инфо.Описание + "', " +
					"ИмяМодуля = '" + Инфо.ИмяМодуля + "', " +
					"НомерСтроки = '" + Инфо.НомерСтроки + "', " +
					"ИсходнаяСтрока = '" + Инфо.ИсходнаяСтрока + "'.";
			
				ЗаписьЖурналаРегистрации(
					"Функция ПолучитьДанные(...)",
					УровеньЖурналаРегистрации.Ошибка,
					Метаданные.Справочники.ПоказателиСчетчиков.МодульОбъекта,
					,
					Комментарий);
				
				ТекущееЗначение = 0;
			КонецПопытки;
			Статистика.Вставить("Текущее", ТекущееЗначение);
			
			Всего = МониторингКлиентСервер.ВычислитьВсего(Данные);
			
			Сообщить(ЭтотОбъект.Ссылка.Описание + " = " + Всего + " [" + ЭтотОбъект.Ссылка.Аналитика + "]");
			
			Статистика.Вставить("Всего", Всего);
			Статистика.Вставить("Горизонт", Неопределено);
		Иначе
			Статистика.Вставить("Горизонт", Неопределено);
		КонецЕсли;
	КонецЕсли;
	
	Возврат Данные;	
КонецФункции

Функция ПолучитьДанныеОбнаруженияИнцидентов(ОпорнаяДата, Смещение, АгрегирующаяФункция, ФорматнаяСтрокаЗначения) Экспорт
	
	Данные = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	{АгрегирующаяФункция}({Ресурс}) КАК Значение
	|ИЗ
	|	Справочник.ПоказателиСчетчиков.Счетчики КАК Счетчики
	|ВНУТРЕННЕЕ СОЕДИНЕНИЕ
	|	РегистрСведений.СтатистикаНеделя КАК Данные
	|ПО
	|	Данные.Период МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И Данные.Событие = Счетчики.Счетчик
	|ГДЕ
	|	Счетчики.Ссылка = &Ссылка
	|";
	
	Запрос.УстановитьПараметр("Ссылка", ЭтотОбъект.Ссылка);
	Запрос.УстановитьПараметр("ДатаНачала", НачалоМинуты(ОпорнаяДата) - 60 - Смещение);
	Запрос.УстановитьПараметр("ДатаОкончания", ОпорнаяДата);
	
	Если АгрегирующаяФункция = Перечисления.ФункцииОповещений.Минимум Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "{АгрегирующаяФункция}", "МИНИМУМ");
	ИначеЕсли АгрегирующаяФункция = Перечисления.ФункцииОповещений.Среднее Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "{АгрегирующаяФункция}", "СРЕДНЕЕ");
	ИначеЕсли АгрегирующаяФункция = Перечисления.ФункцииОповещений.Максимум Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "{АгрегирующаяФункция}", "МАКСИМУМ");
	ИначеЕсли АгрегирующаяФункция = Перечисления.ФункцииОповещений.Сумма Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "{АгрегирующаяФункция}", "СУММА");
	КонецЕсли;
    
    Если ЭтотОбъект.Аналитика = "Среднее значение" ИЛИ ЭтотОбъект.Аналитика = "СреднееЗначение" Тогда
        Запрос.Текст = СтрЗаменить(Запрос.Текст, "{Ресурс}", "СреднееЗначение");
    ИначеЕсли ЭтотОбъект.Аналитика = "Число срабатываний" ИЛИ ЭтотОбъект.Аналитика = "ЧислоСрабатываний" Тогда
        Запрос.Текст = СтрЗаменить(Запрос.Текст, "{Ресурс}", "ЧислоСрабатываний");
    ИначеЕсли ЭтотОбъект.Аналитика = "Сумма" Тогда
        Запрос.Текст = СтрЗаменить(Запрос.Текст, "{Ресурс}", "СреднееЗначение * ЧислоСрабатываний");
    КонецЕсли;
    
    Результат = Запрос.Выполнить();
        
	Выборка = Результат.Выбрать();
	Выборка.Следующий();
	
	Если Выборка.Значение <> Null Тогда
		Данные = Новый Массив;
		Данные.Добавить(Выборка.Значение);
		Если ЗначениеЗаполнено(ФорматнаяСтрокаЗначения) Тогда
			ИндексНачала = СтрНайти(ФорматнаяСтрокаЗначения, "[");
			ИндексОкончания = СтрНайти(ФорматнаяСтрокаЗначения, "]");
	        Если ИндексНачала > 0 И ИндексОкончания > 0 Тогда
				ФорматнаяСтрокаЗначенияБуфер = Сред(ФорматнаяСтрокаЗначения, ИндексНачала + 1, ИндексОкончания - ИндексНачала - 1);
			Иначе
				ФорматнаяСтрокаЗначенияБуфер = ФорматнаяСтрокаЗначения;
			КонецЕсли;
			
			ЗначениеСообщить = СтрЗаменить(ФорматнаяСтрокаЗначения, "[" + ФорматнаяСтрокаЗначенияБуфер + "]", Формат(Выборка.Значение, ФорматнаяСтрокаЗначенияБуфер));
		Иначе
			ЗначениеСообщить = Выборка.Значение;
		КонецЕсли;
		Сообщить(ЭтотОбъект.Ссылка.Описание + " = " + ЗначениеСообщить + " [" + АгрегирующаяФункция + "]");
	Иначе
		Сообщить(ЭтотОбъект.Ссылка.Описание + " - нет данных в регистре сведений 'СтатистикаНеделя'!");
	КонецЕсли;
		
	Возврат Данные;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ИдентификаторТипаПоказателя() Экспорт
	Если ЗначениеЗаполнено(ЭтотОбъект.Метаданные().РасширенноеПредставлениеОбъекта) Тогда
		Возврат ЭтотОбъект.Метаданные().РасширенноеПредставлениеОбъекта;
	ИначеЕсли ЗначениеЗаполнено(ЭтотОбъект.Метаданные().ПредставлениеОбъекта) Тогда
		Возврат ЭтотОбъект.Метаданные().ПредставлениеОбъекта;
	Иначе
		Возврат ЭтотОбъект.Метаданные().Синоним;
	КонецЕсли;
КонецФункции

//Данная функция должна возвращать уникальное значение
//по данному уникальному значению выбираются данные в обработке
//мониторинга и сохраняются в кэше
Функция ИдентификаторВариантаПоказателя() Экспорт
	ФильтрПоОбъектамКонтроля = Новый Массив;
	
	ИД = МониторингСервер.ОбщаяЧастьИдентификатораВариантаПоказателя(
		ЭтотОбъект,
		ФильтрПоОбъектамКонтроля
	);

	РазделительПолей = "__";
	ИД = ИД + РазделительПолей + ГУИД;
	
	Возврат ИД;
КонецФункции

// Заполняет параметры по умолчанию отображения показателя на графике
// 
// Параметры:
//  ПараметрОповещения - Структура - см.МониторингСервер.ПараметрОповещенияПоказательЗаписан()
//
Процедура ЗаполнитьПараметрыОтображенияПоУмолчанию(Знач ПараметрОповещения) Экспорт
	
	ГСЧ = Новый ГенераторСлучайныхЧисел();
	
	ПараметрОповещения.Цвет = Новый Цвет(ГСЧ.СлучайноеЧисло(0,255), ГСЧ.СлучайноеЧисло(0,255), ГСЧ.СлучайноеЧисло(0,255));
	ПараметрОповещения.АвтоМасштаб = Истина;
	ПараметрОповещения.Масштаб = 1;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ПустаяСтрока(Описание) Тогда
		Описание = "Счетчик";
	КонецЕсли;
	
КонецПроцедуры

Функция ВсегоЗаПериод(Знач ДатаНачала, Знач ДатаОкончания) Экспорт
КонецФункции

Процедура ПриКопировании(ОбъектКопирования)
	ЭтотОбъект.ГУИД = Новый УникальныйИдентификатор();
КонецПроцедуры

// Расчитывает данные показателя по периодам
//
//	Параметры:
// 		МенеджерВременныхТаблиц	- МенеджерВременныхТаблиц. Временные таблицы с периодами и показателями.
// 		Детализация				- ТипДополненияПериодаКомпоновкиДанных. Период детализации.
//
//	Возвращаемое значение:
//		ТаблицаДанных. ТаблицаЗначений. Данные показателя.
//
Функция РасчитатьПоказатель(МенеджерВременныхТаблиц, Детализация = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("ПоказательМониторинга", ЭтотОбъект.Владелец);
	Запрос.УстановитьПараметр("Показатель", ЭтотОбъект.Ссылка);
	Запрос.УстановитьПараметр("Аналитика", ЭтотОбъект.Аналитика);
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ТаблицаПодготовительная.ДатаТочки КАК ДатаТочки,
	|	НачалоПериода(ОсновнаяТаблицаЗамеров.Период, Час) КАК Период,
	|	НачалоПериода(ОсновнаяТаблицаЗамеров.Период, День) КАК ПериодДень,
	|	СУММА(ОсновнаяТаблицаЗамеров.ЧислоСрабатываний) КАК ЧислоСрабатываний,
	|	СУММА(ОсновнаяТаблицаЗамеров.Сумма) КАК Сумма
	|
	|ПОМЕСТИТЬ
	|	СтатистикаНеделя
	|
	|ИЗ
	|	РегистрСведений.СтатистикаНеделя КАК ОсновнаяТаблицаЗамеров
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПоказателиСчетчиков.Счетчики КАК ПоказателиСчетчиковСчетчики
	|	ПО ОсновнаяТаблицаЗамеров.Событие = ПоказателиСчетчиковСчетчики.Счетчик
	|	И ПоказателиСчетчиковСчетчики.Ссылка = &Показатель
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаПодготовительная КАК ТаблицаПодготовительная
	|	ПО ТаблицаПодготовительная.ПоказательМониторинга = &ПоказательМониторинга
	|	И ОсновнаяТаблицаЗамеров.Период > НачалоПериода(ТаблицаПодготовительная.ДатаТочкиДанные, Минута)
	|	И ОсновнаяТаблицаЗамеров.Период <= НачалоПериода(ТаблицаПодготовительная.ДатаТочкиДанныеСледующая, Минута)
	|	
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаПодготовительная.ДатаТочки,
	|	НачалоПериода(ОсновнаяТаблицаЗамеров.Период, Час),
	|	НачалоПериода(ОсновнаяТаблицаЗамеров.Период, День)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Период
	|
	|;
	|
	|ВЫБРАТЬ
	|	ТаблицаПодготовительная.ДатаТочки КАК ДатаТочки,
	|	НачалоПериода(ОсновнаяТаблицаЗамеров.Период, Час) КАК Период,
	|	НачалоПериода(ОсновнаяТаблицаЗамеров.Период, День) КАК ПериодДень,
	|	СУММА(ОсновнаяТаблицаЗамеров.ЧислоСрабатываний) КАК ЧислоСрабатываний,
	|	СУММА(ОсновнаяТаблицаЗамеров.Сумма) КАК Сумма
	|
	|ПОМЕСТИТЬ
	|	СтатистикаМесяц
	|
	|ИЗ
	|	РегистрСведений.СтатистикаМесяц КАК ОсновнаяТаблицаЗамеров // ДАННЫЕ ПО ЧАСАМ
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПоказателиСчетчиков.Счетчики КАК ПоказателиСчетчиковСчетчики
	|	ПО ОсновнаяТаблицаЗамеров.Событие = ПоказателиСчетчиковСчетчики.Счетчик
	|	И ПоказателиСчетчиковСчетчики.Ссылка = &Показатель
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаПодготовительная КАК ТаблицаПодготовительная
	|	ПО ТаблицаПодготовительная.ПоказательМониторинга = &ПоказательМониторинга
	|	И ОсновнаяТаблицаЗамеров.Период > НачалоПериода(ТаблицаПодготовительная.ДатаТочкиДанные, Минута)
	|	И ОсновнаяТаблицаЗамеров.Период <= НачалоПериода(ТаблицаПодготовительная.ДатаТочкиДанныеСледующая, Минута)
	|	
	|ГДЕ
	|	НЕ ОсновнаяТаблицаЗамеров.Период В (ВЫБРАТЬ СтатистикаНеделя.Период ИЗ СтатистикаНеделя КАК СтатистикаНеделя)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаПодготовительная.ДатаТочки,
	|	НачалоПериода(ОсновнаяТаблицаЗамеров.Период, Час),
	|	НачалоПериода(ОсновнаяТаблицаЗамеров.Период, День)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ПериодДень
	|
	|;
	|
	|ВЫБРАТЬ
	|	ВнутренняяТаблица.ДатаТочки КАК ДатаТочки,
	|	СУММА(ВнутренняяТаблица.Количество) КАК Количество,
	|	СУММА(ВнутренняяТаблица.Сумма) КАК Сумма,
	|	МАКСИМУМ(ВнутренняяТаблица.Сумма) КАК Максимальное,
	|	МИНИМУМ(ВнутренняяТаблица.Сумма) КАК Минимальное
	|
	|ПОМЕСТИТЬ
	|	ТаблицаДанных
	|
	|ИЗ
	|	(
	|
	|	ВЫБРАТЬ
	|		ТаблицаПодготовительная.ДатаТочки КАК ДатаТочки,
	|		ОсновнаяТаблицаЗамеров.ЧислоСрабатываний КАК Количество,
	|		ОсновнаяТаблицаЗамеров.Сумма КАК Сумма
	|
	|	ИЗ
	|		РегистрСведений.СтатистикаПолный КАК ОсновнаяТаблицаЗамеров // ДАННЫЕ ПО ДНЯМ
	|	
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПоказателиСчетчиков.Счетчики КАК ПоказателиСчетчиковСчетчики
	|		ПО ОсновнаяТаблицаЗамеров.Событие = ПоказателиСчетчиковСчетчики.Счетчик
	|		И ПоказателиСчетчиковСчетчики.Ссылка = &Показатель
	|	
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаПодготовительная КАК ТаблицаПодготовительная
	|		ПО ТаблицаПодготовительная.ПоказательМониторинга = &ПоказательМониторинга
	|		И &УсловиеНаПериод
	|		
	|	ГДЕ
	|		НЕ ОсновнаяТаблицаЗамеров.Период В (
	|											ВЫБРАТЬ РАЗЛИЧНЫЕ СтатистикаНеделя.ПериодДень ИЗ СтатистикаНеделя КАК СтатистикаНеделя
	|											ОБЪЕДИНИТЬ ВСЕ
	|											ВЫБРАТЬ РАЗЛИЧНЫЕ СтатистикаМесяц.ПериодДень ИЗ СтатистикаМесяц КАК СтатистикаМесяц
	|											)
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ОсновнаяТаблицаЗамеров.ДатаТочки КАК ДатаТочки,
	|		ОсновнаяТаблицаЗамеров.ЧислоСрабатываний КАК Количество,
	|		ОсновнаяТаблицаЗамеров.Сумма КАК Сумма
	|	ИЗ
	|		СтатистикаМесяц КАК ОсновнаяТаблицаЗамеров
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ОсновнаяТаблицаЗамеров.ДатаТочки КАК ДатаТочки,
	|		ОсновнаяТаблицаЗамеров.ЧислоСрабатываний КАК Количество,
	|		ОсновнаяТаблицаЗамеров.Сумма КАК Сумма
	|	ИЗ
	|		СтатистикаНеделя КАК ОсновнаяТаблицаЗамеров
	|	
	|	) КАК ВнутренняяТаблица
	|
	|СГРУППИРОВАТЬ ПО
	|	ВнутренняяТаблица.ДатаТочки
	|
	|;
	|
	|ВЫБРАТЬ
	|	ТаблицаПодготовительная.ДатаТочки КАК ДатаТочки,
	|	ВЫБОР КОГДА СУММА(ТаблицаДанных.Количество) = 0 ТОГДА 0 ИНАЧЕ СУММА(ТаблицаДанных.Сумма) / СУММА(ТаблицаДанных.Количество) КОНЕЦ КАК Максимальное,
	|	ВЫБОР КОГДА СУММА(ТаблицаДанных.Количество) = 0 ТОГДА 0 ИНАЧЕ СУММА(ТаблицаДанных.Сумма) / СУММА(ТаблицаДанных.Количество) КОНЕЦ КАК Минимальное,
	|	ВЫБОР 
	|		КОГДА &Аналитика = ""ЧислоСрабатываний"" ТОГДА
	|			СУММА(ТаблицаДанных.Количество)
	|		КОГДА &Аналитика = ""Сумма"" ТОГДА
	|			СУММА(ТаблицаДанных.Сумма)
	|		ИНАЧЕ
	|			ВЫБОР КОГДА СУММА(ТаблицаДанных.Количество) = 0 ТОГДА 0 ИНАЧЕ СУММА(ТаблицаДанных.Сумма) / СУММА(ТаблицаДанных.Количество) 
	|		КОНЕЦ
	|	КОНЕЦ КАК Значение
	|
	|ИЗ
	|	ТаблицаПодготовительная КАК ТаблицаПодготовительная
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаДанных КАК ТаблицаДанных
	|	ПО ТаблицаДанных.ДатаТочки = ТаблицаПодготовительная.ДатаТочки
	|	И ТаблицаПодготовительная.ПоказательМониторинга = &ПоказательМониторинга
	|
	|ГДЕ
	|	ТаблицаПодготовительная.ПоказательМониторинга = &ПоказательМониторинга
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаПодготовительная.ДатаТочки
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаТочки
	|";
	
	Если Детализация = ТипДополненияПериодаКомпоновкиДанных.Минута ИЛИ Детализация = ТипДополненияПериодаКомпоновкиДанных.Минута Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеНаПериод", "ОсновнаяТаблицаЗамеров.Период = НачалоПериода(ТаблицаПодготовительная.ДатаТочкиДанные, День)");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеНаПериод", "ОсновнаяТаблицаЗамеров.Период > НачалоПериода(ТаблицаПодготовительная.ДатаТочкиДанные, День) И ОсновнаяТаблицаЗамеров.Период <= НачалоПериода(ТаблицаПодготовительная.ДатаТочкиДанныеСледующая, День)");
	КонецЕсли;
	
	ТаблицаДетальныхДанных = Запрос.Выполнить().Выгрузить();
	
	МассивПолей = Новый Массив();
	МассивПолей.Добавить("Значение");
	
	РасчетИтоговПоказателей.ЗаполнитьПустыеДанныеПредыдущимиЗначениями(ТаблицаДетальныхДанных, МассивПолей, Null);
	
	Запрос.Текст = "
	|УНИЧТОЖИТЬ СтатистикаНеделя;
	|УНИЧТОЖИТЬ СтатистикаМесяц;
	|УНИЧТОЖИТЬ ТаблицаДанных
	|";
	
	Запрос.Выполнить();
	
	Если ЭтотОбъект.Владелец.ПоказыватьТренд Тогда
		РасчетИтоговПоказателей.СгладитьДанные(ТаблицаДетальныхДанных, ЭтотОбъект.Владелец.ТипСглаживания, ЭтотОбъект.Владелец.КоличествоУсредняемыхЗначений);
	КонецЕсли;
	
	Возврат ТаблицаДетальныхДанных;
	
КонецФункции

#КонецОбласти

#КонецЕсли
