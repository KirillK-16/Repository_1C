
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
    
#Область ПрограммныйИнтерфейс

Функция СоздатьЭлементПоНаименованию(Наименование) Экспорт
    
    РезультатСсылка = НайтиСсылкуПоНаименованию(Наименование);
        
    Если РезультатСсылка.Пустая() Тогда
        Попытка
            БлокировкаДанных = Новый БлокировкаДанных;
            ЭлементБлокировки = БлокировкаДанных.Добавить("Справочник.ПроцессыДамповЦентраМониторинга");
            ЭлементБлокировки.УстановитьЗначение("Наименование", Наименование);
            ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
            БлокировкаДанных.Заблокировать();
		Исключение
			ОписаниеОшибки = ОписаниеОшибки();
			ВызватьИсключение "Центр мониторинга. Ожидаемая управляемая блокировка при создании элемента справочника 'ПроцессыДамповЦентраМониторинга'";
        КонецПопытки;	
			
		РезультатСсылка = НайтиСсылкуПоНаименованию(Наименование);
		Если РезультатСсылка.Пустая() Тогда
			НовыйЭлемент = Справочники.ПроцессыДамповЦентраМониторинга.СоздатьЭлемент();
			НовыйЭлемент.Наименование = Наименование;
			НовыйЭлемент.ВидПроцесса = Перечисления.ВидыПроцессовДамповЦентраМониторинга.Неизвестный;
			НовыйЭлемент.Записать();
			РезультатСсылка = НовыйЭлемент.Ссылка;
		КонецЕсли;
		        
    КонецЕсли;
    
    Возврат РезультатСсылка;
    
КонецФункции

Функция НайтиСсылкуПоНаименованию(Наименование) Экспорт
    
    РезультатСсылка = Справочники.ПроцессыДамповЦентраМониторинга.ПустаяСсылка();
        
    Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	Ссылка
	|ИЗ
	|	Справочник.ПроцессыДамповЦентраМониторинга
	|ГДЕ
	|	Наименование = &Наименование
	|";
	Запрос.УстановитьПараметр("Наименование", Наименование);
    
    Результат = Запрос.Выполнить();
    Если НЕ Результат.Пустой() Тогда
        Выборка = Результат.Выбрать();
        Выборка.Следующий();
        РезультатСсылка = Выборка.Ссылка;
    КонецЕсли;
    
	Возврат РезультатСсылка;
    
КонецФункции

#КонецОбласти

#КонецЕсли
