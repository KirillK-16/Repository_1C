
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		УстановитьПривилегированныйРежим(Истина);
		ДанныеХранилища = РегистрыСведений.БезопасноеХранилище.ПолучитьДанные(Объект.Ссылка);
		УстановитьПривилегированныйРежим(Ложь);
		
		Если ДанныеХранилища <> Неопределено Тогда
			ЭтотОбъект.Логин = ДанныеХранилища.Логин;
			ЭтотОбъект.Пароль = ДанныеХранилища.Пароль;
        КонецЕсли;
    КонецЕсли;
    
    ЗаполнитьПараметрыИнцидентов();
    
    Если Объект.ПлощадкаЭксплуатации = Справочники.ПлощадкиЭксплуатации.Корзина() Тогда
        
        ЭтотОбъект.ТолькоПросмотр = Истина;
        
    КонецЕсли;
    
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	УстановитьПривилегированныйРежим(Истина);
	ДанныеХранилища = Новый Структура("Логин, Пароль");
	ДанныеХранилища.Логин = ПараметрыЗаписи.Логин;
	ДанныеХранилища.Пароль = ПараметрыЗаписи.Пароль;
	РегистрыСведений.БезопасноеХранилище.ЗаписатьДанные(ТекущийОбъект.Ссылка, ДанныеХранилища);
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	ПараметрыЗаписи.Вставить("Логин", ЭтотОбъект.Логин);
	ПараметрыЗаписи.Вставить("Пароль", ЭтотОбъект.Пароль);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗагрузитьОтвет(Команда)
	Результат = ЗагрузитьОтветНаСервере(Объект.АдресРесурса, ЭтотОбъект.Логин, ЭтотОбъект.Пароль);
	Если Результат.КодСостояния = 200 Тогда
		Объект.ОтветДляСравнения = Результат.Тело;
	Иначе
		Сообщить("Ошибка подключения. Код ответа: " + Результат.КодСостояния);
	КонецЕсли;
КонецПроцедуры


#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура АдресРесурсаПриИзменении(Элемент)
    
    Если НЕ ЗначениеЗаполнено(Объект.Наименование) Тогда
        Объект.Наименование = Объект.АдресРесурса;
    КонецЕсли;
    
    Для Каждого ТекСтрока Из ПараметрыИнцидентов Цикл
        
        ТекСтрока.КодИнцидента = "Публикация/" + Объект.АдресРесурса;
        
    КонецЦикла;
        
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ЗагрузитьОтветНаСервере(АдресРесурса, Логин, Пароль)
	Результат = ПубликацииКонтрольДоступности.ВыполнитьHTTPЗапрос(АдресРесурса, Логин, Пароль);
	
	РезультатНаКлиента = Новый Структура("КодСостояния, Тело");
	РезультатНаКлиента.КодСостояния = Результат.КодСостояния;
	Если Результат.КодСостояния = 200 Тогда
		РезультатНаКлиента.Тело = Результат.ПолучитьТелоКакСтроку();
	КонецЕсли;
		
	Возврат РезультатНаКлиента;
КонецФункции

&НаСервере
Процедура ЗаполнитьПараметрыИнцидентов()
    
    НовСтрока = ПараметрыИнцидентов.Добавить();
    НовСтрока.ТипИнцидента = "Публикация";
    НовСтрока.КодИнцидента = "Публикация/" + Объект.АдресРесурса;
    
    НовСтрока = ПараметрыИнцидентов.Добавить();
    НовСтрока.ТипИнцидента = "НедоступностьНетДанных";
    НовСтрока.КодИнцидента = "Публикация/" + Объект.АдресРесурса;
    
КонецПроцедуры

#КонецОбласти
