#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПредопределеннныеПроцедуры

Процедура ПередЗаписью(Отказ)
    
    Если ЗначениеЗаполнено(ЭтотОбъект.Ссылка) Тогда
        УникальныйИдентификаторСтрока = Строка(ЭтотОбъект.Ссылка.УникальныйИдентификатор());
    Иначе
        ЭтотОбъект.УстановитьСсылкуНового(Справочники.Публикации.ПолучитьСсылку());
        УникальныйИдентификаторСтрока = Строка(ЭтотОбъект.ПолучитьСсылкуНового().УникальныйИдентификатор());
    КонецЕсли;
    
    // Тип инцидента "Публикация"
    ХешТипа = Справочники.ТипыИнцидентов.ХешТипИнцидента("Публикация");
    ТипИнцидента = Справочники.ТипыИнцидентов.СоздатьЭлементПоХешу(ХешТипа, "Публикация", Перечисления.УровниИнцидентов.Ошибка);
      
    // Инцидент
    ДанныеХеша = "Публикация/" + УникальныйИдентификаторСтрока;
    Хеш = Справочники.Инциденты.ХешИнцидента(ДанныеХеша);
    Инцидент = Справочники.Инциденты.СоздатьЭлементПоХешу(Хеш, "Публикация/" + Наименование);
    ИнцидентыСервер.СоздатьИнцидентыИсточника(ТипИнцидента, Инцидент); 
    
    // Тип инцидента "НедоступностьНетДанных"
    ХешТипа = Справочники.ТипыИнцидентов.ХешТипИнцидента("НедоступностьНетДанных");
    ТипИнцидента = Справочники.ТипыИнцидентов.СоздатьЭлементПоХешу(ХешТипа, "НедоступностьНетДанных", Перечисления.УровниИнцидентов.Предупреждение);
    
    // Инцидент
    ДанныеХеша = "НедоступностьНетДанных/" + УникальныйИдентификаторСтрока;
    Хеш = Справочники.Инциденты.ХешИнцидента(ДанныеХеша);
    Инцидент = Справочники.Инциденты.СоздатьЭлементПоХешу(Хеш, "НедоступностьНетДанных/" + Наименование);
    ИнцидентыСервер.СоздатьИнцидентыИсточника(ТипИнцидента, Инцидент);
   
    Если ДополнительныеСвойства.Свойство("НеПроверять") И ДополнительныеСвойства.НеПроверять Тогда
        Возврат;
    КонецЕсли;
    
    Корзина = Справочники.ПлощадкиЭксплуатации.Корзина();
    
    ЭтотОбъект.ДополнительныеСвойства.Вставить("СсылкаПлощадкаЭксплуатации", Ссылка.ПлощадкаЭксплуатации);
    ЭтотОбъект.ДополнительныеСвойства.Вставить("Корзина", Корзина);
    
    
    Если ЭтотОбъект.ПометкаУдаления И ЭтотОбъект.ПометкаУдаления <> Ссылка.ПометкаУдаления Тогда
        
        ЭтотОбъект.ПлощадкаДляВосстановления = ЭтотОбъект.ПлощадкаЭксплуатации;
        ЭтотОбъект.ПлощадкаЭксплуатации = Корзина;
        
    ИначеЕсли НЕ ЭтотОбъект.ПометкаУдаления И ЭтотОбъект.ПометкаУдаления <> Ссылка.ПометкаУдаления Тогда
        
        Если ЗначениеЗаполнено(ЭтотОбъект.ПлощадкаДляВосстановления) Тогда
            ЭтотОбъект.ПлощадкаЭксплуатации = ЭтотОбъект.ПлощадкаДляВосстановления;
            ЭтотОбъект.ПлощадкаДляВосстановления = Справочники.ПлощадкиЭксплуатации.ПустаяСсылка();
        КонецЕсли;
        
    КонецЕсли;
    
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
    
    Если ДополнительныеСвойства.Свойство("НеПроверять") И ДополнительныеСвойства.НеПроверять Тогда
        Возврат;
    КонецЕсли;
    
    СсылкаПлощадкаЭксплуатации = ЭтотОбъект.ДополнительныеСвойства.СсылкаПлощадкаЭксплуатации;
    Корзина = ЭтотОбъект.ДополнительныеСвойства.Корзина;
        
    // Это новый элемент или площадка эксплуатации не менялась
    Если НЕ ЗначениеЗаполнено(СсылкаПлощадкаЭксплуатации) ИЛИ СсылкаПлощадкаЭксплуатации = ЭтотОбъект.ПлощадкаЭксплуатации Тогда
        
        ДобавитьВПлощадку(ЭтотОбъект.ПлощадкаЭксплуатации);
                
    //  Изменилась площадка у существующего элемента          
    ИначеЕсли ЗначениеЗаполнено(СсылкаПлощадкаЭксплуатации) И СсылкаПлощадкаЭксплуатации <> ЭтотОбъект.ПлощадкаЭксплуатации Тогда
        
        ПеренестиВПлощадку(ЭтотОбъект.ПлощадкаЭксплуатации, СсылкаПлощадкаЭксплуатации);
            
    КонецЕсли;
    
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДобавитьВПлощадку(ПлощадкаДобавить)
    
    ТипГруппы = Перечисления.ТипЭлементаПлощадки.ГруппаПубликации;
    ТипЭлемента = Перечисления.ТипЭлементаПлощадки.Публикация;    
    ТипыПлощадки = Новый Структура("ТипГруппы, ТипЭлемента", ТипГруппы, ТипЭлемента);
        
    Справочники.ПлощадкиЭксплуатации.ДобавитьВПлощадку(ПлощадкаДобавить, Ссылка, ТипыПлощадки.ТипГруппы, ТипыПлощадки.ТипЭлемента);

КонецПроцедуры

Процедура ПеренестиВПлощадку(ПлощадкаЭксплуатацииВ, ПлощадкаЭксплуатацииИз)
    
    ТипГруппы = Перечисления.ТипЭлементаПлощадки.ГруппаПубликации;
    ТипЭлемента = Перечисления.ТипЭлементаПлощадки.Публикация;    
    ТипыПлощадки = Новый Структура("ТипГруппы, ТипЭлемента", ТипГруппы, ТипЭлемента);
    
    Справочники.ПлощадкиЭксплуатации.ПеренестиВНовуюПлощадку(ЭтотОбъект.Ссылка, ПлощадкаЭксплуатацииВ, ПлощадкаЭксплуатацииИз, ТипыПлощадки.ТипГруппы, ТипыПлощадки.ТипЭлемента);      
    
КонецПроцедуры

#КонецОбласти

#КонецЕсли