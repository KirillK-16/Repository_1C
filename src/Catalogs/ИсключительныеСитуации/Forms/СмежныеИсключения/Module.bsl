&НаКлиенте
Перем КодВозвратаУстановлен;

// При закрытии форма возвращает один из трех возможных результатов
// Неопределено - в базе отсутствуют смежные исключения, форма не была показана, либо форма была показана,
//   но пользователь не выбрал ни одного исключения
// КодВозвратаДиалога.Отмена - форма была показана, пользователь нажал Отмена
// Строка - XML-сериализованная таблица значений с колонкой ИсключительнаяСитуация, содержащая отмеченные пользователем
//   исключительные ситуации, подлежащие удалению

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Таблица = Техжурнал.НайтиСмежныеИсключения(Параметры.УсловиеИсключения);
	Если Таблица.Количество() = 0 Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Таблица.Колонки.Добавить("Отметка");
	Таблица.ЗаполнитьЗначения(Истина, "Отметка");
	СмежныеИсключения.Загрузить(Таблица);

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦЫ ФОРМЫ СмежныеИсключения

&НаКлиенте
Процедура СмежныеИсключенияОтметкаПриИзменении(Элемент)
	
	Отметка = Элементы.СмежныеИсключения.ТекущиеДанные.Отметка;
	Для Каждого Строка Из Элементы.СмежныеИсключения.ВыделенныеСтроки Цикл
		
		Данные = СмежныеИсключения.НайтиПоИдентификатору(Строка);
		Данные.Отметка = Отметка;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СмежныеИсключенияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОткрытьФорму("Справочник.ИсключительныеСитуации.ФормаОбъекта", Новый Структура("Ключ", СмежныеИсключения.НайтиПоИдентификатору(ВыбраннаяСтрока).ИсключительнаяСитуация),,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ПродолжитьСохранение(Команда)
	
	Выбранное = ПродолжитьНаСервере();
	ОповеститьОВыборе(Выбранное);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Функция ПродолжитьНаСервере()
	
	Исключения = СмежныеИсключения.Выгрузить(Новый Структура("Отметка", Истина), "ИсключительнаяСитуация");
	Если Исключения.Количество() = 0 Тогда
		Возврат Неопределено;
	Иначе
		ЗаписьXML = Новый ЗаписьXML;
		ЗаписьXML.УстановитьСтроку();
		Сериализатор = Новый СериализаторXDTO(ФабрикаXDTO);
		Сериализатор.ЗаписатьXML(ЗаписьXML, Исключения);
		Возврат ЗаписьXML.Закрыть();
	КонецЕсли;
	
КонецФункции
