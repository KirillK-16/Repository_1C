////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ОбъединитьВыделенные(Команда)
	
	Если Элементы.Список.ВыделенныеСтроки.Количество() < 2 Тогда
		Возврат;
	КонецЕсли;
	
	ОбъединитьВыделенныеНаСервере();
	ОповеститьОбИзменении(Тип("СправочникСсылка.ИсключительныеСитуации"));
	ОповеститьОбИзменении(Тип("СправочникСсылка.УсловияИсключительныхСитуаций"));
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура ОбъединитьВыделенныеНаСервере()
	
	ЦелевоеИсключение = Элементы.Список.ТекущаяСтрока;
	НачатьТранзакцию();
	Попытка
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	УсловияИсключительныхСитуаций.Ссылка КАК Ссылка
			|ИЗ
			|	Справочник.УсловияИсключительныхСитуаций КАК УсловияИсключительныхСитуаций
			|ГДЕ
			|	УсловияИсключительныхСитуаций.Владелец В(&ИсключительныеСитуации)
			|	И УсловияИсключительныхСитуаций.Владелец <> &ЦелевоеИсключение";
		Запрос.УстановитьПараметр("ИсключительныеСитуации", Элементы.Список.ВыделенныеСтроки);
		Запрос.УстановитьПараметр("ЦелевоеИсключение", ЦелевоеИсключение);
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			
			УсловиеОбъект = Выборка.Ссылка.ПолучитьОбъект();
			УсловиеОбъект.Владелец = ЦелевоеИсключение;
			УсловиеОбъект.ОбменДанными.Загрузка = Истина;
			УсловиеОбъект.Записать();
			
		КонецЦикла;
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ *
			|ИЗ
			|	РегистрСведений.ТехнологическийЖурнал КАК ТехнологическийЖурнал
			|ГДЕ
			|	ТехнологическийЖурнал.ИсключительнаяСитуация В(&ИсключительныеСитуации)
			|	И ТехнологическийЖурнал.ИсключительнаяСитуация <> &ЦелевоеИсключение";
		Запрос.УстановитьПараметр("ИсключительныеСитуации", Элементы.Список.ВыделенныеСтроки);
		Запрос.УстановитьПараметр("ЦелевоеИсключение", ЦелевоеИсключение);
		Выборка = Запрос.Выполнить().Выбрать();
		Набор = РегистрыСведений.ТехнологическийЖурнал.СоздатьНаборЗаписей();
		Пока Выборка.Следующий() Цикл
			Набор.Очистить();
			Набор.Отбор.УникальныйИдентификатор.Установить(Выборка.УникальныйИдентификатор);
			Запись = Набор.Добавить();
			ЗаполнитьЗначенияСвойств(Запись, Выборка);
			Запись.ИсключительнаяСитуация = ЦелевоеИсключение;
			Набор.Записать();
			
		КонецЦикла;
		
		Для Каждого Строка Из Элементы.Список.ВыделенныеСтроки Цикл
			
			Если Строка = ЦелевоеИсключение Тогда
				Продолжить;
			КонецЕсли;
			
			Удаление = Новый УдалениеОбъекта(Строка);
			Удаление.Записать();
			
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
	Исключение
		Инфо = ИнформацияОбОшибке();
		Комментарий =
			"Описание = '" +Инфо.Описание + "', " +
			"ИмяМодуля = '" + Инфо.ИмяМодуля + "', " +
			"НомерСтроки = '" + Инфо.НомерСтроки + "', " +
			"ИсходнаяСтрока = '" + Инфо.ИсходнаяСтрока + "'.";
					
		ЗаписьЖурналаРегистрации(
			"Процедура ОбъединитьВыделенныеНаСервере()",
			УровеньЖурналаРегистрации.Ошибка,
			Метаданные.Справочники.ИсключительныеСитуации.Формы.ФормаСписка.Форма,
			,
			Комментарий);
			
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
		
КонецПроцедуры
