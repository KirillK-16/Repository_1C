
Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоНовый() Тогда
		ЗаполнитьЗначенияИзНаименования();
	КонецЕсли;
КонецПроцедуры

Процедура ЗаполнитьЗначенияИзНаименования()
	
	МассивСтрок = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Наименование, "_",Истина,Истина);
	
	ПозицияНедопустимогоСимвола = НайтиНедопустимыеСимволыXML(Наименование);
	Если ПозицияНедопустимогоСимвола > 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если МассивСтрок.Количество() <> 3 Тогда
		Возврат;
	КонецЕсли;
	
	ИмяПроцессаСтрока = НРЕГ(МассивСтрок[0]);
	Если НЕ ЕстьСторонниеСимволы(ИмяПроцессаСтрока) Тогда
		ИмяПроцесса = Справочники.ПроцессыДамповЦентраМониторинга.СоздатьЭлементПоНаименованию(ИмяПроцессаСтрока);		
	КонецЕсли;
	
	ВерсияСтрокой = МассивСтрок[1];
	ВерсияПлатформы = Справочники.ВерсииПлатформыЦентрМониторинга.НайтиПоНаименованию(ВерсияСтрокой, Истина);
	
	НулевоеСмещение = МассивСтрок[2] = "00000000";
	Смещение = МассивСтрок[2];
	
КонецПроцедуры

Функция ЕстьСторонниеСимволы(ИсходнаяСтрока) Экспорт
	
	ИсходнаяСтрока = СокрЛП(ИсходнаяСтрока);
	Если СтрДлина(ИсходнаяСтрока) > 0 Тогда
		Для Сч = 1 По СтрДлина(ИсходнаяСтрока) Цикл
			Символ = Сред(ИсходнаяСтрока, Сч, 1);
			Если Найти("abcdefghijklmnopqrstuvwxyz0123456789", Символ) = 0 Тогда
				Возврат Истина;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат Ложь;
КонецФункции 
