
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
    
#Область ПрограммныйИнтерфейс

Функция СоздатьЭлементПоНаименованию(Наименование, ХэшПрофиля) Экспорт
	
	РезультатСсылка = НайтиСсылкуПоХэш(ХэшПрофиля);
        
    Если РезультатСсылка.Пустая() Тогда
        Попытка
            БлокировкаДанных = Новый БлокировкаДанных;
            ЭлементБлокировки = БлокировкаДанных.Добавить("Справочник.ПрофилиГруппДоступаЦентрМониторинга");
            ЭлементБлокировки.УстановитьЗначение("ХэшПрофиля", ХэшПрофиля);
            ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
            БлокировкаДанных.Заблокировать();
		Исключение
			ОписаниеОшибки = ОписаниеОшибки();
			ВызватьИсключение "Центр мониторинга. Ожидаемая управляемая блокировка при создании элемента справочника 'ПрофилиГруппДоступаЦентрМониторинга'";
        КонецПопытки;	
			
		РезультатСсылка = НайтиСсылкуПоХэш(ХэшПрофиля);
		Если РезультатСсылка.Пустая() Тогда
			НовыйЭлемент = Справочники.ПрофилиГруппДоступаЦентрМониторинга.СоздатьЭлемент();
			НовыйЭлемент.Наименование = Наименование;
			НовыйЭлемент.ХэшПрофиля = ХэшПрофиля;
			
			НовыйЭлемент.Записать();
			РезультатСсылка = НовыйЭлемент.Ссылка;
		КонецЕсли;
		        
    КонецЕсли;
    
    Возврат РезультатСсылка;
    
КонецФункции

Функция НайтиСсылкуПоХэш(ХэшПрофиля) Экспорт
    
    РезультатСсылка = Справочники.ПрофилиГруппДоступаЦентрМониторинга.ПустаяСсылка();
        
    Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	Ссылка
	|ИЗ
	|	Справочник.ПрофилиГруппДоступаЦентрМониторинга
	|ГДЕ
	|	ХэшПрофиля = &ХэшПрофиля
	|";
	Запрос.УстановитьПараметр("ХэшПрофиля", ХэшПрофиля);
    
    Результат = Запрос.Выполнить();
    Если НЕ Результат.Пустой() Тогда
        Выборка = Результат.Выбрать();
        Выборка.Следующий();
        РезультатСсылка = Выборка.Ссылка;
    КонецЕсли;
    
	Возврат РезультатСсылка;
    
КонецФункции

#КонецОбласти

#КонецЕсли
