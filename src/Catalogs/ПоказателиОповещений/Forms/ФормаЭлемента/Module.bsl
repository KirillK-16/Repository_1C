////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Объект.Функция = Перечисления.ФункцииОповещений.Максимум;
		Объект.ИнтервалРасчета = 60;
		Объект.ЕдиницаВремениИнтервалаРасчета = Перечисления.ЕдиницыВремени.Секунда;
		Объект.ВидСравнения = Перечисления.ВидыСравненияПоказателейОповещения.Больше;
		ДанныеОбъекта = РеквизитФормыВЗначение("Объект");
		ДанныеОбъекта.Записать();
		ЗначениеВРеквизитФормы(ДанныеОбъекта, "Объект");
		ЭтоНовый = Истина;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Показатель) Тогда
		ПоказательСтрокой = Объект.Показатель.Описание;
		ИмяСправочникаПоказателя = Объект.Показатель.Метаданные().Имя;
	КонецЕсли;
	
	СравниватьСПрошлымСтрокой = ?(Объект.СравниватьСПрошлым, "Истина", "Ложь");
	ПорогВПроцентахСтрокой = ?(Объект.ПорогВПроцентах, "Истина", "Ложь");
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОтобразитьБазовоеЗначение();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии()
	
	Если ЭтоНовый Или ПоказательИзменен Тогда
		ПриЗакрытииНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если Источник.ВладелецФормы <> ЭтотОбъект Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Параметр) = Тип("Структура") Тогда
		Если Не ЗначениеЗаполнено(Объект.Показатель) Тогда
			Объект.Показатель = Параметр.Ссылка;
			ПоказательИзменен = Истина;
		КонецЕсли;
		ПоказательСтрокой = Параметр.Описание;
	ИначеЕсли ТипЗнч(Параметр) = Тип("Массив") Тогда
		Если Не ЗначениеЗаполнено(Объект.Показатель) Тогда
			Объект.Показатель = Параметр[0].Ссылка;
		КонецЕсли;
		ПоказательСтрокой = Параметр[0].Описание;
		ЭтотОбъект.Записать();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	Объект.СравниватьСПрошлым = СравниватьСПрошлымСтрокой = "Истина";
	Объект.ПорогВПроцентах = ПорогВПроцентахСтрокой = "Истина";
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	ЭтоНовый = Ложь;
	ПоказательИзменен = Ложь;
	
	Оповестить("ЗаписьПоказателяОповещения",,ЭтотОбъект);
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ПоказательСтрокойОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура("Ключ", Объект.Показатель);
	
	Если Не ЗначениеЗаполнено(Объект.Показатель) Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ПоказательСтрокойОткрытиеЗавершение", ЭтотОбъект, ПараметрыФормы);
		ОткрытьФорму("Обработка.Мониторинг.Форма.ВыборПоказателя",,ЭтотОбъект,,,,ОписаниеОповещения,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	Иначе
		ПараметрыФормы.Вставить("ЗначенияЗаполнения", Новый Структура("Владелец", Объект.Ссылка));
		ПоказательСтрокойОткрытиеОбщий(ПараметрыФормы);
	КонецЕсли;

	
КонецПроцедуры

&НаКлиенте
Процедура ПоказательСтрокойОткрытиеЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если РезультатЗакрытия <> Неопределено Тогда
		ИмяСправочникаПоказателя = РезультатЗакрытия.ИмяСправочника;
		ДополнительныеПараметры.Вставить("ЗначенияЗаполнения", Новый Структура("Владелец", Объект.Ссылка));
		ПоказательСтрокойОткрытиеОбщий(ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказательСтрокойОткрытиеОбщий(ПараметрыФормы)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПоказательСтрокойОткрытиеОбщийЗавершение", ЭтотОбъект);
	ОткрытьФорму(ИмяФормыРедактированияПоказателя(ИмяСправочникаПоказателя), ПараметрыФормы, ЭтотОбъект,,,,ОписаниеОповещения,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

&НаКлиенте
Процедура ПоказательСтрокойОткрытиеОбщийЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если РезультатЗакрытия <> Неопределено Тогда
		Если РезультатЗакрытия = "Закрыть" Тогда
			Оповестить("ЗаписьПоказателяОповещения",,ЭтотОбъект);
			ЭтоНовый = Ложь;
			ПоказательИзменен = Ложь;
			ЭтотОбъект.Закрыть();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СравниватьСПрошлымПриИзменении(Элемент)
	
	ОтобразитьБазовоеЗначение();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура ПриЗакрытииНаСервере()
	
	Если ЭтоНовый Тогда
		УдалениеОбъекта = Новый УдалениеОбъекта(Объект.Ссылка);
		УдалениеОбъекта.Записать();
	ИначеЕсли ПоказательИзменен Тогда
		УдалениеОбъекта = Новый УдалениеОбъекта(Объект.Показатель);
		УдалениеОбъекта.Записать();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ИмяФормыРедактированияПоказателя(Знач ИмяСправочника)
	Возврат "Справочник." + ИмяСправочника + ".ФормаОбъекта";
КонецФункции

&НаКлиенте
Процедура ОтобразитьБазовоеЗначение()
	
	Если СравниватьСПрошлымСтрокой = "Истина" Тогда
		Элементы.СтраницыПараметрыБазовогоЗначения.ТекущаяСтраница = Элементы.СтраницаОтображатьПараметры;
		Элементы.СтраницыПорог.ТекущаяСтраница = Элементы.СтраницаПорогОтносительный;
	Иначе
		Элементы.СтраницыПараметрыБазовогоЗначения.ТекущаяСтраница = Элементы.СтраницаСкрытьПараметры;
		Элементы.СтраницыПорог.ТекущаяСтраница = Элементы.СтраницаПорогАбсолютный;
	КонецЕсли;
		
КонецПроцедуры
