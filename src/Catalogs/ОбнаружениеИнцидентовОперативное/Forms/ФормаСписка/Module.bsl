
#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Автодобавление(Команда)
    АвтодобавлениеНаСервере(Элементы.Список.ВыделенныеСтроки);
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьСейчас(Команда)
    
    ТекДанные = Элементы.Список.ТекущиеДанные;
    Если ТекДанные <> Неопределено Тогда
        Сообщения = ВыполнитьСейчасНаСервере(ТекДанные.Ссылка, ТекДанные.АгрегированиеДанныхИнтервал, ТекДанные.АгрегированиеДанныхВремя);
        ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтрСоединить(Сообщения, Символы.ПС));
    КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
    
    Если Элемент.ТекущийЭлемент.Имя = "Активно" Тогда
        ИзменитьАктивность(ВыбраннаяСтрока);
        СтандартнаяОбработка = Ложь;
    КонецЕсли;
    
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Процедура АвтодобавлениеНаСервере(Знач ВыделенныеСтроки)
    
    Для Каждого ТекОбнаружение Из ВыделенныеСтроки Цикл
        Справочники.ОбнаружениеИнцидентовОперативное.ВыполнитьАвтоДобавление(ТекОбнаружение);
    КонецЦикла;
        
КонецПроцедуры

&НаСервереБезКонтекста
Функция ВыполнитьСейчасНаСервере(Обнаружение, Интервал, Время)
    Возврат Справочники.ОбнаружениеИнцидентовОперативное.ВыполнитьОбнаружение(Обнаружение, Интервал, Время);
КонецФункции

&НаСервере
Процедура ИзменитьАктивность(ОбнаружениеСсылка)
    
    ОбнаружениеОбъект = ОбнаружениеСсылка.ПолучитьОбъект();
    ОбнаружениеОбъект.Активно = НЕ ОбнаружениеОбъект.Активно;
    ОбнаружениеОбъект.Записать();
    Элементы.Список.Обновить();
    
КонецПроцедуры

#КонецОбласти
