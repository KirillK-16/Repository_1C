&НаКлиенте
Перем РасписаниеЗадания2 Экспорт;

&НаКлиенте
Перем ПередЗаписьюПредупреждение, ОбновитьРекомендации;

#Область Обработчики_событий_формы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Общий.ЭтоНовыйОбъект(Объект) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ЗагрузитьНастройки();
       
	ВладелецНастроек = Объект.Ссылка;
	РасписаниеПоУмолчанию = ПолучитьРасписаниеНаФормеНаСервере(ВладелецНастроек); 
	ЭтотОбъект.РасписаниеЗадания = РасписаниеПоУмолчанию;
	ЭтотОбъект.РасписаниеСтрока = Строка(РасписаниеПоУмолчанию);
	АдресВременногоХранилищаРасписания = ПоместитьВоВременноеХранилище(РасписаниеПоУмолчанию, ЭтотОбъект.УникальныйИдентификатор);
			
	Элементы.ИсторияВыполненияСтраница.Видимость = Объект.НастройкаВыполнена; 
	ИсторияВыполнения.Параметры.УстановитьЗначениеПараметра("КонтрольнаяПроцедура", Объект.Ссылка);
    ИсторияВыполнения.Параметры.УстановитьЗначениеПараметра("ОбъектКонтроля", Объект.ОбъектКонтроля);
               
	ОбновитьСостояние();
	СброситьВыполнениеКонтрольнойПроцедуры();
	ЗагрузитьНастройкиПредупреждения();
    
    Если Объект.ОбъектКонтроля.ПлощадкаЭксплуатации.ПометкаУдаления ИЛИ Объект.ОбъектКонтроля.ПлощадкаЭксплуатации = Справочники.ПлощадкиЭксплуатации.Корзина() Тогда
        ЭтотОбъект.ТолькоПросмотр = Истина;
        ЭтотОбъект.Элементы.ГруппаУправлениеПроцедурой.Доступность = Ложь;
        ЭтотОбъект.Элементы.НастроитьРасписание.Доступность = Ложь;
    КонецЕсли;
    
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
    ИзменитьДоступностьПоАгенту();
    ЭтотОбъект.Элементы.ГруппаКаталогТехЖурнала.Доступность = ЭтотОбъект.АвтоматическаяНастройка;
	ЭтотОбъект.Элементы.ГруппаПараметрыРегистрацииДампов.Доступность = ЭтотОбъект.АвтоматическаяНастройка;
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
    
    Каталог = ЭтотОбъект.КаталогВыгрузкиДамповСетевой; 
    ЭтоСетевойКаталог = Ложь;
    Если Лев(Каталог, 2) =  "\\" Тогда
        ЭтоСетевойКаталог = Истина;
        Каталог = Прав(Каталог, СтрДлина(Каталог) - 2);
    КонецЕсли;
    
    ЕстьЗамена = СтрНайти(Каталог, "\\") > 0;
    Пока ЕстьЗамена Цикл
        Каталог = СтрЗаменить(Каталог, "\\", "\");
        ЕстьЗамена = СтрНайти(Каталог, "\\") > 0;
    КонецЦикла;
    
    ЭтотОбъект.КаталогВыгрузкиДамповСетевой = Каталог;
    Если ЭтоСетевойКаталог Тогда
        ЭтотОбъект.КаталогВыгрузкиДамповСетевой = "\\" + ЭтотОбъект.КаталогВыгрузкиДамповСетевой;
    КонецЕсли;

    // Предупреждение о намерении изменить файл настроек ТЖ
	Если НЕ ВыводитьПредупреждение ИЛИ НЕ ЭтотОбъект.АвтоматическаяНастройка Тогда
		Возврат;
	КонецЕсли;
	
	Если ПередЗаписьюПредупреждение <> Истина Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ПередЗаписьюЗавершение", ЭтотОбъект, ПараметрыЗаписи);
		ПараметрыОткрытия = Новый Структура("ТекстПредупреждения", ТехнологическийЖурналКлиент.ТекстПредупрежденияОбИзмененииТехнологическогоЖурнала());
		ОткрытьФорму("ОбщаяФорма.ФормаПредупреждения", ПараметрыОткрытия,,,,,ОписаниеОповещения,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		Отказ = Истина;
	Иначе
		ПередЗаписьюПредупреждение = Неопределено;
    КонецЕсли;
       	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
    
    ТекущийОбъект.ДополнительныеСвойства.Вставить("ИспользоватьАгента", ЭтотОбъект.ИспользоватьАгента);
    
    РезультатПроверки = ПроверитьКаталогСбораДампов(ТекущийОбъект);
    
    Если РезультатПроверки.ЕстьДубли Тогда
        
        СообщениеПользователю = Новый СообщениеПользователю;
        СообщениеПользователю.Текст = "Обнаружены контрольные процедуры, настроенные на текущий сетевой каталог сбора дампов!" + Символы.ПС;
        Для Каждого КонтрольнаяПроцедураДубль Из РезультатПроверки.КонтрольныеПроцедуры Цикл
            СообщениеПользователю.Текст = СообщениеПользователю.Текст + Строка(КонтрольнаяПроцедураДубль) + Символы.ПС;
        КонецЦикла;
                
        СообщениеПользователю.Поле = "КаталогВыгрузкиДамповСетевой";
        СообщениеПользователю.Сообщить();
                        
        Отказ = Истина;
        Возврат;
        
    КонецЕсли;
        
	Если ПринятоРешение Тогда 
		ТекущийОбъект.НастройкаВыполнена = Истина;
	КонецЕсли;
	
	ТекущийОбъект.Выполнять = Выполнять;
	ТекущийОбъект.Пауза = Пауза;
	ТребуетсяЗапускБизнесПроцесса = ТекущийОбъект.Выполнять И НЕ Объект.Ссылка.Выполнять;
	
	// Если расписание было выставлено, то оно должно содержать что-то осмысленное 
	Если НЕ ПустаяСтрока(АдресВременногоХранилищаРасписания) Тогда
		Расписание = ПолучитьИзВременногоХранилища(АдресВременногоХранилищаРасписания);
		Если Расписание = Неопределено Тогда 
			Отказ = Истина;
			ОбщийКлиентСервер.СгенерироватьСообщениеПользователю(
				НСтр("ru = 'Расписание контрольной процедуры не настроено.Сохранение невозможно.'"),
				"РасписаниеСтрока",
				ЭтотОбъект
			);
			Возврат;
		КонецЕсли;
		ТекущийОбъект.Расписание = Новый ХранилищеЗначения(Расписание, Новый СжатиеДанных(9));
	КонецЕсли;
	ТекущийОбъект.Сохранено = Истина;
    
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	МенеджерЗаписи = РегистрыСведений.НастройкиСборДампов.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.КонтрольнаяПроцедура = ТекущийОбъект.Ссылка;
	МенеджерЗаписи.КаталогТЖСетевой = ЭтотОбъект.КаталогТЖСетевой;
	МенеджерЗаписи.КаталогТЖЛокальный = ЭтотОбъект.КаталогТЖЛокальный;
	МенеджерЗаписи.ДлительностьХраненияФайловТЖ = ЭтотОбъект.ДлительностьХраненияФайловТЖ;
	МенеджерЗаписи.КаталогВыгрузкиДамповСетевой = ЭтотОбъект.КаталогВыгрузкиДамповСетевой;
	МенеджерЗаписи.КаталогВыгрузкиДампов = ЭтотОбъект.КаталогВыгрузкиДампов;
	МенеджерЗаписи.КаталогВременныхФайлов = ЭтотОбъект.КаталогВременныхФайлов;
	МенеджерЗаписи.УровеньДетализацииДампов = ЭтотОбъект.УровеньДетализацииДампов;
	МенеджерЗаписи.АвтоматическаяНастройка = ЭтотОбъект.АвтоматическаяНастройка;
    МенеджерЗаписи.ИспользоватьАгента = ЭтотОбъект.ИспользоватьАгента;
	МенеджерЗаписи.Записать(Истина);
	
	УстановитьПривилегированныйРежим(Истина);
	РегистрыСведений.БезопасноеХранилище.ЗаписатьДанные(ТекущийОбъект.Ссылка, Новый Структура("КаталогЭкспортаДампов", ЭтотОбъект.КаталогЭкспортаДампов));
	УстановитьПривилегированныйРежим(Ложь);
	
	МенеджерЗаписи = РегистрыСведений.СхемаАдресацииЗадач.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.ТипИсполнителя = Справочники.ТипыИсполнителейКонтрольПодключений.ОтветственныйЗаВозможностьПодключения;
	МенеджерЗаписи.КонтрольнаяПроцедура = ТекущийОбъект.Ссылка;
	МенеджерЗаписи.РольПользователя = ЭтотОбъект.Ответственный;
	МенеджерЗаписи.Записать(Истина);
		
	Если ПараметрыЗаписи <> Неопределено Тогда
		Если ПараметрыЗаписи.ЗапускКонтрольнойПроцедуры И НЕ ЭтотОбъект.ИспользоватьАгента И ЭтотОбъект.АвтоматическаяНастройка Тогда
			СоздатьЛокальныйКаталогВременныхФайловНаСервере(ЭтотОбъект.КаталогВременныхФайлов);
			СоздатьСетевойКаталогНаСервере(ЭтотОбъект.КаталогВыгрузкиДамповСетевой);	
			СоздатьСетевойКаталогНаСервере(ЭтотОбъект.КаталогТЖСетевой);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// Запускаем бизнес-процесс, если требуется
    Если НЕ ТребуетсяЗапускБизнесПроцесса Тогда
        ДопПараметры = Новый Структура;
		ДопПараметры.Вставить("СборДампов", Новый Массив);
		ДопПараметры.СборДампов.Добавить("УровеньДетализацииДампов");
		ДопПараметры.СборДампов.Добавить("КаталогВыгрузкиДампов");
	
		ТехнологическийЖурнал.ОбновитьФайлНастроекТехнологическогоЖурнала(Объект.ОбъектКонтроля, Новый Структура("КодыКонтрольныхПроцедур",ДопПараметры));
	КонецЕсли;
	

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	ОповеститьОбИзменениях();
	Если ПараметрыЗаписи.Свойство("ЗакрытьФорму") И ПараметрыЗаписи.ЗакрытьФорму Тогда
		ЭтотОбъект.Закрыть();
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если Пауза ИЛИ (ПринятоРешение И НЕ Выполнять) Тогда
		Возврат;
	КонецЕсли;
	
	ИменаОбязательныхРеквизитов = РеквизитФормыВЗначение("ИменаОбязательныхНастроекБизнесПроцесса");
	Для Каждого СтрокаТаблицы Из ИменаОбязательныхРеквизитов Цикл
		ПроверяемыеРеквизиты.Добавить(СтрокаТаблицы.ИмяРеквизита);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Старт(Команда)
	
	ВключитьКонтрольнуюПроцедуру(Истина);
	ПринятоРешение = Истина;
	
	ПараметрыЖР = Новый Структура("Ссылка, ИмяСобытия, Комментарий");
	ПараметрыЖР.Ссылка = Объект.Ссылка;
	ПараметрыЖР.ИмяСобытия = НСтр("ru = 'Контрольная процедура: Старт'");
	ПараметрыЖР.Комментарий = Объект.Наименование + ": <" + Объект.Ссылка.УникальныйИдентификатор() + ">";
	ЗакрытьЭтуФорму(Истина, ПараметрыЖР);
	
КонецПроцедуры

&НаКлиенте
Процедура Пауза(Команда)
	
	ПоставитьНаПаузуКонтрольнуюПроцедуру();
	
	ПараметрыЖР = Новый Структура("Ссылка, ИмяСобытия, Комментарий");
	ПараметрыЖР.Ссылка = Объект.Ссылка;
	ПараметрыЖР.ИмяСобытия = НСтр("ru = 'Контрольная процедура: Пауза'");
	ПараметрыЖР.Комментарий = Объект.Наименование + ": <" + Объект.Ссылка.УникальныйИдентификатор() + ">";
	ЗакрытьЭтуФорму(, ПараметрыЖР);
	
КонецПроцедуры

&НаКлиенте
Процедура Стоп(Команда)
	
	ТекстПредупреждения = Нстр("ru='Вы уверены, что желаете отказать от выполнения контрольной процедуры?'");
	Режим = РежимДиалогаВопрос.ДаНет;
	ОписаниеОповещения = Новый ОписаниеОповещения("СтопЗавершение", ЭтотОбъект);
	ПоказатьВопрос(ОписаниеОповещения, ТекстПредупреждения, Режим, 0);
	
КонецПроцедуры

&НаКлиенте
Процедура СтопЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
    Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
        ВключитьКонтрольнуюПроцедуру(Ложь);
        
        Объект.ДеактивацияТолькоСлужебныхЗадачПриОтключении = РезультатВопроса = КодВозвратаДиалога.Нет;
        ПринятоРешение = Истина;
        
        ПараметрыЖР = Новый Структура("Ссылка, ИмяСобытия, Комментарий");
        ПараметрыЖР.Ссылка = Объект.Ссылка;
        ПараметрыЖР.ИмяСобытия = НСтр("ru = 'Контрольная процедура: Стоп'");
        ПараметрыЖР.Комментарий = Объект.Наименование + ": <" + Объект.Ссылка.УникальныйИдентификатор() + ">";
        ЗакрытьЭтуФорму(, ПараметрыЖР);
        
    КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьСейчас(Команда)
	ВыполнитьСейчасСервер();
КонецПроцедуры

&НаКлиенте
Процедура НастроитьРасписание(Команда)
	ОписаниеОповещения = Новый ОписаниеОповещения("НастроитьРасписаниеЗавершение", ЭтотОбъект);
	РасписаниеКлиент.НастроитьРасписаниеПоложитьВоВременноеХранилище(ОписаниеОповещения);
КонецПроцедуры

&НаКлиенте
Процедура НастроитьРасписаниеЗавершение(Адрес, ДополнительныеПараметры) Экспорт
	Если Адрес <> Неопределено Тогда
		АдресВременногоХранилищаРасписания = Адрес;
		
		ПериодПовтораСек = ПолучитьИзВременногоХранилища(Адрес).ПериодПовтораВТечениеДня;
		Для Каждого ТекТипЗадачи Из Объект.ПараметрыОповещенияПриСбое Цикл
			ТекТипЗадачи.ПериодОповещенияСек = ТекТипЗадачи.ПериодОповещения * ПериодПовтораСек;
		КонецЦикла;	
	КонецЕсли;		
КонецПроцедуры

&НаКлиенте
Процедура УстановитьНастройкиПоУмолчаниюКоманда(Команда)
	Если Объект.Владелец.Пустая() Тогда
		Возврат;
	КонецЕсли;	
	
	Режим = РежимДиалогаВопрос.ДаНет;
	Текст = "ru = ""Продолжить выполнение операции?"";"
    	 + " en = ""Do you want to continue?""";
		 
	ОписаниеОповещение = Новый ОписаниеОповещения("УстановитьНастройкиПоУмолчаниюКомандаЗавершение", ЭтотОбъект);	 
	ПоказатьВопрос(ОписаниеОповещение, НСтр(Текст), Режим, 0);	 
КонецПроцедуры

&НаКлиенте
Процедура УстановитьНастройкиПоУмолчаниюКомандаЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ЗаполнитьНастройки(Истина);
				
		ПараметрыЖР = Новый Структура("Ссылка, ИмяСобытия, Комментарий");
		ПараметрыЖР.Ссылка = Объект.Ссылка;
		ПараметрыЖР.ИмяСобытия = НСтр("ru = 'Контрольная процедура: Установить настройки по умолчанию'");
		ПараметрыЖР.Комментарий = Объект.Наименование + ": <" + Объект.Ссылка.УникальныйИдентификатор() + ">";
		
		ЗаписатьВЖурналРегистрации(ПараметрыЖР);
	КонецЕсли;
КонецПроцедуры	

&НаКлиенте
Процедура ЗакрытьФорму(Команда)
	
	СброситьВыполнениеКонтрольнойПроцедуры();
	ПринятоРешение = Объект.НастройкаВыполнена;
	
	ПараметрыЖР = Новый Структура("Ссылка, ИмяСобытия, Комментарий");
	ПараметрыЖР.Ссылка = Объект.Ссылка;
	ПараметрыЖР.ИмяСобытия = НСтр("ru = 'Контрольная процедура: Записать и закрыть'");
	ПараметрыЖР.Комментарий = Объект.Наименование + ": <" + Объект.Ссылка.УникальныйИдентификатор() + ">";
	ЗакрытьЭтуФорму(, ПараметрыЖР);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьНастройки(Команда)
	СброситьВыполнениеКонтрольнойПроцедуры();
	ПринятоРешение = Объект.НастройкаВыполнена;
	ОчиститьСообщения();
	Если ЭтотОбъект.ПроверитьЗаполнение() Тогда
		ПараметрыЖР = Новый Структура("Ссылка, ИмяСобытия, Комментарий");
		ПараметрыЖР.Ссылка = Объект.Ссылка;
		ПараметрыЖР.ИмяСобытия = НСтр("ru = 'Контрольная процедура: Записать'");
		ПараметрыЖР.Комментарий = Объект.Наименование + ": <" + Объект.Ссылка.УникальныйИдентификатор() + ">";
		ЗаписатьВЖурналРегистрации(ПараметрыЖР);

		ПараметрыЗаписи = Новый Структура("ЗапускКонтрольнойПроцедуры", Ложь);
		ЭтотОбъект.Записать(ПараметрыЗаписи);
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ИсторияВыполненияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
    
    ТекущиеДанные = Элементы.ИсторияВыполнения.ТекущиеДанные;
    
    ПараметрыОткрытия = Новый Структура("КонтрольнаяПроцедура, Результат, Период, Описание", Объект.Ссылка, ТекущиеДанные.Результат, ТекущиеДанные.Период, ТекущиеДанные.Описание);
    ОткрытьФорму("РегистрСведений.ЖурналКонтроля.Форма.СтраницаЖурналаРегистрации", ПараметрыОткрытия);
		
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьАгентаПриИзменении(Элемент)
    Модифицированность = Истина;
    ИзменитьДоступностьПоАгенту();
КонецПроцедуры

#КонецОбласти

#Область Служебные_процедуры_и_функции

&НаКлиенте
Процедура ИзменитьДоступностьПоАгенту()
    
    ЭтотОбъект.Элементы.РасписаниеНастройка.Доступность = НЕ ЭтотОбъект.ИспользоватьАгента;
    ЭтотОбъект.Элементы.КаталогВременныхФайлов.Доступность = НЕ ЭтотОбъект.ИспользоватьАгента;
    ЭтотОбъект.Элементы.ГруппаКаталогВыгрузкиДампов.Доступность = НЕ ЭтотОбъект.ИспользоватьАгента;
    ЭтотОбъект.Элементы.АвтоматическаяНастройка.Доступность = НЕ ЭтотОбъект.ИспользоватьАгента;
    ЭтотОбъект.Элементы.ГруппаКаталогТехЖурнала.Доступность = НЕ ЭтотОбъект.ИспользоватьАгента;
    ЭтотОбъект.Элементы.ГруппаПараметрыРегистрацииДампов.Доступность = НЕ ЭтотОбъект.ИспользоватьАгента;
    
КонецПроцедуры

&НаСервере
Функция ПроверитьКаталогСбораДампов(ТекущийОбъект)
    
    РезультатПроверки = Новый Структура("ЕстьДубли, КонтрольныеПроцедуры", Ложь, Новый Массив);
    
    Если ЗначениеЗаполнено(ЭтотОбъект.КаталогВыгрузкиДамповСетевой) И ЛЕВ(ЭтотОбъект.КаталогВыгрузкиДамповСетевой, 2) = "\\" Тогда
        
        Запрос = Новый Запрос;
        
        КаталогПроверки = ЭтотОбъект.КаталогВыгрузкиДамповСетевой;
        
        ФайлПроверки = Новый Файл(КаталогПроверки);
        
        ПравыйСимвол = Прав(КаталогПроверки, 1);
        Если ПравыйСимвол = "\" ИЛИ ПравыйСимвол = "/" Тогда
            КаталогПроверки = Лев(КаталогПроверки, СтрДлина(КаталогПроверки) - 1);
        КонецЕсли;
                
        Запрос.Текст = "
        |ВЫБРАТЬ
        |   КонтрольнаяПроцедура КАК КонтрольнаяПроцедура
        |ИЗ
        |   РегистрСведений.НастройкиСборДампов
        |ГДЕ
        |   КонтрольнаяПроцедура <> &ТекущаяКонтрольнаяПроцедура
        |   И КонтрольнаяПроцедура <> &КонтрольУстойчивостиСистемы
        |   И
        |   (КаталогВыгрузкиДамповСетевой = &ТекущийКаталогВыгрузкиДамповСетевой
        |   ИЛИ КаталогВыгрузкиДамповСетевой = &ТекущийКаталогВыгрузкиДамповСетевойРазделительПервый
        |   ИЛИ КаталогВыгрузкиДамповСетевой = &ТекущийКаталогВыгрузкиДамповСетевойРазделительВторой)
        |";
        
        Запрос.УстановитьПараметр("ТекущаяКонтрольнаяПроцедура", ТекущийОбъект.Ссылка);
        Запрос.УстановитьПараметр("КонтрольУстойчивостиСистемы", Справочники.ВидыКонтрольныхПроцедур.КонтрольУстойчивостиСистемы());
        Запрос.УстановитьПараметр("ТекущийКаталогВыгрузкиДамповСетевой", КаталогПроверки);
        Запрос.УстановитьПараметр("ТекущийКаталогВыгрузкиДамповСетевойРазделительПервый", КаталогПроверки + "\");
        Запрос.УстановитьПараметр("ТекущийКаталогВыгрузкиДамповСетевойРазделительВторой", КаталогПроверки + "/");
        
        Результат = Запрос.Выполнить();
        
        Если НЕ Результат.Пустой() Тогда
            
            РезультатПроверки.ЕстьДубли = Истина;
            Выборка = Результат.Выбрать();
            Пока Выборка.Следующий() Цикл
                РезультатПроверки.КонтрольныеПроцедуры.Добавить(Выборка.КонтрольнаяПроцедура);
            КонецЦикла;
            
        КонецЕсли;
        
    КонецЕсли;
    
    Возврат РезультатПроверки;
    
КонецФункции

&НаСервере
Процедура ЗагрузитьНастройки()
	
	Настройки = Справочники.КонтрольныеПроцедуры.ПолучитьНастройкиСборДампов(Объект.Ссылка);
	
	Если Настройки <> Неопределено  Тогда
		ЭтотОбъект.Ответственный = Настройки.Ответственный;
		ЭтотОбъект.УровеньДетализацииДампов = Настройки.УровеньДетализацииДампов;
		ЭтотОбъект.КаталогЭкспортаДампов = Настройки.КаталогЭкспортаДампов;
		ЭтотОбъект.КаталогТЖСетевой = Настройки.КаталогТЖСетевой;
		ЭтотОбъект.КаталогТЖЛокальный = Настройки.КаталогТЖЛокальный;
		ЭтотОбъект.КаталогВыгрузкиДамповСетевой = Настройки.КаталогВыгрузкиДамповСетевой;
		ЭтотОбъект.КаталогВыгрузкиДампов = Настройки.КаталогВыгрузкиДампов;
		ЭтотОбъект.КаталогВременныхФайлов = Настройки.КаталогВременныхФайлов;
		ЭтотОбъект.АвтоматическаяНастройка = Настройки.АвтоматическаяНастройка;
		ЭтотОбъект.ДлительностьХраненияФайловТЖ = Настройки.ДлительностьХраненияФайловТЖ;
        ЭтотОбъект.ИспользоватьАгента = Настройки.ИспользоватьАгента;
    КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписьюЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если РезультатЗакрытия <> Неопределено Тогда
		Если РезультатЗакрытия["ПродолжитьВыполнение"] Тогда
			ПередЗаписьюПредупреждение = Истина;
			Записать(ДополнительныеПараметры);
		КонецЕсли;
		
		_ВыводитьПредупреждение = НЕ РезультатЗакрытия["БольшеНеПоказывать"];
		Если _ВыводитьПредупреждение <> ВыводитьПредупреждение Тогда
			ВыводитьПредупреждение = _ВыводитьПредупреждение;
			СохранитьНастройкиПредупреждения();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьСейчасСервер()
	ПараметрыЖР = Новый Структура("Ссылка, ИмяСобытия, Комментарий");
	ПараметрыЖР.Ссылка = Объект.Ссылка;
	ПараметрыЖР.ИмяСобытия = НСтр("ru = 'Контрольная процедура: Выполнить сейчас'");
	ПараметрыЖР.Комментарий = Объект.Наименование + ": <" + Объект.Ссылка.УникальныйИдентификатор() + ">";
	ЗаписатьВЖурналРегистрации(ПараметрыЖР);
	
	УправлениеЗаданиямиСервер.ЗапускКонтрольнойПроцедуры(Объект.Ссылка);
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьЭтуФорму(ЗапускКонтрольнойПроцедуры = Ложь, ПараметрыЖР = Неопределено)
	ОчиститьСообщения();
	Если ЭтотОбъект.ПроверитьЗаполнение() Тогда
		Если ПараметрыЖР <> Неопределено Тогда
			ЗаписатьВЖурналРегистрации(ПараметрыЖР);
		КонецЕсли;
		
		ПараметрыЗаписи = Новый Структура("ЗапускКонтрольнойПроцедуры, ЗакрытьФорму", ЗапускКонтрольнойПроцедуры, Истина);
		ЭтотОбъект.Записать(ПараметрыЗаписи);
	КонецЕсли;
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаписатьВЖурналРегистрации(ПараметрыЖР)
	Справочники.КонтрольныеПроцедуры.ЗаписатьВЖурналРегистрации(ПараметрыЖР);
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьНастройки(ПоУмолчанию = Ложь)			
	Если ПоУмолчанию Тогда
		Если Строка(Объект.Владелец) = "Контроль устойчивости системы" Тогда
			ВладелецНастроек = Объект.Ссылка;
		Иначе
			ВладелецНастроек = Объект.Владелец;
		КонецЕсли;
		
		УстановитьПоУмолчаниюСписокРассылки();
	Иначе
		ВладелецНастроек = Объект.Ссылка;
	КонецЕсли;	
	
	РасписаниеПоУмолчанию = ПолучитьРасписаниеНаФормеНаСервере(ВладелецНастроек); 
	РасписаниеКлиент.ОбновитьРасписание(ЭтотОбъект, РасписаниеПоУмолчанию);
	АдресВременногоХранилищаРасписания = ПоместитьВоВременноеХранилище(РасписаниеПоУмолчанию, ЭтотОбъект.УникальныйИдентификатор);	
	ЗаполнитьДинамическиеНастройки(ВладелецНастроек);	
	
	ЭтотОбъект.Модифицированность = ?(ПоУмолчанию, Истина, ЭтотОбъект.Модифицированность);	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьРасписаниеНаФормеНаСервере(Ссылка)
	Возврат РасписаниеСервер.ПолучитьРасписание(Ссылка);
КонецФункции

&НаСервере
Процедура ЗаполнитьДинамическиеНастройки(ОбъектСсылка)
	
	ИменаОбязательныхРеквизитов = РеквизитФормыВЗначение("ИменаОбязательныхНастроекБизнесПроцесса");
	ИменаОбязательныхРеквизитов.Очистить();	
	
	ИменаНовыхЭлементов = НастройкиСервер.ЗаполнитьПоляДинамическихНастроекНаФорме(
		ОбъектСсылка, 
		ЭтотОбъект, 
		"НастройкиБизнесПроцесса", 
		ИменаОбязательныхРеквизитов
	);
		
	НастройкиСервер.ЗаполнитьПоляОтветственныхПоКонтрольнойПроцедуре(
		ОбъектСсылка, 
		ЭтотОбъект, 
		"ОтветственныеГруппа",
		ИменаОбязательныхРеквизитов
	);
	
	ЗначениеВРеквизитФормы(ИменаОбязательныхРеквизитов, "ИменаОбязательныхНастроекБизнесПроцесса");
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПоУмолчаниюСписокРассылки()
	ВидПроцедуры = Объект.Владелец;
	Объект.ДополнительноеОповещение.Очистить();
	Для Каждого ПоУмолчаниюЗапись Из ВидПроцедуры.ДополнительноеОповещение Цикл
		НоваяЗапись = Объект.ДополнительноеОповещение.Добавить();	
		НоваяЗапись.Пользователь = ПоУмолчаниюЗапись.Пользователь;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ВключитьКонтрольнуюПроцедуру(ВыполнятьНовоеЗначение)
	
	Выполнять = ВыполнятьНовоеЗначение;
	Пауза = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура ПоставитьНаПаузуКонтрольнуюПроцедуру()
	
	Пауза = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСостояние()
	ЗапретитьКнопки = Новый Массив;
	
	Если НЕ Объект.Ссылка.НастройкаВыполнена Тогда
		ЗапретитьКнопки.Добавить("ФормаПауза");
		ЗапретитьКнопки.Добавить("ВыполнитьСейчас");
		СостояниеСтрока = ": НАСТРОЙКА";
	ИначеЕсли НЕ Объект.Ссылка.Выполнять Тогда
		ЗапретитьКнопки.Добавить("ФормаПауза");
		ЗапретитьКнопки.Добавить("ФормаСтоп");
		ЗапретитьКнопки.Добавить("ВыполнитьСейчас");
		СостояниеСтрока = ": ОСТАНОВЛЕНА";	
	ИначеЕсли Объект.Ссылка.Пауза Тогда
		ЗапретитьКнопки.Добавить("ФормаПауза");
		СостояниеСтрока = ": ПАУЗА";	
	Иначе
		ЗапретитьКнопки.Добавить("ФормаСтарт");
		СостояниеСтрока = ": ВЫПОЛНЯЕТСЯ";	
	КонецЕсли;	
		
	ЭтотОбъект.Заголовок = Строка(Объект.Владелец.Наименование) + СостояниеСтрока;

	Элементы.ФормаСтарт.Доступность = Истина;	
	Элементы.ФормаПауза.Доступность = Истина;
	Элементы.ФормаСтоп.Доступность = Истина;
	Для Каждого ИмяКнопки Из ЗапретитьКнопки Цикл
		Элементы[ИмяКнопки].Доступность = Ложь;	
	КонецЦикла;	

	ЭтотОбъект.Прочитать();
КонецПроцедуры	

&НаКлиенте
Процедура ОповеститьОбИзменениях()
	Оповестить("Изменение.Справочник.КонтрольныеПроцедуры");	
КонецПроцедуры

&НаСервере
Процедура ОбновитьРекомендации()
	
	ИмяМакета = НазваниеСтраницыСправки();
	Если ИмяМакета <> Неопределено Тогда
		Справка = Общий.ТекстHTMLМакета(НазваниеСтраницыСправки());
		Формы = РасширяемыеПараметрыЯдра.НазванияВнешнихФормНаФормеКонтрольнойПроцедуры(Объект.Владелец);
		НомерФормы = 0;
		Для Каждого ДанныеПоФорме Из Формы Цикл
			НомерФормы = НомерФормы + 1;
			ДобавитьКомандуНаФорму(
				"ОткрытиеВнешнейФормы" + НомерФормы,
				ДанныеПоФорме.Название,
				ДанныеПоФорме.ПутьКФорме, 
				Новый Структура("ОбъектКонтроля", Объект.ОбъектКонтроля)
			);
		КонецЦикла;
	Иначе
		Элементы.Рекомендации.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция НазваниеСтраницыСправки()
	
	Возврат Объект.Владелец.ИмяБизнесПроцесса;	
	
КонецФункции

&НаСервере
Процедура ДобавитьКомандуНаФорму(ИмяКоманды, Заголовок, ИмяФормы, ПараметрыОткрытия)
	
	НоваяКоманда = Команды.Добавить(ИмяКоманды); 
	НоваяКоманда.Действие = "ОткрытьВспомогательнуюФорму"; 
	
	НовыйЭлемент = Элементы.Добавить(ИмяКоманды,Тип("КнопкаФормы"),Элементы.ГруппаПерейти); 
	НовыйЭлемент.ИмяКоманды = ИмяКоманды; 
	НовыйЭлемент.Вид = ВидКнопкиФормы.Гиперссылка;
	НовыйЭлемент.Заголовок = Заголовок;
	
	ТекущиеПараметры = РеквизитФормыВЗначение("СсылкиНаВнешниеФормы");
	НоваяСтрока = ТекущиеПараметры.Добавить();
	НоваяСтрока.ИмяКоманды = ИмяКоманды;
	НоваяСтрока.ИмяФормы = ИмяФормы;
	
	Для Каждого ИмяЗначениеПараметра Из ПараметрыОткрытия Цикл
		НоваяСтрока = ТекущиеПараметры.Добавить();
		НоваяСтрока.ИмяКоманды = ИмяКоманды;

		НоваяСтрока.ИмяПараметраОткрытия = ИмяЗначениеПараметра.Ключ;
		НоваяСтрока.ЗначениеПараметраОткрытия = ИмяЗначениеПараметра.Значение;
	КонецЦикла;	
		
	ЗначениеВРеквизитФормы(ТекущиеПараметры, "СсылкиНаВнешниеФормы");
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьВспомогательнуюФорму(Команда)
	ПараметрыФормы = ПараметрыВнешнейФормы(Команда.Имя);
		
	ОткрытьФорму(
		ПараметрыФормы.ИмяФормы,
		ПараметрыФормы.ПараметрыОткрытия
	);
	
КонецПроцедуры	

&НаСервере
Функция ПараметрыВнешнейФормы(ИмяКоманды)
	
	СтрокиПараметров = СсылкиНаВнешниеФормы.НайтиСтроки(
		Новый Структура("ИмяКоманды", ИмяКоманды)
	);
	
	Результат = Новый Структура; 
	ПараметрыОткрытия = Новый Структура;
	Для Каждого СтрокаПараметров Из СтрокиПараметров Цикл
		Если НЕ ПустаяСтрока(СтрокаПараметров.ИмяФормы) Тогда
			Результат.Вставить("ИмяФормы", СтрокаПараметров.ИмяФормы);
		КонецЕсли;
		Если НЕ ПустаяСтрока(СтрокаПараметров.ИмяПараметраОткрытия) Тогда
			ПараметрыОткрытия.Вставить(
				СтрокаПараметров.ИмяПараметраОткрытия, 
				СтрокаПараметров.ЗначениеПараметраОткрытия
			);		
		КонецЕсли;
	КонецЦикла;	
	Результат.Вставить("ПараметрыОткрытия", ПараметрыОткрытия);
	Возврат Результат;
		
КонецФункции

&НаСервере
Процедура СброситьВыполнениеКонтрольнойПроцедуры()
	
	Выполнять = Объект.Ссылка.Выполнять;
	Пауза = Объект.Ссылка.Пауза;
	
КонецПроцедуры

&НаКлиенте
Процедура УровеньДетализацииДамповОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОписаниеОповещения = Новый ОписаниеОповещения("УровеньДетализацииДамповОткрытиеЗавершение", ЭтотОбъект);
	ПараметрыОткрытия = Новый Структура("ТекущееЗначение", УровеньДетализацииДампов);
	ОткрытьФорму("ОбщаяФорма.ФормаВыбораДетализацииДампов", ПараметрыОткрытия,,,,,ОписаниеОповещения,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура УровеньДетализацииДамповОткрытиеЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если РезультатЗакрытия <> Неопределено Тогда
		УровеньДетализацииДампов = РезультатЗакрытия;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция КлючНастроек()
	Возврат "НастройкиФормыСборДамповВыводитьПредупреждение";
КонецФункции

&НаСервере
Функция ЗагрузитьНастройкиПредупреждения()
	
	КлючНастроек = КлючНастроек();
	
	_ВыводитьПредупреждение = ХранилищеНастроекДанныхФорм.Загрузить(КлючНастроек, "ВыводитьПредупреждение");
	Если _ВыводитьПредупреждение = Неопределено Тогда
		_ВыводитьПредупреждение = Истина;
	КонецЕсли;
	ВыводитьПредупреждение = _ВыводитьПредупреждение;
		
КонецФункции

&НаСервере
Функция СохранитьНастройкиПредупреждения()
	
	КлючНастроек = КлючНастроек();
	ХранилищеНастроекДанныхФорм.Сохранить(КлючНастроек, "ВыводитьПредупреждение", ВыводитьПредупреждение);
	
КонецФункции

&НаКлиенте
Процедура ПроверитьДоступ(Команда)
	Попытка
		ПроверитьДоступККаталогу(ЭтотОбъект.КаталогЭкспортаДампов);
		Сообщить(НСтр("ru = 'Успешно. Каталог экспорта уникальных дампов'") + ": " + ЭтотОбъект.КаталогЭкспортаДампов + " - " + НСтр("ru = 'Чтение, Запись, Удаление'"));
	Исключение                                           
		Сообщить(НСтр("ru = 'Ошибка! Каталог экспорта уникальных дампов'") + ": " + ИнформацияОбОшибке().Описание);
	КонецПопытки;
    
    Если ЭтотОбъект.АвтоматическаяНастройка Тогда
        Попытка
            ПроверитьДоступККаталогу(ЭтотОбъект.КаталогТЖСетевой);
            Сообщить(НСтр("ru = 'Успешно. Сетевой каталог технологического журнала'") + ": " + ЭтотОбъект.КаталогТЖСетевой + " - " + НСтр("ru = 'Чтение, Запись, Удаление'"));
        Исключение                                           
            Сообщить(НСтр("ru = 'Ошибка! Сетевой каталог технологического журнала'") + ": " + ИнформацияОбОшибке().Описание);
        КонецПопытки;
    КонецЕсли;
	
	Попытка
		ПроверитьДоступККаталогу(ЭтотОбъект.КаталогВыгрузкиДамповСетевой);
		Сообщить(НСтр("ru = 'Успешно. Сетевой путь к каталогу выгрузки дампов'") + ": " + ЭтотОбъект.КаталогВыгрузкиДамповСетевой + " - " + НСтр("ru = 'Чтение, Запись, Удаление'"));
	Исключение                                           
		Сообщить(НСтр("ru = 'Ошибка! Сетевой путь к каталогу выгрузки дампов'") + ": " + ИнформацияОбОшибке().Описание);
	КонецПопытки;
	
	Попытка
		ПроверитьДоступККаталогу(ЭтотОбъект.КаталогВременныхФайлов);
		Сообщить(НСтр("ru = 'Успешно. Локальный каталог временных файлов'") + ": " + ЭтотОбъект.КаталогВременныхФайлов + " - " + НСтр("ru = 'Чтение, Запись, Удаление'"));
	Исключение                                           
		Сообщить(НСтр("ru = 'Ошибка! Локальный каталог временных файлов'") + ": " + ИнформацияОбОшибке().Описание);
	КонецПопытки;
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПроверитьДоступКФТПКаталогу(Путь)
	СтруктураАдреса = ОбщийКлиентСервер.СтруктураURI(Путь);
	Если СтруктураАдреса.Порт = Неопределено Тогда
		СтруктураАдреса.Порт = 21;
	КонецЕсли;
	
	Попытка
		фтп = Новый FTPСоединение(
					СтруктураАдреса.ИмяСервера,
					СтруктураАдреса.Порт,
					СтруктураАдреса.Логин,
					СтруктураАдреса.Пароль,
					,
					Истина);
	Исключение
		ВызватьИсключение НСтр("ru = 'Не существует каталог ""'") + Путь + """";
	КонецПопытки;
	
	Если Прав(Путь, 1) <> "/" Тогда
		Путь = Путь + "/";
	КонецЕсли;
	
	ИмяФайла = Новый УникальныйИдентификатор();
	ИмяФайлаИсточник = ПолучитьИмяВременногоФайла("");
	Текст = Новый ЗаписьТекста(ИмяФайлаИсточник);
	Текст.Записать("Тестовый файл");
	Текст.Закрыть();
	Попытка
		фтп.Записать(ИмяФайлаИсточник, ИмяФайла);
	Исключение
		ВызватьИсключение НСтр("ru = 'Нет прав на запись в ""'") + Путь + """";
	КонецПопытки;
	
	УдалитьФайлы(ИмяФайлаИсточник);
	
	Попытка
		фтп.Удалить(ИмяФайла);
	Исключение
		ВызватьИсключение НСтр("ru = 'Нет прав на удаление в ""'") + Путь + """";
	КонецПопытки;
КонецФункции

&НаСервереБезКонтекста
Функция ПроверитьДоступКЛокальномуКаталогу(Путь)
	Каталог = Новый Файл(Путь);
	Если Каталог.Существует() Тогда
		ИмяФайла = Новый УникальныйИдентификатор();
		Попытка
			Текст = Новый ЗаписьТекста(Каталог.ПолноеИмя + ПолучитьРазделительПутиСервера() + ИмяФайла);
			Текст.Записать("Тестовый файл");
			Текст.Закрыть();
		Исключение
			ВызватьИсключение НСтр("ru = 'Нет прав на запись в ""'") + Каталог.ПолноеИмя + """";
		КонецПопытки;
		
		Попытка
			УдалитьФайлы(Каталог.ПолноеИмя + ПолучитьРазделительПутиСервера() + ИмяФайла);
		Исключение
			ВызватьИсключение НСтр("ru = 'Нет прав на удаление в ""'") + Каталог.ПолноеИмя + """";
		КонецПопытки;
	Иначе
		ВызватьИсключение НСтр("ru = 'Не существует каталог ""'") + Каталог.ПолноеИмя + """";
	КонецЕсли;
КонецФункции

&НаСервереБезКонтекста
Функция ПроверитьДоступККаталогу(Путь)
	Если СтрНайти(Путь, "ftp://") = 1 Тогда
		Попытка
			ПроверитьДоступКФТПКаталогу(Путь);
		Исключение
			ВызватьИсключение ИнформацияОбОшибке().Описание;
		КонецПопытки;
	Иначе
		Попытка
			ПроверитьДоступКЛокальномуКаталогу(Путь);
		Исключение
			ВызватьИсключение ИнформацияОбОшибке().Описание;
		КонецПопытки;
	КонецЕсли;
КонецФункции

&НаКлиенте
Процедура СоздатьЛокальныйКаталогВременныхФайлов(Команда)
	Попытка
		СоздатьЛокальныйКаталогВременныхФайловНаСервере(ЭтотОбъект.КаталогВременныхФайлов);
	Исключение
		Сообщить(НСтр("ru = 'Ошибка! Локальный каталог временных файлов'") + ": " + ИнформацияОбОшибке().Описание);
	КонецПопытки;
КонецПроцедуры

&НаСервереБезКонтекста
Функция СоздатьЛокальныйКаталогВременныхФайловНаСервере(Путь)
	Каталог = Новый Файл(Путь);
	Если НЕ Каталог.Существует() Тогда
		ПутьДляПроверки = СтрЗаменить(Путь, "\", Символы.ПС);
		ПутьПроверка = "";
		
		Для НомерТекСтроки = 1 По СтрЧислоСтрок(ПутьДляПроверки) Цикл
			ПутьПроверка = ПутьПроверка + СтрПолучитьСтроку(ПутьДляПроверки, НомерТекСтроки) + "\";
			
			Каталог = Новый Файл(ПутьПроверка);
			Если НЕ Каталог.Существует() Тогда
				Попытка
					СоздатьКаталог(ПутьПроверка);
				Исключение
					ВызватьИсключение НСтр("ru = 'Не смог создать каталог ""'") + ПутьПроверка + """";
				КонецПопытки;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
КонецФункции

&НаКлиенте
Процедура СоздатьСетевойКаталог(Команда)
	Попытка
		СоздатьСетевойКаталогНаСервере(ЭтотОбъект.КаталогВыгрузкиДамповСетевой);
	Исключение
		Сообщить(НСтр("ru = 'Ошибка! Сетевой путь к каталогу выгрузки дампов:'") + ": " + ИнформацияОбОшибке().Описание);
	КонецПопытки;
КонецПроцедуры

&НаКлиенте
Процедура СоздатьСетевойКаталогТехЖурнала(Команда)
	Попытка
		СоздатьСетевойКаталогНаСервере(ЭтотОбъект.КаталогТЖСетевой);
	Исключение
		Сообщить(НСтр("ru = 'Ошибка! Сетевое имя каталога технологического журнала:'") + ": " + ИнформацияОбОшибке().Описание);
	КонецПопытки;
КонецПроцедуры

&НаСервереБезКонтекста
Функция СоздатьСетевойКаталогНаСервере(Путь)
	РазделительПути = ПолучитьРазделительПутиСервера();
	
	Каталог = Новый Файл(Путь);
	Если НЕ Каталог.Существует() Тогда
		Если РазделительПути = "\" Тогда
			ПутьДляПроверки = СтрЗаменить(Путь, "\\", "?");
			ПутьДляПроверки = СтрЗаменить(ПутьДляПроверки, РазделительПути, Символы.ПС);
			ПутьДляПроверки = СтрЗаменить(ПутьДляПроверки, "?", "\\");
			ПутьПроверка = "";
		Иначе
			ПутьДляПроверки = Прав(Путь, СтрДлина(Путь) - 1);
			ПутьДляПроверки = СтрЗаменить(ПутьДляПроверки, РазделительПути, Символы.ПС);
			ПутьДляПроверки = РазделительПути + ПутьДляПроверки;
			ПутьПроверка = "";
		КонецЕсли;
		
		Если СтрЧислоСтрок(ПутьДляПроверки) > 0 Тогда
			ПутьПроверка = СтрПолучитьСтроку(ПутьДляПроверки, 1) + "\";
			
			Для НомерТекСтроки = 2 По СтрЧислоСтрок(ПутьДляПроверки) Цикл
				ПутьПроверка = ПутьПроверка + СтрПолучитьСтроку(ПутьДляПроверки, НомерТекСтроки) + "\";
				
				Каталог = Новый Файл(ПутьПроверка);
				Если НЕ Каталог.Существует() Тогда
					Попытка
						СоздатьКаталог(ПутьПроверка);
					Исключение
						ВызватьИсключение НСтр("ru = 'Не смог создать каталог ""'") + ПутьПроверка + """";
					КонецПопытки;
				КонецЕсли;
			КонецЦикла;
		Иначе
			ВызватьИсключение НСтр("ru = 'Не указан сетевой каталог'");
		КонецЕсли;
	КонецЕсли;
КонецФункции

&НаКлиенте
Процедура ГруппаСтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	Если ОбновитьРекомендации <> Истина И ТекущаяСтраница = ЭтотОбъект.Элементы.Рекомендации Тогда
		ОбновитьРекомендации();
		ОбновитьРекомендации = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура АвтоматическаяНастройкаПриИзменении(Элемент)
	ЭтотОбъект.Элементы.ГруппаКаталогТехЖурнала.Доступность = ЭтотОбъект.АвтоматическаяНастройка;
	ЭтотОбъект.Элементы.ГруппаПараметрыРегистрацииДампов.Доступность = ЭтотОбъект.АвтоматическаяНастройка;
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОписаниеОповещениеЗавершение = Новый ОписаниеОповещения("ОбработкаНачалоВыбораЗавершение", ЭтотОбъект, Элемент);
	ОбщийКлиент.ВыбратьКаталог(ЭтотОбъект[Элемент.Имя], ОписаниеОповещениеЗавершение);

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаНачалоВыбораЗавершение(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныеФайлы <> Неопределено Тогда
		ЭтотОбъект[ДополнительныеПараметры.Имя] = ВыбранныеФайлы[0];
	КонецЕсли
	
КонецПроцедуры

#КонецОбласти

