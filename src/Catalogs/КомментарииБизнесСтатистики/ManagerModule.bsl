#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Функция СоздатьЭлементПоНаименованию(Наименование, ХешСумма) Экспорт
	Результат = РезультатЗапросаПоХешСумма(ХешСумма);
	Если Результат.Пустой() Тогда
		Попытка
			БлокировкаДанных = Новый БлокировкаДанных;
			ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.ОбъектыБлокировок");
			ЭлементБлокировки.УстановитьЗначение("Объект", "Справочник.КомментарииБизнесСтатистики." + Наименование);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			БлокировкаДанных.Заблокировать();
		Исключение
			ВызватьИсключение "Центр мониторинга. Ожидаемая управляемая блокировка при создании элемента справочника 'КомментарииБизнесСтатистики'";
		КонецПопытки;
		
		Результат = РезультатЗапросаПоХешСумма(ХешСумма);
		Если Результат.Пустой() Тогда
			НовыйЭлемент = Справочники.КомментарииБизнесСтатистики.СоздатьЭлемент();
			НовыйЭлемент.НаименованиеПолное = Наименование;
			НовыйЭлемент.ХешПолноеНаименование = ХешСумма;
			НовыйЭлемент.Записать();
			
			РезультатВыполнения = НовыйЭлемент.Ссылка;
		Иначе
			Выборка = Результат.Выбрать();
			Выборка.Следующий();
			РезультатВыполнения = Выборка.Ссылка;
		КонецЕсли;
	Иначе
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		РезультатВыполнения = Выборка.Ссылка;
	КонецЕсли;
	
	Возврат РезультатВыполнения;
КонецФункции

Функция РезультатЗапросаПоХешСумма(ХешСумма)
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	Ссылка
	|ИЗ
	|	Справочник.КомментарииБизнесСтатистики
	|ГДЕ
	|	ХешПолноеНаименование = &ХешСумма
	|";
	Запрос.УстановитьПараметр("ХешСумма", ХешСумма);
	Результат = Запрос.Выполнить();
	
	Возврат Результат;
КонецФункции

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	НаименованиеПолное
	|ИЗ
	|	Справочник.КомментарииБизнесСтатистики
	|ГДЕ
	|	Ссылка = &Ссылка
	|";
	Запрос.УстановитьПараметр("Ссылка", Данные.Ссылка);
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		
		Представление = Выборка.НаименованиеПолное;
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецЕсли
