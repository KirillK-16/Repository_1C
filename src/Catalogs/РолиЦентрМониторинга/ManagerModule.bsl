
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
    
#Область ПрограммныйИнтерфейс

Функция СоздатьЭлементПоНаименованию(Имя) Экспорт
	
	ХешированиеДанных = Новый ХешированиеДанных(ХешФункция.MD5);
	ХешированиеДанных.Добавить(Имя);
	ХэшИмени = СтрЗаменить(Строка(ХешированиеДанных.ХешСумма), " ", "");
    
    РезультатСсылка = НайтиСсылкуПоХэш(ХэшИмени);
        
    Если РезультатСсылка.Пустая() Тогда
        Попытка
            БлокировкаДанных = Новый БлокировкаДанных;
            ЭлементБлокировки = БлокировкаДанных.Добавить("Справочник.РолиЦентрМониторинга");
            ЭлементБлокировки.УстановитьЗначение("Имя", Имя);
			ЭлементБлокировки.УстановитьЗначение("ХэшИмени", ХэшИмени);
            ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
            БлокировкаДанных.Заблокировать();
		Исключение
			ОписаниеОшибки = ОписаниеОшибки();
			ВызватьИсключение "Центр мониторинга. Ожидаемая управляемая блокировка при создании элемента справочника 'РолиЦентрМониторинга'";
        КонецПопытки;	
			
		РезультатСсылка = НайтиСсылкуПоХэш(ХэшИмени);
		Если РезультатСсылка.Пустая() Тогда
			НовыйЭлемент = Справочники.РолиЦентрМониторинга.СоздатьЭлемент();
			НовыйЭлемент.Наименование = Имя;
			НовыйЭлемент.Имя = Имя;
			НовыйЭлемент.ХэшИмени = ХэшИмени;
			
			НовыйЭлемент.Записать();
			РезультатСсылка = НовыйЭлемент.Ссылка;
		КонецЕсли;
		        
    КонецЕсли;
    
    Возврат РезультатСсылка;
    
КонецФункции

Функция НайтиСсылкуПоХэш(ХэшИмени) Экспорт
    
    РезультатСсылка = Справочники.РолиЦентрМониторинга.ПустаяСсылка();
        
    Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	Ссылка
	|ИЗ
	|	Справочник.РолиЦентрМониторинга
	|ГДЕ
	|	ХэшИмени = &ХэшИмени
	|";
	Запрос.УстановитьПараметр("ХэшИмени", ХэшИмени);
    
    Результат = Запрос.Выполнить();
    Если НЕ Результат.Пустой() Тогда
        Выборка = Результат.Выбрать();
        Выборка.Следующий();
        РезультатСсылка = Выборка.Ссылка;
    КонецЕсли;
    
	Возврат РезультатСсылка;
    
КонецФункции

#КонецОбласти

#КонецЕсли
