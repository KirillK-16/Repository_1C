////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Выборка = ПубликацииКонтрольДоступности.ПолучитьРесурсы();
	
	Пока Выборка.Следующий() Цикл
		Публикация = Выборка.Ссылка;
		Элементы.РесурсыСервис.СписокВыбора.Добавить(Публикация);
		Если Объект.Ссылка.Пустая() Тогда
			СтрокаСервиса = Объект.Ресурсы.Добавить();
			СтрокаСервиса.Ресурс = Публикация;
		КонецЕсли;
	КонецЦикла;
	
	Если Объект.Ссылка.Пустая() Тогда
		Объект.ИнтервалУсреднения = Перечисления.ИнтервалыУсреднения.Час;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ПараметрОповещения = МониторингСервер.ПараметрОповещенияПоказательЗаписан(ТекущийОбъект);
	ПараметрОповещения.Изменился = ОбъектИзменился();

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ПараметрОповещения.Ссылка = Объект.Ссылка;
	ПараметрОповещения.Описание = Объект.Описание;
	Оповестить("ПоказательЗаписан", ПараметрОповещения, ЭтотОбъект);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Функция ОбъектИзменился()
	
	Если Объект.ИнтервалУсреднения <> Объект.Ссылка.ИнтервалУсреднения Тогда
		Возврат Истина;
	КонецЕсли;
	
	//БазыСсылка = Объект.Ссылка.ИнформационныеБазы;
	РесурсыСсылка = Объект.Ссылка.Ресурсы;
	//БазыОбъект = Объект.ИнформационныеБазы;
	РесурсыОбъект = Объект.Ресурсы;
	
	Если РесурсыСсылка.Количество() <> РесурсыОбъект.Количество() Тогда
		Возврат Истина;
	КонецЕсли;
	
	Для Каждого Сервис Из РесурсыОбъект Цикл
		
		Строка = РесурсыСсылка.Найти(Сервис.Ресурс);
		Если Строка = Неопределено Тогда
			Возврат Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции
