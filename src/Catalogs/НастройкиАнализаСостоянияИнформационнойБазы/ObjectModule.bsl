
Процедура ПередЗаписью(Отказ)
	Результат = ОбщегоНазначенияКлиентСервер.РаспределитьСуммуПропорциональноКоэффициентам(1, ПриростДанных.ВыгрузитьКолонку("ВесовойКоэффициент"), 2);
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Для Инд = 0 По Результат.Количество()-1 Цикл
		ПриростДанных[Инд].ВесовойКоэффициент = Результат[Инд];
	КонецЦикла;
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	Конфигурации.Конфигурация КАК Конфигурация
	                      |ПОМЕСТИТЬ ВТ_Конфигурации
	                      |ИЗ
	                      |	&Конфигурации КАК Конфигурации
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	НастройкиАнализаСостоянияИнформационнойБазыКонфигурации.Конфигурация КАК Конфигурация,
	                      |	НастройкиАнализаСостоянияИнформационнойБазыКонфигурации.Ссылка КАК Ссылка
	                      |ИЗ
	                      |	Справочник.НастройкиАнализаСостоянияИнформационнойБазы.Конфигурации КАК НастройкиАнализаСостоянияИнформационнойБазыКонфигурации
	                      |ГДЕ
	                      |	НастройкиАнализаСостоянияИнформационнойБазыКонфигурации.Ссылка <> &Ссылка
	                      |	И НастройкиАнализаСостоянияИнформационнойБазыКонфигурации.Конфигурация В
	                      |			(ВЫБРАТЬ
	                      |				ВТ_Конфигурации.Конфигурация КАК Конфигурация
	                      |			ИЗ
	                      |				ВТ_Конфигурации КАК ВТ_Конфигурации)");
	Запрос.УстановитьПараметр("Конфигурации", Конфигурации.Выгрузить());
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Выборка = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока Выборка.Следующий() Цикл
		ТекстСообщения = "Конфигурация " + Строка(Выборка.Конфигурация) + " используется в настройке " + Строка(Выборка.Ссылка);
		Отказ = Истина;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЦикла;
	
КонецПроцедуры


