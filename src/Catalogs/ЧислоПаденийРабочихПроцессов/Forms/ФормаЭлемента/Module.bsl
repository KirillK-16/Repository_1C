////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Выборка = Общий.ВыборкаРабочихСерверов();
	ПервыйКластер = Истина;
	Пока Выборка.Следующий() Цикл
		
		РабочийСервер = Выборка.Ссылка;
		Элементы.РабочийСервер.СписокВыбора.Добавить(РабочийСервер);
		
	КонецЦикла;
	
	Если Объект.Ссылка.Пустая() И Элементы.РабочийСервер.СписокВыбора.Количество() > 0 Тогда
		
		РабочийСервер = Элементы.РабочийСервер.СписокВыбора[0].Значение;
		Объект.РабочийСервер = РабочийСервер;
		
		Объект.ИнтервалУсреднения = Перечисления.ИнтервалыУсреднения.Час;
		Объект.ВсеСерверы = Истина;
		Объект.ТипПадения = Перечисления.ТипыПаденияРабочегоПроцесса.СОбразованиемИБезОбразованияДампа;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	НастроитьВидимостьРабочихСерверов();
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ПараметрОповещения = МониторингСервер.ПараметрОповещенияПоказательЗаписан(ТекущийОбъект);
	ПараметрОповещения.Изменился = ОбъектИзменился();

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ПараметрОповещения.Ссылка = Объект.Ссылка;
	ПараметрОповещения.Описание = Объект.Описание;
	Оповестить("ПоказательЗаписан", ПараметрОповещения, ЭтотОбъект);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ВсеСерверыПриИзменении(Элемент)
	НастроитьВидимостьРабочихСерверов();
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиенте
Процедура НастроитьВидимостьРабочихСерверов()
	Элементы.РабочийСервер.Видимость = НЕ Объект.ВсеСерверы;
КонецПроцедуры

&НаСервере
Функция ОбъектИзменился()
	
	Если Объект.ВсеСерверы <> Объект.Ссылка.ВсеСерверы Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если НЕ Объект.ВсеСерверы И Объект.РабочийСервер <> Объект.Ссылка.РабочийСервер Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если Объект.ИнтервалУсреднения <> Объект.Ссылка.ИнтервалУсреднения Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если Объект.ТипПадения <> Объект.Ссылка.ТипПадения Тогда
		Возврат Истина;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции
