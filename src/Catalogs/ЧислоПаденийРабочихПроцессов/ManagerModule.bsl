#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Функция ПолучитьМассивСтруктурыПоказателей(ТипПоказателя, ИдентификаторНастройки, МассивСтруктурПоказателей, ТолькоВыбранные) Экспорт
	
	МассивСтруктурыПоказателей = Новый Массив;
	
	ТаблицаПоказателей = Новый ТаблицаЗначений;
	ТаблицаПоказателей.Колонки.Добавить("ПоказательМониторинга", Новый ОписаниеТипов("СправочникСсылка.ПоказателиМониторинга"));
	ТаблицаПоказателей.Колонки.Добавить("ГруппаОбъектовКонтроля", Новый ОписаниеТипов("Булево"));
	
	Для Каждого СтруктураПоказатель Из МассивСтруктурПоказателей Цикл
		
		Если ЗначениеЗаполнено(СтруктураПоказатель.Показатель) И СтруктураПоказатель.ТипПоказателя = "ЧислоПаденийРабочихПроцессов" Тогда
			
			НоваяСтрока							= ТаблицаПоказателей.Добавить();
			НоваяСтрока.ПоказательМониторинга	= СтруктураПоказатель.Показатель;
			НоваяСтрока.ГруппаОбъектовКонтроля	= СтруктураПоказатель.ГруппаОбъектовКонтроля;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("ИдентификаторНастройки", ИдентификаторНастройки);
	Запрос.УстановитьПараметр("ТаблицаПоказателей", ТаблицаПоказателей);
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ТаблицаПоказателей.ПоказательМониторинга КАК ПоказательМониторинга,
	|	ТаблицаПоказателей.ГруппаОбъектовКонтроля КАК ГруппаОбъектовКонтроля
	|ПОМЕСТИТЬ
	|	ТаблицаПоказателей
	|ИЗ
	|	&ТаблицаПоказателей КАК ТаблицаПоказателей
	|
	|;
	|
	|ВЫБРАТЬ
	|	ТаблицаПоказателей.ПоказательМониторинга.Наименование Как Код,
	|	ТаблицаПоказателей.ПоказательМониторинга КАК ПоказательМониторинга
	|ИЗ
	|	ТаблицаПоказателей КАК ТаблицаПоказателей
	|ГДЕ
	|	ТаблицаПоказателей.ГруппаОбъектовКонтроля
	|
	|УПОРЯДОЧИТЬ ПО
	|	Код
	|";
	
	СтруктураДанныхГруппы = Новый Структура();
	СтруктураДанныхГруппы.Вставить("ПоказательМониторинга", "");
	СтруктураДанныхГруппы.Вставить("ОбъектКонтроля", "ГруппаОбъектовКонтроляРодитель");
	СтруктураДанныхГруппы.Вставить("Код", "Группы дампов");
	СтруктураДанныхГруппы.Вставить("МассивСтрок", Новый Массив());
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		СтруктураДанных = Новый Структура();
		СтруктураДанных.Вставить("ПоказательМониторинга", Строка(Выборка.ПоказательМониторинга.УникальныйИдентификатор()));
		СтруктураДанных.Вставить("ОбъектКонтроля", "ГруппаОбъектовКонтроля");
		СтруктураДанных.Вставить("Код", Выборка.Код);
		СтруктураДанных.Вставить("МассивСтрок", Новый Массив());
		
		СтруктураДанныхГруппы.МассивСтрок.Добавить(СтруктураДанных);
		
	КонецЦикла;
	
	МассивСтруктурыПоказателей.Добавить(СтруктураДанныхГруппы);
	
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЧислоПаденийРабочихПроцессов.ТипПадения КАК ТП,
	|	Дампы.ОбъектКонтроля.Наименование КАК СерверНаименование,
	|	Представление(ТипыПаденияРабочегоПроцесса.Ссылка) КАК ТипПаденияНаименование,
	|	Дампы.ОбъектКонтроля КАК Сервер,
	|	ТипыПаденияРабочегоПроцесса.Ссылка КАК ТипПадения,
	|	ТипыПаденияРабочегоПроцесса.Порядок КАК ТипПаденияПорядок,
	|	ЕСТЬNULL(ТаблицаПоказателей.ПоказательМониторинга, Неопределено) КАК ПоказательМониторинга
	|ИЗ
	|	РегистрСведений.Дампы КАК Дампы
	|
	|	ПОЛНОЕ СОЕДИНЕНИЕ Перечисление.ТипыПаденияРабочегоПроцесса КАК ТипыПаденияРабочегоПроцесса
	|	ПО Истина
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ЧислоПаденийРабочихПроцессов КАК ЧислоПаденийРабочихПроцессов
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаПоказателей КАК ТаблицаПоказателейОтбор
	|		ПО ТаблицаПоказателейОтбор.ПоказательМониторинга.Показатель = ЧислоПаденийРабочихПроцессов.Ссылка
	|		И НЕ ТаблицаПоказателейОтбор.ГруппаОбъектовКонтроля
	|
	|	ПО ЧислоПаденийРабочихПроцессов.РабочийСервер = Дампы.ОбъектКонтроля
	|	И ЧислоПаденийРабочихПроцессов.ТипПадения = ТипыПаденияРабочегоПроцесса.Ссылка
	|	И НЕ ЧислоПаденийРабочихПроцессов.ВсеСерверы
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаПоказателей КАК ТаблицаПоказателей
	|	ПО ТаблицаПоказателей.ПоказательМониторинга.Показатель = ЧислоПаденийРабочихПроцессов.Ссылка
	|
	|ГДЕ
	|	Дампы.ОбъектКонтроля <> ЗНАЧЕНИЕ(Справочник.ОбъектыКонтроля.ПустаяСсылка)
	|
	|УПОРЯДОЧИТЬ ПО
	|	СерверНаименование, ТипПаденияПорядок
	|
	|ИТОГИ 
	|	МАКСИМУМ(СерверНаименование) КАК СерверНаименование,
	|	МАКСИМУМ(ПоказательМониторинга) КАК ПоказательМониторинга
	|ПО
	|	Сервер
	|";
	
	ВыборкаСервер = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "Сервер");
	Пока ВыборкаСервер.Следующий() Цикл
		
		Если ТолькоВыбранные И НЕ ЗначениеЗаполнено(ВыборкаСервер.ПоказательМониторинга) Тогда
			Продолжить;
		КонецЕсли;
		
		СтруктураДанныхСервер = Новый Структура();
		СтруктураДанныхСервер.Вставить("ПоказательМониторинга", "");
		СтруктураДанныхСервер.Вставить("Код", ВыборкаСервер.СерверНаименование);
		СтруктураДанныхСервер.Вставить("ОбъектКонтроля", Строка(ВыборкаСервер.Сервер.УникальныйИдентификатор()));
		СтруктураДанныхСервер.Вставить("МассивСтрок", Новый Массив());
		
		ВыборкаТипПадения = ВыборкаСервер.Выбрать();
		Пока ВыборкаТипПадения.Следующий() Цикл
		
			Если ТолькоВыбранные И НЕ ЗначениеЗаполнено(ВыборкаТипПадения.ПоказательМониторинга) Тогда
				Продолжить;
			КонецЕсли;
			
			СтруктураДанныхТипПадения = Новый Структура();
			
			Если ЗначениеЗаполнено(ВыборкаТипПадения.ПоказательМониторинга) Тогда
				СтруктураДанныхТипПадения.Вставить("ПоказательМониторинга", Строка(ВыборкаТипПадения.ПоказательМониторинга.УникальныйИдентификатор()));
			Иначе
				СтруктураДанныхТипПадения.Вставить("ПоказательМониторинга", "");
			КонецЕсли;
			
			СтруктураДанныхТипПадения.Вставить("Код", ВыборкаТипПадения.ТипПаденияНаименование);
			СтруктураДанныхТипПадения.Вставить("ОбъектКонтроля", Строка(ВыборкаТипПадения.Сервер.УникальныйИдентификатор()) + "/" + Метаданные.Перечисления.ТипыПаденияРабочегоПроцесса.ЗначенияПеречисления[ВыборкаТипПадения.ТипПаденияПорядок].Имя);
			СтруктураДанныхТипПадения.Вставить("МассивСтрок", Новый Массив());
			
			СтруктураДанныхСервер.МассивСтрок.Добавить(СтруктураДанныхТипПадения);
			
		КонецЦикла;
		
		МассивСтруктурыПоказателей.Добавить(СтруктураДанныхСервер);
		
	КонецЦикла;
	
	Возврат МассивСтруктурыПоказателей;
	
КонецФункции

Функция ПоискПоказателей(Знач СтрокаПоиска) Экспорт
	
	СтрокаПоиска = СтрЗаменить(СтрокаПоиска, """", "");
	
	МассивРезультатов = Новый Массив;
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("СтрокаПоиска", "%" + СтрокаПоиска + "%");
	
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	""Дампы/"" + Дампы.ОбъектКонтроля.Наименование КАК Представление,
	|	Дампы.ОбъектКонтроля КАК Сервер,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыПаденияРабочегоПроцесса.СОбразованиемИБезОбразованияДампа) КАК ТипПадения
	|ИЗ
	|	РегистрСведений.Дампы КАК Дампы
	|ГДЕ
	|	Дампы.ОбъектКонтроля.Наименование ПОДОБНО &СтрокаПоиска
	|	И Дампы.ОбъектКонтроля <> ЗНАЧЕНИЕ(Справочник.ОбъектыКонтроля.ПустаяСсылка)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Представление
	|";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		СтруктураРезультата = Новый Структура();
		СтруктураРезультата.Вставить("Представление", Выборка.Представление);
		СтруктураРезультата.Вставить("ОбъектКонтроля", Строка(Выборка.Сервер.УникальныйИдентификатор()) + "/СОбразованиемИБезОбразованияДампа");
		СтруктураРезультата.Вставить("ТипПоказателя", "ЧислоПаденийРабочихПроцессов");
		
		МассивРезультатов.Добавить(СтруктураРезультата);
		
	КонецЦикла;
	
	Возврат МассивРезультатов;
	
КонецФункции

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	Запрос = Новый Запрос;
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	Описание
	|ИЗ
	|	Справочник.ЧислоПаденийРабочихПроцессов
	|ГДЕ
	|	Ссылка = &Ссылка
	|";
	Запрос.УстановитьПараметр("Ссылка", Данные.Ссылка);
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		
		Представление = Выборка.Описание;
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
КонецПроцедуры

#КонецЕсли