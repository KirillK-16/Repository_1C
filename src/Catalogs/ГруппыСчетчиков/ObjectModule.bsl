#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Процедура ПередЗаписью(Отказ)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ПолныйКодДляПоиска = ПолныйКодДляПоиска();
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ЭтотОбъект.ДополнительныеСвойства.Свойство("ДопУсловия") Тогда
		ЗаписатьДопУсловия(ЭтотОбъект.ДополнительныеСвойства.ДопУсловия);
    КонецЕсли;
    
    Если НЕ КонтрольПоступленияДанных Тогда
        
        ТипИнцидента = Справочники.ГруппыСчетчиков.ТипИнцидентаЦККСчетчикиНетДанных();
        КодИнцидента = Справочники.ГруппыСчетчиков.КодИнцидентаЦККСчетчикиНедДанных(ПолныйКодДляПоиска);
        ИнцидентыСервер.ЗакрытьИнцидент(ТипИнцидента, КодИнцидента, Истина);
        
    КонецЕсли;
    	
КонецПроцедуры

Процедура ЗаписатьДопУсловия(ДопУсловия)
	
	Если ДопУсловия.Выполнить Тогда
		Если ДопУсловия.РежимУстановки = "Для элементов" Тогда
			ДляЭлементов = Истина;
			ДляГрупп = Ложь;
		ИначеЕсли ДопУсловия.РежимУстановки = "Для групп и элементов" Тогда
			ДляЭлементов = Истина;
			ДляГрупп = Истина;
		ИначеЕсли ДопУсловия.РежимУстановки = "Для групп" Тогда
			ДляЭлементов = Ложь;
			ДляГрупп = Истина;
		КонецЕсли;
		КонтрольПоступленияДанных = ДопУсловия.КонтрольПоступленияДанных;
		ДопустимоеВремяОтсутствияДанных = ДопУсловия.ДопустимоеВремяОтсутствияДанных;
	Иначе
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ГруппыСчетчиков.Ссылка КАК Ссылка,
	|	ВЫБОР
	|		КОГДА 1 В
	|			(ВЫБРАТЬ ПЕРВЫЕ 1
	|				1
	|			ИЗ
	|				Справочник.ГруппыСчетчиков КАК ГруппыСчетчиковПодзапрос
	|			ГДЕ
	|				ГруппыСчетчиковПодзапрос.Родитель = ГруппыСчетчиков.Ссылка
	|			)
	|		ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоГруппа
	|ИЗ
	|	Справочник.ГруппыСчетчиков КАК ГруппыСчетчиков
	|ГДЕ
	|	ГруппыСчетчиков.Родитель В ИЕРАРХИИ (&Ссылка)
	|";
	Запрос.УстановитьПараметр("Ссылка", ЭтотОбъект.Ссылка);
	
	Результат = Запрос.Выполнить();
	
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		Если Выборка.ЭтоГруппа И ДляГрупп Тогда
			СпрОбъект = Выборка.Ссылка.ПолучитьОбъект();
			СпрОбъект.КонтрольПоступленияДанных = КонтрольПоступленияДанных;
			СпрОбъект.ДопустимоеВремяОтсутствияДанных = ДопустимоеВремяОтсутствияДанных;
			СпрОбъект.Записать();
		ИначеЕсли НЕ Выборка.ЭтоГруппа И ДляЭлементов Тогда
			СпрОбъект = Выборка.Ссылка.ПолучитьОбъект();
			СпрОбъект.КонтрольПоступленияДанных = КонтрольПоступленияДанных;
			СпрОбъект.ДопустимоеВремяОтсутствияДанных = ДопустимоеВремяОтсутствияДанных;
			СпрОбъект.Записать();
		КонецЕсли;	
	КонецЦикла;

КонецПроцедуры

Функция ПолныйКодДляПоиска()
	ПолныйКод = ЭтотОбъект.КодДляПоиска;
	
	РодительОбъекта = ЭтотОбъект.Родитель;
	ПустаяСсылка = Справочники.ГруппыСчетчиков.ПустаяСсылка();
	Пока РодительОбъекта <> ПустаяСсылка Цикл
		ПолныйКод = РодительОбъекта.КодДляПоиска + "/" + ПолныйКод;
		РодительОбъекта = РодительОбъекта.Родитель;
	КонецЦикла;
	
	Возврат ПолныйКод;
КонецФункции

#КонецЕсли