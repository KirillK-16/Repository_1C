#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Функция СсылкаПоНаименованию(Наименование) Экспорт
	
	Ссылка = Неопределено;
	
	Ссылка = НайтиСсылкуПоНаименованию(Наименование);
	
	Если Ссылка = Неопределено Тогда
		
		НачатьТранзакцию();
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("Справочник.ПараметрыЗамера");
		ЭлементБлокировки.УстановитьЗначение("Наименование", Наименование);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		
		Попытка
			Блокировка.Заблокировать();
			Ссылка = НайтиСсылкуПоНаименованию(Наименование);
			Если Ссылка = Неопределено Тогда
				Ссылка = СоздатьПоНаименованию(Наименование);
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
        Исключение
            ОтменитьТранзакцию();
			ВызватьИсключение "Ожидаемая управляемая блокировка";
		КонецПопытки;
			
	КонецЕсли;
	
	Возврат Ссылка;
	
КонецФункции

Функция НайтиСсылкуПоНаименованию(Наименование)
	
	Ссылка = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	Ссылка
	|ИЗ
	|	Справочник.ПараметрыЗамера
	|ГДЕ
	|	Наименование = &Наименование
	|";
	Запрос.УстановитьПараметр("Наименование", Наименование);
	
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		
		Ссылка = Выборка.Ссылка;
	КонецЕсли;
	
	Возврат Ссылка;
	
КонецФункции

Функция СоздатьПоНаименованию(Наименование)
	
	Элемент = Справочники.ПараметрыЗамера.СоздатьЭлемент();
	Элемент.Наименование = Наименование;
	Элемент.Записать();
	
	Возврат Элемент.Ссылка;
	
КонецФункции

#КонецЕсли

