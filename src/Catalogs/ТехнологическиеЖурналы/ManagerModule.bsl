#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Функция ПолучитьНаименованиеНовогоЭлемента() Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	КОЛИЧЕСТВО(ТехнологическиеЖурналы.Ссылка) КАК КоличествоЭлементов
	|ИЗ
	|	Справочник.ТехнологическиеЖурналы КАК ТехнологическиеЖурналы
	|";
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Возврат "Настройка 1";
	Иначе
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		Возврат "Настройка " + Формат(Выборка.КоличествоЭлементов + 1, "ЧГ=0");
	КонецЕсли;
КонецФункции

Функция ПолучитьУИД_ТЖ(Ссылка) Экспорт
	Возврат "<!--Настройки ЦКК: " + Ссылка.УникальныйИдентификатор();
КонецФункции

Функция УдалитьСвоиСекции(ТекстНастройки, УИД_ТЖ) Экспорт
	ТекстНастройкиБыл = ТекстНастройки;
	
	ИндексУИД = СтрНайти(ТекстНастройкиБыл, УИД_ТЖ);
	Если ИндексУИД > 0 Тогда
		ТекстНастройкиЧистый = Лев(ТекстНастройкиБыл, ИндексУИД - 1);
		ТекстНастройкиБыл = Сред(ТекстНастройкиБыл, ИндексУИД + СтрДлина(УИД_ТЖ));
		
		ИндексУИД = СтрНайти(ТекстНастройкиБыл, УИД_ТЖ);
		Если ИндексУИД > 0 Тогда
			ТекстНастройкиЧистый = ТекстНастройкиЧистый + Сред(ТекстНастройкиБыл, ИндексУИД + СтрДлина(УИД_ТЖ + "-->"));
		КонецЕсли;
	Иначе
		ТекстНастройкиЧистый = ТекстНастройкиБыл;
	КонецЕсли;
	
	Возврат ТекстНастройкиЧистый;
КонецФункции

Функция УдалитьСекциюDUMP(ТекстНастройки) Экспорт
	ТекстНастройкиБыл = ТекстНастройки;
	
	Индекс = СтрНайти(ТекстНастройкиБыл, "<dump");
	Если Индекс > 0 Тогда
		ТекстНастройкиЧистый = Лев(ТекстНастройкиБыл, Индекс - 1);
		ТекстНастройкиБыл = Сред(ТекстНастройкиБыл, Индекс + СтрДлина("<dump"));
		
		Индекс = СтрНайти(ТекстНастройкиБыл, "/>");
		Если Индекс > 0 Тогда
			ТекстНастройкиЧистый = ТекстНастройкиЧистый + Сред(ТекстНастройкиБыл, Индекс + СтрДлина("/>"));
		Иначе
			ТекстНастройкиЧистый = ТекстНастройкиЧистый + ТекстНастройкиБыл;
		КонецЕсли;
	Иначе
		ТекстНастройкиЧистый = ТекстНастройкиБыл;
	КонецЕсли;
	
	Возврат ТекстНастройкиЧистый; 
КонецФункции

Функция ПолучитьПроцедурыКонтроляУстойчивости(Ссылка) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	СпрКП.Выполнять КАК Выполнять,
	|	СпрТЖ.Ссылка КАК ТехнологическийЖурнал,
	|	СпрКП.Ссылка КАК КонтрольнаяПроцедура,
	|	РегПараметрыРС.ОбъектКонтроля,
	|	ISNULL(РегПараметрыРС.КаталогНастроекТЖСетевой, СпрТЖ.КаталогНастройкиТЖ) КАК КаталогНастройкиТЖ,
	|	ISNULL(РегНастройкиСД.КаталогВыгрузкиДамповСетевой, СпрТЖ.КаталогЛоговСетевой) КАК КаталогВыгрузкиДамповСетевой,
	|	ISNULL(РегНастройкиСД.КаталогВыгрузкиДампов, СпрТЖ.КаталогЛогов) КАК КаталогВыгрузкиДампов,
	|	"""" КАК КаталогЭкспортаДампов,
	|	РегНастройкиСД.КаталогВременныхФайлов
	|ИЗ
	|	Справочник.ТехнологическиеЖурналы КАК СпрТЖ
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	РегистрСведений.ПараметрыРабочихСерверов КАК РегПараметрыРС
	|ПО
	|	СпрТЖ.РабочийСервер = РегПараметрыРС.Оборудование
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	Справочник.КонтрольныеПроцедуры КАК СпрКП
	|ПО
	|	СпрКП.ОбъектКонтроля = РегПараметрыРС.ОбъектКонтроля
	|	И СпрКП.Владелец = &ВидКонтрольнойПроцедуры
	|ЛЕВОЕ СОЕДИНЕНИЕ
	|	РегистрСведений.НастройкиСборДампов КАК РегНастройкиСД
	|ПО
	|	РегНастройкиСД.КонтрольнаяПроцедура = СпрКП.Ссылка
	|ГДЕ
	|	СпрТЖ.Ссылка В (&ТЖ)
	|";
	Запрос.УстановитьПараметр("ТЖ", Ссылка);
	Запрос.УстановитьПараметр("ВидКонтрольнойПроцедуры", Справочники.ВидыКонтрольныхПроцедур.КонтрольУстойчивостиСистемы());
	
	Результат = Запрос.Выполнить();
	
	Данные = Общий.РезультатЗапросаВМассив(Результат);
	
	УстановитьПривилегированныйРежим(Истина);
	Для Каждого ТекДанные Из Данные Цикл
		ДанныеХранилища = РегистрыСведений.БезопасноеХранилище.ПолучитьДанные(ТекДанные.КонтрольнаяПроцедура);
		Если ДанныеХранилища = Неопределено Тогда
			ДанныеХранилища = Новый Структура("КаталогЭкспортаДампов", "");
		КонецЕсли;
		ТекДанные.КаталогЭкспортаДампов = ДанныеХранилища.КаталогЭкспортаДампов;
	КонецЦикла;
	УстановитьПривилегированныйРежим(Ложь);
			
	Возврат Данные;
КонецФункции

Функция ПолучитьТехнологическиеЖурналыШаблона(ШаблонСсылка) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТехнологическиеЖурналыШаблоныТЖ.Ссылка КАК ТехнологическийЖурнал
	|ИЗ
	|	Справочник.ТехнологическиеЖурналы.ШаблоныТЖ КАК ТехнологическиеЖурналыШаблоныТЖ
	|ГДЕ
	|	ТехнологическиеЖурналыШаблоныТЖ.ШаблонТЖВыбранный = &ШаблонСсылка
	|";
	
	Запрос.УстановитьПараметр("ШаблонСсылка", ШаблонСсылка);
	Результат = Запрос.Выполнить();
	
	Данные = Общий.РезультатЗапросаВМассив(Результат);
	
	Возврат Данные;
КонецФункции

#КонецЕсли
