#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Функция СоздатьЭлементПоНаименованию(Наименование) Экспорт
	Результат = РезультатЗапросаПоНаименованию(Наименование);
	Если Результат.Пустой() Тогда
		Попытка
			БлокировкаДанных = Новый БлокировкаДанных;
			ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.ОбъектыБлокировок");
			ЭлементБлокировки.УстановитьЗначение("Объект", "Справочник.КлючевыеОперацииЦентрМониторинга." + Наименование);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			БлокировкаДанных.Заблокировать();
		Исключение
			ВызватьИсключение "Центр мониторинга. Ожидаемая управляемая блокировка при создании элемента справочника 'КлючевыеОперацииЦентрМониторинга'";
		КонецПопытки;
		
		Результат = РезультатЗапросаПоНаименованию(Наименование);
		Если Результат.Пустой() Тогда
			НовыйЭлемент = Справочники.КлючевыеОперацииЦентрМониторинга.СоздатьЭлемент();
			НовыйЭлемент.Наименование = Наименование;
			НовыйЭлемент.Записать();
			
			РезультатВыполнения = НовыйЭлемент.Ссылка;
		Иначе
			Выборка = Результат.Выбрать();
			Выборка.Следующий();
			РезультатВыполнения = Выборка.Ссылка;
		КонецЕсли;
	Иначе
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		РезультатВыполнения = Выборка.Ссылка;
	КонецЕсли;
	
	Возврат РезультатВыполнения;
КонецФункции

Функция СоздатьЭлементПоХешу(Хеш, Наименование, Имя) Экспорт
		
	//Проверка на недопустимые символы
	ПроверитьНаличиеНедопустимыхСимволов(Хеш, "Хеш");
	ПроверитьНаличиеНедопустимыхСимволов(Наименование, "Наименование");
	ПроверитьНаличиеНедопустимыхСимволов(Имя, "Имя");
	
	Результат = РезультатЗапросаПоХешу(Хеш);
	Если Результат.Пустой() Тогда
		Попытка
			БлокировкаДанных = Новый БлокировкаДанных;
			ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.ОбъектыБлокировок");
			ЭлементБлокировки.УстановитьЗначение("Объект", "Справочник.КлючевыеОперацииЦентрМониторинга." + Хеш);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			БлокировкаДанных.Заблокировать();
		Исключение
			ВызватьИсключение "Центр мониторинга. Ожидаемая управляемая блокировка при создании элемента справочника 'КлючевыеОперацииЦентрМониторинга'";
		КонецПопытки;
		
		Результат = РезультатЗапросаПоХешу(Хеш);
		Если Результат.Пустой() Тогда
			НовыйЭлемент = Справочники.КлючевыеОперацииЦентрМониторинга.СоздатьЭлемент();
			НовыйЭлемент.Наименование = Наименование;
			НовыйЭлемент.Имя = Имя;
			НовыйЭлемент.ИмяХеш = Хеш;
			НовыйЭлемент.Записать();
			
			РезультатВыполнения = НовыйЭлемент.Ссылка;
		Иначе
			Выборка = Результат.Выбрать();
			Выборка.Следующий();
			РезультатВыполнения = Выборка.Ссылка;
		КонецЕсли;
	Иначе
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		РезультатВыполнения = Выборка.Ссылка;
	КонецЕсли;
	
	Возврат РезультатВыполнения;
КонецФункции

Процедура УстановитьИмяХеш(Ссылка, Имя, ИмяХеш) Экспорт
	БлокировкаДанных = Новый БлокировкаДанных;
	ЭлементБлокировки = БлокировкаДанных.Добавить("Справочник.КлючевыеОперацииЦентрМониторинга");
	ЭлементБлокировки.УстановитьЗначение("Ссылка", Ссылка); 
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	БлокировкаДанных.Заблокировать();
	
	ОбъектКО = Ссылка.ПолучитьОбъект();
	ОбъектКО.Имя = Имя;
	ОбъектКО.ИмяХеш = ИмяХеш;
	ОбъектКО.Записать();
	
КонецПроцедуры

Функция РезультатЗапросаПоНаименованию(Наименование)
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	Ссылка
	|ИЗ
	|	Справочник.КлючевыеОперацииЦентрМониторинга
	|ГДЕ
	|	Наименование = &Наименование
	|";
	Запрос.УстановитьПараметр("Наименование", Наименование);
	Результат = Запрос.Выполнить();
	
	Возврат Результат;
КонецФункции

Функция РезультатЗапросаПоХешу(ИмяХеш)
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	Ссылка
	|ИЗ
	|	Справочник.КлючевыеОперацииЦентрМониторинга
	|ГДЕ
	|	ИмяХеш = &ИмяХеш
	|";
	Запрос.УстановитьПараметр("ИмяХеш", ИмяХеш);
	Результат = Запрос.Выполнить();
	
	Возврат Результат;
КонецФункции

Процедура ПроверитьНаличиеНедопустимыхСимволов(Значение, Имя)
	ПозицияНедопустимогоСимвола = НайтиНедопустимыеСимволыXML(Имя);
	Если ПозицияНедопустимогоСимвола > 0 Тогда
		ВызватьИсключение СтрШаблон("Обнаружен недопустимый символ XML, позиция: %1, реквизит: %2", ПозицияНедопустимогоСимвола, Имя);
	КонецЕсли;
КонецПроцедуры

#КонецЕсли