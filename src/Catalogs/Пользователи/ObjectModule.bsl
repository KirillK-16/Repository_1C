#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ПередЗаписью(Отказ)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	Если ЭтотОбъект.ПометкаУдаления
		Или ДополнительныеСвойства.Свойство(СловарьКлиентСервер.ИмяСвойстваНеВыполнятьСинхронизациюПользователя())
	Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтотОбъект.ЭтоНовый() Тогда
        
        ПользовательИБ = ПользователиИнформационнойБазы.НайтиПоУникальномуИдентификатору(ЭтотОбъект.УникальныйИдентификатор);
		Если ПользовательИБ = Неопределено Тогда
			ПользовательИБ = ПользователиИнформационнойБазы.СоздатьПользователя();
        КонецЕсли;
        
	Иначе
		ПользовательИБ = ПользователиИнформационнойБазы.НайтиПоУникальномуИдентификатору(ЭтотОбъект.УникальныйИдентификатор);
        Если ПользовательИБ = Неопределено Тогда
            
            ПользовательИБ = ПользователиИнформационнойБазы.НайтиПоИмени(ЭтотОбъект.Наименование);
            Если ПользовательИБ = Неопределено Тогда
                ПользовательИБ = ПользователиИнформационнойБазы.СоздатьПользователя();
            КонецЕсли;
            
		КонецЕсли;
	КонецЕсли;
	
	ПользовательИБ.Имя = ЭтотОбъект.Наименование;
	ПользовательИБ.ПоказыватьВСпискеВыбора = ЭтотОбъект.ПоказыватьВСпискеВыбора;
	ПользовательИБ.АутентификацияСтандартная = Истина;
		
	ПользовательИБ.Роли.Очистить();
	СохраненныеРоли = ЭтотОбъект.Роли.Выгрузить();	
	ЭтотОбъект.Роли.Очистить();
	
	Для Каждого ПраваСтрока Из СохраненныеРоли Цикл
		
		МетаданныеРоли = Метаданные.Роли.Найти(ПраваСтрока.Роль);
		Если МетаданныеРоли = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Если ПраваСтрока.Выбрана Тогда
			ПользовательИБ.Роли.Добавить(МетаданныеРоли);
		КонецЕсли;
		
		РолиСтрокаТЧ = ЭтотОбъект.Роли.Добавить();
		РолиСтрокаТЧ.Выбрана = ПраваСтрока.Выбрана;
		РолиСтрокаТЧ.Роль = МетаданныеРоли.Имя;
		РолиСтрокаТЧ.СинонимРоли = МетаданныеРоли.Синоним;
			
	КонецЦикла;
	
	Если ЭтотОбъект.ДополнительныеСвойства.Свойство(СловарьКлиентСервер.ИмяСвойстваСохраняемыйПарольПользователя()) Тогда
		ПользовательИБ.Пароль = ЭтотОбъект.ДополнительныеСвойства[СловарьКлиентСервер.ИмяСвойстваСохраняемыйПарольПользователя()];
	КонецЕсли;
	
	ПользовательИБ.Записать();
	ЭтотОбъект.УникальныйИдентификатор = ПользовательИБ.УникальныйИдентификатор;
	
	Если ЭтоНовый() Тогда 
		Если ПользовательИБ.Роли.Содержит(Метаданные.Роли.ЦентрМониторингаРуководителиГруппы) Тогда 
			ЭтотОбъект.УстановитьСсылкуНового(Справочники.Пользователи.ПолучитьСсылку());
			ГруппаПользователей = Справочники.ГруппыПользователейЦентрМониторинга.СоздатьЭлемент();
			ГруппаПользователей.РуководительГруппы = ЭтотОбъект.ПолучитьСсылкуНового();
			ГруппаПользователей.Наименование = "Группа " + ЭтотОбъект.Наименование;
			ГруппаПользователей.Записать();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередУдалением(Отказ)
	
	ПользовательИБ = ПользователиИнформационнойБазы.НайтиПоУникальномуИдентификатору(ЭтотОбъект.УникальныйИдентификатор);
	Если ПользовательИБ <> Неопределено Тогда
		ПользовательИБ.Удалить();
	КонецЕсли;
	
	НаборСвязей = РегистрыСведений.СвязьПользовательРоль.СоздатьНаборЗаписей();
	НаборСвязей.Отбор.Пользователь.Установить(ЭтотОбъект.Ссылка);
	НаборСвязей.Записать();
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	УникальныйИдентификатор = "";
	Наименование = "";
КонецПроцедуры

#КонецЕсли