
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
		
	УстановитьПароль = Ложь;
	Пароль = "";
	ПодтверждениеПароля = "";
	Элементы.Пароль.ТолькоПросмотр = Истина;
	Элементы.ПодтверждениеПароля.ТолькоПросмотр = Истина;
	
	ВыборкаРолейПользователя = УправлениеЗадачами.ВыборкаРолейПользователей(
		Новый Структура("Пользователь", Объект.Ссылка)
	);
	Пока ВыборкаРолейПользователя.Следующий() Цикл
		НоваяСтрокаРоли = РолиПользователя.Добавить(); 
		НоваяСтрокаРоли.РольСсылка = ВыборкаРолейПользователя.Роль;
	КонецЦикла;	
	
	Роли = Объект.Роли;
	ТипыПрав = Метаданные.Роли;
	Для Каждого Права ИЗ ТипыПрав Цикл
		СуществующиеРоли = Роли.НайтиСтроки(Новый Структура("Роль", Права.Имя));
		Если СуществующиеРоли.Количество() = 0 Тогда
			НоваяРоль = Роли.Добавить();
			НоваяРоль.Роль = Права.Имя;
			НоваяРоль.СинонимРоли = Права.Синоним;
			НоваяРоль.Выбрана = Ложь;
		КонецЕсли;
	КонецЦикла;
	
	ИндексРоли = 0;
	СохраненныеРоли = Роли.Выгрузить();
	Для Каждого ПраваСтрока Из СохраненныеРоли Цикл
		ИмяРоли = ПраваСтрока.Роль;
		МетаданныеРоли = Метаданные.Роли.Найти(ИмяРоли);
		Если МетаданныеРоли = Неопределено Тогда
			Роли.Удалить(ИндексРоли);
		Иначе
			ИндексРоли = ИндексРоли + 1;
		КонецЕсли;
    КонецЦикла;
    
    Если ЭтотОбъект.Параметры.Свойство("ТекущийЭлемент") Тогда
        ЭтотОбъект.ТекущийЭлемент = Элементы[ЭтотОбъект.Параметры.ТекущийЭлемент];
    КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Оповестить("Справочник.Пользователи.ПослеЗаписи");
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если УстановитьПароль И Пароль <> ПодтверждениеПароля Тогда
		
		ОбщийКлиентСервер.СгенерироватьСообщениеПользователю(
			"Введённое значение пароля не совпадает с подтверждением!",
			"Объект.Пароль",
			Объект
		);
		
		Отказ = Истина;
	КонецЕсли;

	Если СтрНайти(Объект.НомерТелефона, " ") > 0 Тогда 
		Если НЕ ОбщийКлиентСервер.ТелефонЗаполнен(Объект.НомерТелефона, Элементы.НомерТелефона.Маска) Тогда
			Объект.НомерТелефона = "";
		Иначе	
			
			ОбщийКлиентСервер.СгенерироватьСообщениеПользователю(
				"Номер телефона заполнен некорректно!",
				"Объект.НомерТелефона",
				Объект
			);

			Отказ = Истина;
		КонецЕсли;
		
	КонецЕсли;

	Если НЕ ПустаяСтрока(Объект.АдресЭлектроннойПочты) И НЕ ОбщийКлиентСервер.ЭтоАдресЭлектроннойПочты(Объект.АдресЭлектроннойПочты) Тогда
		
		ОбщийКлиентСервер.СгенерироватьСообщениеПользователю(
			"Адрес заполнен некорректно!",
			"Объект.АдресЭлектроннойПочты",
			Объект
		);

		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ВыборкаСвязей = РегистрыСведений.СвязьПользовательРоль.Выбрать(
		Новый Структура("Пользователь", ТекущийОбъект.Ссылка)
	);
	НаУдаление = Новый Массив;
	Пока ВыборкаСвязей.Следующий() Цикл
		Роль = ВыборкаСвязей.Роль;
		Если РолиПользователя.НайтиСтроки(Новый Структура("РольСсылка", Роль)).Количество() = 0 Тогда
			НаУдаление.Добавить(Роль);
		КонецЕсли;
	КонецЦикла;
	Для Каждого Роль Из НаУдаление Цикл
		Если ЧислоСвязей(, Роль) = 1 Тогда
			Сообщить("Сохранение невозможно! После сохранения роль " + Строка(Роль) + " не будет содержать ни одного пользователя.");
			Отказ = Истина;
			Возврат;
		КонецЕсли;	
	КонецЦикла;	
		
	Если УстановитьПароль Тогда
		
		ТекущийОбъект.ДополнительныеСвойства.Вставить(
			СловарьКлиентСервер.ИмяСвойстваСохраняемыйПарольПользователя(), 
			Пароль
		);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	НаборСвязей = РегистрыСведений.СвязьПользовательРоль.СоздатьНаборЗаписей();
	НаборСвязей.Отбор.Пользователь.Установить(ТекущийОбъект.Ссылка);
	НаборСвязей.Записать();
	
	Для Каждого РольСтрока Из РолиПользователя Цикл
		УправлениеЗадачами.СвязатьПользователяИРоль(
			ТекущийОбъект.Ссылка,
			РольСтрока.РольСсылка
		);
	КонецЦикла;	
		
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	НомерСтроки = 0;
	Для Каждого СтрокаРоли Из РолиПользователя Цикл
		
		Если СтрокаРоли.РольСсылка.Пустая() Тогда
			ОбщийКлиентСервер.СгенерироватьСообщениеПользователю(
				"Роль не выбрана!", 
				"РолиПользователя[" + НомерСтроки + "].РольСсылка", 
				ЭтотОбъект
			);
			Отказ = Истина;
		КонецЕсли;
		НомерСтроки = НомерСтроки + 1;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура РолиПользователяРольСсылкаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	Если РольУжеВыбрана(ВыбранноеЗначение) Тогда
		ПоказатьПредупреждение(,"Роль '" + ВыбранноеЗначение + "' уже выбрана");
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПарольПриИзменении(Элемент)
	РежимЗаполненияПароля = НЕ УстановитьПароль;
	Элементы.Пароль.ТолькоПросмотр = РежимЗаполненияПароля;
	Элементы.ПодтверждениеПароля.ТолькоПросмотр = РежимЗаполненияПароля;
	
	Если НЕ РежимЗаполненияПароля Тогда
		
		Пароль = "";
		ПодтверждениеПароля = "";
	
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура РолиПользователяПриИзменении(Элемент)
	ЭтотОбъект.Модифицированность = Истина;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ЧислоСвязей(ПользовательСсылка = Неопределено, РольСсылка = Неопределено)
	
	Фильтр = Новый Структура;
	Если ПользовательСсылка <> Неопределено Тогда
		Фильтр.Вставить("Пользователь", ПользовательСсылка);	
	КонецЕсли;
	
	Если РольСсылка <> Неопределено Тогда
		Фильтр.Вставить("Роль", РольСсылка);	
	КонецЕсли;

	ВыборкаСвязей = РегистрыСведений.СвязьПользовательРоль.Выбрать(
		Фильтр
	);
	
	Результат = 0;
	Пока ВыборкаСвязей.Следующий() Цикл
		Результат = Результат + 1;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция РольУжеВыбрана(Роль)
	Возврат РолиПользователя.НайтиСтроки(
		Новый Структура("РольСсылка", Роль)
	).Количество() > 0;
КонецФункции

#КонецОбласти











