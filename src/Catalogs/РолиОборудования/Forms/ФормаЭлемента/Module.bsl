
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
    
    Если Объект.Ссылка.Предопределенный Тогда
		ЭтотОбъект.Элементы.СчетчикиПроизводительностиЗаполнитьПоШаблону.Видимость = Истина;
		ЭтотОбъект.Элементы.Наименование.РедактированиеТекста = Ложь;
    КонецЕсли;
    
    ОборудованиеМассив = ПолучитьОборудование(Объект.Ссылка);
    ОбновитьТаблицуОборудования(ОборудованиеМассив, ЭтотОбъект.Оборудование);
    
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаполнитьПоШаблону(Команда)
    
    Объект.СчетчикиПроизводительности.Очистить();
    Объект.СчетчикиПроизводительностиLinux.Очистить();
	
	Счетчики = ПолучитьСчетчикиПоШаблонуНаСервере(Объект.Ссылка);
    
	Для Каждого ТекСчетчик Из Счетчики.Windows Цикл
		НовСтрока = Объект.СчетчикиПроизводительности.Добавить();
		НовСтрока.СчетчикПроизводительности = ТекСчетчик;
    КонецЦикла;
    
    Для Каждого ТекСчетчик Из Счетчики.Linux Цикл
		НовСтрока = Объект.СчетчикиПроизводительностиLinux.Добавить();
		НовСтрока.СчетчикПроизводительности = ТекСчетчик;
    КонецЦикла;
    
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьОборудование(Команда)
    
    ОборудованиеМассив = ПолучитьОборудование(Объект.Ссылка);
    ОбновитьТаблицуОборудования(ОборудованиеМассив, ЭтотОбъект.Оборудование);
    
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВсем(Команда)
    
    Для Каждого ТекСтрока Из ЭтотОбъект.Оборудование Цикл
        ТекСтрока.Применить = Истина;
    КонецЦикла;
    
КонецПроцедуры

&НаКлиенте
Процедура СнятьВсем(Команда)
    
    Для Каждого ТекСтрока Из ЭтотОбъект.Оборудование Цикл
        ТекСтрока.Применить = Ложь;
    КонецЦикла;
    
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСчетчики(Команда)
    
    Если Модифицированность Тогда
        ОписаниеОповещения = Новый ОписаниеОповещения("УстановитьСчетчикиВопрос", ЭтотОбъект);
        ПоказатьВопрос(ОписаниеОповещения, НСтр("ru = 'Для синхронизации необходимо записать изменения. Записать и продолжить?'"), РежимДиалогаВопрос.ДаНетОтмена);
    Иначе
        УстановитьСчетчикиОбщая();
    КонецЕсли;
    
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ОборудованиеВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
    
    Если Поле.Имя = "ОборудованиеОборудование" Тогда
        ПараметрыОткрытия = Новый Структура("Ключ", Элемент.ТекущиеДанные.Оборудование);
        ОткрытьФорму("Справочник.Оборудование.Форма.ФормаЭлемента", ПараметрыОткрытия);
        СтандартнаяОбработка = Ложь;
    КонецЕсли;
    
КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ПолучитьСчетчикиПоШаблонуНаСервере(Ссылка)
    
    Счетчики = Новый Структура("Windows, Linux", Новый Массив, Новый Массив);
    
    Если Ссылка = Справочники.РолиОборудования.СерверWindows Тогда
        Счетчики.Windows = Справочники.РолиОборудования.ПолучитьСчетчикиПроизводительностиПредопределенного(Ссылка, Перечисления.СчетчикПроизводительностиПрименимо.Windows);
    ИначеЕсли Ссылка = Справочники.РолиОборудования.СерверLinux Тогда
        Счетчики.Linux = Справочники.РолиОборудования.ПолучитьСчетчикиПроизводительностиПредопределенного(Ссылка, Перечисления.СчетчикПроизводительностиПрименимо.Linux);
    ИначеЕсли Ссылка = Справочники.РолиОборудования.СерверMSSQL Тогда
        Счетчики.Windows = Справочники.РолиОборудования.ПолучитьСчетчикиПроизводительностиПредопределенного(Ссылка, Перечисления.СчетчикПроизводительностиПрименимо.Windows);
    КонецЕсли;
            
	Возврат Счетчики;
    
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьОборудование(РольОборудования)
    Возврат Справочники.РолиОборудования.ПолучитьОборудование(РольОборудования);
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьТаблицуОборудования(МассивСсылок, Оборудование)
    
    Оборудование.Очистить();
    Для Каждого ТекСсылка Из МассивСсылок Цикл
        НовСтрока = Оборудование.Добавить();
        НовСтрока.Применить = Истина;
        НовСтрока.Оборудование = ТекСсылка;
    КонецЦикла;
        
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СинхронизироватьСчетчики(Роль, Оборудование, Счетчики)
    
    Для Каждого ТекОборудование Из Оборудование Цикл
        Справочники.Оборудование.СинхронизироватьСчетчикиПроизводительности(Роль, ТекОборудование, Счетчики);
    КонецЦикла;
        
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСчетчикиВопрос(РезультатВопроса, ДополнительныеПараметры) Экспорт
    
    Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
        ЭтотОбъект.Записать();
        УстановитьСчетчикиОбщая();
    КонецЕсли;
    
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСчетчикиОбщая()
    
    ОборудованиеМассив = Новый Массив;
    Для Каждого ТекСтрока Из ЭтотОбъект.Оборудование Цикл
        Если ТекСтрока.Применить Тогда
            ОборудованиеМассив.Добавить(ТекСтрока.Оборудование);
        КонецЕсли;
    КонецЦикла;
    
    СчетчикиПроизводительностиМассив = Новый Массив;
    Для Каждого ТекСтрока Из Объект.СчетчикиПроизводительности Цикл
        СчетчикиПроизводительностиМассив.Добавить(ТекСтрока.СчетчикПроизводительности);
    КонецЦикла;
    
    СинхронизироватьСчетчики(Объект.Ссылка, ОборудованиеМассив, СчетчикиПроизводительностиМассив);
    
КонецПроцедуры

#КонецОбласти
