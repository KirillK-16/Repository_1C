#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область Экспортные_Процедуры_и_функции

Функция ВыполнитьОповещение(ОповещениеСсылка) Экспорт
    
    Замер = ВнутренниеЗамеры.НачатьЗамер("Выполнить оповещение");
    ДопПараметры = Новый Соответствие;
    ДопПараметры.Вставить("Объект ЦКК", ОповещениеСсылка.Наименование);
    
    ТекДата = ТекущаяУниверсальнаяДата();
    
    ТЗнИнциденты = ПолучитьИнциденты(ОповещениеСсылка, ТекДата);
    
    Если ТЗнИнциденты.Количество() > 0 Тогда
        
        ЭтоНовое = ТЗнИнциденты[0].Новое;
        
    Иначе
     
	    ВнутренниеЗамеры.ЗавершитьЗамер(Замер, ДопПараметры);
        
        Если РегистрыСведений.ТекущееСостояниеПовторногоОповещенияОперативного.ОповещениеЗакрыто(ОповещениеСсылка) Тогда
            Возврат ТекДата;
        Иначе
            Возврат ТекДата + ОповещениеСсылка.ПериодГруппировкиОповещений;
        КонецЕсли;
                
    КонецЕсли;
    
    ПользователиДляОтправки = ПользователиДляОтправки(ОповещениеСсылка);
    
    ПараметрыОтбораОткрытие = Новый Структура("Открытие", Истина);
    ИнцидентыОткрытие = ТЗнИнциденты.НайтиСтроки(ПараметрыОтбораОткрытие);
    
    ПараметрыОтбораЗакрытие = Новый Структура("Открытие", Ложь);
    ИнцидентыЗакрытие = ТЗнИнциденты.НайтиСтроки(ПараметрыОтбораЗакрытие);
    
    ТекстыОповещенияОткрытие = Неопределено;
    Если ИнцидентыОткрытие.Количество() > 0 Тогда 
        ТекстыОповещенияОткрытие = Новый Структура;
        ТекстыОповещенияОткрытие.Вставить("Тема", ПолучитьТекстОповещения(ОповещениеСсылка.ТемаОповещения, ИнцидентыОткрытие,,,ОповещениеСсылка.Наименование));
        ТекстыОповещенияОткрытие.Вставить("ТелоПисьма", ПолучитьТекстОповещения(ОповещениеСсылка.ТекстОповещения, ИнцидентыОткрытие,,,ОповещениеСсылка.Наименование));
        ТекстыОповещенияОткрытие.Вставить("ТелоСМС", ПолучитьТекстОповещения(ОповещениеСсылка.ТекстОповещенияСмс, ИнцидентыОткрытие,,,ОповещениеСсылка.Наименование));
    КонецЕсли;
    
    ТекстыОповещенияЗакрытие = Неопределено;
    Если ИнцидентыЗакрытие.Количество() > 0 Тогда
        ТекстыОповещенияЗакрытие = Новый Структура;
        Если ОповещениеСсылка.ОтдельныеШаблоныОЗакрытии Тогда
            ТекстыОповещенияЗакрытие.Вставить("Тема", ПолучитьТекстОповещения(ОповещениеСсылка.ТемаОповещенияЗакрытие, ИнцидентыЗакрытие,,,ОповещениеСсылка.Наименование));
            ТекстыОповещенияЗакрытие.Вставить("ТелоПисьма", ПолучитьТекстОповещения(ОповещениеСсылка.ТекстОповещенияЗакрытие, ИнцидентыЗакрытие,,,ОповещениеСсылка.Наименование));
            ТекстыОповещенияЗакрытие.Вставить("ТелоСМС", ПолучитьТекстОповещения(ОповещениеСсылка.ТекстОповещенияСмсЗакрытие, ИнцидентыЗакрытие,,,ОповещениеСсылка.Наименование));
        Иначе
            ТекстыОповещенияЗакрытие.Вставить("Тема", ПолучитьТекстОповещения(ОповещениеСсылка.ТемаОповещения, ИнцидентыЗакрытие,,,ОповещениеСсылка.Наименование));
            ТекстыОповещенияЗакрытие.Вставить("ТелоПисьма", ПолучитьТекстОповещения(ОповещениеСсылка.ТекстОповещения, ИнцидентыЗакрытие,,,ОповещениеСсылка.Наименование));
            ТекстыОповещенияЗакрытие.Вставить("ТелоСМС", ПолучитьТекстОповещения(ОповещениеСсылка.ТекстОповещенияСмс, ИнцидентыЗакрытие,,,ОповещениеСсылка.Наименование));            
        КонецЕсли;
    КонецЕсли;
        
    НачатьТранзакцию();
    
    Попытка
        
        ВремяСледующегоОповещения = ТекДата + ЗаписатьТекущееСостояние(ОповещениеСсылка, ЭтоНовое, ТЗнИнциденты, ТекДата);;
        ОповеститьПользователей(ПользователиДляОтправки, ТекстыОповещенияОткрытие, ТекстыОповещенияЗакрытие, ОповещениеСсылка);
        
        ЗафиксироватьТранзакцию();
        
    Исключение
        Комментарий = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
        ЗаписьЖурналаРегистрации("ВыполнитьОповещение", УровеньЖурналаРегистрации.Ошибка,,,Комментарий);
        ВремяСледующегоОповещения = ТекДата + 10;
        ОтменитьТранзакцию();
        
    КонецПопытки;
    
    ВнутренниеЗамеры.ЗавершитьЗамер(Замер, ДопПараметры);
    
    Возврат ВремяСледующегоОповещения; 
    
КонецФункции

Функция ЗаписатьТекущееСостояние(ОповещениеСсылка, ЭтоНовое, ТЗнИнциденты, ТекДата)
    
    ЕстьПовторноеОповещение = Ложь;
    ЕстьУдаление = Ложь;
    
    Для Каждого ТекИнцидент Из ТЗнИнциденты Цикл
        
        МенеджерЗаписи = РегистрыСведений.ТекущееСостояниеОповещенияОперативного.СоздатьМенеджерЗаписи();
        
        // Первое срабатывание инцидента
        Если ТекИнцидент.Открытие И НЕ ТекИнцидент.ОтправитьПовторно Тогда
            
            МенеджерЗаписи.Оповещение = ТекИнцидент.Оповещение;
            МенеджерЗаписи.Инцидент = ТекИнцидент.Инцидент;
            МенеджерЗаписи.ТипИнцидента = ТекИнцидент.ТипИнцидента;
            МенеджерЗаписи.ДатаСоздания = ТекДата;
            МенеджерЗаписи.Записать(Истина);
            
        // Повторное срабатывание активного инцидента    
        ИначеЕсли ТекИнцидент.Открытие И ТекИнцидент.ОтправитьПовторно Тогда
            
            МенеджерЗаписи.Оповещение = ТекИнцидент.Оповещение;
            МенеджерЗаписи.Инцидент = ТекИнцидент.Инцидент;
            МенеджерЗаписи.ТипИнцидента = ТекИнцидент.ТипИнцидента;
            МенеджерЗаписи.Прочитать();
            
            МенеджерЗаписи.ДатаОтправки = ТекДата;
            МенеджерЗаписи.Записать(Истина);
            
            ЕстьПовторноеОповещение = Истина;
            
        // ЗакрытиеИнцидента     
        ИначеЕсли НЕ ТекИнцидент.Открытие Тогда
            
            МенеджерЗаписи.Оповещение = ТекИнцидент.Оповещение;
            МенеджерЗаписи.Инцидент = ТекИнцидент.Инцидент;
            МенеджерЗаписи.ТипИнцидента = ТекИнцидент.ТипИнцидента;
            МенеджерЗаписи.Удалить();
            
            ЕстьУдаление = Истина;
            
        КонецЕсли;
        
    КонецЦикла;
    
    ОповещениеНеАктивно = Ложь;
    Если ЕстьУдаление Тогда
        
        Запрос = Новый Запрос;
        
        Запрос.Текст = "
        |ВЫБРАТЬ ПЕРВЫЕ 1
        |   Оповещение
        |ИЗ
        |   РегистрСведений.ТекущееСостояниеОповещенияОперативного
        |ГДЕ
        |   Оповещение = &Оповещение
        |";
        
        Запрос.УстановитьПараметр("Оповещение", ОповещениеСсылка);
        
        Результат = Запрос.Выполнить();
        ОповещениеНеАктивно = Результат.Пустой();
               
    КонецЕсли;
    
    Если ОповещениеНеАктивно Тогда
        
         МенеджерЗаписиПовторногоОповещения = РегистрыСведений.ТекущееСостояниеПовторногоОповещенияОперативного.СоздатьМенеджерЗаписи();
         МенеджерЗаписиПовторногоОповещения.Оповещение = ОповещениеСсылка;
         МенеджерЗаписиПовторногоОповещения.Удалить();
         
         Возврат 0;
         
    Иначе
        
        Если ЭтоНовое Тогда
            
            МенеджерЗаписиПовторногоОповещения = РегистрыСведений.ТекущееСостояниеПовторногоОповещенияОперативного.СоздатьМенеджерЗаписи();
            МенеджерЗаписиПовторногоОповещения.Оповещение = ОповещениеСсылка;
            МенеджерЗаписиПовторногоОповещения.ВремяГрупповогоОповещения = ТекДата;
            МенеджерЗаписиПовторногоОповещения.ВремяПовторногоОповещения = ТекДата;
            
        Иначе    
            
            МенеджерЗаписиПовторногоОповещения = РегистрыСведений.ТекущееСостояниеПовторногоОповещенияОперативного.СоздатьМенеджерЗаписи();
            МенеджерЗаписиПовторногоОповещения.Оповещение = ОповещениеСсылка;
            МенеджерЗаписиПовторногоОповещения.Прочитать();
            МенеджерЗаписиПовторногоОповещения.Оповещение = ОповещениеСсылка;
            МенеджерЗаписиПовторногоОповещения.ВремяГрупповогоОповещения = ТекДата;
            Если ЕстьПовторноеОповещение Тогда
                МенеджерЗаписиПовторногоОповещения.ВремяПовторногоОповещения = ТекДата;
            КонецЕсли;
            
        КонецЕсли;
    
        МенеджерЗаписиПовторногоОповещения.Записать(Истина);
        
        Возврат ОповещениеСсылка.ПериодГруппировкиОповещений; 
        
    КонецЕсли;
        
КонецФункции

Процедура ОповеститьПользователей(ПользователиДляОтправки, ТекстыОповещенияОткрытие, ТекстыОповещенияЗакрытие, ОповещениеСсылка)
    
    ЕстьОткрытие = ЗначениеЗаполнено(ТекстыОповещенияОткрытие);
    ЕстьЗакрытие = ЗначениеЗаполнено(ТекстыОповещенияЗакрытие);
    
    Для Каждого ТекПользователь Из ПользователиДляОтправки Цикл
        
        ТекПользовательЗначение = ТекПользователь.Значение; 
        
        Если ЕстьОткрытие Тогда
            Оповещение.ОповеститьПользователя(
                ТекстыОповещенияОткрытие.Тема,
                ТекстыОповещенияОткрытие, 
    			ТекПользователь.Ключ,
    			ТекПользовательЗначение.ПоСмс,
    			ТекПользовательЗначение.ПоПочте,
                ТекПользовательЗначение.ПоСкайпу,
                ТекПользовательЗначение.ПоRestAPI,
    			Истина,
    			ТекПользовательЗначение.ОтложитьДо,
    			ТекПользовательЗначение.НеУведомлятьУдалять,
                ОповещениеСсылка);            
        КонецЕсли;
        
        Если ЕстьЗакрытие И ТекПользовательЗначение.ОповещатьЗакрыт Тогда
            Оповещение.ОповеститьПользователя(
                ТекстыОповещенияЗакрытие.Тема,
                ТекстыОповещенияЗакрытие, 
    			ТекПользователь.Ключ,
    			ТекПользовательЗначение.ПоСмс,
    			ТекПользовательЗначение.ПоПочте,
                ТекПользовательЗначение.ПоСкайпу,
                ТекПользовательЗначение.ПоRestAPI,
    			Истина,
    			ТекПользовательЗначение.ОтложитьДо,
    			ТекПользовательЗначение.НеУведомлятьУдалять,
                ОповещениеСсылка);
        КонецЕсли;
                
    КонецЦикла;
        
КонецПроцедуры

Функция ПолучитьИнциденты(ОповещениеСсылка, ТекДата)
    
    ТекстЗапроса = "
    |ВЫБРАТЬ
    |   ИСТИНА КАК Открытие,
    |   ТекущееСостояниеПовторногоОповещения.Оповещение IS NULL КАК Новое,
    |   ВЫБОР
    |       КОГДА (ТекущееСостояниеПовторногоОповещения.Оповещение IS NULL) ТОГДА ИСТИНА
    |       КОГДА (ТекущееСостояниеОповещения.ДатаСоздания IS NULL И ТекущееСостояниеПовторногоОповещения.ВремяГрупповогоОповещения <= ДОБАВИТЬКДАТЕ(&ТекДата, СЕКУНДА, -&ПериодГруппировкиОповещений)) ТОГДА ИСТИНА
    |       КОГДА (НЕ ТекущееСостояниеОповещения.ДатаСоздания IS NULL И ТекущееСостояниеПовторногоОповещения.ВремяПовторногоОповещения <= ДОБАВИТЬКДАТЕ(&ТекДата, СЕКУНДА, -ISNULL(ПовторноеОповещение.ПериодПовторногоОповещения, 315360000))) ТОГДА ИСТИНА
    |       ИНАЧЕ ЛОЖЬ
    |   КОНЕЦ КАК Отправить,
    |   ВЫБОР
    |       КОГДА (ТекущееСостояниеПовторногоОповещения.Оповещение IS NULL) ТОГДА ЛОЖЬ
    |       КОГДА (НЕ ТекущееСостояниеОповещения.ДатаСоздания IS NULL И ТекущееСостояниеПовторногоОповещения.ВремяПовторногоОповещения <= ДОБАВИТЬКДАТЕ(&ТекДата, СЕКУНДА, -ISNULL(ПовторноеОповещение.ПериодПовторногоОповещения, 315360000))) ТОГДА ИСТИНА
    |       ИНАЧЕ ЛОЖЬ
    |   КОНЕЦ КАК ОтправитьПовторно,
    |   РегСвИнцидентыАктивные.ТипИнцидента КАК ТипИнцидента,
    |   РегСвИнцидентыАктивные.Инцидент КАК Инцидент,
    |   Инциденты.Наименование КАК НаименованиеИнцидента,
    |   РегСвИнциденты.Статус КАК Статус,
    |   РегСвИнциденты.ПоследнееСрабатывание КАК ПоследнееСрабатывание,
    |   РегСвИнциденты.ДатаЗакрытия КАК ДатаЗакрытия,
    |   РегСвИнциденты.ЧислоСрабатываний КАК ЧислоСрабатываний,
    |   РегСвИнциденты.ИнформационнаяБаза КАК ИнформационнаяБаза,
    |   РегСвИнциденты.Кластер КАК Кластер,
    |   РегСвИнциденты.Ответственный КАК Ответственный,
    |   РегСвИнциденты.ПодробноеСообщение КАК ПодробноеСообщение,
    |   РегСвИнциденты.ДатаРеакции КАК ДатаРеакции,
    |   РегСвИнциденты.НеОповещатьДо КАК НеОповещатьДо,
    |   РегСвИнциденты.ПроигнорироватьДо КАК ПроигнорироватьДо,
    |   РегСвИнциденты.ДатаОткрытияПовторная КАК ДатаОткрытияПовторная,
    |   РегСвИнциденты.ЧислоСрабатыванийПовторное КАК ЧислоСрабатыванийПовторное,
    |   РегСвИнциденты.УровеньИнцидента КАК УровеньИнцидента,
    |   Инциденты.Наименование КАК КодИнцидента,
    |   &Оповещение КАК Оповещение
    |ИЗ
    |   РегистрСведений.ИнцидентыАктивные КАК РегСвИнцидентыАктивные
    |ВНУТРЕННЕЕ СОЕДИНЕНИЕ
    |   РегистрСведений.Инциденты КАК РегСвИнциденты
    |ПО
    |   РегСвИнциденты.ТипИнцидента = РегСвИнцидентыАктивные.ТипИнцидента
    |   И РегСвИнциденты.Инцидент = РегСвИнцидентыАктивные.Инцидент
    |ВНУТРЕННЕЕ СОЕДИНЕНИЕ
    |   Справочник.Инциденты КАК Инциденты
    |ПО
    |   Инциденты.Ссылка = РегСвИнциденты.Инцидент
    |ЛЕВОЕ СОЕДИНЕНИЕ
    |   Справочник.ОповещенияОперативные.ПовторноеОповещение КАК ПовторноеОповещение
    |ПО
    |   ПовторноеОповещение.Ссылка = &Оповещение
    |   И ПовторноеОповещение.УровеньИнцидента = РегСвИнциденты.УровеньИнцидента
    |   И ПовторноеОповещение.Использовать
    |ЛЕВОЕ СОЕДИНЕНИЕ
    |   РегистрСведений.ТекущееСостояниеПовторногоОповещенияОперативного КАК ТекущееСостояниеПовторногоОповещения
    |ПО
    |   ТекущееСостояниеПовторногоОповещения.Оповещение = &Оповещение
    |ЛЕВОЕ СОЕДИНЕНИЕ
    |   РегистрСведений.ТекущееСостояниеОповещенияОперативного КАК ТекущееСостояниеОповещения
    |ПО
    |   ТекущееСостояниеОповещения.Оповещение = &Оповещение
    |   И ТекущееСостояниеОповещения.Инцидент = РегСвИнциденты.Инцидент
    |   И ТекущееСостояниеОповещения.ТипИнцидента = РегСвИнциденты.ТипИнцидента
    |ГДЕ
    |   ВЫБОР
    |       КОГДА (ТекущееСостояниеПовторногоОповещения.Оповещение IS NULL) ТОГДА ИСТИНА
    |       КОГДА (ТекущееСостояниеОповещения.ДатаСоздания IS NULL И ТекущееСостояниеПовторногоОповещения.ВремяГрупповогоОповещения <= ДОБАВИТЬКДАТЕ(&ТекДата, СЕКУНДА, -&ПериодГруппировкиОповещений)) ТОГДА ИСТИНА
    |       КОГДА (НЕ ТекущееСостояниеОповещения.ДатаСоздания IS NULL И ТекущееСостояниеПовторногоОповещения.ВремяПовторногоОповещения <= ДОБАВИТЬКДАТЕ(&ТекДата, СЕКУНДА, -ISNULL(ПовторноеОповещение.ПериодПовторногоОповещения, 315360000))) ТОГДА ИСТИНА
    |       ИНАЧЕ ЛОЖЬ
    |   КОНЕЦ
    |
    |ОБЪЕДИНИТЬ ВСЕ
    |
    |ВЫБРАТЬ
    |   ЛОЖЬ КАК Открытие,
    |   ЛОЖЬ КАК Новое,
    |   ИСТИНА КАК Отправить,
    |   ЛОЖЬ КАК ОтправитьПовторно,
    |   ТекущееСостояниеОповещения.ТипИнцидента КАК ТипИнцидента,
    |   ТекущееСостояниеОповещения.Инцидент КАК Инцидент,
    |   Инциденты.Наименование КАК НаименованиеИнцидента,
    |   РегСвИнциденты.Статус КАК Статус,
    |   РегСвИнциденты.ПоследнееСрабатывание КАК ПоследнееСрабатывание,
    |   РегСвИнциденты.ДатаЗакрытия КАК ДатаЗакрытия,
    |   РегСвИнциденты.ЧислоСрабатываний КАК ЧислоСрабатываний,
    |   РегСвИнциденты.ИнформационнаяБаза КАК ИнформационнаяБаза,
    |   РегСвИнциденты.Кластер КАК Кластер,
    |   РегСвИнциденты.Ответственный КАК Ответственный,
    |   РегСвИнциденты.ПодробноеСообщение КАК ПодробноеСообщение,
    |   РегСвИнциденты.ДатаРеакции КАК ДатаРеакции,
    |   РегСвИнциденты.НеОповещатьДо КАК НеОповещатьДо,
    |   РегСвИнциденты.ПроигнорироватьДо КАК ПроигнорироватьДо,
    |   РегСвИнциденты.ДатаОткрытияПовторная КАК ДатаОткрытияПовторная,
    |   РегСвИнциденты.ЧислоСрабатыванийПовторное КАК ЧислоСрабатыванийПовторное,
    |   РегСвИнциденты.УровеньИнцидента КАК УровеньИнцидента,
    |   Инциденты.Наименование КАК КодИнцидента,
    |   ТекущееСостояниеОповещения.Оповещение КАК Оповещение
    |ИЗ
    |   РегистрСведений.ТекущееСостояниеОповещенияОперативного КАК ТекущееСостояниеОповещения
    |ВНУТРЕННЕЕ СОЕДИНЕНИЕ
    |   РегистрСведений.Инциденты КАК РегСвИнциденты
    |ПО
    |   РегСвИнциденты.ТипИнцидента = ТекущееСостояниеОповещения.ТипИнцидента
    |   И РегСвИнциденты.Инцидент = ТекущееСостояниеОповещения.Инцидент
    |   И РегСвИнциденты.Статус В (&СтатусыЗакрыто)
    |ВНУТРЕННЕЕ СОЕДИНЕНИЕ
    |   Справочник.Инциденты КАК Инциденты
    |ПО
    |   Инциденты.Ссылка = РегСвИнциденты.Инцидент
    |ГДЕ
    |   ТекущееСостояниеОповещения.Оповещение = &Оповещение
    |";
    
    Компоновщик = Новый КомпоновщикНастроекКомпоновкиДанных();
    СКД = Новый СхемаКомпоновкиДанных();
    ИсточникСКД = СКД.ИсточникиДанных.Добавить();
    ИсточникСКД.Имя = "ИсточникДанных1";
    ИсточникСКД.ТипИсточникаДанных = "local";
    НаборДанных = СКД.НаборыДанных.Добавить(Тип("НаборДанныхЗапросСхемыКомпоновкиДанных"));
    НаборДанных.Запрос = ТекстЗапроса; 
    НаборДанных.ИсточникДанных = ИсточникСКД.Имя;
    НаборДанных.Имя = "НаборДанных1";
    URLСхемы = ПоместитьВоВременноеХранилище(СКД, Новый УникальныйИдентификатор());
    Компоновщик.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(URLСхемы));
    
    Поток = Новый ЧтениеXML();
    Поток.УстановитьСтроку(ОповещениеСсылка.НастройкиОтбораДляИнцидентов);
    Компоновщик.ЗагрузитьНастройки(СериализаторXDTO.ПрочитатьXML(Поток));
    Поток.Закрыть();
    
    ПараметрОповещение = Компоновщик.Настройки.ПараметрыДанных.Элементы.Найти("Оповещение");
    ПараметрОповещение.Использование = Истина;
    ПараметрОповещение.Значение = ОповещениеСсылка;
    
    ПараметрОповещение = Компоновщик.Настройки.ПараметрыДанных.Элементы.Найти("ТекДата");
    ПараметрОповещение.Использование = Истина;
    ПараметрОповещение.Значение = ТекДата;
    
    ПараметрОповещение = Компоновщик.Настройки.ПараметрыДанных.Элементы.Найти("ПериодГруппировкиОповещений");
    ПараметрОповещение.Использование = Истина;
    ПараметрОповещение.Значение = ОповещениеСсылка.ПериодГруппировкиОповещений;
    
    СтатусыЗакрыто = Новый Массив;
    СтатусыЗакрыто.Добавить(Перечисления.СтатусыИнцидентов.Неактуальный);
    СтатусыЗакрыто.Добавить(Перечисления.СтатусыИнцидентов.Закрыто);
    ПараметрОповещение = Компоновщик.Настройки.ПараметрыДанных.Элементы.Найти("СтатусыЗакрыто");
    ПараметрОповещение.Использование = Истина;
    ПараметрОповещение.Значение = СтатусыЗакрыто;
    
    ГруппировкаНастроек = Компоновщик.Настройки.Структура.Добавить(Тип("ГруппировкаКомпоновкиДанных"));
    
    ДобавитьПоле(Компоновщик, ГруппировкаНастроек, "Открытие");
    ДобавитьПоле(Компоновщик, ГруппировкаНастроек, "Новое");
    ДобавитьПоле(Компоновщик, ГруппировкаНастроек, "Отправить");
    ДобавитьПоле(Компоновщик, ГруппировкаНастроек, "ОтправитьПовторно");
    ДобавитьПоле(Компоновщик, ГруппировкаНастроек, "Оповещение");
    ДобавитьПоле(Компоновщик, ГруппировкаНастроек, "ТипИнцидента");
    ДобавитьПоле(Компоновщик, ГруппировкаНастроек, "Инцидент");
    ДобавитьПоле(Компоновщик, ГруппировкаНастроек, "НаименованиеИнцидента");
    ДобавитьПоле(Компоновщик, ГруппировкаНастроек, "УровеньИнцидента");
    ДобавитьПоле(Компоновщик, ГруппировкаНастроек, "Статус");
    ДобавитьПоле(Компоновщик, ГруппировкаНастроек, "ПоследнееСрабатывание");
    ДобавитьПоле(Компоновщик, ГруппировкаНастроек, "ДатаЗакрытия");
    ДобавитьПоле(Компоновщик, ГруппировкаНастроек, "ЧислоСрабатываний");
    ДобавитьПоле(Компоновщик, ГруппировкаНастроек, "ИнформационнаяБаза");
    ДобавитьПоле(Компоновщик, ГруппировкаНастроек, "Кластер");
    ДобавитьПоле(Компоновщик, ГруппировкаНастроек, "Ответственный");
    ДобавитьПоле(Компоновщик, ГруппировкаНастроек, "ПодробноеСообщение");
    ДобавитьПоле(Компоновщик, ГруппировкаНастроек, "ДатаРеакции");
    ДобавитьПоле(Компоновщик, ГруппировкаНастроек, "НеОповещатьДо");
    ДобавитьПоле(Компоновщик, ГруппировкаНастроек, "ПроигнорироватьДо");
    ДобавитьПоле(Компоновщик, ГруппировкаНастроек, "ДатаОткрытияПовторная");
    ДобавитьПоле(Компоновщик, ГруппировкаНастроек, "ЧислоСрабатыванийПовторное");
    ДобавитьПоле(Компоновщик, ГруппировкаНастроек, "КодИнцидента");
        
    ТЗнИнциденты = Новый ТаблицаЗначений;
        
    КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных();
    МакетКомпоновкиДанных = КомпоновщикМакета.Выполнить(
    СКД, 
    Компоновщик.Настройки,,,
    Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений")
    );
    
    ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных();
    ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновкиДанных);
    
    ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений();
    ПроцессорВывода.УстановитьОбъект(ТЗнИнциденты);
    
    ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных, Истина);
    
    ТЗнИнциденты.Индексы.Добавить("Отправить");
    
    УдалитьИзВременногоХранилища(URLСхемы);
    
    Возврат ТЗнИнциденты; 
    
КонецФункции

Процедура ДобавитьПоле(Компоновщик, ГруппировкаНастроек, ИмяПоля)
    
    Поле = ГруппировкаНастроек.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
    ВыбранноеПоле = Компоновщик.Настройки.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
    ВыбранноеПоле.Использование = Истина;
    ВыбранноеПоле.Поле = Новый ПолеКомпоновкиДанных(ИмяПоля);
    
КонецПроцедуры

Функция ПолучитьИнцидентыОповещения(НастройкиОтбораДляИнцидентов, МассивСтатусов = Неопределено) Экспорт
	
	ТЗ = Новый ТаблицаЗначений;
	
	Если ЗначениеЗаполнено(НастройкиОтбораДляИнцидентов) Тогда
		//болванка для выборки из Спр.ВнешниеКомпоненты
		Компоновщик = Новый КомпоновщикНастроекКомпоновкиДанных();
		СКД = Новый СхемаКомпоновкиДанных();
		ИсточникСКД = СКД.ИсточникиДанных.Добавить();
		ИсточникСКД.Имя = "ИсточникДанных1";
		ИсточникСКД.ТипИсточникаДанных = "local";
		НаборДанных = СКД.НаборыДанных.Добавить(Тип("НаборДанныхЗапросСхемыКомпоновкиДанных"));
		НаборДанных.Запрос = РегистрыСведений.Инциденты.ПолучитьТекстЗапросаИнициализацииСКД_Оперативное(); 
		НаборДанных.ИсточникДанных = ИсточникСКД.Имя;
		НаборДанных.Имя = "НаборДанных1";
		URLСхемы = ПоместитьВоВременноеХранилище(СКД, Новый УникальныйИдентификатор());
		Компоновщик.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(URLСхемы));
		
		//загружаем отбор из Спр.Оповещения
		Поток = Новый ЧтениеXML();
		Поток.УстановитьСтроку(НастройкиОтбораДляИнцидентов);
		Компоновщик.ЗагрузитьНастройки(СериализаторXDTO.ПрочитатьXML(Поток));
		Поток.Закрыть();
		
		//добавляем в запрос: ГДЕ Статус В (&АктивныеСтатусы)
		Если МассивСтатусов <> Неопределено Тогда
			ФильтрСсылки = Компоновщик.Настройки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			ФильтрСсылки.ЛевоеЗначение	= Новый ПолеКомпоновкиДанных("Статус");
			ФильтрСсылки.ПравоеЗначение	= МассивСтатусов;
			ФильтрСсылки.ВидСравнения 	= ВидСравненияКомпоновкиДанных.ВСписке;
			ФильтрСсылки.Использование	= Истина;
		КонецЕсли;
		
		//выбираем поле "КодИнцидента" в результат 
		ГруппировкаНастроек = Компоновщик.Настройки.Структура.Добавить(Тип("ГруппировкаКомпоновкиДанных"));
		Поле = ГруппировкаНастроек.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
		
		ВыбранноеПоле = Компоновщик.Настройки.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
		ВыбранноеПоле.Использование = Истина;
		ВыбранноеПоле.Поле = Новый ПолеКомпоновкиДанных("Инцидент");
		
		ВыбранноеПоле = Компоновщик.Настройки.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
		ВыбранноеПоле.Использование = Истина;
		ВыбранноеПоле.Поле = Новый ПолеКомпоновкиДанных("Статус");
        
        ВыбранноеПоле = Компоновщик.Настройки.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
		ВыбранноеПоле.Использование = Истина;
		ВыбранноеПоле.Поле = Новый ПолеКомпоновкиДанных("ТипИнцидента");
		
		//рендерим СКД
		ТЗ = Новый ТаблицаЗначений;
		
		Если Компоновщик.Настройки.Отбор.Элементы.Количество() <> 0 Тогда 
			КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных();
			МакетКомпоновкиДанных = КомпоновщикМакета.Выполнить(
			СКД, 
			Компоновщик.Настройки,,,
			Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений")
			);
			
			ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных();
			ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновкиДанных);
			
			ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений();
			ПроцессорВывода.УстановитьОбъект(ТЗ);
			ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных, Истина);
		КонецЕсли;
	КонецЕсли;
	
	Возврат ТЗ;
	
КонецФункции

Функция ПолучитьТекстОповещения(Знач ТекстОповещения, Инциденты, РазделительТегов = Неопределено, РазделительДанных = Неопределено, НаименованиеОповещения = "") Экспорт
	
	Если РазделительТегов = Неопределено Тогда
		РазделительТегов = Символы.ПС;
	КонецЕсли;
	
	Если РазделительДанных = Неопределено Тогда
		РазделительДанных = Символы.ПС;
	КонецЕсли;
		
	ШАПКА_НАЧАЛО = "<Шапка>";
	ШАПКА_КОНЕЦ = "</Шапка>";
	
	ТЕЛО_НАЧАЛО = "<Тело>";
	ТЕЛО_КОНЕЦ = "</Тело>";
	
	ПОДВАЛ_НАЧАЛО = "<Подвал>";
	ПОДВАЛ_КОНЕЦ = "</Подвал>";
	
	ТекстШапки = "";
	ТекстТело = "";
	ТекстПодвал = "";
	
	ИндексШапкиНачало = СтрНайти(ТекстОповещения, ШАПКА_НАЧАЛО);
	ИндексШапкиКонец = СтрНайти(ТекстОповещения, ШАПКА_КОНЕЦ);
	Если ИндексШапкиНачало > 0 И ИндексШапкиКонец > 0 Тогда
		ТекстШапки = СокрЛП(Сред(ТекстОповещения, ИндексШапкиНачало, ИндексШапкиКонец - ИндексШапкиНачало + СтрДлина(ШАПКА_КОНЕЦ)));
		ТекстОповещения = СокрЛП(СтрЗаменить(ТекстОповещения, ТекстШапки, ""));
		ТекстШапки = СтрЗаменить(ТекстШапки, ШАПКА_НАЧАЛО, "");
		ТекстШапки = СтрЗаменить(ТекстШапки, ШАПКА_КОНЕЦ, "");
	КонецЕсли;
	
	ИндексПодвалНачало = СтрНайти(ТекстОповещения, ПОДВАЛ_НАЧАЛО);
	ИндексПодвалКонец = СтрНайти(ТекстОповещения, ПОДВАЛ_КОНЕЦ);
	Если ИндексПодвалНачало > 0 И ИндексПодвалКонец > 0 Тогда
		ТекстПодвал = СокрЛП(Сред(ТекстОповещения, ИндексПодвалНачало, ИндексПодвалКонец - ИндексПодвалНачало + СтрДлина(ПОДВАЛ_КОНЕЦ)));
		ТекстОповещения = СокрЛП(СтрЗаменить(ТекстОповещения, ТекстПодвал, ""));
		ТекстПодвал = СтрЗаменить(ТекстПодвал, ПОДВАЛ_НАЧАЛО, "");
		ТекстПодвал = СтрЗаменить(ТекстПодвал, ПОДВАЛ_КОНЕЦ, "");
	КонецЕсли;
	
	ИндексТелоНачало = СтрНайти(ТекстОповещения, ТЕЛО_НАЧАЛО);
	ИндексТелоКонец = СтрНайти(ТекстОповещения, ТЕЛО_КОНЕЦ);
	Если ИндексТелоНачало > 0 И ИндексТелоКонец > 0 Тогда
		ТекстТело = СокрЛП(Сред(ТекстОповещения, ИндексТелоНачало + СтрДлина(ТЕЛО_НАЧАЛО), ИндексТелоКонец - (ИндексТелоНачало + СтрДлина(ТЕЛО_НАЧАЛО))));
	Иначе
		ТекстТело = СокрЛП(ТекстОповещения);
	КонецЕсли;
			
	Шапка = Новый ТаблицаЗначений;
	Шапка.Колонки.Добавить("ИндексТекста", Новый ОписаниеТипов("Число"));
	Шапка.Колонки.Добавить("Текст", Новый ОписаниеТипов("Строка"));
	
	ИндексыШапки = Новый Соответствие();
	
	Тело = Новый ТаблицаЗначений;
	Тело.Колонки.Добавить("Текст", Новый ОписаниеТипов("Строка"));
	
	Подвал = Новый ТаблицаЗначений;
	Подвал.Колонки.Добавить("ИндексТекста", Новый ОписаниеТипов("Число"));
	Подвал.Колонки.Добавить("Текст", Новый ОписаниеТипов("Строка"));
	
	ИндексыПодвал = Новый Соответствие();
	
	Для Каждого ТекИнцидент Из Инциденты Цикл
		// Шапка
		ТекстШапкиИнцидента = ЗаменитьШаблон(ТекИнцидент, ТекстШапки, ТекИнцидент.ПодробноеСообщение, НаименованиеОповещения);
		Если ИндексыШапки[ТекстШапкиИнцидента] = Неопределено Тогда
			ИндексыШапки.Вставить(ТекстШапкиИнцидента, ИндексыШапки.Количество());
		КонецЕсли;
		НовТекстШапка = Шапка.Добавить();
		НовТекстШапка.ИндексТекста = ИндексыШапки[ТекстШапкиИнцидента];
		НовТекстШапка.Текст = ТекстШапкиИнцидента;
		
		//Тело
		ТекстТелоИнцидента = ЗаменитьШаблон(ТекИнцидент, ТекстТело, ТекИнцидент.ПодробноеСообщение, НаименованиеОповещения);
		НовТекстТело = Тело.Добавить();
		НовТекстТело.Текст = СокрЛП(ТекстТелоИнцидента);
		
		//Подвал
		ТекстПодвалИнцидента = ЗаменитьШаблон(ТекИнцидент, ТекстПодвал, ТекИнцидент.ПодробноеСообщение, НаименованиеОповещения);
		Если ИндексыПодвал[ТекстПодвалИнцидента] = Неопределено Тогда
			ИндексыПодвал.Вставить(ТекстПодвалИнцидента, ИндексыПодвал.Количество());
		КонецЕсли;
		НовТекстПодвал = Подвал.Добавить();
		НовТекстПодвал.ИндексТекста = ИндексыПодвал[ТекстПодвалИнцидента];
		НовТекстПодвал.Текст = ТекстПодвалИнцидента;
		
	КонецЦикла;
	
	Шапка.Свернуть("ИндексТекста, Текст", "");
	Шапка.Сортировать("ИндексТекста");
	
	Подвал.Свернуть("ИндексТекста, Текст", "");
	Подвал.Сортировать("ИндексТекста");
	
	ТелоМассив = Тело.ВыгрузитьКолонку("Текст");
	ТекИндекс = ТелоМассив.ВГраница();
	Пока ТекИндекс > 0 Цикл
		Если ТелоМассив[ТекИндекс] = "" Тогда
			ТелоМассив.Удалить(ТекИндекс);
		КонецЕсли;
		ТекИндекс = ТекИндекс - 1;
	КонецЦикла;
		
	Результат = СтрСоединить(Шапка.ВыгрузитьКолонку("Текст"), РазделительДанных);
	
	РезультатТело = СтрСоединить(ТелоМассив, РазделительДанных);
	Если ЗначениеЗаполнено(РезультатТело) Тогда
		Результат = Результат + РазделительТегов + РезультатТело;
	КонецЕсли;
	
	РезультатПодвал = СтрСоединить(Подвал.ВыгрузитьКолонку("Текст"), РазделительДанных);
	Если ЗначениеЗаполнено(РезультатПодвал) Тогда
		Результат = Результат + РазделительТегов + РезультатПодвал;
	КонецЕсли;
		
	Если Прав(Результат, СтрДлина(РазделительТегов)) = РазделительТегов Тогда
		Результат = Лев(Результат, СтрДлина(Результат) - СтрДлина(РазделительТегов));
	КонецЕсли;
	
	Возврат СокрЛП(Результат);
	
КонецФункции

Функция ПолучитьТекстыОповещения(Оповещение, Инцидент, ЗначенияПоказателей = "", ЭтоТест = Ложь, ЭтоЗакрытие = Ложь) Экспорт
	
	ИнцидентМассив = Новый Массив;
	ИнцидентМассив.Добавить(Инцидент);
	
	Результат = Новый Структура;
		
	Если ЭтоЗакрытие И Оповещение.ОтдельныеШаблоныОЗакрытии Тогда
		Результат.Вставить("Тема", ПолучитьТекстОповещения(Оповещение.ТемаОповещенияЗакрытие, ИнцидентМассив));
		Результат.Вставить("ТелоПисьма", ПолучитьТекстОповещения(Оповещение.ТекстОповещенияЗакрытие, ИнцидентМассив));
		Результат.Вставить("ТелоСМС", ПолучитьТекстОповещения(Оповещение.ТекстОповещенияСмсЗакрытие, ИнцидентМассив));
	Иначе
		Результат.Вставить("Тема", ПолучитьТекстОповещения(Оповещение.ТемаОповещения, ИнцидентМассив));
		Результат.Вставить("ТелоПисьма", ПолучитьТекстОповещения(Оповещение.ТекстОповещения, ИнцидентМассив));
		Результат.Вставить("ТелоСМС", ПолучитьТекстОповещения(Оповещение.ТекстОповещенияСмс, ИнцидентМассив));
	КонецЕсли;
	
	Если ЭтоТест Тогда
		Результат.Тема = "ЭТО ТЕСТОВОЕ СООБЩЕНИЕ! " + Результат.Тема;
		Результат.ТелоПисьма = "ЭТО ТЕСТОВОЕ СООБЩЕНИЕ!
		|" + Результат.ТелоПисьма;
		Результат.ТелоСМС = "ЭТО ТЕСТОВОЕ СООБЩЕНИЕ!
		|" + Результат.ТелоСМС;
	КонецЕсли;
		
	Возврат Результат;
    
КонецФункции

Функция ВыполненыУсловияДляИнцидента(Знач ОповещениеСсылка, Знач ЗаписьИнцидента) Экспорт
	
	//болванка для выборки из Спр.ВнешниеКомпоненты
	Компоновщик = Новый КомпоновщикНастроекКомпоновкиДанных();
	СКД = Новый СхемаКомпоновкиДанных();
	ИсточникСКД = СКД.ИсточникиДанных.Добавить();
	ИсточникСКД.Имя = "ИсточникДанных1";
	ИсточникСКД.ТипИсточникаДанных = "local";
	НаборДанных = СКД.НаборыДанных.Добавить(Тип("НаборДанныхЗапросСхемыКомпоновкиДанных"));
	НаборДанных.Запрос = РегистрыСведений.Инциденты.ПолучитьТекстЗапросаИнициализацииСКД_Оперативное(); 
	НаборДанных.ИсточникДанных = ИсточникСКД.Имя;
	НаборДанных.Имя = "НаборДанных1";
	URLСхемы = ПоместитьВоВременноеХранилище(СКД, Новый УникальныйИдентификатор());
	Компоновщик.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(URLСхемы));
	
	//загружаем отбор из Спр.Оповещения
	Поток = Новый ЧтениеXML();
	Поток.УстановитьСтроку(ОповещениеСсылка.НастройкиОтбораДляИнцидентов);
	Компоновщик.ЗагрузитьНастройки(СериализаторXDTO.ПрочитатьXML(Поток));
	Поток.Закрыть();
	
	// добавляем в запрос: ГДЕ КодИнцидента = &КодИнцидента
	ФильтрСсылки = Компоновщик.Настройки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ФильтрСсылки.ЛевоеЗначение	= Новый ПолеКомпоновкиДанных("ИнцидентСсылка");
	ФильтрСсылки.ПравоеЗначение	= ЗаписьИнцидента.Инцидент;
	ФильтрСсылки.ВидСравнения 	= ВидСравненияКомпоновкиДанных.Равно;
	ФильтрСсылки.Использование	= Истина;
	
	//выбираем поле "Ссылка" в результат 
	ГруппировкаНастроек = Компоновщик.Настройки.Структура.Добавить(Тип("ГруппировкаКомпоновкиДанных"));
	Поле = ГруппировкаНастроек.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
	
	ВыбранноеПоле = Компоновщик.Настройки.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
	ВыбранноеПоле.Использование = Истина;
	ВыбранноеПоле.Поле = Новый ПолеКомпоновкиДанных("ИнцидентСсылка");
	
	//рендерим СКД
    ТЗ = Новый ТаблицаЗначений;
	
    КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных();
    МакетКомпоновкиДанных = КомпоновщикМакета.Выполнить(
		СКД, 
		Компоновщик.Настройки,,,
		Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений")
	);

    ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных();
    ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновкиДанных);

    ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений();
    ПроцессорВывода.УстановитьОбъект(ТЗ);
    ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных, Истина);
	
    Возврат ТЗ.Количество() > 0;
	
КонецФункции

// Создает оповещение
// Параметры - Структура
//	- Инцидент - СправочникСсылка.Инциденты
//	- Пользователи - Массив, содержащий элементы СправочникСсылка.Пользователи
// Возвращает
//	- СправочникСсылка.Оповещения
//
Функция СоздатьОповещениеПоТипуИнцидента(Параметры) Экспорт
	Инцидент = Параметры.Инцидент;
	
	ОповещениеЭлемент = Справочники.Оповещения.СоздатьЭлемент();
	Если Инцидент <> Неопределено Тогда
		ОповещениеЭлемент.Наименование = Инцидент.ТипИнцидента;
		ОповещениеЭлемент.ОтноситсяКТипуИнцидента = Инцидент.ТипИнцидента;
	Иначе
		ОповещениеЭлемент.Наименование = Строка(Параметры.Отбор.ТипИнцидента);
		ОповещениеЭлемент.ОтноситсяКТипуИнцидента = Параметры.Отбор.ТипИнцидента;
	КонецЕсли;
	
	ТекстОповещения = Параметры.ТекстОповещения;
	Если ТипЗнч(ТекстОповещения) = Тип("Строка") Тогда
		ОповещениеЭлемент.ТемаОповещения = ТекстОповещения;
		ОповещениеЭлемент.ТекстОповещения = ТекстОповещения;
		ОповещениеЭлемент.ТекстОповещенияСмс = ТекстОповещения;
	Иначе
		ОповещениеЭлемент.ТемаОповещения = ТекстОповещения.Тема;
		ОповещениеЭлемент.ТекстОповещения = ТекстОповещения.ТелоПисьма;
		ОповещениеЭлемент.ТекстОповещенияСмс = ТекстОповещения.ТелоСМС;
	КонецЕсли;
		
	Компоновщик = Новый КомпоновщикНастроекКомпоновкиДанных;
	Для Каждого ТекОтбор Из Параметры.Отбор Цикл
		ЭлОтбора = Компоновщик.Настройки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ТекОтбор.Ключ);
		ЭлОтбора.ПравоеЗначение = ТекОтбор.Значение;
	КонецЦикла;
		
	Поток = Новый ЗаписьXML();
	Поток.УстановитьСтроку();
	СериализаторXDTO.ЗаписатьXML(Поток, Компоновщик.ПолучитьНастройки());
	ОповещениеЭлемент.НастройкиОтбораДляИнцидентов = Поток.Закрыть();
	
	Пользователи = Параметры.Пользователи;
	Если Пользователи <> Неопределено Тогда
		Для Каждого Пользователь ИЗ Пользователи Цикл
			НовПолучатель = ОповещениеЭлемент.Получатели.Добавить();
			НовПолучатель.Адресат = Пользователь;
			Если ТипЗнч(Пользователь) = Тип("СправочникСсылка.РолиПользователей") Тогда
				НовПолучатель.ПоПочте = Константы.ВыполнятьОповещениеПоПочте.Получить();
				НовПолучатель.ПоСМС = Константы.ВыполнятьОповещениеПоСМС.Получить();
			ИначеЕсли ТипЗнч(Пользователь) = Тип("СправочникСсылка.Пользователи") Тогда
				НовПолучатель.ПоПочте = ЗначениеЗаполнено(Пользователь.АдресЭлектроннойПочты);
				НовПолучатель.ПоСМС = ЗначениеЗаполнено(Пользователь.НомерТелефона);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Параметры.Свойство("ИнтервалОЗакрытии", ОповещениеЭлемент.ИнтервалОЗакрытии);
	Параметры.Свойство("ПериодГруппировкиОповещений", ОповещениеЭлемент.ПериодГруппировкиОповещений);
	Расписание = новый РасписаниеРегламентногоЗадания();
	Расписание.ПериодПовтораДней = 1;
	Расписание.ПериодПовтораВТечениеДня = 20;
	РасписаниеПредставление = Расписание;
	РасписаниеИспользование = Истина;
	ОповещениеЭлемент.ДополнительныеСвойства.Вставить("Расписание", Расписание);
	
	Использование = Истина;
	Если Параметры.Свойство("Использование") Тогда
		Использование = Параметры.Использование;
	КонецЕсли;
	
	ОповещениеЭлемент.ДополнительныеСвойства.Вставить("Программно", Истина);
	Если Использование Тогда
		ОповещениеЭлемент.ДополнительныеСвойства.Вставить("Использование", Истина);
		ОповещениеЭлемент.ДополнительныеСвойства.Вставить("ЗапланированныйМоментЗапуска", ТекущаяУниверсальнаяДата());
	КонецЕсли;
	
	ОповещениеЭлемент.Записать();
	
	Возврат ОповещениеЭлемент.Ссылка;
КонецФункции

Функция СссылкаПоТипуИнцидента(ТипИнцидента) Экспорт
	Ссылка = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	Ссылка,
	|	НастройкиОтбораДляИнцидентов
	|ИЗ
	|	Справочник.Оповещения
	|ГДЕ
	|	ОтноситсяКТипуИнцидента = &ТипИнцидента
	|";
	Запрос.УстановитьПараметр("ТипИнцидента", ТипИнцидента);
	
	Результат = Запрос.Выполнить();
	Компоновщик = Новый КомпоновщикНастроекКомпоновкиДанных;
	Если НЕ Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		ПолеКомпоновкиДанныхТипИнцидента = Новый ПолеКомпоновкиДанных("ТипИнцидента");
		Пока Выборка.Следующий() Цикл
			Справочники.Оповещения.НастройкиОтбораДляИнцидентовДесериализация(Компоновщик, Выборка.НастройкиОтбораДляИнцидентов);
			Отбор = ОтборВСоответствие(Компоновщик.Настройки.Отбор);
			Если Отбор[ПолеКомпоновкиДанныхТипИнцидента] = ТипИнцидента И Отбор.Количество() = 1 Тогда
				Ссылка = Выборка.Ссылка;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат Ссылка;
КонецФункции

Функция СссылкаПоТипуИКодуИнцидента(ТипИнцидента, КодИнцидента) Экспорт
	Ссылка = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	Ссылка
	|ИЗ
	|	Справочник.Оповещения
	|ГДЕ
	|	ОтноситсяКТипуИнцидента = &ТипИнцидента
	|	И Наименование = &КодИнцидента
	|";
	
	Запрос.УстановитьПараметр("ТипИнцидента", ТипИнцидента);
	Запрос.УстановитьПараметр("КодИнцидента", КодИнцидента);
	
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		
		Ссылка = Выборка.Ссылка;
	КонецЕсли;
	
	Возврат Ссылка;
КонецФункции

Функция НастройкиОтбораДляИнцидентовСериализация(Настройки) Экспорт
	
	Поток = Новый ЗаписьXML();
	Поток.УстановитьСтроку();
	СериализаторXDTO.ЗаписатьXML(Поток, Настройки);
	НастройкиОтбораДляИнцидентов = Поток.Закрыть();
	
	Возврат НастройкиОтбораДляИнцидентов;
	
КонецФункции

Процедура НастройкиОтбораДляИнцидентовДесериализация(Компоновщик, НастройкиОтбораДляИнцидентов) Экспорт
	
	Поток = Новый ЧтениеXML();
	Поток.УстановитьСтроку(НастройкиОтбораДляИнцидентов);
	Компоновщик.ЗагрузитьНастройки(СериализаторXDTO.ПрочитатьXML(Поток));
	Поток.Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область Служебные_Процедуры_и_функции

Функция ЗаменитьШаблон(Инцидент, Текст, ЗначенияПоказателей, НаименованиеОповещения)
	ТекДата = ТекущаяДата();
    
    Если ТипЗнч(Инцидент) = Тип("РегистрСведенийМенеджерЗаписи.Инциденты") Тогда
        КодИнцидента = Инцидент.Инцидент.Наименование;
    Иначе
        КодИнцидента = Инцидент.КодИнцидента;
    КонецЕсли;
    
    Если Инцидент <> Неопределено Тогда
        НовыйТекст = ЗаменитьШаблонЗначения(Текст, "НаименованиеОповещения", НаименованиеОповещения);
		НовыйТекст = ЗаменитьШаблонДаты(НовыйТекст, "ДатаФормированияОповещения", ТекДата);
		НовыйТекст = ЗаменитьШаблонЗначения(НовыйТекст, "КодИнцидента", КодИнцидента);
		НовыйТекст = ЗаменитьШаблонЗначения(НовыйТекст, "Наименование", КодИнцидента); // для совместимости
		НовыйТекст = ЗаменитьШаблонДаты(НовыйТекст, "ДатаОткрытияИнцидента", Инцидент.ДатаОткрытияПовторная);
		НовыйТекст = ЗаменитьШаблонДаты(НовыйТекст, "ДатаЗакрытияИнцидента", Инцидент.ДатаЗакрытия);
		НовыйТекст = ЗаменитьШаблонЗначения(НовыйТекст, "СтатусИнцидента", Инцидент.Статус);
		НовыйТекст = ЗаменитьШаблонДаты(НовыйТекст, "ПоследнееСрабатывание", Инцидент.ПоследнееСрабатывание);
		        
        Если Инцидент.Статус = Перечисления.СтатусыИнцидентов.Закрыто ИЛИ Инцидент.Статус = Перечисления.СтатусыИнцидентов.Неактуальный Тогда
            НовыйТекст = ЗаменитьШаблонЗначения(НовыйТекст, "ВремяВСтатусе", ФорматДлительностьДни(Инцидент.ДатаОткрытияПовторная, Инцидент.ДатаЗакрытия));
		    НовыйТекст = ЗаменитьШаблонЗначения(НовыйТекст, "StatusTime", ФорматДлительностьДниEng(Инцидент.ДатаОткрытияПовторная, Инцидент.ДатаЗакрытия));
            
            НовыйТекст = ЗаменитьШаблонЗначения(НовыйТекст, "ВремяАктуальностиПроблемы", ФорматДлительностьДни(Инцидент.ДатаОткрытияПовторная, Инцидент.ДатаЗакрытия));
            НовыйТекст = ЗаменитьШаблонЗначения(НовыйТекст, "RelevanceTimeProblems", ФорматДлительностьДниEng(Инцидент.ДатаОткрытияПовторная, Инцидент.ДатаЗакрытия));
        Иначе
            НовыйТекст = ЗаменитьШаблонЗначения(НовыйТекст, "ВремяВСтатусе", ФорматДлительностьДни(Инцидент.ДатаОткрытияПовторная, ТекДата));
		    НовыйТекст = ЗаменитьШаблонЗначения(НовыйТекст, "StatusTime", ФорматДлительностьДниEng(Инцидент.ДатаОткрытияПовторная, ТекДата));
            
            НовыйТекст = ЗаменитьШаблонЗначения(НовыйТекст, "ВремяАктуальностиПроблемы", ФорматДлительностьДни(Инцидент.ДатаОткрытияПовторная, ТекДата));
            НовыйТекст = ЗаменитьШаблонЗначения(НовыйТекст, "RelevanceTimeProblems", ФорматДлительностьДниEng(Инцидент.ДатаОткрытияПовторная, ТекДата));
        КонецЕсли;
		
		НовыйТекст = ЗаменитьШаблонЗначения(НовыйТекст, "СообщениеИнцидента", Инцидент.ПодробноеСообщение);
		НовыйТекст = ЗаменитьШаблонЗначения(НовыйТекст, "Показатели", ЗначенияПоказателей);
		НовыйТекст = ЗаменитьШаблонЗначения(НовыйТекст, "Отправитель", ОбщийСерверПовтИсп.ИдентификаторЦКК());
		
		НовыйТекст = ЗаменитьШаблонЗначения(НовыйТекст, "ТипИнцидента", Инцидент.ТипИнцидента);
		НовыйТекст = ЗаменитьШаблонЗначения(НовыйТекст, "Кластер", Инцидент.Кластер);
		НовыйТекст = ЗаменитьШаблонЗначения(НовыйТекст, "СтрокаПодключения", Инцидент.ИнформационнаяБаза);
	КонецЕсли;
	
	Возврат НовыйТекст;
КонецФункции

Функция ФорматДлительностьДни(ДатаНачала, ДатаОкончания)
	РазницаСекунд = ДатаОкончания - ДатаНачала;
	Если РазницаСекунд < 86400 Тогда
		Если РазницаСекунд = 0 Тогда
			Возврат "1 сек";
		ИначеЕсли РазницаСекунд < 60 Тогда
			Возврат Строка(РазницаСекунд) + " сек";
		Иначе
			Возврат Формат(Дата(1,1,1) + РазницаСекунд,"ДФ='ЧЧ''ч ''мм''мин'''");
		КонецЕсли;
	ИначеЕсли РазницаСекунд >=2592000 Тогда
		Возврат "больше 30дн.";
	Иначе
		ДатаБуфер = Дата(1,1,1) + РазницаСекунд - (Цел(РазницаСекунд/86400)*86400);
		Возврат Формат(Цел(РазницаСекунд/86400), "ЧН=0") + "дн. " + Формат(ДатаБуфер,"ДФ='ЧЧ''ч ''мм''мин'''");
	КонецЕсли;
КонецФункции

Функция ФорматДлительностьДниEng(ДатаНачала, ДатаОкончания)
	РазницаСекунд = ДатаОкончания - ДатаНачала;
	Если РазницаСекунд < 86400 Тогда
		Если РазницаСекунд = 0 Тогда
			Возврат "1 s";
		ИначеЕсли РазницаСекунд < 60 Тогда
			Возврат Строка(РазницаСекунд) + " s";
		Иначе
			Возврат Формат(Дата(1,1,1) + РазницаСекунд,"ДФ='ЧЧ''h ''мм''min'''");
		КонецЕсли;
	ИначеЕсли РазницаСекунд >=2592000 Тогда
		Возврат "more 30d";
	Иначе
		ДатаБуфер = Дата(1,1,1) + РазницаСекунд - (Цел(РазницаСекунд/86400)*86400);
		Возврат Формат(Цел(РазницаСекунд/86400), "ЧН=0") + "d " + Формат(ДатаБуфер,"ДФ='ЧЧ''h ''мм''min'''");
	КонецЕсли;
КонецФункции

Функция ЗаменитьШаблонЗначения(Текст, Шаблон, Значение)
	Возврат СтрЗаменить(Текст, "[" + Шаблон + "]", Строка(Значение));
КонецФункции

Функция ЗаменитьШаблонДаты(Текст, Шаблон, Дата)
	
	ШаблонНачала = "[" + Шаблон + "=";
	
	ИндексНачала = СтрНайти(Текст, ШаблонНачала);
	Если ИндексНачала > 0 Тогда
		ИндексКрайний = СтрНайти(Текст, "]", НаправлениеПоиска.СНачала, ИндексНачала);
		ФорматДаты = Сред(Текст, ИндексНачала + СтрДлина(ШаблонНачала), ИндексКрайний - (ИндексНачала + СтрДлина(ШаблонНачала)));
		НовыйТекст = Лев(Текст, ИндексНачала -1) + Формат(Дата, "ДФ ='" + ФорматДаты + "'") + Прав(Текст, СтрДлина(Текст) - ИндексКрайний);
	Иначе
		НовыйТекст = Текст;
	КонецЕсли;
	
	Возврат НовыйТекст;
	
КонецФункции

Функция ПользователиДляОтправки(ОповещениеСсылка, ЭтоЗакрытие = Ложь)
	
	ПользователиДляОтправки = Новый Соответствие;
	
	Для Каждого Получатель Из ОповещениеСсылка.Получатели Цикл
		
		Если НЕ Получатель.ПоПочте И НЕ Получатель.ПоСМС И НЕ Получатель.ПоСкайпу И НЕ Получатель.ПоRestAPI Тогда
			Продолжить;
		КонецЕсли;
		
		Если ЭтоЗакрытие И НЕ Получатель.ОповещатьЗакрыт Тогда
			Продолжить;
		КонецЕсли;
		
		ОтложитьДо = Неопределено;
		НеУведомлятьУдалять = Ложь;
		
		Если ЗначениеЗаполнено(Получатель.НеУведомлятьС) И ЗначениеЗаполнено(Получатель.НеУведомлятьПо) Тогда
			
			Сейчас		= ТекущаяДатаСеанса();
			ПроверкаС	= Дата(2000, 1, 1, Час(Получатель.НеУведомлятьС),	Минута(Получатель.НеУведомлятьС),	Секунда(Получатель.НеУведомлятьС));
			ПроверкаПо  = Дата(2000, 1, 1, Час(Получатель.НеУведомлятьПо),	Минута(Получатель.НеУведомлятьПо),	Секунда(Получатель.НеУведомлятьПо));
			Проверка	= Дата(2000, 1, 1, Час(Сейчас),						Минута(Сейчас), 					Секунда(Сейчас));
			
			Если ПроверкаС < ПроверкаПо Тогда //диапазон не переходит через 00:00. Например: с 12:00 до 15:00
				Если Проверка >= ПроверкаС И Проверка <= ПроверкаПо Тогда
					ОтложитьДо = НачалоДня(Сейчас) + (Получатель.НеУведомлятьПо - Дата(1,1,1));
				КонецЕсли
			Иначе         // если проверка переходит через 00:00, например: с 21:00 по 08:00
				Если Проверка >= ПроверкаС ИЛИ Проверка <= ПроверкаПо Тогда
					ОтложитьДо = НачалоДня(Сейчас + 86400) + (Получатель.НеУведомлятьС - Дата(1,1,1));
				КонецЕсли;
			КонецЕсли;
			
			НеУведомлятьУдалять = Получатель.НеУведомлятьУдалять;
		КонецЕсли;
		
		Если ТипЗнч(Получатель.Адресат) = Тип("СправочникСсылка.РолиПользователей") Тогда
			ВыборкаИзРезультатаЗапроса = УправлениеЗадачами.ВыборкаРолейПользователей(Новый Структура("Роль", Получатель.Адресат));
			ВыборкаИзРезультатаЗапроса.Выбрать();
			Пока ВыборкаИзРезультатаЗапроса.Следующий() Цикл
				ДобавитьПользователяДляОтправки(ПользователиДляОтправки, ВыборкаИзРезультатаЗапроса.Пользователь, Получатель.ПоПочте, Получатель.ПоСМС, Получатель.ПоСкайпу, Получатель.ПоRestAPI, ОтложитьДо, НеУведомлятьУдалять, Получатель.ОповещатьЗакрыт);
			КонецЦикла;
		Иначе
			ДобавитьПользователяДляОтправки(ПользователиДляОтправки, Получатель.Адресат, Получатель.ПоПочте, Получатель.ПоСМС, Получатель.ПоСкайпу, Получатель.ПоRestAPI, ОтложитьДо, НеУведомлятьУдалять, Получатель.ОповещатьЗакрыт);
		КонецЕсли;
	КонецЦикла;
	
	Возврат ПользователиДляОтправки;
	
КонецФункции

Процедура ДобавитьПользователяДляОтправки(ПользователиДляОтправки, Знач Пользователь, Знач ПоПочте, Знач ПоСмс, Знач ПоСкайпу, Знач ПоRestAPI, Знач ОтложитьДо, Знач НеУведомлятьУдалять, Знач ОповещатьЗакрыт)
	
	Если ПользователиДляОтправки[Пользователь] = Неопределено Тогда
		ПользователиДляОтправки.Вставить(Пользователь, Новый Структура("ПоПочте, ПоСмс, ПоСкайпу, ПоRestAPI, ОтложитьДо, НеУведомлятьУдалять, ОповещатьЗакрыт", Ложь, Ложь, Ложь, Ложь, Неопределено, Ложь, Ложь));
	КонецЕсли;
	
	Пользователь = ПользователиДляОтправки[Пользователь];
	Пользователь.ПоПочте = Пользователь.ПоПочте ИЛИ ПоПочте;
	Пользователь.ПоСмс = Пользователь.ПоСмс ИЛИ ПоСмс;
    Пользователь.ПоСкайпу = Пользователь.ПоСкайпу ИЛИ ПоСкайпу;
    Пользователь.ПоRestAPI = Пользователь.ПоRestAPI ИЛИ ПоRestAPI;
	Пользователь.ОтложитьДо = ОтложитьДо;
	Пользователь.НеУведомлятьУдалять = НеУведомлятьУдалять;
    Пользователь.ОповещатьЗакрыт = ОповещатьЗакрыт;
	
КонецПроцедуры

Функция ОтборВСоответствие(Отбор)
	
	Результат = Новый Соответствие;
	Для Каждого ТекЭлемент Из Отбор.Элементы Цикл
		Результат.Вставить(ТекЭлемент.ЛевоеЗначение, ТекЭлемент.ПравоеЗначение);
	КонецЦикла;
	
	Возврат Результат;
		
КонецФункции

#КонецОбласти

#КонецЕсли