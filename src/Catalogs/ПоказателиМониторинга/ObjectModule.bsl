#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Процедура ПередУдалением(Отказ)
	
	Отказ = УдалениеНеактуальныхДанных.ЕстьПрофилиМониторингаПоПоказателю(Ссылка);
	
	Если Не Отказ Тогда 
		Если ЗначениеЗаполнено(Показатель) Тогда
			УдалениеОбъекта = Новый УдалениеОбъекта(Показатель);
			УдалениеОбъекта.Записать();
		КонецЕсли;
	Иначе 
		Комментарий = Строка(Ссылка) + " . Уникальный идентификатор " + Ссылка.УникальныйИдентификатор();
		
		ЗаписьЖурналаРегистрации(
		"ОшибкаПриУдаленииПрофиляМониторинга",
		УровеньЖурналаРегистрации.Ошибка,
		Метаданные.Справочники.ПоказателиМониторинга.МодульОбъекта,
		,
		Комментарий);
	КонецЕсли;
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	СкопироватьПоказатель(ОбъектКопирования.Показатель);
	Номер = МониторингСервер.СледующийНомер(ОбъектКопирования.Владелец); 
	
КонецПроцедуры

Процедура СкопироватьПоказатель(Знач ИсходныйПоказатель)
	
	Если Не ЗначениеЗаполнено(ИсходныйПоказатель) Тогда
		Возврат;
	КонецЕсли;
	
	СсылкаНового = Справочники.ПоказателиМониторинга.ПолучитьСсылку();
	УстановитьСсылкуНового(СсылкаНового);
	
	ПоказательОбъект = ИсходныйПоказатель.ПолучитьОбъект().Скопировать();
	ПоказательОбъект.Владелец = СсылкаНового;
	ПоказательОбъект.ОбменДанными.Загрузка = Истина;
	ПоказательОбъект.Записать();
	
	ЭтотОбъект.Показатель = ПоказательОбъект.Ссылка;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Попытка
		Если ЭтотОбъект.Показатель <> Неопределено И ЭтотОбъект.Наименование <> ЭтотОбъект.Показатель.Описание Тогда
			ЭтотОбъект.Наименование = ЭтотОбъект.Показатель.Описание;
		КонецЕсли;
	Исключение
		Комментарий = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
					
		ЗаписьЖурналаРегистрации(
			"Процедура ПередЗаписью(Отказ)",
			УровеньЖурналаРегистрации.Ошибка,
			Метаданные.Справочники.ПоказателиМониторинга.МодульОбъекта,
			,
			Комментарий);
	КонецПопытки;	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	Индекс = ПроверяемыеРеквизиты.Найти("Наименование");
	Если Индекс <> Неопределено Тогда
		ПроверяемыеРеквизиты.Удалить(Индекс);
	КонецЕсли;
КонецПроцедуры

#КонецЕсли
