#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Функция ПолучитьДанные(
	Знач ОпорнаяДата,
	Знач НачальноеСмещение,
	Знач ЧислоТочек,
	Знач Шаг, 
	Статистика = Неопределено, 
	ВычислитьАгрегатныеЗначения = Истина, 
	ВычислитьГоризонтАктуальности = Ложь,
	РежимСдвига = Ложь
) Экспорт
	
	Запрос = Новый Запрос;
	ЗапросТекст = "
	|ВЫБРАТЬ
	|	ОсновнаяТаблицаЗамеров.Период КАК Время,
	|	1 КАК ТекущееЗначение,
	|	ВЫБОР
	|		%ИндексацияИнтервалов
	|	КОНЕЦ КАК НомерИнтервала
	|ПОМЕСТИТЬ ВТ_ВыборкаЗамеров
	|ИЗ
	|	РегистрСведений.НовыеПроблемы КАК ОсновнаяТаблицаЗамеров
	|ГДЕ
	|	ОсновнаяТаблицаЗамеров.Решена = Ложь И %УсловиеНаИнтервалДат
	|
	|";
	
	Данные = МониторингСервер.ВыбратьДанныеПоИнтервалам(
		ЭтотОбъект.Ссылка,
		ОпорнаяДата,
		НачальноеСмещение,
		ЧислоТочек,
		Шаг,
		МониторингСервер.СекундВИнтервалеУсреднения(ЭтотОбъект.ИнтервалУсреднения),
		Запрос,
		ЗапросТекст,
		"Период"
	);
	
	Если Статистика <> Неопределено Тогда
		Если ВычислитьАгрегатныеЗначения Тогда
			Статистика = МониторингКлиентСервер.ВычислитьСредниеЗначения(Данные);
			Статистика.Вставить("ПоТочкам");
			ДатаНачала = ОпорнаяДата + НачальноеСмещение * Шаг;
			ДатаОкончания = ОпорнаяДата + (НачальноеСмещение + ЧислоТочек - 1)* Шаг;

			// вычислим параметр "Текущее"
			Попытка
				ТекущееЗначение = Данные[Данные.Количество() - 1];
			Исключение
				Инфо = ИнформацияОбОшибке();
				Комментарий =
					"Описание = '" +Инфо.Описание + "', " +
					"ИмяМодуля = '" + Инфо.ИмяМодуля + "', " +
					"НомерСтроки = '" + Инфо.НомерСтроки + "', " +
					"ИсходнаяСтрока = '" + Инфо.ИсходнаяСтрока + "'.";
			
				ЗаписьЖурналаРегистрации(
					"Функция ПолучитьДанные(...)",
					УровеньЖурналаРегистрации.Ошибка,
					Метаданные.Справочники.ЧислоНерешенныхПроблем.МодульОбъекта,
					,
					Комментарий);
				
				ТекущееЗначение = 0;
			КонецПопытки;
			Статистика.Вставить("Текущее", ТекущееЗначение);

			Статистика.Вставить("Всего", ВсегоЗаПериод(ДатаНачала, ДатаОкончания));
		КонецЕсли;
		
		Если ВычислитьГоризонтАктуальности Тогда
			Запрос = Новый Запрос;
			ЗапросТекст = "ВЫБРАТЬ
			|	МАКСИМУМ(НовыеПроблемы.Период) КАК Время
			|ИЗ
			|	РегистрСведений.НовыеПроблемы КАК НовыеПроблемы";
			
			Запрос.Текст = ЗапросТекст;
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() И Выборка.Время <> Null Тогда
				Статистика.Вставить("Горизонт", Выборка.Время);
			Иначе
				Статистика.Вставить("Горизонт", Неопределено);
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
	Возврат Данные;
	
КонецФункции

Функция ВсегоЗаПериод(Знач ДатаНачала, Знач ДатаОкончания) Экспорт
	
	Запрос = Новый Запрос;
	ЗапросТекст = "
	|ВЫБРАТЬ
	|	Количество(ОсновнаяТаблицаЗамеров.ВариантДампа) КАК Всего
	|ИЗ
	|	РегистрСведений.НовыеПроблемы КАК ОсновнаяТаблицаЗамеров
	|ГДЕ
	|	ОсновнаяТаблицаЗамеров.Решена = Ложь
	|	И ОсновнаяТаблицаЗамеров.Период <= &ДатаОкончания И ОсновнаяТаблицаЗамеров.Период >= &ДатаНачала
	|
	|";
	
	МониторингСервер.УстановитьПараметрыДатыВЗапросе(Запрос, ДатаНачала, ДатаОкончания);
	
	Запрос.Текст = ЗапросТекст;
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() И Выборка.Всего <> Null Тогда
		Возврат Выборка.Всего;
	Иначе
		Возврат 0;
	КонецЕсли;
	
КонецФункции

Функция ИдентификаторВариантаПоказателя() Экспорт
	ИД = МониторингСервер.ОбщаяЧастьИдентификатораВариантаПоказателя(ЭтотОбъект);
	Возврат ИД;
КонецФункции

// Возвращает текстовую строку, описывающую тип показателя
//
// Возвращаемое значение:
//  Строка
//
Функция ИдентификаторТипаПоказателя() Экспорт
	
	Если ЗначениеЗаполнено(ЭтотОбъект.Метаданные().РасширенноеПредставлениеОбъекта) Тогда
		Возврат ЭтотОбъект.Метаданные().РасширенноеПредставлениеОбъекта;
	ИначеЕсли ЗначениеЗаполнено(ЭтотОбъект.Метаданные().ПредставлениеОбъекта) Тогда
		Возврат ЭтотОбъект.Метаданные().ПредставлениеОбъекта;
	Иначе
		Возврат ЭтотОбъект.Метаданные().Синоним;
	КонецЕсли;
	
КонецФункции

// Заполняет параметры по умолчанию отображения показателя на графике
// 
// Параметры:
//  ПараметрОповещения - Структура - см.МониторингСервер.ПараметрОповещенияПоказательЗаписан()
//
Процедура ЗаполнитьПараметрыОтображенияПоУмолчанию(Знач ПараметрОповещения) Экспорт
	ГСЧ = Новый ГенераторСлучайныхЧисел();
	
	ПараметрОповещения.Цвет = Новый Цвет(ГСЧ.СлучайноеЧисло(0,255), ГСЧ.СлучайноеЧисло(0,255), ГСЧ.СлучайноеЧисло(0,255)); 
	ПараметрОповещения.АвтоМасштаб = Ложь;
	ПараметрОповещения.Масштаб = 1;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ПустаяСтрока(Описание) Тогда
		Описание = Описание();
	КонецЕсли;
	
КонецПроцедуры

Функция Описание()
	
	НаименованиеВСтроку = ИдентификаторТипаПоказателя() + " " + ИнтервалУсреднения;
	Возврат НаименованиеВСтроку;
	
КонецФункции

// Расчитывает данные показателя по периодам
//
//	Параметры:
// 		МенеджерВременныхТаблиц	- МенеджерВременныхТаблиц. Временные таблицы с периодами и показателями.
// 		Детализация				- ТипДополненияПериодаКомпоновкиДанных. Период детализации.
//
//	Возвращаемое значение:
//		ТаблицаДанных. ТаблицаЗначений. Данные показателя.
//
Функция РасчитатьПоказатель(МенеджерВременныхТаблиц, Детализация = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("ПоказательМониторинга", ЭтотОбъект.Владелец);
	Запрос.УстановитьПараметр("Показатель", ЭтотОбъект.Ссылка);
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	НачалоПериода(ОсновнаяТаблицаЗамеров.Период, Квартал) КАК Период,
	|	КОЛИЧЕСТВО(ОсновнаяТаблицаЗамеров.Период) КАК Количество
	|
	|ПОМЕСТИТЬ
	|	ОсновнаяТаблицаЗамеров
	|
	|ИЗ
	|	РегистрСведений.НовыеПроблемы КАК ОсновнаяТаблицаЗамеров
	|	
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ЧислоНерешенныхПроблем КАК ЧислоНерешенныхПроблем
	|	ПО ЧислоНерешенныхПроблем.Ссылка = &Показатель
	|
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаГраницПериода КАК ТаблицаГраницПериода
	|	ПО ОсновнаяТаблицаЗамеров.Период МЕЖДУ НачалоПериода(ТаблицаГраницПериода.ДатаНачала, Квартал) И КонецПериода(ТаблицаГраницПериода.ДатаОкончания, Квартал)
	|
	|ГДЕ
	|	НЕ ОсновнаяТаблицаЗамеров.Решена
	|
	|СГРУППИРОВАТЬ ПО
	|	НачалоПериода(ОсновнаяТаблицаЗамеров.Период, Квартал)
	|
	|;
	|
	|ВЫБРАТЬ
	|	ТаблицаПодготовительная.ДатаТочки КАК ДатаТочки,
	|	МАКСИМУМ(ЕСТЬNULL(ОсновнаяТаблицаЗамеров.Количество, 0)) КАК Максимальное,
	|	МИНИМУМ(ЕСТЬNULL(ОсновнаяТаблицаЗамеров.Количество, 0)) КАК Минимальное,
	|	СУММА(ЕСТЬNULL(ОсновнаяТаблицаЗамеров.Количество, 0)) КАК Значение
	|
	|
	|ИЗ
	|	ТаблицаПодготовительная КАК ТаблицаПодготовительная
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ ОсновнаяТаблицаЗамеров КАК ОсновнаяТаблицаЗамеров
	|	ПО ТаблицаПодготовительная.ПоказательМониторинга = &ПоказательМониторинга
	|	И ОсновнаяТаблицаЗамеров.Период > НачалоПериода(ТаблицаПодготовительная.ДатаТочкиДанные, Квартал) 
	|	И ОсновнаяТаблицаЗамеров.Период <= НачалоПериода(ТаблицаПодготовительная.ДатаТочкиДанныеСледующая, Квартал)
	|
	|ГДЕ
	|	ТаблицаПодготовительная.ПоказательМониторинга = &ПоказательМониторинга
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаПодготовительная.ДатаТочки
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаТочки
	|";
	
	ИмяИнтервалУсреднения = "Час";
	
	Если ЗначениеЗаполнено(ИнтервалУсреднения) Тогда
		
		Индекс					= Перечисления.ИнтервалыУсреднения.Индекс(ИнтервалУсреднения);
		ИмяИнтервалУсреднения	= Метаданные.Перечисления.ИнтервалыУсреднения.ЗначенияПеречисления[Индекс].Имя;
		
	КонецЕсли;
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "Квартал", ИмяИнтервалУсреднения);
	
	ТаблицаДетальныхДанных = Запрос.Выполнить().Выгрузить();
	
	Запрос.Текст = "УНИЧТОЖИТЬ ОсновнаяТаблицаЗамеров";
	Запрос.Выполнить();
	
	Если ЭтотОбъект.Владелец.ПоказыватьТренд Тогда
		РасчетИтоговПоказателей.СгладитьДанные(ТаблицаДетальныхДанных, ЭтотОбъект.Владелец.ТипСглаживания, ЭтотОбъект.Владелец.КоличествоУсредняемыхЗначений);
	КонецЕсли;
	
	Возврат ТаблицаДетальныхДанных;
	
КонецФункции

#КонецЕсли