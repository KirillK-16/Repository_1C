
&НаКлиенте
Процедура КлючевыеОперацииНаименованиеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
    
    ИнформационныеБазыМассив = Новый Массив;
    Для Каждого ТекИнформационнаяБаза Из Объект.ИнформационныеБазы Цикл
        ИнформационныеБазыМассив.Добавить(ТекИнформационнаяБаза.ИнформационнаяБаза);
    КонецЦикла;
        
    ПараметрыОткрытия = Новый Структура("ИнформационныеБазы", ИнформационныеБазыМассив);
    ПараметрыОткрытия.Вставить("РежимВыделения", "Одиночный");
    ОписаниеОповещенияОЗакрытии = Новый ОписаниеОповещения("КлючевыеОперацииНаименованиеОповещениеОЗакрытии", ЭтотОбъект);
    ОткрытьФорму("РегистрСведений.ОценкаПроизводительностиКлючевыеОперации.Форма.ФормаВыбораИменКлючевыхОперацийИнформационныхБаз", ПараметрыОткрытия,,,,,ОписаниеОповещенияОЗакрытии,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
    
КонецПроцедуры

&НаКлиенте
Процедура КлючевыеОперацииНаименованиеОповещениеОЗакрытии(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
    
    Если ТипЗнч(РезультатЗакрытия) = Тип("Массив") Тогда
        
        Если РезультатЗакрытия.Количество() = 1 Тогда
            Элементы.КлючевыеОперации.ТекущиеДанные.Наименование = РезультатЗакрытия[0];
        ИначеЕсли РезультатЗакрытия.Количество() > 1 Тогда
            Для Каждого ТекКлючеваяОперация Из РезультатЗакрытия Цикл
                НовКлючеваяОперация = Объект.КлючевыеОперации.Добавить();
                НовКлючеваяОперация.Наименование = ТекКлючеваяОперация;
            КонецЦикла;
        КонецЕсли;
        
    КонецЕсли;
    
КонецПроцедуры

&НаКлиенте
Процедура КлючевыеОперацииДобавитьНесколько(Команда)
    
    ИнформационныеБазыМассив = Новый Массив;
    Для Каждого ТекИнформационнаяБаза Из Объект.ИнформационныеБазы Цикл
        ИнформационныеБазыМассив.Добавить(ТекИнформационнаяБаза.ИнформационнаяБаза);
    КонецЦикла;
        
    ПараметрыОткрытия = Новый Структура("ИнформационныеБазы", ИнформационныеБазыМассив);
    ПараметрыОткрытия.Вставить("РежимВыделения", "Множественный");
    ОписаниеОповещенияОЗакрытии = Новый ОписаниеОповещения("КлючевыеОперацииНаименованиеОповещениеОЗакрытии", ЭтотОбъект);
    ОткрытьФорму("РегистрСведений.ОценкаПроизводительностиКлючевыеОперации.Форма.ФормаВыбораИменКлючевыхОперацийИнформационныхБаз", ПараметрыОткрытия,,,,,ОписаниеОповещенияОЗакрытии,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
    
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ПараметрОповещения = МониторингСервер.ПараметрОповещенияПоказательЗаписан(ТекущийОбъект);
	ПараметрОповещения.Изменился = ОбъектИзменился();

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ПараметрОповещения.Ссылка = Объект.Ссылка;
	ПараметрОповещения.Описание = Объект.Описание;
	Оповестить("ПоказательЗаписан", ПараметрОповещения, ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Функция ОбъектИзменился()
	
	Если Объект.ИнформационнаяБаза <> Объект.Ссылка.ИнформационнаяБаза Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если Объект.ИдентификаторКлючевойОперации <> Объект.Ссылка.ИдентификаторКлючевойОперации Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если Объект.ТипСпискаПользователей <> Объект.Ссылка.ТипСпискаПользователей Тогда
		Возврат Истина;
	КонецЕсли;
	
	ПользователиСсылка = Объект.Ссылка.Пользователи;
	ВыбранныеПользователи = Объект.Пользователи;
	
	Если ВыбранныеПользователи.Количество() <> ПользователиСсылка.Количество() Тогда
		Возврат Истина;
	КонецЕсли;
	
	Для Каждого СтрокаПользователя Из ВыбранныеПользователи Цикл
		
		Если ПользователиСсылка.Найти(СтрокаПользователя.ИмяПользователя) = Неопределено Тогда
			Возврат Истина;
		КонецЕсли;
			
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

