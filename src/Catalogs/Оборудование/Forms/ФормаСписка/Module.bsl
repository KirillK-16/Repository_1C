
&НаКлиенте
Процедура ИзменитьНесколько(Команда)
    
    ОповещениеОЗакрытии = Новый ОписаниеОповещения("ИзменитьЭлементыЗакрытие", ЭтотОбъект);
    ПараметрыОткрытия = Неопределено;
    
    Если ЭтотОбъект.Элементы.Список.ТекущиеДанные <> Неопределено Тогда
        ПараметрыОткрытия = Новый Структура("СобиратьДанныеПроизводительности, ПериодЗаписи, ПроверятьДоступность, Таймаут, ПериодКонтроля, МинимальныйПроцентДоступности, ДопустимоНетДанных, ИспользоватьВнешнегоАгента, РегистрироватьДампы");
        ТекДанные = ЭтотОбъект.Элементы.Список.ТекущиеДанные;
        ПараметрыОткрытия.СобиратьДанныеПроизводительности = ТекДанные.СобиратьДанныеПроизводительности;
        ПараметрыОткрытия.ПериодЗаписи = ТекДанные.ПериодЗаписи;
        ПараметрыОткрытия.ПроверятьДоступность = ТекДанные.ПроверятьДоступность;
        ПараметрыОткрытия.Таймаут = ТекДанные.Таймаут;
        ПараметрыОткрытия.ПериодКонтроля = ТекДанные.ПериодКонтроля;
        ПараметрыОткрытия.МинимальныйПроцентДоступности = ТекДанные.МинимальныйПроцентДоступности;
        ПараметрыОткрытия.ДопустимоНетДанных = ТекДанные.ДопустимоНетДанных;
        ПараметрыОткрытия.ИспользоватьВнешнегоАгента = ТекДанные.ИспользоватьВнешнегоАгента;
        ПараметрыОткрытия.РегистрироватьДампы = ТекДанные.РегистрироватьДампы;
    КонецЕсли;
    
    ОткрытьФорму("Справочник.Оборудование.Форма.ФормаИзменитьЭлементыСписка", ПараметрыОткрытия, ЭтотОбъект,,,,ОповещениеОЗакрытии, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
    
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьЭлементыЗакрытие(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
    
    Если ТипЗнч(РезультатЗакрытия) = Тип("Структура") И РезультатЗакрытия.Количество() > 0  Тогда
        ИзменитьЭлементыЗакрытиеНаСервере(ЭтотОбъект.Элементы.Список.ВыделенныеСтроки, РезультатЗакрытия);
        ЭтотОбъект.Элементы.Список.Обновить();
    КонецЕсли;
        
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ИзменитьЭлементыЗакрытиеНаСервере(Знач ВыделенныеСтроки, РезультатЗакрытия)
    
    Для Каждого ТекЭлемент Из ВыделенныеСтроки Цикл
        
        СпрОбъект = ТекЭлемент.ПолучитьОбъект();
        
        Для Каждого ТекРезультат Из РезультатЗакрытия Цикл
            
            СпрОбъект[ТекРезультат.Ключ] = ТекРезультат.Значение;
            
            Если ТекРезультат.Ключ = "ПериодЗаписи" Тогда
                
                Если ТекРезультат.Значение = 0 Тогда
                    СпрОбъект.ПериодЗаписиПредставление = "Не записывать";
                ИначеЕсли ТекРезультат.Значение = 1 Тогда
                    СпрОбъект.ПериодЗаписиПредставление = "1 сек";
                ИначеЕсли ТекРезультат.Значение = 5 Тогда
                    СпрОбъект.ПериодЗаписиПредставление = "5 сек";
                ИначеЕсли ТекРезультат.Значение = 10 Тогда
                    СпрОбъект.ПериодЗаписиПредставление = "10 сек";
                ИначеЕсли ТекРезультат.Значение = 30 Тогда
                    СпрОбъект.ПериодЗаписиПредставление = "30 сек";
                ИначеЕсли ТекРезультат.Значение = 60 Тогда
                    СпрОбъект.ПериодЗаписиПредставление = "1 мин";
                КонецЕсли;    
            КонецЕсли;
            
        КонецЦикла;
        
        СпрОбъект.Записать();
                
    КонецЦикла;
    
КонецПроцедуры


