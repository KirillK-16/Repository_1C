
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
    
    ЭтотОбъект.Список.Параметры.УстановитьЗначениеПараметра("ТекущаяДатаUTC", ТекущаяУниверсальнаяДата());
    
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Настройки(Команда)
    ОткрытьФорму("Справочник.АгентыКИП.Форма.ФормаНастройки");
КонецПроцедуры

&НаКлиенте
Процедура Обновить(Команда)
    ЭтотОбъект.Список.Параметры.УстановитьЗначениеПараметра("ТекущаяДатаUTC", Дата(1,1,1) + ТекущаяУниверсальнаяДатаВМиллисекундах()/1000);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Процедура СписокПриПолученииДанныхНаСервере(ИмяЭлемента, Настройки, Строки)
    
    Запрос = Новый Запрос;
    
    Запрос.Текст = "
    |ВЫБРАТЬ
    |   СпрАгентыКИП.Ссылка КАК АгентКИП,
    |   СпрОборудование.Ссылка КАК Оборудование
    |ИЗ
    |   Справочник.АгентыКИП КАК СпрАгентыКИП
    |ВНУТРЕННЕЕ СОЕДИНЕНИЕ
    |   Справочник.Оборудование КАК СпрОборудование
    |ПО
    |   СпрОборудование.АгентКИП = СпрАгентыКИП.Ссылка
    |ГДЕ
    |   СпрАгентыКИП.Ссылка В (&АгентыКИП)
    |УПОРЯДОЧИТЬ ПО
    |   СпрАгентыКИП.Ссылка
    |";
    
    Запрос.УстановитьПараметр("АгентыКИП", Строки.ПолучитьКлючи());
    
    Результат = Запрос.Выполнить();
    
    ОборудованиеАгентаКИП = Новый Соответствие;
    
    Выборка = Результат.Выбрать();
    Пока Выборка.Следующий() Цикл
        
        ТекОборудование = ОборудованиеАгентаКИП[Выборка.АгентКИП];
        Если ТекОборудование = Неопределено Тогда
            ТекОборудование = Новый Массив;
            ОборудованиеАгентаКИП.Вставить(Выборка.АгентКИП, ТекОборудование);
        КонецЕсли;
        
        ТекОборудование.Добавить(Выборка.Оборудование);
        
    КонецЦикла;
    
    Для Каждого ТекОборудование Из ОборудованиеАгентаКИП Цикл
        
        ТекСтрока = Строки.Получить(ТекОборудование.Ключ);
        ТекСтрокаДанные = ТекСтрока.Данные;
        ТекСтрокаДанные.Оборудование = СтрСоединить(ТекОборудование.Значение, ", ");
        ТекСтрокаДанные.ОборудованиеКоличество = ТекОборудование.Значение.Количество();
                
    КонецЦикла;
        
КонецПроцедуры

#КонецОбласти