#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Процедура ПередЗаписью(Отказ)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Описание = "";
	Для Каждого Критерий Из Критерии Цикл
		
		Описание = Описание + Критерий.Вид + " """ + Критерий.Значение + """ И ";
		
	КонецЦикла;
	
	Описание = Лев(Описание, СтрДлина(Описание) - 3);
	
	Наименование = СтрЗаменить(Описание, Символы.ПС, " ");
	
	Техжурнал.ПересчитатьХеши(Критерии);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	УдалитьСмежныеСобытия();
КонецПроцедуры

// Производит переназначение строк технологического журнала в соответствии с изменившимися условиями
// В дополнительном свойстве УдаляемыеИсключения может лежать XML-сериализованная таблица значений с 
// одной колонкой "ИсключительнаяСитуация", указанные в ней исключения подлежат удалению
//
Процедура УдалитьСмежныеСобытия()
	
	УдаляемыеИсключенияXML = Неопределено;
	Если ДополнительныеСвойства.Свойство("УдаляемыеИсключения", УдаляемыеИсключенияXML) Тогда
		ЧтениеXML = Новый ЧтениеXML;
		ЧтениеXML.УстановитьСтроку(УдаляемыеИсключенияXML);
		Сериализатор = Новый СериализаторXDTO(ФабрикаXDTO);
		УдаляемыеИсключения = Сериализатор.ПрочитатьXML(ЧтениеXML, Тип("ТаблицаЗначений"));
		
		// Переносим строки техжурнала из удаляемых исключений, удовлетворяющие условиям текущего исключения
		Выборка = Техжурнал.ЗапросЗаписейЖурналаСмежныхИсключений(УдаляемыеИсключения, Владелец).Выполнить().Выбрать();
		Набор = РегистрыСведений.ТехнологическийЖурнал.СоздатьНаборЗаписей();
		Пока Выборка.Следующий() Цикл
			
			Набор.Очистить();
			Набор.Отбор.УникальныйИдентификатор.Установить(Выборка.УникальныйИдентификатор);
			Запись = Набор.Добавить();
			ЗаполнитьЗначенияСвойств(Запись, Выборка);
			Запись.ИсключительнаяСитуация = Владелец;
			Набор.Записать();
			
		КонецЦикла;
		
		// Получаем набор строк техжурнала, входивших в удаляемые исключения
		НовыеСобытия = Техжурнал.ЗапросЗаписейЖурналаСмежныхИсключений(УдаляемыеИсключения).Выполнить().Выгрузить();
		// Получаем набор строк техжуранала, которые больше не удовлетворяют условиям текущего исключения
		ОтключаемыеСобытия = Техжурнал.НайтиОсиротевшиеСтрокиЖурнала(Владелец);
		ОбщийКлиентСервер.ОбъединитьТаблицы(НовыеСобытия, ОтключаемыеСобытия);
		
		//Удаляем исключения
		Для Каждого Строка Из УдаляемыеИсключения Цикл
			
			Удаление = Новый УдалениеОбъекта(Строка.ИсключительнаяСитуация);
			Удаление.Записать();
			
		КонецЦикла;
		
	Иначе
		
		НовыеСобытия = Техжурнал.НайтиОсиротевшиеСтрокиЖурнала(Владелец);
		
	КонецЕсли;
	
	Если НовыеСобытия.Количество() > 0 Тогда
		НовыеСобытия.ЗаполнитьЗначения(Справочники.ИсключительныеСитуации.ПустаяСсылка(), "ИсключительнаяСитуация");
		ИмпортТехжурналаИсключения.СобратьИсключения(НовыеСобытия);
		
		Набор = РегистрыСведений.ТехнологическийЖурнал.СоздатьНаборЗаписей();
		Для Каждого Строка Из НовыеСобытия Цикл
			
			Набор.Очистить();
			Набор.Отбор.УникальныйИдентификатор.Установить(Строка.УникальныйИдентификатор);
			Запись = Набор.Добавить();
			ЗаполнитьЗначенияСвойств(Запись, Строка);
			Набор.Записать();
			
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецЕсли
