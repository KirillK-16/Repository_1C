#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Процедура ЗаписатьЭлемент(Знач ПараметрыЗадания, Знач АдресХранилища) Экспорт
	
	//ТекущийОбъект = ДанныеФормыВЗначение(ПараметрыЗадания.ЗаписываемыйОбъект, Тип("СправочникОбъект.УсловияИсключительныхСитуаций"));
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.УстановитьСтроку(ПараметрыЗадания.ЗаписываемыйОбъект);
	ТекущийОбъект = СериализаторXDTO.ПрочитатьXML(ЧтениеXML);
	Если ПараметрыЗадания.УдаляемыеИсключения <> Неопределено Тогда
		ТекущийОбъект.ДополнительныеСвойства.Вставить("УдаляемыеИсключения", ПараметрыЗадания.УдаляемыеИсключения);
	КонецЕсли;
	ТекущийОбъект.Записать();
	ПоместитьВоВременноеХранилище(ТекущийОбъект.Ссылка, АдресХранилища);
	
КонецПроцедуры

Процедура УдалитьУсловия(Знач ПараметрыЗадания, Знач АдресХранилища) Экспорт
	
	НачатьТранзакцию();
	Попытка
		Для Каждого Условие Из ПараметрыЗадания.УдаляемыеУсловия Цикл
			
			ИсключительнаяСитуация = Условие.Владелец;
			Удаление = Новый УдалениеОбъекта(Условие);
			Удаление.Записать();
			
		КонецЦикла;
		ОбновитьЗаписиЖурнала(ИсключительнаяСитуация);
		ЗафиксироватьТранзакцию();
	Исключение
		Инфо = ИнформацияОбОшибке();
		Комментарий =
			"Описание = '" +Инфо.Описание + "', " +
			"ИмяМодуля = '" + Инфо.ИмяМодуля + "', " +
			"НомерСтроки = '" + Инфо.НомерСтроки + "', " +
			"ИсходнаяСтрока = '" + Инфо.ИсходнаяСтрока + "'.";
			
		ЗаписьЖурналаРегистрации(
			"Процедура УдалитьУсловия(Знач ПараметрыЗадания, Знач АдресХранилища) Экспорт",
			УровеньЖурналаРегистрации.Ошибка,
			Метаданные.Справочники.УсловияИсключительныхСитуаций.МодульМенеджера,
			,
			Комментарий);
		
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

Процедура ОбновитьЗаписиЖурнала(Знач ИсключительнаяСитуация)
	
	НовыеСобытия = Техжурнал.НайтиОсиротевшиеСтрокиЖурнала(ИсключительнаяСитуация);
	Если НовыеСобытия.Количество() > 0 Тогда
		НовыеСобытия.ЗаполнитьЗначения(Справочники.ИсключительныеСитуации.ПустаяСсылка(), "ИсключительнаяСитуация");
		ИмпортТехжурналаИсключения.СобратьИсключения(НовыеСобытия);
		
		Набор = РегистрыСведений.ТехнологическийЖурнал.СоздатьНаборЗаписей();
		Для Каждого Строка Из НовыеСобытия Цикл
			
			Набор.Очистить();
			Набор.Отбор.УникальныйИдентификатор.Установить(Строка.УникальныйИдентификатор);
			Запись = Набор.Добавить();
			ЗаполнитьЗначенияСвойств(Запись, Строка);
			Набор.Записать();
			
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

#КонецЕсли