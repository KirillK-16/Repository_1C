#Область ОбработчикиСобытий

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
Процедура ПередЗаписью(Отказ)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоНовый() Тогда
		Поставляется = Ложь;
	КонецЕсли;
	
	ПустыеЗначения = ПараметрыШаговАвтоматизации.НайтиСтроки(Новый Структура("Значение", ""));
	Для Каждого Элемент Из ПустыеЗначения Цикл
		ПараметрыШаговАвтоматизации.Удалить(Элемент);
	КонецЦикла;
	
	ПустыеНазванияШагов = ШагиАвтоматизации.НайтиСтроки(Новый Структура("Название", ""));
	Для Каждого Элемент Из ПустыеНазванияШагов Цикл
		Элемент.Название = ?(ЗначениеЗаполнено(Элемент.Команда), Элемент.Команда , Элемент.ТипШага);
	КонецЦикла;
	
	Если ПометкаУдаления <> Ссылка.ПометкаУдаления Тогда
		Запрос = Новый Запрос("ВЫБРАТЬ
		                      |	ЭкземплярыСценариевАвтоматизации.Ссылка КАК Ссылка
		                      |ИЗ
		                      |	Справочник.ЭкземплярыСценариевАвтоматизации КАК ЭкземплярыСценариевАвтоматизации
		                      |ГДЕ
		                      |	ЭкземплярыСценариевАвтоматизации.Владелец = &Владелец");
		Запрос.УстановитьПараметр("Владелец", Ссылка);
		Результат = Запрос.Выполнить();
		Если Не Результат.Пустой() Тогда
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;	
КонецПроцедуры
#КонецЕсли

#КонецОбласти