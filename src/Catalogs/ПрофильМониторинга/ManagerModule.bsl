#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
// <Описание функции>
//
// Параметры:
//  <ОбъектСсылка>	- <СправочникСсылка.ПрофильМониторинга> - ссылка на профиль мониторинга,
//					показатели которого необходимо получить
//  <Настройка>		- <УникальныйИдентификатор> - УИД настройки профиля, показатели которого
//					необходимо получить
//
// Возвращаемое значение:
//   <ТаблицаЗначений>
//
// Примечание:
//	
Функция ПолучитьПоказатели(ОбъектСсылка, Настройка) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ПрофильМониторингаПорядокПоказателей.Номер,
	|	ПрофильМониторингаПорядокПоказателей.ОтображатьНаДиаграмме,
	|	ПрофильМониторингаПорядокПоказателей.Автомасштаб,
	|	ПрофильМониторингаПорядокПоказателей.Масштаб,
	|	ПрофильМониторингаПорядокПоказателей.ПоказатьДанныеЗаПрошлыйПериод,
	|	ПрофильМониторингаПорядокПоказателей.ВременноеСмещение,
	|	ПрофильМониторингаПорядокПоказателей.ПоказыватьТренд,
	|	ПрофильМониторингаПорядокПоказателей.КоличествоУсредняемыхЗначений,
	|	ПрофильМониторингаПорядокПоказателей.ТипЛинии,
	|	ПрофильМониторингаПорядокПоказателей.ТипСглаживания,
	|	ПрофильМониторингаПорядокПоказателей.ТолщинаЛинии,
	|	ПрофильМониторингаПорядокПоказателей.Показатель
	|ИЗ
	|	Справочник.ПрофильМониторинга.ПорядокПоказателей КАК ПрофильМониторингаПорядокПоказателей
	|ГДЕ
	|	ПрофильМониторингаПорядокПоказателей.Ссылка = &Ссылка
	|	И ПрофильМониторингаПорядокПоказателей.НастройкаПоказателя = &НастройкаПоказателя
	|УПОРЯДОЧИТЬ ПО
	|	ПрофильМониторингаПорядокПоказателей.Номер
	|";
	Запрос.УстановитьПараметр("Ссылка", ОбъектСсылка);
	Запрос.УстановитьПараметр("НастройкаПоказателя", Настройка);
    
    
	Возврат Запрос.Выполнить().Выгрузить();
    
КонецФункции

Функция ПолучитьНастройки(ОбъектСсылка) Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПрофильМониторингаНастройкиПоказателей.УИД КАК УИД,
		|	ПрофильМониторингаНастройкиПоказателей.Наименование КАК Наименование
		|ИЗ
		|	Справочник.ПрофильМониторинга.НастройкиПоказателей КАК ПрофильМониторингаНастройкиПоказателей
		|ГДЕ
		|	ПрофильМониторингаНастройкиПоказателей.Ссылка = &Ссылка";

	Запрос.УстановитьПараметр("Ссылка", ОбъектСсылка);

	РезультатЗапроса = Запрос.Выполнить();
	
	Настройки = Новый Массив;
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		Настройка = Новый Структура;
		Настройка.Вставить("УИД", Выборка.УИД);
		Настройка.Вставить("Наименование", Выборка.Наименование);
		
		Настройки.Добавить(Настройка);
	КонецЦикла;
	
	Возврат Настройки;
КонецФункции

Функция РазобратьНавигационнуюСсылку(РезультатВвода) Экспорт
	ИмяСекцииНастройка = "?ref_set=";
	ИмяСекцииДата = "e1cib/data/";
	ИмяСекцииСсылка = "?ref=";
	
	Результат = Неопределено;
	
	НачалоСекцииНастройки = СтрНайти(РезультатВвода, ИмяСекцииНастройка);
	Если НачалоСекцииНастройки > 0 Тогда
		СсылкаНастройка = Сред(РезультатВвода, НачалоСекцииНастройки + СтрДлина(ИмяСекцииНастройка));
		
		РезультатВвода = Лев(РезультатВвода, НачалоСекцииНастройки - 1);
		
		НачалоСекцииДата = СтрНайти(РезультатВвода, ИмяСекцииДата);
		НачалоСекцииСсылки = СтрНайти(РезультатВвода, ИмяСекцииСсылка);
		
		Если НачалоСекцииДата > 0  И НачалоСекцииСсылки > 0 Тогда
			ПредставлениеТипа   = Сред(РезультатВвода, НачалоСекцииДата + СтрДлина(ИмяСекцииДата), НачалоСекцииСсылки - НачалоСекцииДата - СтрДлина(ИмяСекцииДата));
			ПредставлениеТипа = СтрЗаменить(ПредставлениеТипа, "Справочник.", "");
			ПустаяСсылка = ЗначениеВСтрокуВнутр(Справочники[ПредставлениеТипа].ПустаяСсылка());
			СсылкаСтрока = Сред(РезультатВвода, НачалоСекцииСсылки + СтрДлина(ИмяСекцииСсылка));
			СсылкаСтрока = СтрЗаменить(СсылкаСтрока, """", "");
			ЗначениеСсылки = СтрЗаменить(ПустаяСсылка, "00000000000000000000000000000000", СсылкаСтрока);
			СсылкаНаПрофиль = ЗначениеИзСтрокиВнутр(ЗначениеСсылки);
			
			Результат = Новый Структура;
			Результат.Вставить("СсылкаНаПрофиль", СсылкаНаПрофиль);
			Результат.Вставить("СсылкаНастройка", Новый УникальныйИдентификатор(СсылкаНастройка));
		КонецЕсли;
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

#КонецЕсли