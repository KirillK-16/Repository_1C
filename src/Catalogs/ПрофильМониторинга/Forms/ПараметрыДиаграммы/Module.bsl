
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если ПустаяСтрока(Объект.Наименование) Тогда
		Объект.Наименование = "Настройки параметров мониторинга";
	КонецЕсли;
	ЭтотОбъект.Заголовок = Объект.Наименование;
	Если Объект.ЧислоТочек = 0 Тогда
		Объект.ЧислоТочек = 100;
	КонецЕсли;
	
	Если Объект.ШагСдвига = 0 Тогда
		Объект.ШагСдвига = 10;
	КонецЕсли;
	
	ИзображениеЦветаСтрока = "ggggg";
	НастроитьОтображениеЦвета();
	
	Если Объект.ПоложениеЗаголовкаТекущейТочки = 0 Тогда
		Объект.ПоложениеЗаголовкаТекущейТочки = 90;
	КонецЕсли;
	
	Если Объект.МинимальныйШагОбновления = 0 Тогда
		Объект.МинимальныйШагОбновления = 30;
	КонецЕсли;
	
	Если Объект.ВремяОтображенияПрофиляПриРотации = 0 Тогда
		Объект.ВремяОтображенияПрофиляПриРотации = 30;
	КонецЕсли;
	
	Если Объект.ИнтервалПроверкиВыпоненияФоновогоЗадания = 0 Тогда
		Объект.ИнтервалПроверкиВыпоненияФоновогоЗадания = 3;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	ПараметрыОповещения = Новый Структура("Ссылка, ЧислоТочек, ШагСдвига, ЦветЗаголовкаТекущейТочки, ПоложениеЗаголовкаТекущейТочки");
	ПараметрыОповещения.Ссылка = Объект.Ссылка;
	ПараметрыОповещения.ЧислоТочек = Объект.ЧислоТочек;
	ПараметрыОповещения.ШагСдвига = Объект.ШагСдвига;
	ПараметрыОповещения.ЦветЗаголовкаТекущейТочки = ЭтотОбъект.Цвет;
	ПараметрыОповещения.ПоложениеЗаголовкаТекущейТочки = Объект.ПоложениеЗаголовкаТекущейТочки;
	
	Оповестить("ПрофильМониторингаПараметрыДиаграммыЗаписан", ПараметрыОповещения, ЭтотОбъект);
КонецПроцедуры

&НаСервере
Процедура НастроитьОтображениеЦвета(Цвет = Неопределено)
	Если Цвет = Неопределено Тогда
		Попытка
			ЭтотОбъект.Цвет = ЗначениеИзСтрокиВнутр(Объект.ЦветЗаголовкаТекущейТочки);
		Исключение
			ЭтотОбъект.Цвет = Новый Цвет(0,0,0);
		КонецПопытки;
	Иначе
		ЭтотОбъект.Цвет = Цвет;
	КонецЕсли;
	
	ДанныеОЦвете = МониторингКлиентСервер.ДанныеЦветаЛинии(ЭтотОбъект.Цвет);
	Если ДанныеОЦвете <> Неопределено Тогда
		НазваниеЦветаСтрока = ДанныеОЦвете.Название;
		ИзображениеЦветаСтрока = "ggggg";
		ЭтотОбъект.Элементы.ИзображениеЦветаСтрока.ЦветТекста = ЭтотОбъект.Цвет;
		ЭтотОбъект.ЦветКрасный = ЭтотОбъект.Цвет.Красный;
		ЭтотОбъект.ЦветЗеленый = ЭтотОбъект.Цвет.Зеленый;
		ЭтотОбъект.ЦветСиний = ЭтотОбъект.Цвет.Синий;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЦветНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОписаниеОповещения = Новый ОписаниеОповещения("ЦветНачалоВыбораЗавершение", ЭтотОбъект);
	ПараметрыОткрытия = Новый Структура("Красный, Зеленый, Синий", Цвет.Красный, Цвет.Зеленый, Цвет.Синий);
	ОткрытьФорму("ОбщаяФорма.ФормаВыбораЦвета", ПараметрыОткрытия,,,,,ОписаниеОповещения,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

&НаКлиенте
Процедура ЦветНачалоВыбораЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если РезультатЗакрытия <> Неопределено Тогда
		НастроитьОтображениеЦвета(РезультатЗакрытия);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьЦветНаКлиенте()
	НовыйЦвет = Новый Цвет(ЭтотОбъект.ЦветКрасный, ЭтотОбъект.ЦветЗеленый, ЭтотОбъект.ЦветСиний);
	ЭтотОбъект.Элементы.ИзображениеЦветаСтрока.ЦветТекста = НовыйЦвет;
	ЭтотОбъект.НазваниеЦветаСтрока = "Произвольный(" + ЭтотОбъект.ЦветКрасный + ", " + ЭтотОбъект.ЦветЗеленый + ", " + ЭтотОбъект.ЦветСиний + ")";
	ЭтотОбъект.Цвет = НовыйЦвет;
КонецПроцедуры

&НаКлиенте
Процедура ЦветКрасныйПолзунокПриИзменении(Элемент)
	ИзменитьЦветНаКлиенте();
КонецПроцедуры

&НаКлиенте
Процедура ЦветЗеленыйПолзунокПриИзменении(Элемент)
	ИзменитьЦветНаКлиенте();
КонецПроцедуры

&НаКлиенте
Процедура ЦветСинийПозунокПриИзменении(Элемент)
	ИзменитьЦветНаКлиенте();
КонецПроцедуры

&НаКлиенте
Процедура ЦветКрасныйПриИзменении(Элемент)
	Если ЭтотОбъект.ЦветКрасный > 255 Тогда
		ЭтотОбъект.ЦветКрасный = 255;
	КонецЕсли;
	ИзменитьЦветНаКлиенте();
КонецПроцедуры

&НаКлиенте
Процедура ЦветЗеленыйПриИзменении(Элемент)
	Если ЭтотОбъект.ЦветЗеленый > 255 Тогда
		ЭтотОбъект.ЦветЗеленый = 255;
	КонецЕсли;
	ИзменитьЦветНаКлиенте();
КонецПроцедуры

&НаКлиенте
Процедура ЦветСинийПриИзменении(Элемент)
	Если ЭтотОбъект.ЦветСиний > 255 Тогда
		ЭтотОбъект.ЦветСиний = 255;
	КонецЕсли;
	ИзменитьЦветНаКлиенте();
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	ТекущийОбъект.ЦветЗаголовкаТекущейТочки = ЗначениеВСтрокуВнутр(Цвет);
КонецПроцедуры
