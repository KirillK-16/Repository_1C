#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	Индекс = ПроверяемыеРеквизиты.Найти("Наименование");
	Если Индекс <> Неопределено Тогда
		ПроверяемыеРеквизиты.Удалить(Индекс);
	КонецЕсли;
КонецПроцедуры

Функция УдалитьНастройку(НастройкаУИД) Экспорт
	Результат = Ложь;
	
	НачатьТранзакцию(РежимУправленияБлокировкойДанных.Управляемый);
	Попытка
		Настройки = ЭтотОбъект.НастройкиПоказателей.НайтиСтроки(Новый Структура("УИД", НастройкаУИД));
		Для Каждого Настройка Из Настройки Цикл
			ЭтотОбъект.НастройкиПоказателей.Удалить(ЭтотОбъект.НастройкиПоказателей.Индекс(Настройка));
		КонецЦикла;
		
		Настройки = ЭтотОбъект.ПорядокПоказателей.НайтиСтроки(Новый Структура("НастройкаПоказателя", НастройкаУИД));
		Для Каждого Настройка Из Настройки Цикл
			ЭтотОбъект.ПорядокПоказателей.Удалить(ЭтотОбъект.ПорядокПоказателей.Индекс(Настройка));
		КонецЦикла;
		
		ЭтотОбъект.Записать();
		Результат = Истина;
		
		ЗафиксироватьТранзакцию();
	Исключение
		Инфо = ИнформацияОбОшибке();
		Комментарий =
			"Описание = '" +Инфо.Описание + "', " +
			"ИмяМодуля = '" + Инфо.ИмяМодуля + "', " +
			"НомерСтроки = '" + Инфо.НомерСтроки + "', " +
			"ИсходнаяСтрока = '" + Инфо.ИсходнаяСтрока + "'.";
			
		ЗаписьЖурналаРегистрации(
			"Функция УдалитьНастройку(НастройкаУИД) Экспорт",
			УровеньЖурналаРегистрации.Ошибка,
			Метаданные.Справочники.ПрофильМониторинга.МодульОбъекта,
			,
			Комментарий);
					
		ОтменитьТранзакцию();
		Результат = Ложь;
	КонецПопытки;
		
	Возврат Результат;
КонецФункции

Функция КопироватьПоказатели(ПрофильСсылка, НастройкаУИД, НаименованиеНастройки) Экспорт
	УИДНастройки = Новый УникальныйИдентификатор();
	
	НоваяНастройка = ЭтотОбъект.НастройкиПоказателей.Добавить();
	НоваяНастройка.УИД = УИДНастройки;
	НоваяНастройка.Наименование = НаименованиеНастройки;
	
	Показатели = Справочники.ПрофильМониторинга.ПолучитьПоказатели(ПрофильСсылка, НастройкаУИД);
	НомерПоказателя = 0;
	Для Каждого Показатель Из Показатели Цикл
		Если Показатель.Показатель = Справочники.ПоказателиМониторинга.ПустаяСсылка() Тогда
			Продолжить;
		КонецЕсли;
		
		НовыйПоказатель = ЭтотОбъект.ПорядокПоказателей.Добавить();
		НовыйПоказатель.Номер = НомерПоказателя;
		НовыйПоказатель.ОтображатьНаДиаграмме = Показатель.ОтображатьНаДиаграмме;
		НовыйПоказатель.Автомасштаб = Показатель.Автомасштаб;
		НовыйПоказатель.Масштаб = Показатель.Масштаб;
		НовыйПоказатель.ПоказатьДанныеЗаПрошлыйПериод = Показатель.ПоказатьДанныеЗаПрошлыйПериод;
		НовыйПоказатель.ВременноеСмещение = Показатель.ВременноеСмещение;
		НовыйПоказатель.ПоказыватьТренд = Показатель.ПоказыватьТренд;
		НовыйПоказатель.КоличествоУсредняемыхЗначений = Показатель.КоличествоУсредняемыхЗначений;
		НовыйПоказатель.ТипЛинии = Показатель.ТипЛинии;
		НовыйПоказатель.ТипСглаживания = Показатель.ТипСглаживания;
		НовыйПоказатель.ТолщинаЛинии = Показатель.ТолщинаЛинии;
		
		НовыйПоказательПоказателя = Показатель.Показатель.Скопировать();
		НовыйПоказательПоказателя.Владелец = ЭтотОбъект.Ссылка;
		НовыйПоказательПоказателя.Записать();
		
		НовыйПоказатель.Показатель = НовыйПоказательПоказателя.Ссылка;
		НовыйПоказатель.НастройкаПоказателя = УИДНастройки;
	КонецЦикла;
	
	Возврат УИДНастройки;
КонецФункции

Процедура ПередЗаписью(Отказ)
	НастройкиПоказателей.Сортировать("Наименование");
КонецПроцедуры

#КонецЕсли
