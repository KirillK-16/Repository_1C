
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ВыполнитьАнализПроблемПроизводительности() Экспорт
	
	// 1. Получить список настроек
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	НастройкиАнализаПроблемПроизводительности.Ссылка КАК Ссылка
	                      |ИЗ
	                      |	Справочник.НастройкиАнализаПроблемПроизводительности КАК НастройкиАнализаПроблемПроизводительности
	                      |ГДЕ
	                      |	НЕ НастройкиАнализаПроблемПроизводительности.ПометкаУдаления");	
	Запрос.УстановитьПараметр("ТекущаяДата", НачалоДня(ТекущаяДата()));
	Выборка = Запрос.Выполнить().Выбрать();
	// 2. Выполнить анализа по настройке.
	Пока Выборка.Следующий() Цикл
		ВыполнитьАнализПроблемПроизводительностиПоНастройке(Выборка.Ссылка);	
	КонецЦикла;                                                             	
	
КонецПроцедуры

// Выполняет анализ проблем производительности по конкретной настройке.
// Параметры
// Настройка - ссылка на справочник НастройкиАнализаПроблемПроизводительности.
//
Процедура ВыполнитьАнализПроблемПроизводительностиПоНастройке(Настройка) Экспорт
	
	СтруктураНастроек = Новый Структура("ПрофильКлючевыхОпераций, МинимальноеЗначениеAPDEX, ПорогСнижения, КаталогВыгрузки,
	|ГлубинаИнтервалаАнализа, ОтступитьОтТекущейДаты, РегистрироватьПервые, МаксимальныйПриоритет, Конфигурации, Статусы");
	
	// 1. Получить данные настройки.
	ЗаполнитьЗначенияСвойств(СтруктураНастроек, Настройка);
	НастройкиКонфигураций = Настройка.Конфигурации.Выгрузить();
	СтруктураНастроек.Вставить("Конфигурации", НастройкиКонфигураций.ВыгрузитьКолонку("Конфигурация"));
	СтруктураНастроек.Вставить("НастройкиКонфигураций", НастройкиКонфигураций);
	СтруктураНастроек.Вставить("Статусы", Настройка.СтатусыИнформационнныхБаз.Выгрузить().ВыгрузитьКолонку("СтатусИнформационнойБазы"));
	ДатаОкончания = КонецДня(ТекущаяДата()) - СтруктураНастроек.ОтступитьОтТекущейДаты * 86400;
	ДатаНачала = ДатаОкончания + 1  - СтруктураНастроек.ГлубинаИнтервалаАнализа * 86400;
	
	// 2. Получить данные для анализа.
	Запрос = ЗапросДляАнализаНаЗакрытие(СтруктураНастроек, ДатаНачала, ДатаОкончания);
	Выборка = Запрос.Выполнить().Выбрать();
	
	// 3. Записать закрытые ошибки
	ШаблонПричинаЗакрытия = "Ошибка закрыта: %1. По причине: APDEX %2 в версии %3. Процент с низкой производительностью: %4. Плохих баз %5 из %6";
	ДатаПроверки = ТекущаяДата();	
	
	Пока Выборка.Следующий() Цикл
		ЗаписьОбъект = РегистрыСведений.РезультатАвтоматическогоАнализаПроизводительности.СоздатьМенеджерЗаписи();
		ЗаписьОбъект.КлючеваяОперация = Выборка.КлючеваяОперацияОшибки;
		ЗаписьОбъект.Конфигурация = Выборка.КонфигурацияОшибки;
		ЗаписьОбъект.ВерсияКонфигурации = Выборка.ВерсияКонфигурацииОшибки;
		ЗаписьОбъект.Прочитать();
		
		ЗаписьОбъект.Статус = Перечисления.СтатусыОшибокПроизводительности.Закрыта;
		ПричинаЗакрытия = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонПричинаЗакрытия, ДатаПроверки, Выборка.APDEX, Выборка.ВерсияКонфигурации,
							Выборка.ПроцентБаз, Выборка.НизкаяПроизводительность, Выборка.ВсегоИБ); 
		ЗаписьОбъект.ПричинаЗакрытия  = ПричинаЗакрытия;
		ЗаписьОбъект.Записать();
	КонецЦикла;
	
	// 4. Получить данные для анализа.
	Запрос = ЗапросДляАнализа(СтруктураНастроек, ДатаНачала, ДатаОкончания);
	РезультатМассив = Запрос.ВыполнитьПакет();
	Пакетов = РезультатМассив.Количество();
	ПроизводительностьПоВерсиям = РезультатМассив[Пакетов-3].Выгрузить();
	ПроизводительностьПоВерсиям.Индексы.Добавить("КлючеваяОперация, Конфигурация");
	ПроизводительностьПоИБ = РезультатМассив[Пакетов-2].Выгрузить();
	ПроизводительностьПоИБ.Индексы.Добавить("КлючеваяОперация, Конфигурация");
	ДанныеПоФО = РезультатМассив[Пакетов-1].Выгрузить();
	ДанныеПоФО.Индексы.Добавить("КлючеваяОперация, Конфигурация");
	
	Выборка = РезультатМассив[Пакетов-4].Выбрать();
	
	// 5. Записать ошибки.    	
	ДатаВыполненияАнализа = ТекущаяДата();
	Пока Выборка.Следующий() Цикл
		Если НЕ Выборка.Регистрировать Тогда
			Продолжить;
		КонецЕсли;
		Запись = РегистрыСведений.РезультатАвтоматическогоАнализаПроизводительности.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(Запись, Выборка);
		Запись.Прочитать();
		// Если такая ошибка уже была, новый идентификатор не нужен.
		Если НЕ Запись.Выбран() Тогда
			ЗаполнитьЗначенияСвойств(Запись, Выборка);
			Запись.ИдентификаторОшибки = Строка(Новый УникальныйИдентификатор);
			Запись.Статус = Перечисления.СтатусыОшибокПроизводительности.Создана;
		ИначеЕсли Запись.Статус = Перечисления.СтатусыОшибокПроизводительности.Закрыта Тогда
			Запись.Статус = Перечисления.СтатусыОшибокПроизводительности.Зарегистрирована;
			ЗаполнитьЗначенияСвойств(Запись, Выборка, "APDEX, Важность, Ответственный, РазницаПроизводительности, ВсегоЗамеров, ПроцентБаз, ВерсияКонфигурацииЛучшая");
			Запись.ПричинаЗакрытия = "";
		КонецЕсли;                                                             		
		Запись.ДатаВыполнения = ДатаВыполненияАнализа;
		
		// Отбор и заполнение дополнительных параметров.
		Отбор = Новый Структура("КлючеваяОперация, Конфигурация", Выборка.КлючеваяОперация, Выборка.Конфигурация);
		ПроизводительностьКОПоВерсиям = ПроизводительностьПоВерсиям.Скопировать(ПроизводительностьПоВерсиям.НайтиСтроки(Отбор));
		ПроизводительностьКОПоИБ = ПроизводительностьПоИБ.Скопировать(ПроизводительностьПоИБ.НайтиСтроки(Отбор));		
		ДанныеПоФОиКО = ДанныеПоФО.Скопировать(ДанныеПоФО.НайтиСтроки(Отбор));
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ПроизводительностьКОПоВерсиям", ПроизводительностьКОПоВерсиям);
		ДополнительныеПараметры.Вставить("ПроизводительностьКОПоИБ", ПроизводительностьКОПоИБ);
		ДополнительныеПараметры.Вставить("ДанныеПоФОиКО", ДанныеПоФОиКО);
		
		ТабличныйДокумент = РегистрыСведений.РезультатАвтоматическогоАнализаПроизводительности.ДанныеОшибки(Выборка, СтруктураНастроек, ДатаНачала, ДатаОкончания, ДополнительныеПараметры);
		Запись.ДанныеАнализа = Новый ХранилищеЗначения(ТабличныйДокумент, Новый СжатиеДанных(9));
		Запись.НастройкаАнализа = Настройка;          		
		Запись.Записать();	
	КонецЦикла;      	
		
КонецПроцедуры

Функция ЗапросДляАнализа(СтруктураНастроек, ДатаНачала, ДатаОкончания)
	Запрос = Новый Запрос;
    МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
    Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
    Справочники.ПрофилиКлючевыхОпераций.ДобавитьВременнуюТаблицуКлючевыхОпераций(СтруктураНастроек.ПрофильКлючевыхОпераций, Запрос, "КлючевыеОперации");	
	Запрос.Текст = Запрос.Текст + ТекстЗапросаАнализа(ДатаНачала, ДатаОкончания);
	Если СтруктураНастроек.Статусы.Количество() Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "//СоединениеСтатусы", "			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтатусИнформационнойБазы КАК СтатусИнформационнойБазы
			|			ПО ИнформацияИстория.ИнформационнаяБаза = СтатусИнформационнойБазы.ИнформационнаяБаза
			|				И (СтатусИнформационнойБазы.Статус В (&Статус))");
	КонецЕсли;
	Если ЗначениеЗаполнено(СтруктураНастроек.РегистрироватьПервые) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Первые 1 //#Первые", "Первые " + Формат(СтруктураНастроек.РегистрироватьПервые,"ЧГ="));
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Первые 1 //#Первые", "");
	КонецЕсли;
	Если ЗначениеЗаполнено(СтруктураНастроек.МаксимальныйПриоритет) Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "//#ОтборПоПриоритету","И (КлючевыеОперации.Важность <= &МаксимальныйПриоритет)");
	КонецЕсли;
	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", ДатаОкончания);
	Запрос.УстановитьПараметр("Конфигурация", СтруктураНастроек.Конфигурации);
	Запрос.УстановитьПараметр("НастройкиКонфигураций", СтруктураНастроек.НастройкиКонфигураций);
	Запрос.УстановитьПараметр("ПустаяСтрока", "");
	Запрос.УстановитьПараметр("Статус", СтруктураНастроек.Статусы);
	Запрос.УстановитьПараметр("ПорогСнижения", СтруктураНастроек.ПорогСнижения);
	Запрос.УстановитьПараметр("МинимальноеЗначениеAPDEX", СтруктураНастроек.МинимальноеЗначениеAPDEX);
	Запрос.УстановитьПараметр("МаксимальныйПриоритет", СтруктураНастроек.МаксимальныйПриоритет);
	Возврат Запрос;
КонецФункции

Функция ЗапросДляАнализаНаЗакрытие(СтруктураНастроек, ДатаНачала, ДатаОкончания)
	Запрос = Новый Запрос;
    МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
    Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
    Справочники.ПрофилиКлючевыхОпераций.ДобавитьВременнуюТаблицуКлючевыхОпераций(СтруктураНастроек.ПрофильКлючевыхОпераций, Запрос, "КлючевыеОперации");	
	Запрос.Текст = Запрос.Текст + ТекстЗапросаАнализаНаЗакрытие(ДатаНачала, ДатаОкончания);
	Если СтруктураНастроек.Статусы.Количество() Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "//СоединениеСтатусы", "			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтатусИнформационнойБазы КАК СтатусИнформационнойБазы
			|			ПО ИнформацияИстория.ИнформационнаяБаза = СтатусИнформационнойБазы.ИнформационнаяБаза
			|				И (СтатусИнформационнойБазы.Статус В (&Статус))");
	КонецЕсли;
	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", ДатаОкончания);
	Запрос.УстановитьПараметр("Конфигурация", СтруктураНастроек.Конфигурации);
	Запрос.УстановитьПараметр("НастройкиКонфигураций", СтруктураНастроек.НастройкиКонфигураций);
	Запрос.УстановитьПараметр("ПустаяСтрока", "");
	Запрос.УстановитьПараметр("Статус", СтруктураНастроек.Статусы);
	Запрос.УстановитьПараметр("ПорогСнижения", СтруктураНастроек.ПорогСнижения);
	Запрос.УстановитьПараметр("МинимальноеЗначениеAPDEX", СтруктураНастроек.МинимальноеЗначениеAPDEX);
	Возврат Запрос;
КонецФункции

Функция ТекстЗапросаАнализа(ДатаНачала, ДатаОкончания)
	ТекстЗапроса = Общий.ТекстЗапросаПериодов(ДатаНачала, ДатаОкончания, "День");
    ТекстЗапроса = ТекстЗапроса + ";
	       	|
			|////////////////////////////////////////////////////////////////////////////////			
			|ВЫБРАТЬ
			|	НастройкиКонфигураций.Конфигурация КАК Конфигурация,
			|	НастройкиКонфигураций.МинимальноеКоличествоБаз КАК МинимальноеКоличествоБаз,
			|	НастройкиКонфигураций.МинимальныйПроцентБаз КАК МинимальныйПроцентБаз
			|ПОМЕСТИТЬ НастройкиКонфигураций
			|ИЗ
			|	&НастройкиКонфигураций КАК НастройкиКонфигураций
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Конфигурация
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ВерсииПлатформыЦентрМониторинга.Ссылка КАК Ссылка
			|ПОМЕСТИТЬ РелизныеПлатформы
			|ИЗ
			|	Справочник.ВерсииПлатформыЦентрМониторинга КАК ВерсииПлатформыЦентрМониторинга
			|ГДЕ
			|	ВерсииПлатформыЦентрМониторинга.Релизная
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Ссылка
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ВерсииКонфигурации.Ссылка КАК Ссылка,
			|	ВерсииКонфигурации.ВерсияЧисло КАК ВерсияКонфигурацииЧисло
			|ПОМЕСТИТЬ РелизныеВерсииКонфигурации
			|ИЗ
			|	Справочник.ВерсииКонфигурации КАК ВерсииКонфигурации
			|ГДЕ
			|	ВерсииКонфигурации.Владелец В(&Конфигурация)
			|	И ВерсииКонфигурации.Релизная
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Ссылка
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ИнформацияИстория.ПериодЗаписи КАК ПериодЗаписи,
			|	ИнформацияИстория.ИнформационнаяБаза КАК ИнформационнаяБаза,
			|	ИнформацияИстория.Конфигурация КАК Конфигурация,
			|	ИнформацияИстория.ВерсияКонфигурации КАК ВерсияКонфигурации,
			|	РелизныеВерсииКонфигурации.ВерсияКонфигурацииЧисло КАК ВерсияКонфигурацииЧисло
			|ПОМЕСТИТЬ ИнформацияИстория
			|ИЗ
			|	Периоды КАК Периоды
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ИнформацияИсторияПодробно КАК ИнформацияИстория
			|				//СоединениеСтатусы
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РелизныеПлатформы КАК РелизныеПлатформы
			|			ПО ИнформацияИстория.ВерсияПлатформы = РелизныеПлатформы.Ссылка
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РелизныеВерсииКонфигурации КАК РелизныеВерсииКонфигурации
			|			ПО ИнформацияИстория.ВерсияКонфигурации = РелизныеВерсииКонфигурации.Ссылка
			|		ПО (ИнформацияИстория.ПериодЗаписи = Периоды.Период)
			|			И (ИнформацияИстория.Конфигурация В (&Конфигурация))
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	ИнформацияИстория.ИнформационнаяБаза,
			|	ИнформацияИстория.ПериодЗаписи
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ИнформацияИстория.Конфигурация КАК Конфигурация,
			|	ИнформацияИстория.ВерсияКонфигурации КАК ВерсияКонфигурации,
			|	ИнформацияИстория.ВерсияКонфигурацииЧисло КАК ВерсияКонфигурацииЧисло,
			|	ИнформацияИстория.ИнформационнаяБаза КАК ИнформационнаяБаза,
			|	Замеры.КлючеваяОперация КАК КлючеваяОперация,
			|	ВЫБОР
			|		КОГДА КлючевыеОперации.ИмяПредставление = &ПустаяСтрока
			|			ТОГДА Замеры.КлючеваяОперация.Наименование
			|		ИНАЧЕ КлючевыеОперации.ИмяПредставление
			|	КОНЕЦ КАК КлючеваяОперацияПредставление,
			|	КлючевыеОперации.ЦелевоеВремя КАК ЦелевоеВремя,
			|	КлючевыеОперации.Ответственный КАК Ответственный,
			|	КлючевыеОперации.Важность КАК Важность,
			|	СУММА(Замеры.КоличествоОпераций) КАК КоличествоОпераций,
			|	СУММА(Замеры.ВремяВыполнения * Замеры.КоличествоОпераций) КАК СуммаВремяВыполнения,
			|	СУММА(ВЫБОР
			|			КОГДА Замеры.ВремяВыполнения <= ЕСТЬNULL(КлючевыеОперации.ЦелевоеВремя, 1)
			|				ТОГДА Замеры.КоличествоОпераций
			|			ИНАЧЕ 0
			|		КОНЕЦ) КАК N_T,
			|	СУММА(ВЫБОР
			|			КОГДА Замеры.ВремяВыполнения > ЕСТЬNULL(КлючевыеОперации.ЦелевоеВремя, 1)
			|					И Замеры.ВремяВыполнения <= ЕСТЬNULL(КлючевыеОперации.ЦелевоеВремя, 1) * 4
			|				ТОГДА Замеры.КоличествоОпераций
			|			ИНАЧЕ 0
			|		КОНЕЦ) КАК N_T_4T,
			|	СУММА(ВЫБОР
			|			КОГДА Замеры.ВремяВыполнения > ЕСТЬNULL(КлючевыеОперации.ЦелевоеВремя, 1) * 4
			|				ТОГДА Замеры.КоличествоОпераций
			|			ИНАЧЕ 0
			|		КОНЕЦ) КАК N_4T,
			|	ВЫБОР
			|		КОГДА (СУММА(ВЫБОР
			|					КОГДА Замеры.ВремяВыполнения <= ЕСТЬNULL(КлючевыеОперации.ЦелевоеВремя, 1)
			|						ТОГДА Замеры.КоличествоОпераций
			|					ИНАЧЕ 0
			|				КОНЕЦ) + СУММА(ВЫБОР
			|					КОГДА Замеры.ВремяВыполнения > ЕСТЬNULL(КлючевыеОперации.ЦелевоеВремя, 1)
			|							И Замеры.ВремяВыполнения <= ЕСТЬNULL(КлючевыеОперации.ЦелевоеВремя, 1) * 4
			|						ТОГДА Замеры.КоличествоОпераций
			|					ИНАЧЕ 0
			|				КОНЕЦ) / 2) / СУММА(Замеры.КоличествоОпераций) < &МинимальноеЗначениеAPDEX
			|			ТОГДА 1
			|		ИНАЧЕ 0
			|	КОНЕЦ КАК НизкаяПроизводительность
			|ПОМЕСТИТЬ ВсеЗамеры
			|ИЗ
			|	РегистрСведений.ЗамерыВремениЦентрМониторинга КАК Замеры
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ИнформацияИстория КАК ИнформацияИстория
			|		ПО Замеры.Период = ИнформацияИстория.ПериодЗаписи
			|			И Замеры.ИнформационнаяБаза = ИнформацияИстория.ИнформационнаяБаза
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ КлючевыеОперации КАК КлючевыеОперации
			|		ПО Замеры.КлючеваяОперация = КлючевыеОперации.КлючеваяОперация
			|		//#ОтборПоПриоритету
			|
			|СГРУППИРОВАТЬ ПО
			|	ИнформацияИстория.Конфигурация,
			|	ИнформацияИстория.ВерсияКонфигурации,
			|	ИнформацияИстория.ВерсияКонфигурацииЧисло,
			|	ИнформацияИстория.ИнформационнаяБаза,
			|	Замеры.КлючеваяОперация,
			|	КлючевыеОперации.МинимальноеКоличествоЗамеров,
			|	ВЫБОР
			|		КОГДА КлючевыеОперации.ИмяПредставление = &ПустаяСтрока
			|			ТОГДА Замеры.КлючеваяОперация.Наименование
			|		ИНАЧЕ КлючевыеОперации.ИмяПредставление
			|	КОНЕЦ,
			|	КлючевыеОперации.ЦелевоеВремя,
			|	КлючевыеОперации.Ответственный,
			|	КлючевыеОперации.Важность
			|
			|ИМЕЮЩИЕ
			|	СУММА(Замеры.КоличествоОпераций) >= КлючевыеОперации.МинимальноеКоличествоЗамеров
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	КлючеваяОперация,
			|	ИнформационнаяБаза,
			|	ВерсияКонфигурации,
			|	Конфигурация
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ВсеЗамеры.Конфигурация КАК Конфигурация,
			|	ВсеЗамеры.ВерсияКонфигурации КАК ВерсияКонфигурации,
			|	ВсеЗамеры.КлючеваяОперация КАК КлючеваяОперация,
			|	СУММА(ВсеЗамеры.КоличествоОпераций) КАК КоличествоОпераций,
			|	СУММА(ВсеЗамеры.СуммаВремяВыполнения) КАК СуммаВремяВыполнения,
			|	СУММА(ВсеЗамеры.N_T) КАК N_T,
			|	СУММА(ВсеЗамеры.N_T_4T) КАК N_T_4T,
			|	СУММА(ВсеЗамеры.N_4T) КАК N_4T,
			|	СУММА(ВсеЗамеры.НизкаяПроизводительность) КАК НизкаяПроизводительность,
			|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВсеЗамеры.ИнформационнаяБаза) КАК ВсегоИБ,
			|	ВсеЗамеры.ВерсияКонфигурацииЧисло КАК ВерсияКонфигурацииЧисло,
			|	МАКСИМУМ(НастройкиКонфигураций.МинимальноеКоличествоБаз) КАК МинимальноеКоличествоБаз,
			|	МАКСИМУМ(НастройкиКонфигураций.МинимальныйПроцентБаз) КАК МинимальныйПроцентБаз
			|ПОМЕСТИТЬ ПроизводительностьКОВРазрезеВерсий
			|ИЗ
			|	ВсеЗамеры КАК ВсеЗамеры
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ НастройкиКонфигураций КАК НастройкиКонфигураций
			|		ПО ВсеЗамеры.Конфигурация = НастройкиКонфигураций.Конфигурация
			|
			|СГРУППИРОВАТЬ ПО
			|	ВсеЗамеры.Конфигурация,
			|	ВсеЗамеры.ВерсияКонфигурации,
			|	ВсеЗамеры.КлючеваяОперация,
			|	ВсеЗамеры.ВерсияКонфигурацииЧисло
			|
			|ИМЕЮЩИЕ
			|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВсеЗамеры.ИнформационнаяБаза) >= МАКСИМУМ(НастройкиКонфигураций.МинимальноеКоличествоБаз)
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	КлючеваяОперация,
			|	ВерсияКонфигурацииЧисло,
			|	Конфигурация,
			|	ВерсияКонфигурации
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ПроизводительностьКОВРазрезеВерсий.Конфигурация КАК Конфигурация,
			|	ПроизводительностьКОВРазрезеВерсий.КлючеваяОперация КАК КлючеваяОперация,
			|	МАКСИМУМ(ПроизводительностьКОВРазрезеВерсий.ВерсияКонфигурацииЧисло) КАК ВерсияКонфигурацииЧисло
			|ПОМЕСТИТЬ КОПоследнихВерсий
			|ИЗ
			|	ПроизводительностьКОВРазрезеВерсий КАК ПроизводительностьКОВРазрезеВерсий
			|
			|СГРУППИРОВАТЬ ПО
			|	ПроизводительностьКОВРазрезеВерсий.Конфигурация,
			|	ПроизводительностьКОВРазрезеВерсий.КлючеваяОперация
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	КлючеваяОперация,
			|	Конфигурация
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ПроизводительностьКОВРазрезеВерсий.Конфигурация КАК Конфигурация,
			|	ПроизводительностьКОВРазрезеВерсий.ВерсияКонфигурации КАК ВерсияКонфигурации,
			|	ПроизводительностьКОВРазрезеВерсий.КлючеваяОперация КАК КлючеваяОперация,
			|	ПроизводительностьКОВРазрезеВерсий.КоличествоОпераций КАК КоличествоОпераций,
			|	ПроизводительностьКОВРазрезеВерсий.СуммаВремяВыполнения КАК СуммаВремяВыполнения,
			|	ПроизводительностьКОВРазрезеВерсий.N_T КАК N_T,
			|	ПроизводительностьКОВРазрезеВерсий.N_T_4T КАК N_T_4T,
			|	ПроизводительностьКОВРазрезеВерсий.N_4T КАК N_4T,
			|	ПроизводительностьКОВРазрезеВерсий.НизкаяПроизводительность КАК НизкаяПроизводительность,
			|	ПроизводительностьКОВРазрезеВерсий.ВсегоИБ КАК ВсегоИБ,
			|	ПроизводительностьКОВРазрезеВерсий.ВерсияКонфигурацииЧисло КАК ВерсияКонфигурацииЧисло,
			|	ПроизводительностьКОВРазрезеВерсий.МинимальноеКоличествоБаз КАК МинимальноеКоличествоБаз,
			|	ПроизводительностьКОВРазрезеВерсий.МинимальныйПроцентБаз КАК МинимальныйПроцентБаз
			|ПОМЕСТИТЬ ПроблемныеКО
			|ИЗ
			|	КОПоследнихВерсий КАК КОПоследнихВерсий
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПроизводительностьКОВРазрезеВерсий КАК ПроизводительностьКОВРазрезеВерсий
			|		ПО КОПоследнихВерсий.КлючеваяОперация = ПроизводительностьКОВРазрезеВерсий.КлючеваяОперация
			|			И КОПоследнихВерсий.ВерсияКонфигурацииЧисло = ПроизводительностьКОВРазрезеВерсий.ВерсияКонфигурацииЧисло
			|			И КОПоследнихВерсий.Конфигурация = ПроизводительностьКОВРазрезеВерсий.Конфигурация
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	КлючеваяОперация,
			|	ВерсияКонфигурации,
			|	Конфигурация
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ВсеЗамеры.Конфигурация КАК Конфигурация,
			|	ВсеЗамеры.ВерсияКонфигурации КАК ВерсияКонфигурации,
			|	ВсеЗамеры.ИнформационнаяБаза КАК ИнформационнаяБаза,
			|	ВсеЗамеры.КлючеваяОперация КАК КлючеваяОперация,
			|	ВсеЗамеры.КлючеваяОперацияПредставление КАК КлючеваяОперацияПредставление,
			|	ВсеЗамеры.ЦелевоеВремя КАК ЦелевоеВремя,
			|	ВсеЗамеры.Ответственный КАК Ответственный,
			|	ВсеЗамеры.Важность КАК Важность,
			|	ВсеЗамеры.КоличествоОпераций КАК КоличествоОпераций,
			|	ВЫРАЗИТЬ((ВсеЗамеры.N_T + ВсеЗамеры.N_T_4T / 2) / ВсеЗамеры.КоличествоОпераций КАК ЧИСЛО(3, 2)) КАК APDEX,
			|	ВЫРАЗИТЬ(ВсеЗамеры.СуммаВремяВыполнения / ВсеЗамеры.КоличествоОпераций КАК ЧИСЛО(15, 3)) КАК СреднееВремя,
			|	ВсеЗамеры.СуммаВремяВыполнения КАК СуммаВремяВыполнения,
			|	ВсеЗамеры.N_T КАК N_T,
			|	ВсеЗамеры.N_T_4T КАК N_T_4T,
			|	ВсеЗамеры.N_4T КАК N_4T,
			|	ВсеЗамеры.НизкаяПроизводительность КАК НизкаяПроизводительность,
			|	ВсеЗамеры.ВерсияКонфигурацииЧисло КАК ВерсияКонфигурацииЧисло,
			|	ПроблемныеКО.МинимальноеКоличествоБаз КАК МинимальноеКоличествоБаз,
			|	ПроблемныеКО.МинимальныйПроцентБаз КАК МинимальныйПроцентБаз
			|ПОМЕСТИТЬ ИБПроблемныхКО
			|ИЗ
			|	ПроблемныеКО КАК ПроблемныеКО
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВсеЗамеры КАК ВсеЗамеры
			|		ПО ПроблемныеКО.КлючеваяОперация = ВсеЗамеры.КлючеваяОперация
			|			И ПроблемныеКО.Конфигурация = ВсеЗамеры.Конфигурация
			|			И ПроблемныеКО.ВерсияКонфигурации = ВсеЗамеры.ВерсияКонфигурации
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	КлючеваяОперация,
			|	ИнформационнаяБаза,
			|	Конфигурация,
			|	ВерсияКонфигурации
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ВсеЗамеры.Конфигурация КАК Конфигурация,
			|	ВсеЗамеры.ВерсияКонфигурации КАК ВерсияКонфигурации,
			|	ВсеЗамеры.ВерсияКонфигурацииЧисло КАК ВерсияКонфигурацииЧисло,
			|	ВсеЗамеры.ИнформационнаяБаза КАК ИнформационнаяБаза,
			|	ВсеЗамеры.КлючеваяОперация КАК КлючеваяОперация,
			|	ВсеЗамеры.КоличествоОпераций КАК КоличествоОпераций,
			|	ВЫРАЗИТЬ((ВсеЗамеры.N_T + ВсеЗамеры.N_T_4T / 2) / ВсеЗамеры.КоличествоОпераций КАК ЧИСЛО(3, 2)) КАК APDEX,
			|	ВЫРАЗИТЬ(ВсеЗамеры.СуммаВремяВыполнения / ВсеЗамеры.КоличествоОпераций КАК ЧИСЛО(15, 3)) КАК СреднееВремя,
			|	ВсеЗамеры.СуммаВремяВыполнения КАК СуммаВремяВыполнения,
			|	ВсеЗамеры.N_T КАК N_T,
			|	ВсеЗамеры.N_T_4T КАК N_T_4T,
			|	ВсеЗамеры.N_4T КАК N_4T,
			|	ВсеЗамеры.НизкаяПроизводительность КАК НизкаяПроизводительность,
			|	ВЫБОР
			|		КОГДА ВсеЗамеры.ВерсияКонфигурации = ИБПроблемныхКО.ВерсияКонфигурации
			|			ТОГДА ИСТИНА
			|		ИНАЧЕ ЛОЖЬ
			|	КОНЕЦ КАК ЭтоПоследняяВерсия
			|ПОМЕСТИТЬ ИнформацияПоИБ
			|ИЗ
			|	ВсеЗамеры КАК ВсеЗамеры
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ИБПроблемныхКО КАК ИБПроблемныхКО
			|		ПО ВсеЗамеры.КлючеваяОперация = ИБПроблемныхКО.КлючеваяОперация
			|			И ВсеЗамеры.ИнформационнаяБаза = ИБПроблемныхКО.ИнформационнаяБаза
			|			И ВсеЗамеры.Конфигурация = ИБПроблемныхКО.Конфигурация
			|			И ВсеЗамеры.ВерсияКонфигурацииЧисло <= ИБПроблемныхКО.ВерсияКонфигурацииЧисло
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	КлючеваяОперация,
			|	ИнформационнаяБаза,
			|	Конфигурация,
			|	ВерсияКонфигурацииЧисло
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ИнформацияПоИБ.Конфигурация КАК Конфигурация,
			|	ИнформацияПоИБ.ИнформационнаяБаза КАК ИнформационнаяБаза,
			|	ИнформацияПоИБ.КлючеваяОперация КАК КлючеваяОперация,
			|	МАКСИМУМ(ИнформацияПоИБ.APDEX) КАК APDEX
			|ПОМЕСТИТЬ КОЛучшийAPDEXКОПоИБ
			|ИЗ
			|	ИнформацияПоИБ КАК ИнформацияПоИБ
			|	Внутреннее Соединение ИнформацияПоИБ КАК ИнформацияПоИБПоследнииВерсии
			|		По ИнформацияПоИБ.КлючеваяОперация = ИнформацияПоИБПоследнииВерсии.КлючеваяОперация
			|			И ИнформацияПоИБ.ИнформационнаяБаза = ИнформацияПоИБПоследнииВерсии.ИнформационнаяБаза
			|			И ИнформацияПоИБ.Конфигурация = ИнформацияПоИБПоследнииВерсии.Конфигурация
			|			И ИнформацияПоИБПоследнииВерсии.ЭтоПоследняяВерсия
			|			И (ИнформацияПоИБ.КоличествоОпераций >= ИнформацияПоИБПоследнииВерсии.КоличествоОпераций / 10)
			|			И (ИнформацияПоИБ.КоличествоОпераций <= ИнформацияПоИБПоследнииВерсии.КоличествоОпераций * 10)
			|ГДЕ
			|	НЕ ИнформацияПоИБ.ЭтоПоследняяВерсия
			|
			|СГРУППИРОВАТЬ ПО
			|	ИнформацияПоИБ.Конфигурация,
			|	ИнформацияПоИБ.ИнформационнаяБаза,
			|	ИнформацияПоИБ.КлючеваяОперация
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	КлючеваяОперация,
			|	ИнформационнаяБаза,
			|	Конфигурация
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ИнформацияПоИБ.Конфигурация КАК Конфигурация,
			|	МАКСИМУМ(ИнформацияПоИБ.ВерсияКонфигурацииЧисло) КАК ВерсияКонфигурацииЧисло,
			|	ИнформацияПоИБ.ИнформационнаяБаза КАК ИнформационнаяБаза,
			|	ИнформацияПоИБ.КлючеваяОперация КАК КлючеваяОперация
			|ПОМЕСТИТЬ ЛучшиеВерсииИБПоКО
			|ИЗ
			|	КОЛучшийAPDEXКОПоИБ КАК КОЛучшийAPDEXКОПоИБ
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ИнформацияПоИБ КАК ИнформацияПоИБ
			|		ПО КОЛучшийAPDEXКОПоИБ.КлючеваяОперация = ИнформацияПоИБ.КлючеваяОперация
			|			И КОЛучшийAPDEXКОПоИБ.ИнформационнаяБаза = ИнформацияПоИБ.ИнформационнаяБаза
			|			И КОЛучшийAPDEXКОПоИБ.Конфигурация = ИнформацияПоИБ.Конфигурация
			|			И КОЛучшийAPDEXКОПоИБ.APDEX = ИнформацияПоИБ.APDEX
			|			И (НЕ ИнформацияПоИБ.ЭтоПоследняяВерсия)
			|
			|СГРУППИРОВАТЬ ПО
			|	ИнформацияПоИБ.Конфигурация,
			|	ИнформацияПоИБ.ИнформационнаяБаза,
			|	ИнформацияПоИБ.КлючеваяОперация
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	КлючеваяОперация,
			|	ИнформационнаяБаза,
			|	Конфигурация,
			|	ВерсияКонфигурацииЧисло
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ИнформацияПоИБ.Конфигурация КАК Конфигурация,
			|	ИнформацияПоИБ.ВерсияКонфигурации КАК ВерсияКонфигурации,
			|	ИнформацияПоИБ.ВерсияКонфигурацииЧисло КАК ВерсияКонфигурацииЧисло,
			|	ИнформацияПоИБ.ИнформационнаяБаза КАК ИнформационнаяБаза,
			|	ИнформацияПоИБ.КлючеваяОперация КАК КлючеваяОперация,
			|	ИнформацияПоИБ.КоличествоОпераций КАК КоличествоОпераций,
			|	ИнформацияПоИБ.APDEX КАК APDEX,
			|	ИнформацияПоИБ.СреднееВремя КАК СреднееВремя,
			|	ИнформацияПоИБ.СуммаВремяВыполнения КАК СуммаВремяВыполнения,
			|	ИнформацияПоИБ.N_T КАК N_T,
			|	ИнформацияПоИБ.N_T_4T КАК N_T_4T,
			|	ИнформацияПоИБ.N_4T КАК N_4T
			|ПОМЕСТИТЬ ДанныеКОЛучшихВерсийПоИБ
			|ИЗ
			|	ЛучшиеВерсииИБПоКО КАК ЛучшиеВерсииИБПоКО
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ИнформацияПоИБ КАК ИнформацияПоИБ
			|		ПО ЛучшиеВерсииИБПоКО.КлючеваяОперация = ИнформацияПоИБ.КлючеваяОперация
			|			И ЛучшиеВерсииИБПоКО.ИнформационнаяБаза = ИнформацияПоИБ.ИнформационнаяБаза
			|			И ЛучшиеВерсииИБПоКО.Конфигурация = ИнформацияПоИБ.Конфигурация
			|			И ЛучшиеВерсииИБПоКО.ВерсияКонфигурацииЧисло = ИнформацияПоИБ.ВерсияКонфигурацииЧисло
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	КлючеваяОперация,
			|	ИнформационнаяБаза,
			|	Конфигурация
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ИнформацияПоИБ.КлючеваяОперация КАК КлючеваяОперация,
			|	ИнформацияПоИБ.ИнформационнаяБаза КАК ИнформационнаяБаза,
			|	ИнформацияПоИБ.Конфигурация КАК Конфигурация
			|ПОМЕСТИТЬ ИБИмеющиеНесколькоВерсий
			|ИЗ
			|	ИнформацияПоИБ КАК ИнформацияПоИБ
			|ГДЕ
			|	НЕ ИнформацияПоИБ.ЭтоПоследняяВерсия
			|
			|СГРУППИРОВАТЬ ПО
			|	ИнформацияПоИБ.КлючеваяОперация,
			|	ИнформацияПоИБ.ИнформационнаяБаза,
			|	ИнформацияПоИБ.Конфигурация
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	КлючеваяОперация,
			|	ИнформационнаяБаза,
			|	Конфигурация
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ИБПроблемныхКО.Конфигурация КАК Конфигурация,
			|	ИБПроблемныхКО.ВерсияКонфигурации КАК ВерсияКонфигурации,
			|	ИБПроблемныхКО.ИнформационнаяБаза КАК ИнформационнаяБаза,
			|	ИБПроблемныхКО.КлючеваяОперация КАК КлючеваяОперация,
			|	ИБПроблемныхКО.КлючеваяОперацияПредставление КАК КлючеваяОперацияПредставление,
			|	ИБПроблемныхКО.ЦелевоеВремя КАК ЦелевоеВремя,
			|	ИБПроблемныхКО.Ответственный КАК Ответственный,
			|	ИБПроблемныхКО.Важность КАК Важность,
			|	ВЫБОР
			|		КОГДА ЕСТЬNULL(ДанныеКОЛучшихВерсийПоИБ.APDEX, 0) - ИБПроблемныхКО.APDEX >= &ПорогСнижения
			|			ТОГДА 1
			|		ИНАЧЕ 0
			|	КОНЕЦ КАК Ухудшение,
			|	ВЫБОР
			|		КОГДА ЕСТЬNULL(ДанныеКОЛучшихВерсийПоИБ.APDEX, 0) - ИБПроблемныхКО.APDEX <= -&ПорогСнижения
			|			ТОГДА 1
			|		ИНАЧЕ 0
			|	КОНЕЦ КАК Улучшение,
			|	ВЫБОР
			|		КОГДА ЕСТЬNULL(ДанныеКОЛучшихВерсийПоИБ.APDEX, 0) - ИБПроблемныхКО.APDEX < &ПорогСнижения
			|			И ЕСТЬNULL(ДанныеКОЛучшихВерсийПоИБ.APDEX, 0) - ИБПроблемныхКО.APDEX > -&ПорогСнижения
			|			ТОГДА 1
			|		ИНАЧЕ 0
			|	КОНЕЦ КАК БезИзменений,
			|	ИБПроблемныхКО.КоличествоОпераций КАК КоличествоОпераций,
			|	ИБПроблемныхКО.APDEX КАК APDEX,
			|	ИБПроблемныхКО.СреднееВремя КАК СреднееВремя,
			|	ИБПроблемныхКО.СуммаВремяВыполнения КАК СуммаВремяВыполнения,
			|	ИБПроблемныхКО.N_T КАК N_T,
			|	ИБПроблемныхКО.N_T_4T КАК N_T_4T,
			|	ИБПроблемныхКО.N_4T КАК N_4T,
			|	ИБПроблемныхКО.НизкаяПроизводительность КАК НизкаяПроизводительность,
			|	ИБПроблемныхКО.ВерсияКонфигурацииЧисло КАК ВерсияКонфигурацииЧисло,
			|	ДанныеКОЛучшихВерсийПоИБ.ВерсияКонфигурации КАК ВерсияКонфигурацииЛучшая,
			|	ДанныеКОЛучшихВерсийПоИБ.ВерсияКонфигурацииЧисло КАК ВерсияКонфигурацииЧислоЛучшая,
			|	ДанныеКОЛучшихВерсийПоИБ.КоличествоОпераций КАК КоличествоОперацийЛучшая,
			|	ДанныеКОЛучшихВерсийПоИБ.APDEX КАК APDEXЛучшая,
			|	ДанныеКОЛучшихВерсийПоИБ.N_T КАК N_TЛучшая,
			|	ДанныеКОЛучшихВерсийПоИБ.N_T_4T КАК N_T_4TЛучшая,
			|	ДанныеКОЛучшихВерсийПоИБ.N_4T КАК N_4TЛучшая,
			|	ДанныеКОЛучшихВерсийПоИБ.СреднееВремя КАК СреднееВремяЛучшая,
			|	ДанныеКОЛучшихВерсийПоИБ.СуммаВремяВыполнения КАК СуммаВремяВыполненияЛучшая,
			|	ИБПроблемныхКО.МинимальноеКоличествоБаз КАК МинимальноеКоличествоБаз,
			|	ИБПроблемныхКО.МинимальныйПроцентБаз КАК МинимальныйПроцентБаз
			|ПОМЕСТИТЬ ДинамикаКОПоследнейВерсииПоИБ
			|ИЗ
			|	ИБПроблемныхКО КАК ИБПроблемныхКО
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ИБИмеющиеНесколькоВерсий КАК ИБИмеющиеНесколькоВерсий
			|		ПО ИБПроблемныхКО.КлючеваяОперация = ИБИмеющиеНесколькоВерсий.КлючеваяОперация
			|			И ИБПроблемныхКО.ИнформационнаяБаза = ИБИмеющиеНесколькоВерсий.ИнформационнаяБаза
			|			И ИБПроблемныхКО.Конфигурация = ИБИмеющиеНесколькоВерсий.Конфигурация
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеКОЛучшихВерсийПоИБ КАК ДанныеКОЛучшихВерсийПоИБ
			|		ПО ИБПроблемныхКО.КлючеваяОперация = ДанныеКОЛучшихВерсийПоИБ.КлючеваяОперация
			|			И ИБПроблемныхКО.ИнформационнаяБаза = ДанныеКОЛучшихВерсийПоИБ.ИнформационнаяБаза
			|			И ИБПроблемныхКО.Конфигурация = ДанныеКОЛучшихВерсийПоИБ.Конфигурация
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	КлючеваяОперация,
			|	ИнформационнаяБаза,
			|	Конфигурация
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ДинамикаКОПоследнейВерсииПоИБ.Конфигурация КАК Конфигурация,
			|	ДинамикаКОПоследнейВерсииПоИБ.ВерсияКонфигурации КАК ВерсияКонфигурации,
			|	ДинамикаКОПоследнейВерсииПоИБ.КлючеваяОперация КАК КлючеваяОперация,
			|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДинамикаКОПоследнейВерсииПоИБ.ИнформационнаяБаза) КАК ВсегоИБ
			|ПОМЕСТИТЬ ВсегоИБВАнализеПоКО
			|ИЗ
			|	ДинамикаКОПоследнейВерсииПоИБ КАК ДинамикаКОПоследнейВерсииПоИБ
			|
			|СГРУППИРОВАТЬ ПО
			|	ДинамикаКОПоследнейВерсииПоИБ.Конфигурация,
			|	ДинамикаКОПоследнейВерсииПоИБ.ВерсияКонфигурации,
			|	ДинамикаКОПоследнейВерсииПоИБ.КлючеваяОперация
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	КлючеваяОперация,
			|	Конфигурация
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ДинамикаКОПоследнейВерсииПоИБ.Конфигурация КАК Конфигурация,
			|	ДинамикаКОПоследнейВерсииПоИБ.ВерсияКонфигурации КАК ВерсияКонфигурации,
			|	ДинамикаКОПоследнейВерсииПоИБ.КлючеваяОперация КАК КлючеваяОперация,
			|	ДинамикаКОПоследнейВерсииПоИБ.ВерсияКонфигурацииЛучшая КАК ВерсияКонфигурацииЛучшая,
			|	ДинамикаКОПоследнейВерсииПоИБ.ВерсияКонфигурацииЧислоЛучшая КАК ВерсияКонфигурацииЧислоЛучшая,
			|	ВЫБОР
			|		КОГДА КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДинамикаКОПоследнейВерсииПоИБ.ИнформационнаяБаза) = 1
			|			ТОГДА СУММА(ДинамикаКОПоследнейВерсииПоИБ.APDEXЛучшая)
			|		ИНАЧЕ КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДинамикаКОПоследнейВерсииПоИБ.ИнформационнаяБаза)
			|	КОНЕЦ КАК Приоритет
			|ПОМЕСТИТЬ ПриоритетЛучшихВерсийКО
			|ИЗ
			|	ДинамикаКОПоследнейВерсииПоИБ КАК ДинамикаКОПоследнейВерсииПоИБ
			|ГДЕ
			|	ДинамикаКОПоследнейВерсииПоИБ.Ухудшение = 1
			|
			|СГРУППИРОВАТЬ ПО
			|	ДинамикаКОПоследнейВерсииПоИБ.Конфигурация,
			|	ДинамикаКОПоследнейВерсииПоИБ.ВерсияКонфигурации,
			|	ДинамикаКОПоследнейВерсииПоИБ.КлючеваяОперация,
			|	ДинамикаКОПоследнейВерсииПоИБ.ВерсияКонфигурацииЛучшая,
			|	ДинамикаКОПоследнейВерсииПоИБ.ВерсияКонфигурацииЧислоЛучшая
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	КлючеваяОперация,
			|	Конфигурация
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ПриоритетЛучшихВерсийКО.Конфигурация КАК Конфигурация,
			|	ПриоритетЛучшихВерсийКО.ВерсияКонфигурации КАК ВерсияКонфигурации,
			|	ПриоритетЛучшихВерсийКО.КлючеваяОперация КАК КлючеваяОперация,
			|	МАКСИМУМ(ПриоритетЛучшихВерсийКО.Приоритет) КАК Приоритет
			|ПОМЕСТИТЬ МаксимальныйПриоритетКО
			|ИЗ
			|	ПриоритетЛучшихВерсийКО КАК ПриоритетЛучшихВерсийКО
			|
			|СГРУППИРОВАТЬ ПО
			|	ПриоритетЛучшихВерсийКО.Конфигурация,
			|	ПриоритетЛучшихВерсийКО.ВерсияКонфигурации,
			|	ПриоритетЛучшихВерсийКО.КлючеваяОперация
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	КлючеваяОперация,
			|	Конфигурация
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ПриоритетЛучшихВерсийКО.Конфигурация КАК Конфигурация,
			|	ПриоритетЛучшихВерсийКО.ВерсияКонфигурации КАК ВерсияКонфигурации,
			|	ПриоритетЛучшихВерсийКО.КлючеваяОперация КАК КлючеваяОперация,
			|	МАКСИМУМ(ПриоритетЛучшихВерсийКО.ВерсияКонфигурацииЧислоЛучшая) КАК ВерсияКонфигурацииЧислоЛучшая
			|ПОМЕСТИТЬ ЛучшиеВерсииЧислоКО
			|ИЗ
			|	ПриоритетЛучшихВерсийКО КАК ПриоритетЛучшихВерсийКО
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ МаксимальныйПриоритетКО КАК МаксимальныйПриоритетКО
			|		ПО ПриоритетЛучшихВерсийКО.КлючеваяОперация = МаксимальныйПриоритетКО.КлючеваяОперация
			|			И ПриоритетЛучшихВерсийКО.Конфигурация = МаксимальныйПриоритетКО.Конфигурация
			|			И ПриоритетЛучшихВерсийКО.ВерсияКонфигурации = МаксимальныйПриоритетКО.ВерсияКонфигурации
			|			И ПриоритетЛучшихВерсийКО.Приоритет = МаксимальныйПриоритетКО.Приоритет
			|
			|СГРУППИРОВАТЬ ПО
			|	ПриоритетЛучшихВерсийКО.Конфигурация,
			|	ПриоритетЛучшихВерсийКО.ВерсияКонфигурации,
			|	ПриоритетЛучшихВерсийКО.КлючеваяОперация
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	КлючеваяОперация,
			|	Конфигурация
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ПриоритетЛучшихВерсийКО.Конфигурация КАК Конфигурация,
			|	ПриоритетЛучшихВерсийКО.ВерсияКонфигурации КАК ВерсияКонфигурации,
			|	ПриоритетЛучшихВерсийКО.КлючеваяОперация КАК КлючеваяОперация,
			|	ПриоритетЛучшихВерсийКО.ВерсияКонфигурацииЛучшая КАК ВерсияКонфигурацииЛучшая,
			|	ВсегоИБВАнализеПоКО.ВсегоИБ КАК ВсегоИБ
			|ПОМЕСТИТЬ ЛучшиеВерсииКО
			|ИЗ
			|	ПриоритетЛучшихВерсийКО КАК ПриоритетЛучшихВерсийКО
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ЛучшиеВерсииЧислоКО КАК ЛучшиеВерсииКО
			|		ПО ПриоритетЛучшихВерсийКО.КлючеваяОперация = ЛучшиеВерсииКО.КлючеваяОперация
			|			И ПриоритетЛучшихВерсийКО.Конфигурация = ЛучшиеВерсииКО.Конфигурация
			|			И ПриоритетЛучшихВерсийКО.ВерсияКонфигурации = ЛучшиеВерсииКО.ВерсияКонфигурации
			|			И ПриоритетЛучшихВерсийКО.ВерсияКонфигурацииЧислоЛучшая = ЛучшиеВерсииКО.ВерсияКонфигурацииЧислоЛучшая
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВсегоИБВАнализеПоКО КАК ВсегоИБВАнализеПоКО
			|		ПО ПриоритетЛучшихВерсийКО.КлючеваяОперация = ВсегоИБВАнализеПоКО.КлючеваяОперация
			|			И ПриоритетЛучшихВерсийКО.Конфигурация = ВсегоИБВАнализеПоКО.Конфигурация
			|			И ПриоритетЛучшихВерсийКО.ВерсияКонфигурации = ВсегоИБВАнализеПоКО.ВерсияКонфигурации
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	КлючеваяОперация,
			|	Конфигурация
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ДинамикаКОПоследнейВерсииПоИБ.Конфигурация КАК Конфигурация,
			|	ДинамикаКОПоследнейВерсииПоИБ.КлючеваяОперация КАК КлючеваяОперация,
			|	СУММА(ДинамикаКОПоследнейВерсииПоИБ.Ухудшение) КАК Ухудшение,
			|	СУММА(ДинамикаКОПоследнейВерсииПоИБ.Улучшение) КАК Улучшение,
			|	СУММА(ДинамикаКОПоследнейВерсииПоИБ.БезИзменений) КАК БезИзменений
			|Поместить КОКонфигурацийПоКатегориям
			|ИЗ
			|	ДинамикаКОПоследнейВерсииПоИБ КАК ДинамикаКОПоследнейВерсииПоИБ
			|
			|СГРУППИРОВАТЬ ПО
			|	ДинамикаКОПоследнейВерсииПоИБ.Конфигурация,
			|	ДинамикаКОПоследнейВерсииПоИБ.КлючеваяОперация
			|Индексировать ПО
			|	ДинамикаКОПоследнейВерсииПоИБ.КлючеваяОперация,
			|	ДинамикаКОПоследнейВерсииПоИБ.Конфигурация
			|;
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ДинамикаКОПоследнейВерсииПоИБ.Конфигурация КАК Конфигурация,
			|	ДинамикаКОПоследнейВерсииПоИБ.ВерсияКонфигурации КАК ВерсияКонфигурации,
			|	ДинамикаКОПоследнейВерсииПоИБ.ВерсияКонфигурацииЧисло КАК ВерсияКонфигурацииЧисло,
			|	ДинамикаКОПоследнейВерсииПоИБ.КлючеваяОперация КАК КлючеваяОперация,
			|	ДинамикаКОПоследнейВерсииПоИБ.КлючеваяОперацияПредставление КАК КлючеваяОперацияПредставление,
			|	ДинамикаКОПоследнейВерсииПоИБ.ЦелевоеВремя КАК ЦелевоеВремя,
			|	ДинамикаКОПоследнейВерсииПоИБ.Ответственный КАК Ответственный,
			|	ДинамикаКОПоследнейВерсииПоИБ.Важность КАК Важность,
			|	СУММА(ДинамикаКОПоследнейВерсииПоИБ.Ухудшение) КАК НизкаяПроизводительность,
			|	МАКСИМУМ(КОКонфигурацийПоКатегориям.БезИзменений) КАК БезИзменений,
			|	МАКСИМУМ(КОКонфигурацийПоКатегориям.Улучшение) КАК Улучшение,	
			|	Выразить(СУММА(ДинамикаКОПоследнейВерсииПоИБ.Ухудшение)/МАКСИМУМ(ЛучшиеВерсииКО.ВсегоИБ)*100 КАК Число(15,2)) КАК ПроцентБаз,
			|	МАКСИМУМ(ЛучшиеВерсииКО.ВсегоИБ) КАК ВсегоИБ,			
			|	Выразить((СУММА(ДинамикаКОПоследнейВерсииПоИБ.N_T)+СУММА(ДинамикаКОПоследнейВерсииПоИБ.N_T_4T)/2)/СУММА(ДинамикаКОПоследнейВерсииПоИБ.КоличествоОпераций) КАК Число(3,2)) КАК APDEX,
			|	Выразить(СУММА(ДинамикаКОПоследнейВерсииПоИБ.СуммаВремяВыполнения) / СУММА(ДинамикаКОПоследнейВерсииПоИБ.КоличествоОпераций) КАК Число(15,3)) КАК СреднееВремя,
			|	СУММА(ДинамикаКОПоследнейВерсииПоИБ.КоличествоОпераций) КАК ВсегоЗамеров,
			|	ЛучшиеВерсииКО.ВерсияКонфигурацииЛучшая,
			|	Выразить((СУММА(ДинамикаКОПоследнейВерсииПоИБ.N_TЛучшая)+СУММА(ДинамикаКОПоследнейВерсииПоИБ.N_T_4TЛучшая)/2)/СУММА(ДинамикаКОПоследнейВерсииПоИБ.КоличествоОперацийЛучшая) КАК Число(3,2)) КАК APDEXЛучшая,
			|	Выразить(СУММА(ДинамикаКОПоследнейВерсииПоИБ.СуммаВремяВыполненияЛучшая) / СУММА(ДинамикаКОПоследнейВерсииПоИБ.КоличествоОперацийЛучшая) КАК Число(15,3)) КАК СреднееВремяЛучшая,
			|	СУММА(ДинамикаКОПоследнейВерсииПоИБ.КоличествоОперацийЛучшая) КАК ВсегоЗамеровЛучшая,
			|	Выразить((СУММА(ДинамикаКОПоследнейВерсииПоИБ.N_TЛучшая)+СУММА(ДинамикаКОПоследнейВерсииПоИБ.N_T_4TЛучшая)/2)/СУММА(ДинамикаКОПоследнейВерсииПоИБ.КоличествоОперацийЛучшая) КАК Число(3,2)) - Выразить((СУММА(ДинамикаКОПоследнейВерсииПоИБ.N_T)+СУММА(ДинамикаКОПоследнейВерсииПоИБ.N_T_4T)/2)/СУММА(ДинамикаКОПоследнейВерсииПоИБ.КоличествоОпераций) КАК Число(3,2)) КАК РазницаПроизводительности
			|Поместить ЗафиксированныеОшибки
			|ИЗ
			|	ДинамикаКОПоследнейВерсииПоИБ КАК ДинамикаКОПоследнейВерсииПоИБ
			|		Внутреннее Соединение ЛучшиеВерсииКО КАК ЛучшиеВерсииКО
			|		По ДинамикаКОПоследнейВерсииПоИБ.КлючеваяОперация = ЛучшиеВерсииКО.КлючеваяОперация
			|			И ДинамикаКОПоследнейВерсииПоИБ.Конфигурация = ЛучшиеВерсииКО.Конфигурация
			|			И ДинамикаКОПоследнейВерсииПоИБ.ВерсияКонфигурации = ЛучшиеВерсииКО.ВерсияКонфигурации
		    |			И ДинамикаКОПоследнейВерсииПоИБ.Ухудшение = 1
			|		Внутреннее Соединение КОКонфигурацийПоКатегориям КАК КОКонфигурацийПоКатегориям
			|		ПО ДинамикаКОПоследнейВерсииПоИБ.КлючеваяОперация = КОКонфигурацийПоКатегориям.КлючеваяОперация
			|			И ДинамикаКОПоследнейВерсииПоИБ.Конфигурация = КОКонфигурацийПоКатегориям.Конфигурация
			|
			|СГРУППИРОВАТЬ ПО
			|	ДинамикаКОПоследнейВерсииПоИБ.Конфигурация,
			|	ДинамикаКОПоследнейВерсииПоИБ.ВерсияКонфигурации,
			|	ДинамикаКОПоследнейВерсииПоИБ.ВерсияКонфигурацииЧисло,
			|	ДинамикаКОПоследнейВерсииПоИБ.КлючеваяОперация,
			|	ДинамикаКОПоследнейВерсииПоИБ.КлючеваяОперацияПредставление,
			|	ДинамикаКОПоследнейВерсииПоИБ.ЦелевоеВремя,
			|	ДинамикаКОПоследнейВерсииПоИБ.Ответственный,
			|	ДинамикаКОПоследнейВерсииПоИБ.Важность,
			|	ЛучшиеВерсииКО.ВерсияКонфигурацииЛучшая
			|
			|ИМЕЮЩИЕ
			|	СУММА(ДинамикаКОПоследнейВерсииПоИБ.Ухудшение) >= МАКСИМУМ(ДинамикаКОПоследнейВерсииПоИБ.МинимальноеКоличествоБаз) И
			|	(СУММА(ДинамикаКОПоследнейВерсииПоИБ.Ухудшение) >= МАКСИМУМ(ЛучшиеВерсииКО.ВсегоИБ) * МАКСИМУМ(ДинамикаКОПоследнейВерсииПоИБ.МинимальныйПроцентБаз) / 100
			|	ИЛИ СУММА(ДинамикаКОПоследнейВерсииПоИБ.Ухудшение) > МАКСИМУМ(КОКонфигурацийПоКатегориям.БезИзменений) > МАКСИМУМ(КОКонфигурацийПоКатегориям.Улучшение))
			|Индексировать По
			|	КлючеваяОперация,
			|	Конфигурация
			|;
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ДинамикаКОПоследнейВерсииПоИБ.Конфигурация КАК Конфигурация,
			|	ДинамикаКОПоследнейВерсииПоИБ.ИнформационнаяБаза КАК ИнформационнаяБаза,
			|	ДинамикаКОПоследнейВерсииПоИБ.КлючеваяОперация КАК КлючеваяОперация,
			|	ДинамикаКОПоследнейВерсииПоИБ.Ухудшение КАК Ухудшение,
			|	ДинамикаКОПоследнейВерсииПоИБ.Улучшение КАК Улучшение,
			|	ДинамикаКОПоследнейВерсииПоИБ.БезИзменений КАК БезИзменений
			|ПОМЕСТИТЬ КлючевыеОперацииИБДляФО
			|ИЗ
			|	ДинамикаКОПоследнейВерсииПоИБ КАК ДинамикаКОПоследнейВерсииПоИБ
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ЗафиксированныеОшибки КАК ЗафиксированныеОшибки
			|		ПО ДинамикаКОПоследнейВерсииПоИБ.КлючеваяОперация = ЗафиксированныеОшибки.КлючеваяОперация
			|			И ДинамикаКОПоследнейВерсииПоИБ.Конфигурация = ЗафиксированныеОшибки.Конфигурация
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	ДинамикаКОПоследнейВерсииПоИБ.ИнформационнаяБаза,
			|	ДинамикаКОПоследнейВерсииПоИБ.Конфигурация,
			|	ДинамикаКОПоследнейВерсииПоИБ.КлючеваяОперация
			|;
			|ВЫБРАТЬ
			|	Ошибки.Конфигурация,
			|	Ошибки.КлючеваяОперация,
			|	Ошибки.ВерсияКонфигурации.ВерсияЧисло КАК ВерсияКонфигурацииЧисло,
			|	Ошибки.Статус,
			|	Ошибки.ИсправленоВВерсии.ВерсияЧисло КАК ИсправленоВВерсииЧисло
			|ПОМЕСТИТЬ ВТ_Ошибки
			|ИЗ 
			|	РегистрСведений.РезультатАвтоматическогоАнализаПроизводительности КАК Ошибки
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ЗафиксированныеОшибки КАК ЗафиксированныеОшибки	
			|			ПО Ошибки.КлючеваяОперация = ЗафиксированныеОшибки.КлючеваяОперация
			|			И Ошибки.Конфигурация = ЗафиксированныеОшибки.Конфигурация
			|ИНДЕКСИРОВАТЬ ПО
			|	Ошибки.КлючеваяОперация,
	        |	Ошибки.Конфигурация
			|;    
			|
			|ВЫБРАТЬ Первые 1 //#Первые
			|	ЗафиксированныеОшибки.Конфигурация КАК Конфигурация,
			|	ЗафиксированныеОшибки.ВерсияКонфигурации КАК ВерсияКонфигурации,
			|	ЗафиксированныеОшибки.КлючеваяОперация КАК КлючеваяОперация,
			|	ЗафиксированныеОшибки.КлючеваяОперацияПредставление КАК КлючеваяОперацияПредставление,
			|	ЗафиксированныеОшибки.ЦелевоеВремя КАК ЦелевоеВремя,
			|	ЗафиксированныеОшибки.Ответственный КАК Ответственный,
			|	ЗафиксированныеОшибки.Важность КАК Важность,
			|	ЗафиксированныеОшибки.НизкаяПроизводительность КАК НизкаяПроизводительность,
			|	ЗафиксированныеОшибки.БезИзменений КАК БезИзменений,
			|	ЗафиксированныеОшибки.Улучшение КАК Улучшение,
			|	ЗафиксированныеОшибки.ПроцентБаз КАК ПроцентБаз,
			|	ЗафиксированныеОшибки.ВсегоИБ КАК ВсегоИБ,
			|	ЗафиксированныеОшибки.APDEX КАК APDEX,
			|	ЗафиксированныеОшибки.СреднееВремя КАК СреднееВремя,
			|	ЗафиксированныеОшибки.ВсегоЗамеров КАК ВсегоЗамеров,
			|	ЗафиксированныеОшибки.ВерсияКонфигурацииЛучшая КАК ВерсияКонфигурацииЛучшая,
			|	ЗафиксированныеОшибки.APDEXЛучшая КАК APDEXЛучшая,
			|	ЗафиксированныеОшибки.СреднееВремяЛучшая КАК СреднееВремяЛучшая,
			|	ЗафиксированныеОшибки.ВсегоЗамеровЛучшая КАК ВсегоЗамеровЛучшая,
			|	ЗафиксированныеОшибки.РазницаПроизводительности КАК РазницаПроизводительности,
			|	Выбор 
			|		Когда ВТ_Ошибки.Статус В (Значение(Перечисление.СтатусыОшибокПроизводительности.Создана),Значение(Перечисление.СтатусыОшибокПроизводительности.Зарегистрирована))
			|			Тогда Ложь
			|		Когда ВТ_Ошибки.Статус = Значение(Перечисление.СтатусыОшибокПроизводительности.Отклонена)
			|			Тогда Ложь
			|		Когда ВТ_Ошибки.Статус В (Значение(Перечисление.СтатусыОшибокПроизводительности.Исправлена),Значение(Перечисление.СтатусыОшибокПроизводительности.Закрыта))
			|			Тогда Ложь
			|		Иначе Истина
			|	Конец КАК Регистрировать,
			|	ВТ_Ошибки.ИсправленоВВерсииЧисло,
			|	ВТ_Ошибки.Статус
			|ИЗ
			|	ЗафиксированныеОшибки КАК ЗафиксированныеОшибки
			|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_Ошибки КАК ВТ_Ошибки
			|		ПО ЗафиксированныеОшибки.КлючеваяОперация = ВТ_Ошибки.КлючеваяОперация
			|			И ЗафиксированныеОшибки.Конфигурация = ВТ_Ошибки.Конфигурация
			|			И Выбор 
			|				Когда ВТ_Ошибки.Статус В (Значение(Перечисление.СтатусыОшибокПроизводительности.Создана),Значение(Перечисление.СтатусыОшибокПроизводительности.Зарегистрирована))
			|					Тогда Истина
			|				Когда ВТ_Ошибки.Статус = Значение(Перечисление.СтатусыОшибокПроизводительности.Отклонена)
			|					Тогда ЗафиксированныеОшибки.ВерсияКонфигурацииЧисло = ВТ_Ошибки.ВерсияКонфигурацииЧисло
			|				Когда ВТ_Ошибки.Статус В (Значение(Перечисление.СтатусыОшибокПроизводительности.Исправлена),Значение(Перечисление.СтатусыОшибокПроизводительности.Закрыта))
			|					Тогда ЗафиксированныеОшибки.ВерсияКонфигурацииЧисло < ВТ_Ошибки.ИсправленоВВерсииЧисло
			|				Иначе Ложь 
			|			Конец
			|Упорядочить ПО
			|	ПроцентБаз УБЫВ,
			|	РазницаПроизводительности УБЫВ
			|;      			
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ИнформацияПоИБ.Конфигурация КАК Конфигурация,
			|	ИнформацияПоИБ.ВерсияКонфигурации КАК ВерсияКонфигурации,
			|	ИнформацияПоИБ.ВерсияКонфигурацииЧисло КАК ВерсияКонфигурацииЧисло,
			|	ИнформацияПоИБ.КлючеваяОперация КАК КлючеваяОперация,
			|	СУММА(ВЫБОР
			|			КОГДА ИнформацияПоИБ.APDEX - ЕСТЬNULL(ИБПроблемныхКО.APDEX, 1) >= &ПорогСнижения
			|				ТОГДА -1
			|			ИНАЧЕ 0
			|		КОНЕЦ) КАК Хуже,
			|	СУММА(ВЫБОР
			|			КОГДА ИнформацияПоИБ.APDEX - ЕСТЬNULL(ИБПроблемныхКО.APDEX, 1) <= -&ПорогСнижения
			|				ТОГДА 1
			|			ИНАЧЕ 0
			|		КОНЕЦ) КАК Лучше,
			|	СУММА(ВЫБОР
			|			КОГДА ИнформацияПоИБ.APDEX - ЕСТЬNULL(ИБПроблемныхКО.APDEX, 1) < &ПорогСнижения
			|				И ИнформацияПоИБ.APDEX - ЕСТЬNULL(ИБПроблемныхКО.APDEX, 1) > -&ПорогСнижения
			|				ТОГДА 1
			|			ИНАЧЕ 0
			|		КОНЕЦ) КАК БезИзменений,
			|	(СУММА(ВЫБОР
			|			КОГДА ИнформацияПоИБ.APDEX - ЕСТЬNULL(ИБПроблемныхКО.APDEX, 1) >= &ПорогСнижения
			|				ТОГДА -1
			|			ИНАЧЕ 0
			|		КОНЕЦ) + СУММА(ВЫБОР
			|			КОГДА ИнформацияПоИБ.APDEX - ЕСТЬNULL(ИБПроблемныхКО.APDEX, 1) >= &ПорогСнижения
			|				ТОГДА 0
			|			ИНАЧЕ 1
			|		КОНЕЦ)) / 2 КАК СРЕДНЕЕ,
			|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ИнформацияПоИБ.ИнформационнаяБаза) КАК ИнформационнаяБаза
			|ИЗ
			|	ИнформацияПоИБ КАК ИнформацияПоИБ
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ЗафиксированныеОшибки КАК ЗафиксированныеОшибки
			|		ПО ИнформацияПоИБ.КлючеваяОперация = ЗафиксированныеОшибки.КлючеваяОперация
			|			И ИнформацияПоИБ.Конфигурация = ЗафиксированныеОшибки.Конфигурация
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ИБИмеющиеНесколькоВерсий КАК ИБИмеющиеНесколькоВерсий
			|		ПО ИнформацияПоИБ.КлючеваяОперация = ИБИмеющиеНесколькоВерсий.КлючеваяОперация
			|			И ИнформацияПоИБ.ИнформационнаяБаза = ИБИмеющиеНесколькоВерсий.ИнформационнаяБаза
			|			И ИнформацияПоИБ.Конфигурация = ИБИмеющиеНесколькоВерсий.Конфигурация
			|		ЛЕВОЕ СОЕДИНЕНИЕ ИБПроблемныхКО КАК ИБПроблемныхКО
			|		ПО ИнформацияПоИБ.КлючеваяОперация = ИБПроблемныхКО.КлючеваяОперация
			|			И ИнформацияПоИБ.ИнформационнаяБаза = ИБПроблемныхКО.ИнформационнаяБаза
			|			И ИнформацияПоИБ.Конфигурация = ИБПроблемныхКО.Конфигурация
			|			И (НЕ ИнформацияПоИБ.ЭтоПоследняяВерсия)
			|ГДЕ
			|	НЕ ИнформацияПоИБ.ЭтоПоследняяВерсия
			|
			|СГРУППИРОВАТЬ ПО
			|	ИнформацияПоИБ.Конфигурация,
			|	ИнформацияПоИБ.ВерсияКонфигурации,
			|	ИнформацияПоИБ.ВерсияКонфигурацииЧисло,
			|	ИнформацияПоИБ.КлючеваяОперация
			|
			|УПОРЯДОЧИТЬ ПО
			|	КлючеваяОперация,
			|	Конфигурация,
			|	ВерсияКонфигурацииЧисло
			|;
			|ВЫБРАТЬ
			|	ДинамикаКОПоследнейВерсииПоИБ.Конфигурация КАК Конфигурация,
			|	ДинамикаКОПоследнейВерсииПоИБ.ВерсияКонфигурации КАК ВерсияКонфигурации,
			|	ДинамикаКОПоследнейВерсииПоИБ.ИнформационнаяБаза КАК ИнформационнаяБаза,
			|	ДинамикаКОПоследнейВерсииПоИБ.КлючеваяОперация КАК КлючеваяОперация,
			|	ДинамикаКОПоследнейВерсииПоИБ.Ухудшение КАК Ухудшение,
			|	ДинамикаКОПоследнейВерсииПоИБ.Улучшение КАК Улучшение,
			|	ДинамикаКОПоследнейВерсииПоИБ.БезИзменений КАК БезИзменений,
			|	ДинамикаКОПоследнейВерсииПоИБ.КоличествоОпераций КАК ВсегоЗамеров,
			|	ДинамикаКОПоследнейВерсииПоИБ.APDEX КАК APDEX,
			|	ДинамикаКОПоследнейВерсииПоИБ.СреднееВремя КАК СреднееВремя,
			|	ДинамикаКОПоследнейВерсииПоИБ.ВерсияКонфигурацииЛучшая КАК ВерсияКонфигурацииЛучшая,
			|	ДинамикаКОПоследнейВерсииПоИБ.КоличествоОперацийЛучшая КАК ВсегоЗамеровЛучшая,
			|	ДинамикаКОПоследнейВерсииПоИБ.APDEXЛучшая КАК APDEXЛучшая,
			|	ДинамикаКОПоследнейВерсииПоИБ.СреднееВремяЛучшая КАК СреднееВремяЛучшая,
			|	ДинамикаКОПоследнейВерсииПоИБ.APDEXЛучшая - ДинамикаКОПоследнейВерсииПоИБ.APDEX КАК Разница 
			|ИЗ
			|	ДинамикаКОПоследнейВерсииПоИБ КАК ДинамикаКОПоследнейВерсииПоИБ
			|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ЗафиксированныеОшибки КАК ЗафиксированныеОшибки
			|		ПО ДинамикаКОПоследнейВерсииПоИБ.КлючеваяОперация = ЗафиксированныеОшибки.КлючеваяОперация
			|			И ДинамикаКОПоследнейВерсииПоИБ.Конфигурация = ЗафиксированныеОшибки.Конфигурация
			|Упорядочить ПО
			|	КлючеваяОперация,
			|	Конфигурация,
			|	Ухудшение,
			|	КоличествоОпераций УБЫВ
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	КлючевыеОперацииИБДляФО.Конфигурация КАК Конфигурация,
			|	КлючевыеОперацииИБДляФО.КлючеваяОперация КАК КлючеваяОперация,
			|	ВЫБОР
			|		КОГДА КлючевыеОперацииИБДляФО.Ухудшение > 0
			|			ТОГДА ""Ухудшение""
			|		КОГДА КлючевыеОперацииИБДляФО.Улучшение > 0
			|			ТОГДА ""Улучшение""
			|		КОГДА КлючевыеОперацииИБДляФО.БезИзменений > 0
			|			ТОГДА ""БезИзменений""
			|	КОНЕЦ КАК Категория,
			|	ФункциональныеОпцииКонфигурацииСрезПоследних.ФункциональнаяОпцияКонфигурации КАК ИмяФункциональнойОпции,
			|	ФункциональныеОпцииКонфигурацииСрезПоследних.Используется КАК ЗначениеФункциональнойОпции,
			|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ФункциональныеОпцииКонфигурацииСрезПоследних.ИнформационнаяБаза) КАК КоличествоИБ
			|ИЗ
			|	КлючевыеОперацииИБДляФО КАК КлючевыеОперацииИБДляФО
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ФункциональныеОпцииКонфигурацииСрезПоследних КАК ФункциональныеОпцииКонфигурацииСрезПоследних
			|		ПО (КлючевыеОперацииИБДляФО.ИнформационнаяБаза = ФункциональныеОпцииКонфигурацииСрезПоследних.ИнформационнаяБаза)
			|
			|СГРУППИРОВАТЬ ПО
			|	ВЫБОР
			|		КОГДА КлючевыеОперацииИБДляФО.Ухудшение > 0
			|			ТОГДА ""Ухудшение""
			|		КОГДА КлючевыеОперацииИБДляФО.Улучшение > 0
			|			ТОГДА ""Улучшение""
			|		КОГДА КлючевыеОперацииИБДляФО.БезИзменений > 0
			|			ТОГДА ""БезИзменений""
			|	КОНЕЦ,
			|	ФункциональныеОпцииКонфигурацииСрезПоследних.ФункциональнаяОпцияКонфигурации,
			|	ФункциональныеОпцииКонфигурацииСрезПоследних.Используется,
			|	КлючевыеОперацииИБДляФО.Конфигурация,
			|	КлючевыеОперацииИБДляФО.КлючеваяОперация
			|
			|УПОРЯДОЧИТЬ ПО
			|	КлючеваяОперация,
			|	Конфигурация,    			
			|	КоличествоИБ УБЫВ,
			|	ИмяФункциональнойОпции";
			Возврат ТекстЗапроса;
КонецФункции

Функция ТекстЗапросаАнализаНаЗакрытие(ДатаНачала, ДатаОкончания)
	ТекстЗапроса = Общий.ТекстЗапросаПериодов(ДатаНачала, ДатаОкончания, "День");
    ТекстЗапроса = ТекстЗапроса + ";
	       	|
			|////////////////////////////////////////////////////////////////////////////////			
			|ВЫБРАТЬ
			|	Ошибки.Конфигурация,
			|	Ошибки.Статус,
			|	Ошибки.ВерсияКонфигурации,
			|	Ошибки.ВерсияКонфигурации.ВерсияЧисло КАК ВерсияКонфигурацииЧисло,
			|	Ошибки.КлючеваяОперация
			|ПОМЕСТИТЬ ВТ_Ошибки
			|ИЗ 
			|	РегистрСведений.РезультатАвтоматическогоАнализаПроизводительности КАК Ошибки
			|ГДЕ
			|	Ошибки.Конфигурация В (&Конфигурация)
			|   И Ошибки.Статус В (Значение(Перечисление.СтатусыОшибокПроизводительности.Создана),Значение(Перечисление.СтатусыОшибокПроизводительности.Исправлена),Значение(Перечисление.СтатусыОшибокПроизводительности.Зарегистрирована),Значение(Перечисление.СтатусыОшибокПроизводительности.Отклонена))
			|ИНДЕКСИРОВАТЬ ПО
			|	Ошибки.КлючеваяОперация,
	        |	Ошибки.Конфигурация
			|;
			|ВЫБРАТЬ
			|	КлючевыеОперации.КлючеваяОперация КАК КлючеваяОперация,
			|	КлючевыеОперации.ИмяПредставление КАК ИмяПредставление,
			|	КлючевыеОперации.ЦелевоеВремя КАК ЦелевоеВремя,
			|	КлючевыеОперации.Важность КАК Важность,
			|	КлючевыеОперации.МинимальноеКоличествоЗамеров КАК МинимальноеКоличествоЗамеров,
			|	КлючевыеОперации.Ответственный КАК Ответственный
			|Поместить КлючевыеОперацииОтборанные
			|ИЗ КлючевыеОперации КАК КлючевыеОперации
			|	Внутреннее Соединение ВТ_Ошибки КАК КлючевыеОперацииОшибок
			|		ПО КлючевыеОперации.КлючеваяОперация = КлючевыеОперацииОшибок.КлючеваяОперация
			|ИНДЕКСИРОВАТЬ ПО
			|	КлючеваяОперация
			|;
			|ВЫБРАТЬ
			|	НастройкиКонфигураций.Конфигурация КАК Конфигурация,
			|	НастройкиКонфигураций.МинимальноеКоличествоБаз КАК МинимальноеКоличествоБаз,
			|	НастройкиКонфигураций.МинимальныйПроцентБаз - 10 КАК МинимальныйПроцентБаз
			|ПОМЕСТИТЬ НастройкиКонфигураций
			|ИЗ
			|	&НастройкиКонфигураций КАК НастройкиКонфигураций
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Конфигурация
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ВерсииПлатформыЦентрМониторинга.Ссылка КАК Ссылка
			|ПОМЕСТИТЬ РелизныеПлатформы
			|ИЗ
			|	Справочник.ВерсииПлатформыЦентрМониторинга КАК ВерсииПлатформыЦентрМониторинга
			|ГДЕ
			|	ВерсииПлатформыЦентрМониторинга.Релизная
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Ссылка
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ РАЗЛИЧНЫЕ
			|	ВерсииКонфигурации.Ссылка КАК Ссылка,
			|	ВерсииКонфигурации.ВерсияЧисло КАК ВерсияКонфигурацииЧисло
			|ПОМЕСТИТЬ РелизныеВерсииКонфигурации
			|ИЗ
			|	Справочник.ВерсииКонфигурации КАК ВерсииКонфигурации
			|ГДЕ
			|	ВерсииКонфигурации.Владелец В(&Конфигурация)
			|	И ВерсииКонфигурации.Релизная
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	Ссылка
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ИнформацияИстория.ПериодЗаписи КАК ПериодЗаписи,
			|	ИнформацияИстория.ИнформационнаяБаза КАК ИнформационнаяБаза,
			|	ИнформацияИстория.Конфигурация КАК Конфигурация,
			|	ИнформацияИстория.ВерсияКонфигурации КАК ВерсияКонфигурации,
			|	РелизныеВерсииКонфигурации.ВерсияКонфигурацииЧисло КАК ВерсияКонфигурацииЧисло
			|ПОМЕСТИТЬ ИнформацияИстория
			|ИЗ
			|	Периоды КАК Периоды
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ИнформацияИсторияПодробно КАК ИнформацияИстория
			|				//СоединениеСтатусы
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РелизныеПлатформы КАК РелизныеПлатформы
			|			ПО ИнформацияИстория.ВерсияПлатформы = РелизныеПлатформы.Ссылка
			|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РелизныеВерсииКонфигурации КАК РелизныеВерсииКонфигурации
			|			ПО ИнформацияИстория.ВерсияКонфигурации = РелизныеВерсииКонфигурации.Ссылка
			|		ПО (ИнформацияИстория.ПериодЗаписи = Периоды.Период)
			|			И (ИнформацияИстория.Конфигурация В (&Конфигурация))
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	ИнформацияИстория.ИнформационнаяБаза,
			|	ИнформацияИстория.ПериодЗаписи
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ИнформацияИстория.Конфигурация КАК Конфигурация,
			|	ИнформацияИстория.ВерсияКонфигурации КАК ВерсияКонфигурации,
			|	ИнформацияИстория.ВерсияКонфигурацииЧисло КАК ВерсияКонфигурацииЧисло,
			|	ИнформацияИстория.ИнформационнаяБаза КАК ИнформационнаяБаза,
			|	Замеры.КлючеваяОперация КАК КлючеваяОперация,
			|	ВЫБОР
			|		КОГДА КлючевыеОперации.ИмяПредставление = &ПустаяСтрока
			|			ТОГДА Замеры.КлючеваяОперация.Наименование
			|		ИНАЧЕ КлючевыеОперации.ИмяПредставление
			|	КОНЕЦ КАК КлючеваяОперацияПредставление,
			|	КлючевыеОперации.ЦелевоеВремя КАК ЦелевоеВремя,
			|	КлючевыеОперации.Ответственный КАК Ответственный,
			|	КлючевыеОперации.Важность КАК Важность,
			|	СУММА(Замеры.КоличествоОпераций) КАК КоличествоОпераций,
			|	СУММА(Замеры.ВремяВыполнения * Замеры.КоличествоОпераций) КАК СуммаВремяВыполнения,
			|	СУММА(ВЫБОР
			|			КОГДА Замеры.ВремяВыполнения <= ЕСТЬNULL(КлючевыеОперации.ЦелевоеВремя, 1)
			|				ТОГДА Замеры.КоличествоОпераций
			|			ИНАЧЕ 0
			|		КОНЕЦ) КАК N_T,
			|	СУММА(ВЫБОР
			|			КОГДА Замеры.ВремяВыполнения > ЕСТЬNULL(КлючевыеОперации.ЦелевоеВремя, 1)
			|					И Замеры.ВремяВыполнения <= ЕСТЬNULL(КлючевыеОперации.ЦелевоеВремя, 1) * 4
			|				ТОГДА Замеры.КоличествоОпераций
			|			ИНАЧЕ 0
			|		КОНЕЦ) КАК N_T_4T,
			|	СУММА(ВЫБОР
			|			КОГДА Замеры.ВремяВыполнения > ЕСТЬNULL(КлючевыеОперации.ЦелевоеВремя, 1) * 4
			|				ТОГДА Замеры.КоличествоОпераций
			|			ИНАЧЕ 0
			|		КОНЕЦ) КАК N_4T,
			|	ВЫБОР
			|		КОГДА (СУММА(ВЫБОР
			|					КОГДА Замеры.ВремяВыполнения <= ЕСТЬNULL(КлючевыеОперации.ЦелевоеВремя, 1)
			|						ТОГДА Замеры.КоличествоОпераций
			|					ИНАЧЕ 0
			|				КОНЕЦ) + СУММА(ВЫБОР
			|					КОГДА Замеры.ВремяВыполнения > ЕСТЬNULL(КлючевыеОперации.ЦелевоеВремя, 1)
			|							И Замеры.ВремяВыполнения <= ЕСТЬNULL(КлючевыеОперации.ЦелевоеВремя, 1) * 4
			|						ТОГДА Замеры.КоличествоОпераций
			|					ИНАЧЕ 0
			|				КОНЕЦ) / 2) / СУММА(Замеры.КоличествоОпераций) < &МинимальноеЗначениеAPDEX
			|			ТОГДА 1
			|		ИНАЧЕ 0
			|	КОНЕЦ КАК НизкаяПроизводительность
			|ПОМЕСТИТЬ ВсеЗамеры
			|ИЗ
			|	РегистрСведений.ЗамерыВремениЦентрМониторинга КАК Замеры
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ИнформацияИстория КАК ИнформацияИстория
			|		ПО Замеры.Период = ИнформацияИстория.ПериодЗаписи
			|			И Замеры.ИнформационнаяБаза = ИнформацияИстория.ИнформационнаяБаза
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ КлючевыеОперацииОтборанные КАК КлючевыеОперации
			|		ПО Замеры.КлючеваяОперация = КлючевыеОперации.КлючеваяОперация
			|
			|СГРУППИРОВАТЬ ПО
			|	ИнформацияИстория.Конфигурация,
			|	ИнформацияИстория.ВерсияКонфигурации,
			|	ИнформацияИстория.ВерсияКонфигурацииЧисло,
			|	ИнформацияИстория.ИнформационнаяБаза,
			|	Замеры.КлючеваяОперация,
			|	КлючевыеОперации.МинимальноеКоличествоЗамеров,
			|	ВЫБОР
			|		КОГДА КлючевыеОперации.ИмяПредставление = &ПустаяСтрока
			|			ТОГДА Замеры.КлючеваяОперация.Наименование
			|		ИНАЧЕ КлючевыеОперации.ИмяПредставление
			|	КОНЕЦ,
			|	КлючевыеОперации.ЦелевоеВремя,
			|	КлючевыеОперации.Ответственный,
			|	КлючевыеОперации.Важность
			|
			|ИМЕЮЩИЕ
			|	СУММА(Замеры.КоличествоОпераций) >= КлючевыеОперации.МинимальноеКоличествоЗамеров
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	КлючеваяОперация,
			|	ИнформационнаяБаза,
			|	ВерсияКонфигурации,
			|	Конфигурация
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ВсеЗамеры.Конфигурация КАК Конфигурация,
			|	ВсеЗамеры.ВерсияКонфигурации КАК ВерсияКонфигурации,
			|	ВсеЗамеры.КлючеваяОперация КАК КлючеваяОперация,
			|	СУММА(ВсеЗамеры.КоличествоОпераций) КАК КоличествоОпераций,
			|	СУММА(ВсеЗамеры.СуммаВремяВыполнения) КАК СуммаВремяВыполнения,
			|	СУММА(ВсеЗамеры.N_T) КАК N_T,
			|	СУММА(ВсеЗамеры.N_T_4T) КАК N_T_4T,
			|	СУММА(ВсеЗамеры.N_4T) КАК N_4T,
			|	СУММА(ВсеЗамеры.НизкаяПроизводительность) КАК НизкаяПроизводительность,
			|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВсеЗамеры.ИнформационнаяБаза) КАК ВсегоИБ,
			|	ВсеЗамеры.ВерсияКонфигурацииЧисло КАК ВерсияКонфигурацииЧисло,
			|	МАКСИМУМ(НастройкиКонфигураций.МинимальноеКоличествоБаз) КАК МинимальноеКоличествоБаз,
			|	МАКСИМУМ(НастройкиКонфигураций.МинимальныйПроцентБаз) КАК МинимальныйПроцентБаз
			|ПОМЕСТИТЬ ПроизводительностьКОВРазрезеВерсий
			|ИЗ
			|	ВсеЗамеры КАК ВсеЗамеры
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ НастройкиКонфигураций КАК НастройкиКонфигураций
			|		ПО ВсеЗамеры.Конфигурация = НастройкиКонфигураций.Конфигурация
			|
			|СГРУППИРОВАТЬ ПО
			|	ВсеЗамеры.Конфигурация,
			|	ВсеЗамеры.ВерсияКонфигурации,
			|	ВсеЗамеры.КлючеваяОперация,
			|	ВсеЗамеры.ВерсияКонфигурацииЧисло
			|
			|ИМЕЮЩИЕ
			|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ВсеЗамеры.ИнформационнаяБаза) > МАКСИМУМ(НастройкиКонфигураций.МинимальноеКоличествоБаз)
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	КлючеваяОперация,
			|	ВерсияКонфигурацииЧисло,
			|	Конфигурация,
			|	ВерсияКонфигурации
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ПроизводительностьКОВРазрезеВерсий.Конфигурация КАК Конфигурация,
			|	ПроизводительностьКОВРазрезеВерсий.КлючеваяОперация КАК КлючеваяОперация,
			|	МАКСИМУМ(ПроизводительностьКОВРазрезеВерсий.ВерсияКонфигурацииЧисло) КАК ВерсияКонфигурацииЧисло
			|ПОМЕСТИТЬ КОПоследнихВерсий
			|ИЗ
			|	ПроизводительностьКОВРазрезеВерсий КАК ПроизводительностьКОВРазрезеВерсий
			|
			|СГРУППИРОВАТЬ ПО
			|	ПроизводительностьКОВРазрезеВерсий.Конфигурация,
			|	ПроизводительностьКОВРазрезеВерсий.КлючеваяОперация
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	КлючеваяОперация,
			|	Конфигурация
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ПроизводительностьКОВРазрезеВерсий.Конфигурация КАК Конфигурация,
			|	ПроизводительностьКОВРазрезеВерсий.ВерсияКонфигурации КАК ВерсияКонфигурации,
			|	ПроизводительностьКОВРазрезеВерсий.КлючеваяОперация КАК КлючеваяОперация,
			|	ПроизводительностьКОВРазрезеВерсий.КоличествоОпераций КАК КоличествоОпераций,
			|	ПроизводительностьКОВРазрезеВерсий.СуммаВремяВыполнения КАК СуммаВремяВыполнения,
			|	ПроизводительностьКОВРазрезеВерсий.N_T КАК N_T,
			|	ПроизводительностьКОВРазрезеВерсий.N_T_4T КАК N_T_4T,
			|	ПроизводительностьКОВРазрезеВерсий.N_4T КАК N_4T,
			|	ПроизводительностьКОВРазрезеВерсий.НизкаяПроизводительность КАК НизкаяПроизводительность,
			|	ПроизводительностьКОВРазрезеВерсий.ВсегоИБ КАК ВсегоИБ,
			|	ПроизводительностьКОВРазрезеВерсий.ВерсияКонфигурацииЧисло КАК ВерсияКонфигурацииЧисло,
			|	ПроизводительностьКОВРазрезеВерсий.МинимальноеКоличествоБаз КАК МинимальноеКоличествоБаз,
			|	ПроизводительностьКОВРазрезеВерсий.МинимальныйПроцентБаз КАК МинимальныйПроцентБаз
			|ПОМЕСТИТЬ ПроблемныеКО
			|ИЗ
			|	КОПоследнихВерсий КАК КОПоследнихВерсий
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПроизводительностьКОВРазрезеВерсий КАК ПроизводительностьКОВРазрезеВерсий
			|		ПО КОПоследнихВерсий.КлючеваяОперация = ПроизводительностьКОВРазрезеВерсий.КлючеваяОперация
			|			И КОПоследнихВерсий.ВерсияКонфигурацииЧисло = ПроизводительностьКОВРазрезеВерсий.ВерсияКонфигурацииЧисло
			|			И КОПоследнихВерсий.Конфигурация = ПроизводительностьКОВРазрезеВерсий.Конфигурация
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	КлючеваяОперация,
			|	ВерсияКонфигурации,
			|	Конфигурация
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ВсеЗамеры.Конфигурация КАК Конфигурация,
			|	ВсеЗамеры.ВерсияКонфигурации КАК ВерсияКонфигурации,
			|	ВсеЗамеры.ИнформационнаяБаза КАК ИнформационнаяБаза,
			|	ВсеЗамеры.КлючеваяОперация КАК КлючеваяОперация,
			|	ВсеЗамеры.КлючеваяОперацияПредставление КАК КлючеваяОперацияПредставление,
			|	ВсеЗамеры.ЦелевоеВремя КАК ЦелевоеВремя,
			|	ВсеЗамеры.Ответственный КАК Ответственный,
			|	ВсеЗамеры.Важность КАК Важность,
			|	ВсеЗамеры.КоличествоОпераций КАК КоличествоОпераций,
			|	ВЫРАЗИТЬ((ВсеЗамеры.N_T + ВсеЗамеры.N_T_4T / 2) / ВсеЗамеры.КоличествоОпераций КАК ЧИСЛО(3, 2)) КАК APDEX,
			|	ВЫРАЗИТЬ(ВсеЗамеры.СуммаВремяВыполнения / ВсеЗамеры.КоличествоОпераций КАК ЧИСЛО(15, 3)) КАК СреднееВремя,
			|	ВсеЗамеры.СуммаВремяВыполнения КАК СуммаВремяВыполнения,
			|	ВсеЗамеры.N_T КАК N_T,
			|	ВсеЗамеры.N_T_4T КАК N_T_4T,
			|	ВсеЗамеры.N_4T КАК N_4T,
			|	ВсеЗамеры.НизкаяПроизводительность КАК НизкаяПроизводительность,
			|	ВсеЗамеры.ВерсияКонфигурацииЧисло КАК ВерсияКонфигурацииЧисло,
			|	ПроблемныеКО.МинимальноеКоличествоБаз КАК МинимальноеКоличествоБаз,
			|	ПроблемныеКО.МинимальныйПроцентБаз КАК МинимальныйПроцентБаз
			|ПОМЕСТИТЬ ИБПроблемныхКО
			|ИЗ
			|	ПроблемныеКО КАК ПроблемныеКО
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВсеЗамеры КАК ВсеЗамеры
			|		ПО ПроблемныеКО.КлючеваяОперация = ВсеЗамеры.КлючеваяОперация
			|			И ПроблемныеКО.Конфигурация = ВсеЗамеры.Конфигурация
			|			И ПроблемныеКО.ВерсияКонфигурации = ВсеЗамеры.ВерсияКонфигурации
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	КлючеваяОперация,
			|	ИнформационнаяБаза,
			|	Конфигурация,
			|	ВерсияКонфигурации
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ВсеЗамеры.Конфигурация КАК Конфигурация,
			|	ВсеЗамеры.ВерсияКонфигурации КАК ВерсияКонфигурации,
			|	ВсеЗамеры.ВерсияКонфигурацииЧисло КАК ВерсияКонфигурацииЧисло,
			|	ВсеЗамеры.ИнформационнаяБаза КАК ИнформационнаяБаза,
			|	ВсеЗамеры.КлючеваяОперация КАК КлючеваяОперация,
			|	ВсеЗамеры.КоличествоОпераций КАК КоличествоОпераций,
			|	ВЫРАЗИТЬ((ВсеЗамеры.N_T + ВсеЗамеры.N_T_4T / 2) / ВсеЗамеры.КоличествоОпераций КАК ЧИСЛО(3, 2)) КАК APDEX,
			|	ВЫРАЗИТЬ(ВсеЗамеры.СуммаВремяВыполнения / ВсеЗамеры.КоличествоОпераций КАК ЧИСЛО(15, 3)) КАК СреднееВремя,
			|	ВсеЗамеры.СуммаВремяВыполнения КАК СуммаВремяВыполнения,
			|	ВсеЗамеры.N_T КАК N_T,
			|	ВсеЗамеры.N_T_4T КАК N_T_4T,
			|	ВсеЗамеры.N_4T КАК N_4T,
			|	ВсеЗамеры.НизкаяПроизводительность КАК НизкаяПроизводительность,
			|	ВЫБОР
			|		КОГДА ВсеЗамеры.ВерсияКонфигурации = ИБПроблемныхКО.ВерсияКонфигурации
			|			ТОГДА ИСТИНА
			|		ИНАЧЕ ЛОЖЬ
			|	КОНЕЦ КАК ЭтоПоследняяВерсия
			|ПОМЕСТИТЬ ИнформацияПоИБ
			|ИЗ
			|	ВсеЗамеры КАК ВсеЗамеры
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ИБПроблемныхКО КАК ИБПроблемныхКО
			|		ПО ВсеЗамеры.КлючеваяОперация = ИБПроблемныхКО.КлючеваяОперация
			|			И ВсеЗамеры.ИнформационнаяБаза = ИБПроблемныхКО.ИнформационнаяБаза
			|			И ВсеЗамеры.Конфигурация = ИБПроблемныхКО.Конфигурация
			|			И ВсеЗамеры.ВерсияКонфигурацииЧисло <= ИБПроблемныхКО.ВерсияКонфигурацииЧисло
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	КлючеваяОперация,
			|	ИнформационнаяБаза,
			|	Конфигурация,
			|	ВерсияКонфигурацииЧисло
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ИнформацияПоИБ.Конфигурация КАК Конфигурация,
			|	ИнформацияПоИБ.ИнформационнаяБаза КАК ИнформационнаяБаза,
			|	ИнформацияПоИБ.КлючеваяОперация КАК КлючеваяОперация,
			|	МАКСИМУМ(ИнформацияПоИБ.APDEX) КАК APDEX
			|ПОМЕСТИТЬ КОЛучшийAPDEXКОПоИБ
			|ИЗ
			|	ИнформацияПоИБ КАК ИнформацияПоИБ
			|	Внутреннее Соединение ИнформацияПоИБ КАК ИнформацияПоИБПоследнииВерсии
			|		По ИнформацияПоИБ.КлючеваяОперация = ИнформацияПоИБПоследнииВерсии.КлючеваяОперация
			|			И ИнформацияПоИБ.ИнформационнаяБаза = ИнформацияПоИБПоследнииВерсии.ИнформационнаяБаза
			|			И ИнформацияПоИБ.Конфигурация = ИнформацияПоИБПоследнииВерсии.Конфигурация
			|			И ИнформацияПоИБПоследнииВерсии.ЭтоПоследняяВерсия
			|			И (ИнформацияПоИБ.КоличествоОпераций >= ИнформацияПоИБПоследнииВерсии.КоличествоОпераций / 10)
			|			И (ИнформацияПоИБ.КоличествоОпераций <= ИнформацияПоИБПоследнииВерсии.КоличествоОпераций * 10)
			|ГДЕ
			|	НЕ ИнформацияПоИБ.ЭтоПоследняяВерсия
			|
			|СГРУППИРОВАТЬ ПО
			|	ИнформацияПоИБ.Конфигурация,
			|	ИнформацияПоИБ.ИнформационнаяБаза,
			|	ИнформацияПоИБ.КлючеваяОперация
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	КлючеваяОперация,
			|	ИнформационнаяБаза,
			|	Конфигурация
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ИнформацияПоИБ.Конфигурация КАК Конфигурация,
			|	МАКСИМУМ(ИнформацияПоИБ.ВерсияКонфигурацииЧисло) КАК ВерсияКонфигурацииЧисло,
			|	ИнформацияПоИБ.ИнформационнаяБаза КАК ИнформационнаяБаза,
			|	ИнформацияПоИБ.КлючеваяОперация КАК КлючеваяОперация
			|ПОМЕСТИТЬ ЛучшиеВерсииИБПоКО
			|ИЗ
			|	КОЛучшийAPDEXКОПоИБ КАК КОЛучшийAPDEXКОПоИБ
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ИнформацияПоИБ КАК ИнформацияПоИБ
			|		ПО КОЛучшийAPDEXКОПоИБ.КлючеваяОперация = ИнформацияПоИБ.КлючеваяОперация
			|			И КОЛучшийAPDEXКОПоИБ.ИнформационнаяБаза = ИнформацияПоИБ.ИнформационнаяБаза
			|			И КОЛучшийAPDEXКОПоИБ.Конфигурация = ИнформацияПоИБ.Конфигурация
			|			И КОЛучшийAPDEXКОПоИБ.APDEX = ИнформацияПоИБ.APDEX
			|			И (НЕ ИнформацияПоИБ.ЭтоПоследняяВерсия)
			|
			|СГРУППИРОВАТЬ ПО
			|	ИнформацияПоИБ.Конфигурация,
			|	ИнформацияПоИБ.ИнформационнаяБаза,
			|	ИнформацияПоИБ.КлючеваяОперация
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	КлючеваяОперация,
			|	ИнформационнаяБаза,
			|	Конфигурация,
			|	ВерсияКонфигурацииЧисло
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ИнформацияПоИБ.Конфигурация КАК Конфигурация,
			|	ИнформацияПоИБ.ВерсияКонфигурации КАК ВерсияКонфигурации,
			|	ИнформацияПоИБ.ВерсияКонфигурацииЧисло КАК ВерсияКонфигурацииЧисло,
			|	ИнформацияПоИБ.ИнформационнаяБаза КАК ИнформационнаяБаза,
			|	ИнформацияПоИБ.КлючеваяОперация КАК КлючеваяОперация,
			|	ИнформацияПоИБ.КоличествоОпераций КАК КоличествоОпераций,
			|	ИнформацияПоИБ.APDEX КАК APDEX,
			|	ИнформацияПоИБ.СреднееВремя КАК СреднееВремя,
			|	ИнформацияПоИБ.СуммаВремяВыполнения КАК СуммаВремяВыполнения,
			|	ИнформацияПоИБ.N_T КАК N_T,
			|	ИнформацияПоИБ.N_T_4T КАК N_T_4T,
			|	ИнформацияПоИБ.N_4T КАК N_4T
			|ПОМЕСТИТЬ ДанныеКОЛучшихВерсийПоИБ
			|ИЗ
			|	ЛучшиеВерсииИБПоКО КАК ЛучшиеВерсииИБПоКО
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ИнформацияПоИБ КАК ИнформацияПоИБ
			|		ПО ЛучшиеВерсииИБПоКО.КлючеваяОперация = ИнформацияПоИБ.КлючеваяОперация
			|			И ЛучшиеВерсииИБПоКО.ИнформационнаяБаза = ИнформацияПоИБ.ИнформационнаяБаза
			|			И ЛучшиеВерсииИБПоКО.Конфигурация = ИнформацияПоИБ.Конфигурация
			|			И ЛучшиеВерсииИБПоКО.ВерсияКонфигурацииЧисло = ИнформацияПоИБ.ВерсияКонфигурацииЧисло
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	КлючеваяОперация,
			|	ИнформационнаяБаза,
			|	Конфигурация
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ИнформацияПоИБ.КлючеваяОперация КАК КлючеваяОперация,
			|	ИнформацияПоИБ.ИнформационнаяБаза КАК ИнформационнаяБаза,
			|	ИнформацияПоИБ.Конфигурация КАК Конфигурация
			|ПОМЕСТИТЬ ИБИмеющиеНесколькоВерсий
			|ИЗ
			|	ИнформацияПоИБ КАК ИнформацияПоИБ
			|ГДЕ
			|	НЕ ИнформацияПоИБ.ЭтоПоследняяВерсия
			|
			|СГРУППИРОВАТЬ ПО
			|	ИнформацияПоИБ.КлючеваяОперация,
			|	ИнформацияПоИБ.ИнформационнаяБаза,
			|	ИнформацияПоИБ.Конфигурация
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	КлючеваяОперация,
			|	ИнформационнаяБаза,
			|	Конфигурация
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ИБПроблемныхКО.Конфигурация КАК Конфигурация,
			|	ИБПроблемныхКО.ВерсияКонфигурации КАК ВерсияКонфигурации,
			|	ИБПроблемныхКО.ИнформационнаяБаза КАК ИнформационнаяБаза,
			|	ИБПроблемныхКО.КлючеваяОперация КАК КлючеваяОперация,
			|	ИБПроблемныхКО.КлючеваяОперацияПредставление КАК КлючеваяОперацияПредставление,
			|	ИБПроблемныхКО.ЦелевоеВремя КАК ЦелевоеВремя,
			|	ИБПроблемныхКО.Ответственный КАК Ответственный,
			|	ИБПроблемныхКО.Важность КАК Важность,
			|	ВЫБОР
			|		КОГДА ЕСТЬNULL(ДанныеКОЛучшихВерсийПоИБ.APDEX, 0) - ИБПроблемныхКО.APDEX >= (&ПорогСнижения - 0.03)
			|			ТОГДА 1
			|		ИНАЧЕ 0
			|	КОНЕЦ КАК Ухудшение,
			|	ВЫБОР
			|		КОГДА ЕСТЬNULL(ДанныеКОЛучшихВерсийПоИБ.APDEX, 0) - ИБПроблемныхКО.APDEX <= -(&ПорогСнижения - 0.03)
			|			ТОГДА 1
			|		ИНАЧЕ 0
			|	КОНЕЦ КАК Улучшение,
			|	ВЫБОР
			|		КОГДА ЕСТЬNULL(ДанныеКОЛучшихВерсийПоИБ.APDEX, 0) - ИБПроблемныхКО.APDEX < (&ПорогСнижения - 0.03)
			|			И ЕСТЬNULL(ДанныеКОЛучшихВерсийПоИБ.APDEX, 0) - ИБПроблемныхКО.APDEX > -(&ПорогСнижения - 0.03)
			|			ТОГДА 1
			|		ИНАЧЕ 0
			|	КОНЕЦ КАК БезИзменений,
			|	ИБПроблемныхКО.КоличествоОпераций КАК КоличествоОпераций,
			|	ИБПроблемныхКО.APDEX КАК APDEX,
			|	ИБПроблемныхКО.СреднееВремя КАК СреднееВремя,
			|	ИБПроблемныхКО.СуммаВремяВыполнения КАК СуммаВремяВыполнения,
			|	ИБПроблемныхКО.N_T КАК N_T,
			|	ИБПроблемныхКО.N_T_4T КАК N_T_4T,
			|	ИБПроблемныхКО.N_4T КАК N_4T,
			|	ИБПроблемныхКО.НизкаяПроизводительность КАК НизкаяПроизводительность,
			|	ИБПроблемныхКО.ВерсияКонфигурацииЧисло КАК ВерсияКонфигурацииЧисло,
			|	ДанныеКОЛучшихВерсийПоИБ.ВерсияКонфигурации КАК ВерсияКонфигурацииЛучшая,
			|	ДанныеКОЛучшихВерсийПоИБ.ВерсияКонфигурацииЧисло КАК ВерсияКонфигурацииЧислоЛучшая,
			|	ДанныеКОЛучшихВерсийПоИБ.КоличествоОпераций КАК КоличествоОперацийЛучшая,
			|	ДанныеКОЛучшихВерсийПоИБ.APDEX КАК APDEXЛучшая,
			|	ДанныеКОЛучшихВерсийПоИБ.N_T КАК N_TЛучшая,
			|	ДанныеКОЛучшихВерсийПоИБ.N_T_4T КАК N_T_4TЛучшая,
			|	ДанныеКОЛучшихВерсийПоИБ.N_4T КАК N_4TЛучшая,
			|	ДанныеКОЛучшихВерсийПоИБ.СреднееВремя КАК СреднееВремяЛучшая,
			|	ДанныеКОЛучшихВерсийПоИБ.СуммаВремяВыполнения КАК СуммаВремяВыполненияЛучшая,
			|	ИБПроблемныхКО.МинимальноеКоличествоБаз КАК МинимальноеКоличествоБаз,
			|	ИБПроблемныхКО.МинимальныйПроцентБаз КАК МинимальныйПроцентБаз
			|ПОМЕСТИТЬ ДинамикаКОПоследнейВерсииПоИБ
			|ИЗ
			|	ИБПроблемныхКО КАК ИБПроблемныхКО
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ИБИмеющиеНесколькоВерсий КАК ИБИмеющиеНесколькоВерсий
			|		ПО ИБПроблемныхКО.КлючеваяОперация = ИБИмеющиеНесколькоВерсий.КлючеваяОперация
			|			И ИБПроблемныхКО.ИнформационнаяБаза = ИБИмеющиеНесколькоВерсий.ИнформационнаяБаза
			|			И ИБПроблемныхКО.Конфигурация = ИБИмеющиеНесколькоВерсий.Конфигурация
			|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеКОЛучшихВерсийПоИБ КАК ДанныеКОЛучшихВерсийПоИБ
			|		ПО ИБПроблемныхКО.КлючеваяОперация = ДанныеКОЛучшихВерсийПоИБ.КлючеваяОперация
			|			И ИБПроблемныхКО.ИнформационнаяБаза = ДанныеКОЛучшихВерсийПоИБ.ИнформационнаяБаза
			|			И ИБПроблемныхКО.Конфигурация = ДанныеКОЛучшихВерсийПоИБ.Конфигурация
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	КлючеваяОперация,
			|	ИнформационнаяБаза,
			|	Конфигурация
			|;
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ДинамикаКОПоследнейВерсииПоИБ.Конфигурация КАК Конфигурация,
			|	ДинамикаКОПоследнейВерсииПоИБ.КлючеваяОперация КАК КлючеваяОперация,
			|	СУММА(ДинамикаКОПоследнейВерсииПоИБ.Ухудшение) КАК Ухудшение,
			|	СУММА(ДинамикаКОПоследнейВерсииПоИБ.Улучшение) КАК Улучшение,
			|	СУММА(ДинамикаКОПоследнейВерсииПоИБ.БезИзменений) КАК БезИзменений
			|Поместить КОКонфигурацийПоКатегориям
			|ИЗ
			|	ДинамикаКОПоследнейВерсииПоИБ КАК ДинамикаКОПоследнейВерсииПоИБ
			|
			|СГРУППИРОВАТЬ ПО
			|	ДинамикаКОПоследнейВерсииПоИБ.Конфигурация,
			|	ДинамикаКОПоследнейВерсииПоИБ.КлючеваяОперация
			|Индексировать ПО
			|	ДинамикаКОПоследнейВерсииПоИБ.КлючеваяОперация,
			|	ДинамикаКОПоследнейВерсииПоИБ.Конфигурация
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
			|ВЫБРАТЬ
			|	ДинамикаКОПоследнейВерсииПоИБ.Конфигурация КАК Конфигурация,
			|	ДинамикаКОПоследнейВерсииПоИБ.ВерсияКонфигурации КАК ВерсияКонфигурации,
			|	ДинамикаКОПоследнейВерсииПоИБ.ВерсияКонфигурацииЧисло КАК ВерсияКонфигурацииЧисло,
			|	ДинамикаКОПоследнейВерсииПоИБ.КлючеваяОперация КАК КлючеваяОперация,
			|	ДинамикаКОПоследнейВерсииПоИБ.КлючеваяОперацияПредставление КАК КлючеваяОперацияПредставление,
			|	ДинамикаКОПоследнейВерсииПоИБ.ЦелевоеВремя КАК ЦелевоеВремя,
			|	ДинамикаКОПоследнейВерсииПоИБ.Ответственный КАК Ответственный,
			|	ДинамикаКОПоследнейВерсииПоИБ.Важность КАК Важность,
			|	СУММА(ДинамикаКОПоследнейВерсииПоИБ.Ухудшение) КАК НизкаяПроизводительность,
			|	МАКСИМУМ(КОКонфигурацийПоКатегориям.БезИзменений) КАК БезИзменений,
			|	МАКСИМУМ(КОКонфигурацийПоКатегориям.Улучшение) КАК Улучшение,	
			|	Выразить(СУММА(ДинамикаКОПоследнейВерсииПоИБ.Ухудшение)/Количество(Различные ДинамикаКОПоследнейВерсииПоИБ.ИнформационнаяБаза)*100 КАК Число(15,2)) КАК ПроцентБаз,
			|	Количество(Различные ДинамикаКОПоследнейВерсииПоИБ.ИнформационнаяБаза) КАК ВсегоИБ,			
			|	Выразить((СУММА(ДинамикаКОПоследнейВерсииПоИБ.N_T)+СУММА(ДинамикаКОПоследнейВерсииПоИБ.N_T_4T)/2)/СУММА(ДинамикаКОПоследнейВерсииПоИБ.КоличествоОпераций) КАК Число(3,2)) КАК APDEX,
			|	Выразить(СУММА(ДинамикаКОПоследнейВерсииПоИБ.СуммаВремяВыполнения) / СУММА(ДинамикаКОПоследнейВерсииПоИБ.КоличествоОпераций) КАК Число(15,3)) КАК СреднееВремя,
			|	СУММА(ДинамикаКОПоследнейВерсииПоИБ.КоличествоОпераций) КАК ВсегоЗамеров,
			|	ВТ_Ошибки.Конфигурация КАК КонфигурацияОшибки,
			|	ВТ_Ошибки.КлючеваяОперация КАК КлючеваяОперацияОшибки,
			|	ВТ_Ошибки.ВерсияКонфигурации КАК ВерсияКонфигурацииОшибки
			|ИЗ
			|	ДинамикаКОПоследнейВерсииПоИБ КАК ДинамикаКОПоследнейВерсииПоИБ
			|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_Ошибки КАК ВТ_Ошибки
			|		ПО ДинамикаКОПоследнейВерсииПоИБ.КлючеваяОперация = ВТ_Ошибки.КлючеваяОперация
			|			И ДинамикаКОПоследнейВерсииПоИБ.Конфигурация = ВТ_Ошибки.Конфигурация
			|			И Выбор 
			|				Когда ВТ_Ошибки.Статус В (Значение(Перечисление.СтатусыОшибокПроизводительности.Исправлена),Значение(Перечисление.СтатусыОшибокПроизводительности.Зарегистрирована),Значение(Перечисление.СтатусыОшибокПроизводительности.Отклонена))
			|					Тогда ДинамикаКОПоследнейВерсииПоИБ.ВерсияКонфигурацииЧисло >= ВТ_Ошибки.ВерсияКонфигурацииЧисло
			|				Иначе Ложь 
			|			Конец
			|		Внутреннее Соединение КОКонфигурацийПоКатегориям КАК КОКонфигурацийПоКатегориям
			|		ПО ДинамикаКОПоследнейВерсииПоИБ.КлючеваяОперация = КОКонфигурацийПоКатегориям.КлючеваяОперация
			|			И ДинамикаКОПоследнейВерсииПоИБ.Конфигурация = КОКонфигурацийПоКатегориям.Конфигурация
			|
			|СГРУППИРОВАТЬ ПО
			|	ДинамикаКОПоследнейВерсииПоИБ.Конфигурация,
			|	ДинамикаКОПоследнейВерсииПоИБ.ВерсияКонфигурации,
			|	ДинамикаКОПоследнейВерсииПоИБ.ВерсияКонфигурацииЧисло,
			|	ДинамикаКОПоследнейВерсииПоИБ.КлючеваяОперация,
			|	ДинамикаКОПоследнейВерсииПоИБ.КлючеваяОперацияПредставление,
			|	ДинамикаКОПоследнейВерсииПоИБ.ЦелевоеВремя,
			|	ДинамикаКОПоследнейВерсииПоИБ.Ответственный,
			|	ДинамикаКОПоследнейВерсииПоИБ.Важность,
			|	ВТ_Ошибки.Конфигурация,
			|	ВТ_Ошибки.КлючеваяОперация,
			|	ВТ_Ошибки.ВерсияКонфигурации
			|
			|ИМЕЮЩИЕ
			|	СУММА(ДинамикаКОПоследнейВерсииПоИБ.Ухудшение) < МАКСИМУМ(ДинамикаКОПоследнейВерсииПоИБ.МинимальноеКоличествоБаз) И
			|	СУММА(ДинамикаКОПоследнейВерсииПоИБ.Ухудшение) < Количество(Различные ДинамикаКОПоследнейВерсииПоИБ.ИнформационнаяБаза) * МАКСИМУМ(ДинамикаКОПоследнейВерсииПоИБ.МинимальныйПроцентБаз) / 100
			|	И НЕ СУММА(ДинамикаКОПоследнейВерсииПоИБ.Ухудшение) > МАКСИМУМ(КОКонфигурацийПоКатегориям.БезИзменений) > МАКСИМУМ(КОКонфигурацийПоКатегориям.Улучшение)";
	Возврат ТекстЗапроса;
КонецФункции

#КонецЕсли