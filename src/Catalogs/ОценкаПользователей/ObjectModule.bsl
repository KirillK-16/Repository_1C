#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Функция ПолучитьДанные(
	Знач ОпорнаяДата,
	Знач НачальноеСмещение,
	Знач ЧислоТочек,
	Знач Шаг, 
	Статистика = Неопределено, 
	ВычислитьАгрегатныеЗначения = Истина, 
	ВычислитьГоризонтАктуальности = Ложь,
	РежимСдвига = Ложь
) Экспорт
	
	ТребуемаяДатаНачала = ОпорнаяДата + НачальноеСмещение;
	
	Запрос = Новый Запрос;
	
	ЗапросТекст = "
	|ВЫБРАТЬ
	|	ОбъединенныйЗапрос.Время КАК Время,
	|	ОбъединенныйЗапрос.ТекущееЗначение КАК ТекущееЗначение,
	|	ОбъединенныйЗапрос.ИнформационнаяБаза КАК ОбъектКонтроля,
	|	ОбъединенныйЗапрос.НомерИнтервала КАК НомерИнтервала
	|ПОМЕСТИТЬ ВТ_ВыборкаЗамеров
	|ИЗ
	|(ВЫБРАТЬ
	|	ОсновнаяТаблицаЗамеров.Период КАК Время,
	|	СРЕДНЕЕ(
	|		ВЫБОР 
	|			КОГДА ОсновнаяТаблицаЗамеров.%ТипОценки = Значение(Перечисление.ОценкаПользователей.Отлично) Тогда 5
	|			КОГДА ОсновнаяТаблицаЗамеров.%ТипОценки = Значение(Перечисление.ОценкаПользователей.Хорошо) Тогда 4
	|			КОГДА ОсновнаяТаблицаЗамеров.%ТипОценки = Значение(Перечисление.ОценкаПользователей.Удовлетворительно) Тогда 3
	|			КОГДА ОсновнаяТаблицаЗамеров.%ТипОценки = Значение(Перечисление.ОценкаПользователей.Плохо) Тогда 2
	|			Иначе 1
	|		КОНЕЦ
	|	) КАК ТекущееЗначение,
	|	ОсновнаяТаблицаЗамеров.ИнформационнаяБаза КАК ИнформационнаяБаза,
	|	%ИндексацияИнтервалов
	|
	|ИЗ
	|	РегистрСведений.ОценкаПользователей КАК ОсновнаяТаблицаЗамеров
	|ГДЕ 
	|	%УсловиеНаИнтервалДат
	|	%УсловиеНаБазу
	|СГРУППИРОВАТЬ ПО
	|	Период, ИнформационнаяБаза
	|%ЗапросНаНачальноеЗначение ) КАК ОбъединенныйЗапрос ";
	
	ШаблонЗапросаНачальногоЗначения = " 
	|ВЫБРАТЬ
	|	ВложенныйЗапрос%НомерЗапроса.Время КАК Время,
	|	ВложенныйЗапрос%НомерЗапроса.ТекущееЗначение КАК ТекущееЗначение,
	|	ВложенныйЗапрос%НомерЗапроса.ИнформационнаяБаза КАК ИнформационнаяБаза,
	|	-1 КАК НомерИнтервала
	|ИЗ
	|	(ВЫБРАТЬ ПЕРВЫЕ 1
	|		ОсновнаяТаблицаЗамеров.Период КАК Время,
	|		СРЕДНЕЕ(
	|			ВЫБОР 
	|				КОГДА ОсновнаяТаблицаЗамеров.%ТипОценки = Значение(Перечисление.ОценкаПользователей.Отлично) Тогда 5
	|				КОГДА ОсновнаяТаблицаЗамеров.%ТипОценки = Значение(Перечисление.ОценкаПользователей.Хорошо) Тогда 4
	|				КОГДА ОсновнаяТаблицаЗамеров.%ТипОценки = Значение(Перечисление.ОценкаПользователей.Удовлетворительно) Тогда 3
	|				КОГДА ОсновнаяТаблицаЗамеров.%ТипОценки = Значение(Перечисление.ОценкаПользователей.Плохо) Тогда 2
	|				Иначе 1
	|			КОНЕЦ
	|		) КАК ТекущееЗначение,
	|		ОсновнаяТаблицаЗамеров.ИнформационнаяБаза КАК ИнформационнаяБаза
	|		ИЗ
	|			РегистрСведений.ОценкаПользователей КАК ОсновнаяТаблицаЗамеров
	|		ГДЕ
	|			ОсновнаяТаблицаЗамеров.ИнформационнаяБаза = &ИнформационнаяБаза%НомерЗапроса
	|			И ОсновнаяТаблицаЗамеров.Период < &ВремяЗамеровНачало
	|	СГРУППИРОВАТЬ ПО
	|		Период, ИнформационнаяБаза
	|	УПОРЯДОЧИТЬ ПО
	|		Период УБЫВ
	|	) КАК ВложенныйЗапрос%НомерЗапроса " ;
	
	Базы = СписокБаз();
	
	ЗапросНачальныхЗначений = "";
	НомерБазы = 1;
	Для Каждого База Из Базы Цикл
		НомерБазыСтрокой = ОбщийКлиентСервер.ЧислоВСтроку(НомерБазы);
		ТекстПодзапроса = СтрЗаменить(ШаблонЗапросаНачальногоЗначения, "%НомерЗапроса", НомерБазыСтрокой);
		Запрос.УстановитьПараметр("ИнформационнаяБаза" + НомерБазыСтрокой, База);
		
		ЗапросНачальныхЗначений = ЗапросНачальныхЗначений + " ОБЪЕДИНИТЬ ВСЕ" + ТекстПодзапроса;
		
		НомерБазы = НомерБазы + 1;
	КонецЦикла;
	
	ПараметрыУсловийНаБазу = Новый Массив;
	ПараметрыУсловийНаБазу.Добавить(Новый Структура("ЗаменяемаяСтрока, Префикс, Постфикс", "%УсловиеНаБазу", " И ", ""));
	
	УстановитьФильтрПоОбъектамКонтроля(Запрос, ЗапросТекст, 
		ПараметрыУсловийНаБазу
	);
	
	Запрос.УстановитьПараметр("ВремяЗамеровНачало", ТребуемаяДатаНачала);
	
	ЗапросТекст = СтрЗаменить(ЗапросТекст, "%ЗапросНаНачальноеЗначение", ЗапросНачальныхЗначений);
	
	ЗапросТекст = СтрЗаменить(ЗапросТекст, "%ТипОценки", ЭтотОбъект.ТипОценки);
	
	Замеры = МониторингСервер.ВыбратьДанныеПоРешеткеДат(
		ЭтотОбъект.Ссылка,
		ОпорнаяДата,
		НачальноеСмещение,
		ЧислоТочек,
		Шаг,
		Запрос,
		ЗапросТекст,
		"Период",
		Шаг
	);
	
	КоличествоБаз = Базы.Количество();
	Если КоличествоБаз > 0 Тогда
		Если ТипЗнч(Замеры) = Тип("Структура") Тогда
			Для Каждого Замер Из Замеры Цикл
				Замер.Среднее = Замер.Среднее / КоличествоБаз;
				Замер.Минимум = Замер.Минимум / КоличествоБаз;
				Замер.Максимум = Замер.Максимум / КоличествоБаз;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Если Статистика <> Неопределено Тогда
		Если ВычислитьАгрегатныеЗначения Тогда
			ОбновитьСтатистику(
				ОпорнаяДата + НачальноеСмещение * Шаг, 
				ОпорнаяДата + (НачальноеСмещение + ЧислоТочек - 1) * Шаг, 
				Статистика
			);
			// вычислим параметр "Текущее" 
			Попытка
				Если Статистика.Кол > 0 Тогда
					ТекущееЗначение = Замеры[Замеры.Количество() - 1].Среднее;
				Иначе 
					ТекущееЗначение = 0;
				КонецЕсли;
			Исключение
				Инфо = ИнформацияОбОшибке();
				Комментарий =
					"Описание = '" +Инфо.Описание + "', " +
					"ИмяМодуля = '" + Инфо.ИмяМодуля + "', " +
					"НомерСтроки = '" + Инфо.НомерСтроки + "', " +
					"ИсходнаяСтрока = '" + Инфо.ИсходнаяСтрока + "'.";
					
				ЗаписьЖурналаРегистрации(
					"Функция ПолучитьДанные(...)",
					УровеньЖурналаРегистрации.Ошибка,
					Метаданные.Справочники.ОценкаПользователей.МодульОбъекта,
					,
					Комментарий);
			
				ТекущееЗначение = 0;
			КонецПопытки;
			Статистика.Вставить("Текущее", ТекущееЗначение);
		КонецЕсли;
		
		Если ВычислитьГоризонтАктуальности Тогда
			Статистика.Вставить("Горизонт", ТекущаяДата());
		КонецЕсли;
	КонецЕсли;
	
	Возврат Замеры;
	
КонецФункции

Функция ОбновитьСтатистику(Знач ДатаНачала, Знач ДатаОкончания, Статистика) Экспорт
	
	Запрос = Новый Запрос;
	
	ЗапросТекст = "
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.ИнформационнаяБаза КАК ОбъектКонтроля,
	|	СРЕДНЕЕ(ВложенныйЗапрос.ТекущееЗначение) КАК Сред,
	|	МАКСИМУМ(ВложенныйЗапрос.ТекущееЗначение) КАК Макс,
	|	МИНИМУМ(ВложенныйЗапрос.ТекущееЗначение) КАК Мин,
	|	КОЛИЧЕСТВО(ВложенныйЗапрос.ТекущееЗначение) КАК Кол
	|
	|ИЗ
	|(ВЫБРАТЬ
	|	СРЕДНЕЕ(
	|		ВЫБОР 
	|			КОГДА ОсновнаяТаблицаЗамеров.%ТипОценки = Значение(Перечисление.ОценкаПользователей.Отлично) Тогда 5
	|			КОГДА ОсновнаяТаблицаЗамеров.%ТипОценки = Значение(Перечисление.ОценкаПользователей.Хорошо) Тогда 4
	|			КОГДА ОсновнаяТаблицаЗамеров.%ТипОценки = Значение(Перечисление.ОценкаПользователей.Удовлетворительно) Тогда 3
	|			КОГДА ОсновнаяТаблицаЗамеров.%ТипОценки = Значение(Перечисление.ОценкаПользователей.Плохо) Тогда 2
	|			Иначе 1
	|		КОНЕЦ
	|	) КАК ТекущееЗначение,
	|	ОсновнаяТаблицаЗамеров.ИнформационнаяБаза КАК ИнформационнаяБаза
	|ИЗ
	|	РегистрСведений.ОценкаПользователей КАК ОсновнаяТаблицаЗамеров
	|ГДЕ
	|	%УсловиеНаБазу 
	|	ОсновнаяТаблицаЗамеров.Период >= &ДатаНачала
	|	И ОсновнаяТаблицаЗамеров.Период <= &ДатаОкончания
	|	
	|СГРУППИРОВАТЬ ПО
	|	Период, ИнформационнаяБаза
	|) КАК ВложенныйЗапрос
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.ИнформационнаяБаза";
	
	ПараметрыУсловийНаБазу = Новый Массив;
	ПараметрыУсловийНаБазу.Добавить(Новый Структура("ЗаменяемаяСтрока, Префикс, Постфикс", "%УсловиеНаБазу", " ", " И "));
	
	УстановитьФильтрПоОбъектамКонтроля(Запрос, ЗапросТекст, 
		ПараметрыУсловийНаБазу
	);
	
	МониторингСервер.УстановитьПараметрыДатыВЗапросе(Запрос, ДатаНачала, ДатаОкончания);
	ЗапросТекст = СтрЗаменить(ЗапросТекст, "%ТипОценки", ЭтотОбъект.ТипОценки);
	
	Запрос.Текст = ЗапросТекст;
	Выборка = Запрос.Выполнить().Выбрать();
	
	Сред = 0;
	Макс = 0;
	Мин = 0;
	ОбщееКоличество = 0;
	ЧислоЗамеров = 0;
	ПоследнееСреднее = Неопределено;
	Пока Выборка.Следующий() Цикл
		Кол = Выборка.Кол;
		Если Кол > 0 Тогда
			Сред = Сред + Выборка.Сред;
			Макс = Макс + Выборка.Макс;
			Мин = Мин + Выборка.Мин;
			ПоследнееСреднее = Выборка.Сред;
		КонецЕсли;
		ОбщееКоличество = ОбщееКоличество + Кол;
		ЧислоЗамеров = ЧислоЗамеров + 1;
	КонецЦикла;
	
	Если ЧислоЗамеров > 0 Тогда
		Сред = Сред / ЧислоЗамеров;
		Макс = Макс / ЧислоЗамеров;
		Мин = Мин / ЧислоЗамеров;
	КонецЕсли;
	
	Статистика.Вставить("Сред", Сред);
	Статистика.Вставить("Макс", Макс);
	Статистика.Вставить("Мин", Мин);
	Статистика.Вставить("Всего", "");
	Статистика.Вставить("Кол", ОбщееКоличество);
	
	// вычислим параметр "Текущее" 
	Попытка
		Если Статистика.Кол > 0 Тогда
			ТекущееЗначение = ПоследнееСреднее;
		Иначе 
			ТекущееЗначение = 0;
		КонецЕсли;
	Исключение
		Инфо = ИнформацияОбОшибке();
		Комментарий =
			"Описание = '" +Инфо.Описание + "', " +
			"ИмяМодуля = '" + Инфо.ИмяМодуля + "', " +
			"НомерСтроки = '" + Инфо.НомерСтроки + "', " +
			"ИсходнаяСтрока = '" + Инфо.ИсходнаяСтрока + "'.";
					
		ЗаписьЖурналаРегистрации(
			"Функция ОбновитьСтатистику(Знач ДатаНачала, Знач ДатаОкончания, Статистика) Экспорт",
			УровеньЖурналаРегистрации.Ошибка,
			Метаданные.Справочники.ОценкаПользователей.МодульОбъекта,
			,
			Комментарий);
			
		ТекущееЗначение = 0;
	КонецПопытки;
	Статистика.Вставить("Текущее", ТекущееЗначение);
	
КонецФункции

Функция УстановитьФильтрПоОбъектамКонтроля(
	Запрос, 
	ЗапросТекст,
	ПараметрыУсловийНаБазу
)

	СписокБаз = СписокБаз();
	Если СписокБаз.Количество() = 1 Тогда
		УсловиеНаБазу = " ОсновнаяТаблицаЗамеров.ИнформационнаяБаза = &СписокБазПерваяБаза ";
		Запрос.УстановитьПараметр("СписокБазПерваяБаза", СписокБаз[0]);
	Иначе
		УсловиеНаБазу = " ОсновнаяТаблицаЗамеров.ИнформационнаяБаза В (&СписокБаз) ";
		Запрос.УстановитьПараметр("СписокБаз", СписокБаз());
	КонецЕсли;
	
	Для Каждого ПараметрУсловий Из ПараметрыУсловийНаБазу Цикл
		ТекущееУсловиеНаБазу = УсловиеНаБазу;
		Префикс = ПараметрУсловий.Префикс;
		Постфикс = ПараметрУсловий.Постфикс;
		Если НЕ ПустаяСтрока(ТекущееУсловиеНаБазу) Тогда
			ТекущееУсловиеНаБазу = Префикс + ТекущееУсловиеНаБазу + Постфикс;
		КонецЕсли;
		ЗапросТекст = СтрЗаменить(ЗапросТекст, ПараметрУсловий.ЗаменяемаяСтрока, ТекущееУсловиеНаБазу);
	КонецЦикла;
	
КонецФункции

Функция СписокБаз()
	СписокБаз = Новый Массив;
	Для Каждого БазаСтрока Из ЭтотОбъект.ИнформационныеБазы Цикл
		СписокБаз.Добавить(БазаСтрока.ИнформационнаяБазаСсылка);
	КонецЦикла;
	Возврат СписокБаз;
КонецФункции

Функция ИдентификаторВариантаПоказателя() Экспорт
	
	ФильтрПоОбъектамКонтроля = Новый Массив;
	
	ИД = МониторингСервер.ОбщаяЧастьИдентификатораВариантаПоказателя(
		ЭтотОбъект,
		ФильтрПоОбъектамКонтроля
	);
	
	РазделительПолей = "__";
	ИД = ИД + РазделительПолей + ЭтотОбъект.ТипОценки;
	
	ИД = ИД + РазделительПолей;
	Для Каждого СтрокаБазы Из ЭтотОбъект.ИнформационныеБазы Цикл
		ИД = ИД + "_" + Строка(СтрокаБазы.ИнформационнаяБазаСсылка.УникальныйИдентификатор());
	КонецЦикла;
	
	Возврат ИД;
	
КонецФункции

// Возвращает текстовую строку, описывающую тип показателя
//
// Возвращаемое значение:
//  Строка
//
Функция ИдентификаторТипаПоказателя() Экспорт
	
	Если ЗначениеЗаполнено(ЭтотОбъект.Метаданные().РасширенноеПредставлениеОбъекта) Тогда
		Возврат ЭтотОбъект.Метаданные().РасширенноеПредставлениеОбъекта;
	ИначеЕсли ЗначениеЗаполнено(ЭтотОбъект.Метаданные().ПредставлениеОбъекта) Тогда
		Возврат ЭтотОбъект.Метаданные().ПредставлениеОбъекта;
	Иначе
		Возврат ЭтотОбъект.Метаданные().Синоним;
	КонецЕсли;
	
КонецФункции

// Заполняет параметры по умолчанию отображения показателя на графике
// 
// Параметры:
//  ПараметрОповещения - Структура - см.МониторингСервер.ПараметрОповещенияПоказательЗаписан()
//
Процедура ЗаполнитьПараметрыОтображенияПоУмолчанию(Знач ПараметрОповещения) Экспорт
	ГСЧ = Новый ГенераторСлучайныхЧисел();
	
	ПараметрОповещения.Цвет = Новый Цвет(ГСЧ.СлучайноеЧисло(0,255), ГСЧ.СлучайноеЧисло(0,255), ГСЧ.СлучайноеЧисло(0,255));
	ПараметрОповещения.АвтоМасштаб = Истина;
	ПараметрОповещения.Масштаб = 1;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ПустаяСтрока(Описание) Тогда
		Описание = Описание();
	КонецЕсли;
	
КонецПроцедуры

Функция Описание()
	
	НаименованиеВСтроку = ИдентификаторТипаПоказателя();
	НастройкиВСтроку = Справочники.ОценкаПользователей.ТипыОценки().НайтиПоЗначению(ТипОценки).Представление;
	НастройкиВСтроку = НастройкиВСтроку +  ", информационные базы = [" 
		+ МониторингСервер.ИнформационныеБазыВСтроку(ИнформационныеБазы.ВыгрузитьКолонку("ИнформационнаяБазаСсылка")) + "]";
	
	Если НЕ ПустаяСтрока(НастройкиВСтроку) Тогда
		НаименованиеВСтроку = НаименованиеВСтроку + " (" + НастройкиВСтроку + ")";
	КонецЕсли;
	
	Возврат НаименованиеВСтроку;
	
КонецФункции

// Расчитывает данные показателя по периодам
//
//	Параметры:
// 		МенеджерВременныхТаблиц	- МенеджерВременныхТаблиц. Временные таблицы с периодами и показателями.
// 		Детализация				- ТипДополненияПериодаКомпоновкиДанных. Период детализации.
//
//	Возвращаемое значение:
//		ТаблицаДанных. ТаблицаЗначений. Данные показателя.
//
Функция РасчитатьПоказатель(МенеджерВременныхТаблиц, Детализация = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("ПоказательМониторинга", ЭтотОбъект.Владелец);
	Запрос.УстановитьПараметр("Показатель", ЭтотОбъект.Ссылка);
	Запрос.УстановитьПараметр("ПараметрТипОценки", ЭтотОбъект.ТипОценки);
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ТаблицаПодготовительная.ДатаТочки КАК ДатаТочки,
	|	МИНИМУМ(ВЫБОР 
	|		КОГДА &ТипОценки = Значение(Перечисление.ОценкаПользователей.Отлично) Тогда 5
	|		КОГДА &ТипОценки = Значение(Перечисление.ОценкаПользователей.Хорошо) Тогда 4
	|		КОГДА &ТипОценки = Значение(Перечисление.ОценкаПользователей.Удовлетворительно) Тогда 3
	|		КОГДА &ТипОценки = Значение(Перечисление.ОценкаПользователей.Плохо) Тогда 2
	|		Иначе 1
	|	КОНЕЦ)  КАК Минимальное,
	|	МАКСИМУМ(ВЫБОР 
	|		КОГДА &ТипОценки = Значение(Перечисление.ОценкаПользователей.Отлично) Тогда 5
	|		КОГДА &ТипОценки = Значение(Перечисление.ОценкаПользователей.Хорошо) Тогда 4
	|		КОГДА &ТипОценки = Значение(Перечисление.ОценкаПользователей.Удовлетворительно) Тогда 3
	|		КОГДА &ТипОценки = Значение(Перечисление.ОценкаПользователей.Плохо) Тогда 2
	|		Иначе 1
	|	КОНЕЦ)  КАК Максимальное,
	|	СУММА(ВЫБОР 
	|		КОГДА &ТипОценки = Значение(Перечисление.ОценкаПользователей.Отлично) Тогда 5
	|		КОГДА &ТипОценки = Значение(Перечисление.ОценкаПользователей.Хорошо) Тогда 4
	|		КОГДА &ТипОценки = Значение(Перечисление.ОценкаПользователей.Удовлетворительно) Тогда 3
	|		КОГДА &ТипОценки = Значение(Перечисление.ОценкаПользователей.Плохо) Тогда 2
	|		Иначе 1
	|	КОНЕЦ)  КАК Значение
	|
	|ИЗ
	|	ТаблицаПодготовительная КАК ТаблицаПодготовительная
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОценкаПользователей КАК ОсновнаяТаблицаЗамеров
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ОценкаПользователей.ИнформационныеБазы КАК ОценкаПользователейИнформационныеБазы
	|		ПО ОсновнаяТаблицаЗамеров.ИнформационнаяБаза = ОценкаПользователейИнформационныеБазы.ИнформационнаяБазаСсылка
	|		И ОценкаПользователейИнформационныеБазы.Ссылка = &Показатель
	|		И ОценкаПользователейИнформационныеБазы.Ссылка.ТипОценки = &ПараметрТипОценки
	|
	|	ПО ТаблицаПодготовительная.ПоказательМониторинга = &ПоказательМониторинга
	|	И ОсновнаяТаблицаЗамеров.Период > ТаблицаПодготовительная.ДатаТочкиДанные
	|	И ОсновнаяТаблицаЗамеров.Период <= ТаблицаПодготовительная.ДатаТочкиДанныеСледующая
	|
	|ГДЕ
	|	ТаблицаПодготовительная.ПоказательМониторинга = &ПоказательМониторинга
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаПодготовительная.ДатаТочки
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаТочки
	|";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ТипОценки", "ОсновнаяТаблицаЗамеров." + СокрЛП(ЭтотОбъект.ТипОценки));
	
	ТаблицаДетальныхДанных = Запрос.Выполнить().Выгрузить();
	
	МассивПолей = Новый Массив();
	МассивПолей.Добавить("Значение");
	
	РасчетИтоговПоказателей.ЗаполнитьПустыеДанныеПредыдущимиЗначениями(ТаблицаДетальныхДанных, МассивПолей, Null);
	
	Если ЭтотОбъект.Владелец.ПоказыватьТренд Тогда
		РасчетИтоговПоказателей.СгладитьДанные(ТаблицаДетальныхДанных, ЭтотОбъект.Владелец.ТипСглаживания, ЭтотОбъект.Владелец.КоличествоУсредняемыхЗначений);
	КонецЕсли;
	
	Возврат ТаблицаДетальныхДанных;
	
КонецФункции

#КонецЕсли