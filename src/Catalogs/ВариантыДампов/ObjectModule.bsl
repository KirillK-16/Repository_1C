#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ПередЗаписью(Отказ)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ЭтотОбъект.СостояниеИзменилось = Общий.ЭтоНовыйОбъект(ЭтотОбъект) 
		ИЛИ (ЭтотОбъект.Состояние <> ЭтотОбъект.Ссылка.Состояние);
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтотОбъект.СостояниеИзменилось Тогда
		
		ПодправитьЧислоПроблем(ЭтотОбъект.Состояние = Перечисления.СостоянияВариантаДампа.Исправлен);
		
	КонецЕсли;
КонецПроцедуры

Процедура ПодправитьЧислоПроблем(ПроблемаРешена)
	
	ТекущаяДата = ТекущаяДата();
	
	АктуальноеЧислоПроблем = РегистрыСведений.ЧислоПроблемПлатформы.ПолучитьПоследнее()["ЧислоПроблем"];
	СчетчикПроблем = РегистрыСведений.ЧислоПроблемПлатформы.СоздатьМенеджерЗаписи();
	СчетчикПроблем.ЧислоПроблем = АктуальноеЧислоПроблем + ?(ПроблемаРешена, -1, 1);
	СчетчикПроблем.Период = ТекущаяДата;
	СчетчикПроблем.Записать();
	
	ЗаписьОПроблеме = РегистрыСведений.НовыеПроблемы.СоздатьМенеджерЗаписи();
	ЗаписьОПроблеме.ВариантДампа = ЭтотОбъект.Ссылка;
	ЗаписьОПроблеме.Период = ТекущаяДата;
	ЗаписьОПроблеме.Решена = ПроблемаРешена;
	ЗаписьОПроблеме.Записать();
	
КонецПроцедуры

Процедура ПередУдалением(Отказ)
	
	ПодправитьЧислоПроблем(Истина);
	
КонецПроцедуры

#КонецЕсли