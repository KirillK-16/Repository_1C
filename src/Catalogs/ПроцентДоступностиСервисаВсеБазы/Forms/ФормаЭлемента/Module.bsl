////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Выборка = Общий.ВыборкаИнформационныхБаз();
	Пока Выборка.Следующий() Цикл
		
		ИБ = Выборка.Ссылка;
		Элементы.ИнформационныеБазыИнформационнаяБаза.СписокВыбора.Добавить(ИБ);
		Если Объект.Ссылка.Пустая() Тогда
			СтрокаИнформационнойБазы = Объект.ИнформационныеБазы.Добавить();
			СтрокаИнформационнойБазы.ИнформационнаяБаза = ИБ;
		КонецЕсли;
	КонецЦикла;
	
	Если Объект.Ссылка.Пустая() Тогда
		
		Объект.ИнтервалУсреднения = Перечисления.ИнтервалыУсреднения.Час;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ПараметрОповещения = МониторингСервер.ПараметрОповещенияПоказательЗаписан(ТекущийОбъект);
	ПараметрОповещения.Изменился = ОбъектИзменился();

КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ПараметрОповещения.Ссылка = Объект.Ссылка;
	ПараметрОповещения.Описание = Объект.Описание;
	Оповестить("ПоказательЗаписан", ПараметрОповещения, ЭтотОбъект);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Функция ОбъектИзменился()
	
	Если Объект.ИнтервалУсреднения <> Объект.Ссылка.ИнтервалУсреднения Тогда
		Возврат Истина;
	КонецЕсли;
	
	БазыСсылка = Объект.Ссылка.ИнформационныеБазы;
	БазыОбъект = Объект.ИнформационныеБазы;
	Если БазыСсылка.Количество() <> БазыОбъект.Количество() Тогда
		Возврат Истина;
	КонецЕсли;
	
	Для Каждого База Из БазыОбъект Цикл
		
		Строка = БазыСсылка.Найти(База.ИнформационнаяБаза);
		Если Строка = Неопределено Тогда
			Возврат Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции
