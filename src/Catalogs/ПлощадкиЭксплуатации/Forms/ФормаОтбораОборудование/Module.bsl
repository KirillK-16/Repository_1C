
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
    
    ИнициализироватьКомпоновщик();
    
    Если ЭтотОбъект.Параметры.Свойство("Компоновщик") Тогда
        Компоновщик.ЗагрузитьФиксированныеНастройки(ЭтотОбъект.Параметры.Компоновщик.ФиксированныеНастройки);
    ИначеЕсли ЭтотОбъект.Параметры.Свойство("РегулярноеВыражение") Тогда
        ЭтотОбъект.РегулярноеВыражение = ЭтотОбъект.Параметры.РегулярноеВыражение;    
    КонецЕсли;
    
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
    УстановитьВидимостьВидаОтбора();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УстановитьОтбор(Команда)
    
    Если ЭтотОбъект.ВидОтбора = 0 Тогда
        ЭтотОбъект.Закрыть(ЭтотОбъект.Компоновщик);
    Иначе
        ЭтотОбъект.Закрыть(ЭтотОбъект.РегулярноеВыражение);
    КонецЕсли;
        
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ВидОтбораПриИзменении(Элемент)
    УстановитьВидимостьВидаОтбора();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ИнициализироватьКомпоновщик()
    
    СКД = Новый СхемаКомпоновкиДанных();
	ИсточникСКД = СКД.ИсточникиДанных.Добавить();
	ИсточникСКД.Имя = "ИсточникДанных1";
	ИсточникСКД.ТипИсточникаДанных = "local";
    
    НаборДанных = СКД.НаборыДанных.Добавить(Тип("НаборДанныхОбъектСхемыКомпоновкиДанных"));
    НаборДанных.ИсточникДанных = ИсточникСКД.Имя;
    НаборДанных.Имя = "НаборДанных1";
    НаборДанных.ИмяОбъекта = "НаборДанных1";
    
    Поле = НаборДанных.Поля.Добавить(Тип("ПолеНабораДанныхСхемыКомпоновкиДанных"));
    Поле.Поле = "ЕстьСсылка";
    Поле.ПутьКДанным = "ЕстьСсылка";
    Поле.Заголовок = "Создано ранее";
    Поле.ТипЗначения = Новый ОписаниеТипов("Булево");
    
    Поле = НаборДанных.Поля.Добавить(Тип("ПолеНабораДанныхСхемыКомпоновкиДанных"));
    Поле.Поле = "Наименование";
    Поле.ПутьКДанным = "Наименование";
    Поле.Заголовок = "Наименование";
    Поле.ТипЗначения = Новый ОписаниеТипов("Строка",,,,Новый КвалификаторыСтроки(100));
    
    URLСхемы = ПоместитьВоВременноеХранилище(СКД, Новый УникальныйИдентификатор());
    Компоновщик.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(URLСхемы));
        
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимостьВидаОтбора()
    
    Если ЭтотОбъект.ВидОтбора = 0 Тогда
        Элементы.Группа1.Видимость = Истина;
        Элементы.Группа2.Видимость = Ложь;
    Иначе
        Элементы.Группа1.Видимость = Ложь;
        Элементы.Группа2.Видимость = Истина;
    КонецЕсли;
    
КонецПроцедуры

#КонецОбласти
