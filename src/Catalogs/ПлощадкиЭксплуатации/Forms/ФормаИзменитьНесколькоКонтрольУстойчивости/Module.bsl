
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
    
    ЗаполнитьПараметрыЗаписи();
    
    Если ЭтотОбъект.Параметры.Свойство("ЭлементыПлощадки") Тогда
        
        МассивКонтрольУстойчивости = ПолучитьКонтрольУстойчивости(ЭтотОбъект.Параметры.ЭлементыПлощадки);
        КонтрольУстойчивостиСписок = Новый СписокЗначений;
        КонтрольУстойчивостиСписок.ЗагрузитьЗначения(МассивКонтрольУстойчивости);
        
        //Если КонтрольУстойчивостиСписок.Количество() > 0 Тогда
            НовЭлемент = Компоновщик.Настройки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
            НовЭлемент.Использование = Истина;
            НовЭлемент.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("КонтрольУстойчивости");
            НовЭлемент.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
            НовЭлемент.ПравоеЗначение = КонтрольУстойчивостиСписок;
        //КонецЕсли;
                    
    КонецЕсли;
    
    ИнициализироватьКомпоновщик();
    УстановитьОтбор();
    
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УстановитьВсе(Команда)
    
    Для Каждого ТекСтрока Из ЭтотОбъект.ПараметрыЗаписи Цикл
        ТекСтрока.Изменить = Истина;
        Элементы["Список" + ТекСтрока.Параметр].Видимость = Истина; 
    КонецЦикла;
    
КонецПроцедуры

&НаКлиенте
Процедура СнятьВсе(Команда)
    
    Для Каждого ТекСтрока Из ЭтотОбъект.ПараметрыЗаписи Цикл
        ТекСтрока.Изменить = Ложь;
        Элементы["Список" + ТекСтрока.Параметр].Видимость = Ложь; 
    КонецЦикла;
    
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
    
    ЗаписатьИзменения();
    ЭтотОбъект.Закрыть();
    
КонецПроцедуры

&НаКлиенте
Процедура Записать(Команда)
    ЗаписатьИзменения();
КонецПроцедуры

&НаКлиенте
Процедура НайтиОтличныеОт(Команда)
    
    ТекДанные = Элементы.ПараметрыЗаписи.ТекущиеДанные;
    ТекДанные.ЕстьОтбор = Истина;
    
    Если ТекДанные.Параметр <> "РасписаниеПредставление" Тогда
        УстановитьОтборПоПараметру(ТекДанные.Параметр, ТекДанные.ПараметрЗначение);
    Иначе
        УстановитьОтборПоПараметру(ТекДанные.Параметр, ТекДанные.ПараметрЗначениеПароль);
    КонецЕсли;
    
    
КонецПроцедуры

&НаКлиенте
Процедура СнятьОтличныеОт(Команда)
    
    ТекДанные = Элементы.ПараметрыЗаписи.ТекущиеДанные;
    ТекДанные.ЕстьОтбор = Ложь;
    СнятьОтборПоПараметру(ТекДанные.Параметр);
    
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПараметрыЗаписиПараметрЗначениеПриИзменении(Элемент)
    
    ТекДанные = Элементы.ПараметрыЗаписи.ТекущиеДанные; 
    ЭтотОбъект[ТекДанные.Параметр] = ТекДанные.ПараметрЗначение; 
    
    Если ТекДанные.ЕстьОтбор Тогда
        Если ТекДанные.Параметр <> "РасписаниеПредставление" Тогда
            УстановитьОтборПоПараметру(ТекДанные.Параметр, ТекДанные.ПараметрЗначение);
        Иначе
            УстановитьОтборПоПараметру(ТекДанные.Параметр, ТекДанные.ПараметрЗначениеПароль);
        КонецЕсли;
    КонецЕсли;
    
КонецПроцедуры

&НаКлиенте
Процедура ПараметрыЗаписиПараметрЗначениеПарольПриИзменении(Элемент)
    
    ТекДанные = Элементы.ПараметрыЗаписи.ТекущиеДанные;
    
    Если ТекДанные.Параметр = "ПарольПриПодключении" Тогда
        
        ТекДанные.ПараметрЗначение = ТекДанные.ПараметрЗначениеПароль;
        ЭтотОбъект[ТекДанные.Параметр] = ТекДанные.ПараметрЗначение; 
        
        Если ТекДанные.ЕстьОтбор Тогда
            Если ТекДанные.Параметр <> "РасписаниеПредставление" Тогда
                УстановитьОтборПоПараметру(ТекДанные.Параметр, ТекДанные.ПараметрЗначение);
            Иначе
                УстановитьОтборПоПараметру(ТекДанные.Параметр, ТекДанные.ПараметрЗначениеПароль);
            КонецЕсли;
        КонецЕсли;
        
    КонецЕсли;
    
КонецПроцедуры

&НаКлиенте
Процедура ПараметрыЗаписиИзменитьПриИзменении(Элемент)
    
    Элементы["Список" + Элемент.Родитель.ТекущиеДанные.Параметр].Видимость = Элемент.Родитель.ТекущиеДанные.Изменить; 
    
КонецПроцедуры

&НаКлиенте
Процедура КомпоновщикНастройкиОтборПриИзменении(Элемент)
    УстановитьОтбор();
КонецПроцедуры

&НаКлиенте
Процедура ПараметрыЗаписиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
    
    ТекДанные = Элемент.ТекущиеДанные;
    
    Если Поле.Имя = "ПараметрыЗаписиЕстьОтбор" Тогда
        
        Если Элемент.ТекущиеДанные.ЕстьОтбор Тогда
            Если ТекДанные.Параметр <> "РасписаниеПредставление" Тогда
                УстановитьОтборПоПараметру(ТекДанные.Параметр, ТекДанные.ПараметрЗначение);
            Иначе
                УстановитьОтборПоПараметру(ТекДанные.Параметр, ТекДанные.ПараметрЗначениеПароль);
            КонецЕсли;
        Иначе
            СнятьОтборПоПараметру(ТекДанные.Параметр);
        КонецЕсли;
        
        СтандартнаяОбработка = Ложь;
        
    КонецЕсли;
    
КонецПроцедуры

&НаКлиенте
Процедура ПараметрыЗаписиПриАктивизацииСтроки(Элемент)
    
    ТекДанные = Элемент.ТекущиеДанные;
    
    Если ТекДанные.Параметр = "Состояние" Тогда
        
        Элементы.ПараметрыЗаписиПараметрЗначение.РежимВыбораИзСписка = Истина;
        Элементы.ПараметрыЗаписиПараметрЗначение.СписокВыбора.Добавить("Выполняется");
        Элементы.ПараметрыЗаписиПараметрЗначение.СписокВыбора.Добавить("Пауза");
        Элементы.ПараметрыЗаписиПараметрЗначение.СписокВыбора.Добавить("Отключен");
        Элементы.ПараметрыЗаписиПараметрЗначение.РедактированиеТекста = Ложь;
           
    Иначе
        
        Элементы.ПараметрыЗаписиПараметрЗначение.РежимВыбораИзСписка = Ложь;
        Элементы.ПараметрыЗаписиПараметрЗначение.СписокВыбора.Очистить();
        Элементы.ПараметрыЗаписиПараметрЗначение.РедактированиеТекста = Истина;
        
    КонецЕсли;
    
КонецПроцедуры

&НаКлиенте
Процедура ПараметрыЗаписиПараметрЗначениеПарольНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
    
    РасписаниеБуфер = Элементы.ПараметрыЗаписи.ТекущиеДанные.ПараметрЗначение;
    Если РасписаниеБуфер = Неопределено Тогда
        РасписаниеБуфер = Новый РасписаниеРегламентногоЗадания;
    КонецЕсли;
    
    Диалог = Новый ДиалогРасписанияРегламентногоЗадания(РасписаниеБуфер);
    
    ОписаниеОповещения = Новый ОписаниеОповещения("РасписаниеЗавершениеВыбора", ЭтотОбъект);
    Диалог.Показать(ОписаниеОповещения);
    
    СтандартнаяОбработка = Ложь;
    
КонецПроцедуры

&НаКлиенте
Процедура КомпоновщикНастройкиОтборНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
    
    ОписаниеОповещенияОЗакрытии = Новый ОписаниеОповещения("КомпоновщикНастройкиОтборЗаверешениеВыбора", ЭтотОбъект);
    ПараметрыОткрытия = Новый Структура("Компоновщик, Расписание", Компоновщик, Расписание);
    ОткрытьФорму("ОбщаяФорма.ФормаОтбораПереопределяемая", ПараметрыОткрытия, ЭтотОбъект,,,,ОписаниеОповещенияОЗакрытии,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
    
    СтандартнаяОбработка = Ложь;
    
КонецПроцедуры

&НаКлиенте
Процедура ПараметрыЗаписиЕстьОтборПриИзменении(Элемент)
    
    ТекДанные = Элементы.ПараметрыЗаписи.ТекущиеДанные;
    
    Если ТекДанные.ЕстьОтбор Тогда
        Если ТекДанные.Параметр <> "РасписаниеПредставление" Тогда
            УстановитьОтборПоПараметру(ТекДанные.Параметр, ТекДанные.ПараметрЗначение);
        Иначе
            УстановитьОтборПоПараметру(ТекДанные.Параметр, ТекДанные.ПараметрЗначениеПароль);
        КонецЕсли;
    Иначе
        СнятьОтборПоПараметру(ТекДанные.Параметр);
    КонецЕсли;
    
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПолучитьКонтрольУстойчивости(ЭлементыПлощадкиМассив)
    
    Запрос = Новый Запрос;
    
    Запрос.Текст = "
    |ВЫБРАТЬ РАЗЛИЧНЫЕ
    |   ВЫРАЗИТЬ(ЕдиницаКонтроля КАК Справочник.КонтрольныеПроцедуры) КАК КонтрольРегламентныхЗаданий
    |ИЗ
    |   Справочник.ПлощадкиЭксплуатации
    |ГДЕ
    |   Ссылка В (&МассивСсылок)
    |   И ТипЭлемента = &ТипЭлемента
    |";
    
    Запрос.УстановитьПараметр("МассивСсылок", ЭлементыПлощадкиМассив);
    Запрос.УстановитьПараметр("ТипЭлемента", Перечисления.ТипЭлементаПлощадки.КонтрольУстойчивости);
    
    Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("КонтрольРегламентныхЗаданий");
    
КонецФункции

&НаСервере
Процедура ЗаполнитьПараметрыЗаписи()
    
    НастройкиПоУмолчанию = РегистрыСведений.НастройкиСборДампов.ПрочитатьНастройки(Справочники.ВидыКонтрольныхПроцедур.КонтрольУстойчивостиСистемы());
    
    ДобавитьПараметр("Состояние", "Состояние", "Выполняется");
    
    Если НастройкиПоУмолчанию["Расписание"] = Неопределено Тогда
        НастройкиПоУмолчанию.Вставить("Расписание", Новый РасписаниеРегламентногоЗадания);
    КонецЕсли;
        
    ДобавитьПараметр("РасписаниеПредставление", "Расписание", НастройкиПоУмолчанию["Расписание"]);
    ДобавитьПараметр("КаталогЭкспортаДампов", "Каталог экспорта дампов", НастройкиПоУмолчанию["КаталогЭкспортаДампов"]);
    ДобавитьПараметр("ИспользоватьАгента", "Использовать агента", НастройкиПоУмолчанию["ИспользоватьАгента"]);
    ДобавитьПараметр("КаталогВременныхФайлов", "Каталог временных файлов", НастройкиПоУмолчанию["КаталогВременныхФайлов"]);
    ДобавитьПараметр("КаталогВыгрузкиДампов", "Каталог выгрузки дампов локальный", НастройкиПоУмолчанию["КаталогВыгрузкиДампов"]);
    ДобавитьПараметр("КаталогВыгрузкиДамповСетевой", "Каталог выгрузки дампов сетевой", НастройкиПоУмолчанию["КаталогВыгрузкиДамповСетевой"]);
    ДобавитьПараметр("АвтоматическаяНастройка", "Автоматическая настройка ТЖ", НастройкиПоУмолчанию["АвтоматическаяНастройка"]);
    ДобавитьПараметр("КаталогТЖЛокальный", "Каталог данных ТЖ локальный", НастройкиПоУмолчанию["КаталогТЖЛокальный"]);
    ДобавитьПараметр("КаталогТЖСетевой", "Каталог данных ТЖ сетевой", НастройкиПоУмолчанию["КаталогТЖСетевой"]);
    ДобавитьПараметр("УровеньДетализацииДампов", "Уровень детализации дампов", НастройкиПоУмолчанию["УровеньДетализацииДампов"]);
    ДобавитьПараметр("ДлительностьХраненияФайловТЖ", "Длительность хранения ТЖ, часов", НастройкиПоУмолчанию["ДлительностьХраненияФайловТЖ"]);
            
    Расписание = НастройкиПоУмолчанию["Расписание"]; 
            
КонецПроцедуры

&НаСервере
Процедура ДобавитьПараметр(Параметр, ПараметрПредставление, ПараметрЗначение)
    
    ЭтотОбъект[Параметр] = ПараметрЗначение;
    
    НовСтрока = ЭтотОбъект.ПараметрыЗаписи.Добавить();
    НовСтрока.Изменить = Истина;
    НовСтрока.Параметр = Параметр;
    НовСтрока.ПараметрПредставление = ПараметрПредставление;
    НовСтрока.ПараметрЗначение = ПараметрЗначение;
    НовСтрока.ПараметрЗначениеПароль = ПараметрЗначение;
    НовСтрока.ЕстьОтбор = Ложь;
    
КонецПроцедуры

&НаСервере
Процедура ЗаписатьИзменения()
    
    НастройкиДляЗаписи = Новый Структура;
    СостояниеЗаписать = Неопределено;
    РасписаниеЗаписать = Неопределено;
    
    Для Каждого ТекСтрока Из ПараметрыЗаписи Цикл
        
        Если ТекСтрока.Изменить Тогда
            
            Если ТекСтрока.Параметр = "Состояние" Тогда
                СостояниеЗаписать = ТекСтрока.ПараметрЗначение;
            ИначеЕсли ТекСтрока.Параметр = "РасписаниеПредставление" Тогда
                РасписаниеЗаписать = ТекСтрока.ПараметрЗначение;
            Иначе
                НастройкиДляЗаписи.Вставить(ТекСтрока.Параметр, ТекСтрока.ПараметрЗначение);
            КонецЕсли;
            
        КонецЕсли;
               
    КонецЦикла;
        
    ЗаписьКонтрольнойПроцедуры = СостояниеЗаписать <> Неопределено ИЛИ РасписаниеЗаписать <> Неопределено;
    
    КонтрольУстойчивостиСписок = Новый СписокЗначений;
    Для Каждого ТекСтрока Из Список Цикл
        
        РегистрыСведений.НастройкиСборДампов.ЗаписатьНастройки(ТекСтрока.КонтрольУстойчивости, НастройкиДляЗаписи);
        КонтрольУстойчивостиСписок.Добавить(ТекСтрока.КонтрольУстойчивости);
        
        Если ЗаписьКонтрольнойПроцедуры Тогда
            
            КонтрольУстойчивостиОбъект = ТекСтрока.КонтрольУстойчивости.ПолучитьОбъект();
            
            Если РасписаниеЗаписать <> Неопределено Тогда
                КонтрольУстойчивостиОбъект.Расписание  = Новый ХранилищеЗначения(РасписаниеЗаписать);
            КонецЕсли;
            
            Если СостояниеЗаписать <> Неопределено Тогда
                
                Если СостояниеЗаписать = "Выполняется" Тогда
                    КонтрольУстойчивостиОбъект.Выполнять = Истина;
                    КонтрольУстойчивостиОбъект.Пауза = Ложь;
                ИначеЕсли СостояниеЗаписать = "Пауза" Тогда
                    КонтрольУстойчивостиОбъект.Выполнять = Истина;
                    КонтрольУстойчивостиОбъект.Пауза = Истина;
                ИначеЕсли СостояниеЗаписать = "Отключен" Тогда
                    КонтрольУстойчивостиОбъект.Выполнять = Ложь;
                    КонтрольУстойчивостиОбъект.Пауза = Ложь;
                КонецЕсли;
                
            КонецЕсли;
            
            КонтрольУстойчивостиОбъект.Записать();
            
        КонецЕсли;
                
    КонецЦикла;
    
    Компоновщик.Настройки.Отбор.Элементы.Очистить();
    
    Для Каждого ТекПараметр Из ПараметрыЗаписи Цикл
        ТекПараметр.ЕстьОтбор = Ложь;
    КонецЦикла;
    
    НовЭлемент = Компоновщик.Настройки.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
    НовЭлемент.Использование = Истина;
    НовЭлемент.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("КонтрольУстойчивости");
    НовЭлемент.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
    НовЭлемент.ПравоеЗначение = КонтрольУстойчивостиСписок;
    
    УстановитьОтбор();
    
КонецПроцедуры

&НаСервере
Процедура ИнициализироватьКомпоновщик()
    
    СКД = Новый СхемаКомпоновкиДанных();
	ИсточникСКД = СКД.ИсточникиДанных.Добавить();
	ИсточникСКД.Имя = "ИсточникДанных1";
	ИсточникСКД.ТипИсточникаДанных = "local";
    
    НаборДанных = СКД.НаборыДанных.Добавить(Тип("НаборДанныхОбъектСхемыКомпоновкиДанных"));
    НаборДанных.ИсточникДанных = ИсточникСКД.Имя;
    НаборДанных.Имя = "НаборДанных1";
    НаборДанных.ИмяОбъекта = "НаборДанных1";
    
    Поле = НаборДанных.Поля.Добавить(Тип("ПолеНабораДанныхСхемыКомпоновкиДанных"));
    Поле.Поле = "КонтрольУстойчивости";
    Поле.ПутьКДанным = "КонтрольУстойчивости";
    Поле.Заголовок = "Контроль устойчивости";
    Поле.ТипЗначения = Новый ОписаниеТипов("СправочникСсылка.КонтрольныеПроцедуры");
    Поле.ОграничениеИспользованияРеквизитов.Условие = Истина;
    
    Поле = НаборДанных.Поля.Добавить(Тип("ПолеНабораДанныхСхемыКомпоновкиДанных"));
    Поле.Поле = "Наименование";
    Поле.ПутьКДанным = "Наименование";
    Поле.Заголовок = "Наименование";
    Поле.ТипЗначения = Новый ОписаниеТипов("Строка",,,,Новый КвалификаторыСтроки(150));
    
    Поле = НаборДанных.Поля.Добавить(Тип("ПолеНабораДанныхСхемыКомпоновкиДанных"));
    Поле.Поле = "РасписаниеПредставление";
    Поле.ПутьКДанным = "РасписаниеПредставление";
    Поле.Заголовок = "Расписание";
    Поле.ТипЗначения = Новый ОписаниеТипов("Строка",,,,Новый КвалификаторыСтроки(150));
                    
    Поле = НаборДанных.Поля.Добавить(Тип("ПолеНабораДанныхСхемыКомпоновкиДанных"));
    Поле.Поле = "Состояние";
    Поле.ПутьКДанным = "Состояние";
    Поле.Заголовок = "Состояние";
    Поле.ТипЗначения = Новый ОписаниеТипов("Строка",,,,Новый КвалификаторыСтроки(20));
    СЗнСостояния = Новый СписокЗначений;
    СЗнСостояния.Добавить("Выполняется");
    СЗнСостояния.Добавить("Пауза");
    СЗнСостояния.Добавить("Отключен");
    Поле.УстановитьДоступныеЗначения(СЗнСостояния);
    
    Поле = НаборДанных.Поля.Добавить(Тип("ПолеНабораДанныхСхемыКомпоновкиДанных"));
    Поле.Поле = "КаталогЭкспортаДампов";
    Поле.ПутьКДанным = "КаталогЭкспортаДампов";
    Поле.Заголовок = "Каталог экспорта дампов";
    Поле.ТипЗначения = Новый ОписаниеТипов("Строка",,,,Новый КвалификаторыСтроки(256));
    
    Поле = НаборДанных.Поля.Добавить(Тип("ПолеНабораДанныхСхемыКомпоновкиДанных"));
    Поле.Поле = "ИспользоватьАгента";
    Поле.ПутьКДанным = "ИспользоватьАгента";
    Поле.Заголовок = "Использовать агента";
    Поле.ТипЗначения = Новый ОписаниеТипов("Булево");
    
    Поле = НаборДанных.Поля.Добавить(Тип("ПолеНабораДанныхСхемыКомпоновкиДанных"));
    Поле.Поле = "КаталогВременныхФайлов";
    Поле.ПутьКДанным = "КаталогВременныхФайлов";
    Поле.Заголовок = "Каталог временных файлов";
    Поле.ТипЗначения = Новый ОписаниеТипов("Строка",,,,Новый КвалификаторыСтроки(256));
    
    Поле = НаборДанных.Поля.Добавить(Тип("ПолеНабораДанныхСхемыКомпоновкиДанных"));
    Поле.Поле = "КаталогВыгрузкиДампов";
    Поле.ПутьКДанным = "КаталогВыгрузкиДампов";
    Поле.Заголовок = "Каталог выгрузки дампов локальный";
    Поле.ТипЗначения = Новый ОписаниеТипов("Строка",,,,Новый КвалификаторыСтроки(256));
    
    Поле = НаборДанных.Поля.Добавить(Тип("ПолеНабораДанныхСхемыКомпоновкиДанных"));
    Поле.Поле = "КаталогВыгрузкиДамповСетевой";
    Поле.ПутьКДанным = "КаталогВыгрузкиДамповСетевой";
    Поле.Заголовок = "Каталог выгрузки дампов сетевой";
    Поле.ТипЗначения = Новый ОписаниеТипов("Строка",,,,Новый КвалификаторыСтроки(256));
    
    Поле = НаборДанных.Поля.Добавить(Тип("ПолеНабораДанныхСхемыКомпоновкиДанных"));
    Поле.Поле = "АвтоматическаяНастройка";
    Поле.ПутьКДанным = "АвтоматическаяНастройка";
    Поле.Заголовок = "Автоматическая настройка ТЖ";
    Поле.ТипЗначения = Новый ОписаниеТипов("Булево");
    
    Поле = НаборДанных.Поля.Добавить(Тип("ПолеНабораДанныхСхемыКомпоновкиДанных"));
    Поле.Поле = "КаталогТЖЛокальный";
    Поле.ПутьКДанным = "КаталогТЖЛокальный";
    Поле.Заголовок = "Каталог данных ТЖ локальный";
    Поле.ТипЗначения = Новый ОписаниеТипов("Строка",,,,Новый КвалификаторыСтроки(256));
    
    Поле = НаборДанных.Поля.Добавить(Тип("ПолеНабораДанныхСхемыКомпоновкиДанных"));
    Поле.Поле = "КаталогТЖСетевой";
    Поле.ПутьКДанным = "КаталогТЖСетевой";
    Поле.Заголовок = "Каталог данных ТЖ сетевой";
    Поле.ТипЗначения = Новый ОписаниеТипов("Строка",,,,Новый КвалификаторыСтроки(256));
    
    Поле = НаборДанных.Поля.Добавить(Тип("ПолеНабораДанныхСхемыКомпоновкиДанных"));
    Поле.Поле = "УровеньДетализацииДампов";
    Поле.ПутьКДанным = "УровеньДетализацииДампов";
    Поле.Заголовок = "Уровень детализации дампов";
    Поле.ТипЗначения = Новый ОписаниеТипов("Число",,,Новый КвалификаторыЧисла(3,0));
    
    Поле = НаборДанных.Поля.Добавить(Тип("ПолеНабораДанныхСхемыКомпоновкиДанных"));
    Поле.Поле = "ДлительностьХраненияФайловТЖ";
    Поле.ПутьКДанным = "ДлительностьХраненияФайловТЖ";
    Поле.Заголовок = "Длительность хранения ТЖ, часов";
    Поле.ТипЗначения = Новый ОписаниеТипов("Число",,,Новый КвалификаторыЧисла(10,0));
                    
    URLСхемы = ПоместитьВоВременноеХранилище(СКД, Новый УникальныйИдентификатор());
    Компоновщик.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(URLСхемы));
    
    ГруппировкаНастроек = Компоновщик.Настройки.Структура.Добавить(Тип("ГруппировкаКомпоновкиДанных"));
        
    Поле = ГруппировкаНастроек.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
    ВыбранноеПоле = Компоновщик.Настройки.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
    ВыбранноеПоле.Использование = Истина;
    ВыбранноеПоле.Поле = Новый ПолеКомпоновкиДанных("КонтрольУстойчивости");
    
    Поле = ГруппировкаНастроек.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
    ВыбранноеПоле = Компоновщик.Настройки.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
    ВыбранноеПоле.Использование = Истина;
    ВыбранноеПоле.Поле = Новый ПолеКомпоновкиДанных("Наименование");
    
    Поле = ГруппировкаНастроек.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
    ВыбранноеПоле = Компоновщик.Настройки.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
    ВыбранноеПоле.Использование = Истина;
    ВыбранноеПоле.Поле = Новый ПолеКомпоновкиДанных("Расписание");
    
    Поле = ГруппировкаНастроек.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
    ВыбранноеПоле = Компоновщик.Настройки.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
    ВыбранноеПоле.Использование = Истина;
    ВыбранноеПоле.Поле = Новый ПолеКомпоновкиДанных("ДанныеХранилища");
        
    Для Каждого ТекПараметр Из ПараметрыЗаписи Цикл
        
        Поле = ГруппировкаНастроек.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
        ВыбранноеПоле = Компоновщик.Настройки.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
        ВыбранноеПоле.Использование = Истина;
        ВыбранноеПоле.Поле = Новый ПолеКомпоновкиДанных(ТекПараметр.Параметр);
                
    КонецЦикла;
    
КонецПроцедуры

&НаСервере
Процедура КомпоновщикНастройкиОтборПриИзмененииНаСервере()
    
    УстановитьОтбор();
    
КонецПроцедуры

&НаКлиенте
Функция ГруппаНайтиОтличныеОт(Создать = Истина)
    
    ГруппаНайтиОтличныеОт = Неопределено;
    
    Для Каждого ТекЭлемент Из Компоновщик.Настройки.Отбор.Элементы Цикл
        
        Если ТипЗнч(ТекЭлемент) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") И ТекЭлемент.ИдентификаторПользовательскойНастройки = "b1c27381-4a1c-4dc2-9aea-3dc9b02baa72" Тогда
            ГруппаНайтиОтличныеОт = ТекЭлемент;
            Прервать;
        КонецЕсли;
        
    КонецЦикла;
    
    Если ГруппаНайтиОтличныеОт = Неопределено И Создать Тогда
        
        ГруппаНайтиОтличныеОт = Компоновщик.Настройки.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
        ГруппаНайтиОтличныеОт.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
        ГруппаНайтиОтличныеОт.Использование = Истина;
        ГруппаНайтиОтличныеОт.ИдентификаторПользовательскойНастройки = "b1c27381-4a1c-4dc2-9aea-3dc9b02baa72";
        
    КонецЕсли;
    
    Возврат ГруппаНайтиОтличныеОт;
    
КонецФункции

&НаКлиенте
Процедура УстановитьОтборПоПараметру(Параметр, ПараметрЗначение)
    
    ГруппаНайтиОтличныеОт = ГруппаНайтиОтличныеОт();
        
    ЭлементОтбора = Неопределено;
    
    Для Каждого ТекЭлемент Из ГруппаНайтиОтличныеОт.Элементы Цикл
        Если Строка(ТекЭлемент.ЛевоеЗначение) = Параметр Тогда
            ЭлементОтбора = ТекЭлемент;
            Прервать;
        КонецЕсли;
    КонецЦикла;
    
    Если ЭлементОтбора = Неопределено Тогда
        ЭлементОтбора = ГруппаНайтиОтличныеОт.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
    КонецЕсли;
    
    ЭлементОтбора.Использование = Истина;
    ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(Параметр);
    ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
    ЭлементОтбора.ПравоеЗначение = ПараметрЗначение;
    
    Элементы.КомпоновщикНастройкиОтбор.ОбновитьТекстРедактирования();
    
    КомпоновщикНастройкиОтборПриИзмененииНаСервере();
    
КонецПроцедуры

&НаКлиенте
Процедура СнятьОтборПоПараметру(Параметр)
    
    ГруппаНайтиОтличныеОт = ГруппаНайтиОтличныеОт(Ложь);
    
    Если ГруппаНайтиОтличныеОт <> Неопределено Тогда
                
        ЭлементОтбора = Неопределено;
        
        Для Каждого ТекЭлемент Из ГруппаНайтиОтличныеОт.Элементы Цикл
            Если Строка(ТекЭлемент.ЛевоеЗначение) = Параметр Тогда
                ЭлементОтбора = ТекЭлемент;
                Прервать;
            КонецЕсли;
        КонецЦикла;
        
        Если ЭлементОтбора = Неопределено Тогда
            ЭлементОтбора = ГруппаНайтиОтличныеОт.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
        КонецЕсли;
        
        Если ЭлементОтбора <> Неопределено Тогда
            
            ГруппаНайтиОтличныеОт.Элементы.Удалить(ЭлементОтбора);
            Элементы.КомпоновщикНастройкиОтбор.ОбновитьТекстРедактирования();
            
            Если ГруппаНайтиОтличныеОт.Элементы.Количество() = 0 Тогда
                Компоновщик.Настройки.Отбор.Элементы.Удалить(ГруппаНайтиОтличныеОт);
            КонецЕсли;
            
            КомпоновщикНастройкиОтборПриИзмененииНаСервере();
            
        КонецЕсли;
        
    КонецЕсли;
    
КонецПроцедуры

&НаСервере
Процедура УдалитьЭлементОтбора(ГруппаЭлементов, ИмяЭлемента)
    
    ЭлементыУдалить = Новый Массив;
    
    Для Каждого ТекЭлемент Из ГруппаЭлементов.Элементы Цикл
        
        Если ТипЗнч(ТекЭлемент) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
            УдалитьЭлементОтбора(ТекЭлемент, ИмяЭлемента);
        Иначе
            Если Строка(ТекЭлемент.ЛевоеЗначение) = ИмяЭлемента Тогда
                ЭлементыУдалить.Добавить(ТекЭлемент);
            КонецЕсли;
        КонецЕсли;
                
    КонецЦикла;
    
    Для Каждого ТекЭлемент Из ЭлементыУдалить Цикл
        ГруппаЭлементов.Элементы.Удалить(ТекЭлемент);
    КонецЦикла;
            
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьНастройкиУстановитьОтбор(КомпоновщикНовый)
    
    Компоновщик.ЗагрузитьНастройки(КомпоновщикНовый.ПолучитьНастройки());
    УстановитьОтбор();
    
КонецПроцедуры

&НаСервере
Процедура УстановитьОтбор()
    
    Если НЕ Компоновщик.ПолучитьНастройки().НаличиеОтбораУЭлемента(Компоновщик.ПолучитьНастройки()) Тогда
        Для Каждого ТекСтрока Из ПараметрыЗаписи Цикл
            ТекСтрока.ЕстьОтбор = Ложь;
        КонецЦикла;
    КонецЕсли;
    
    УстановитьПривилегированныйРежим(Истина);
    
    КомпоновщикЗапрос = Новый КомпоновщикНастроекКомпоновкиДанных();
    СКД = Новый СхемаКомпоновкиДанных();
    
    ИсточникСКД = СКД.ИсточникиДанных.Добавить();
    ИсточникСКД.Имя = "ИсточникДанных1";
    ИсточникСКД.ТипИсточникаДанных = "local";
    НаборДанных = СКД.НаборыДанных.Добавить(Тип("НаборДанныхЗапросСхемыКомпоновкиДанных"));
    НаборДанных.Запрос = ТекстЗапроса(); 
    НаборДанных.ИсточникДанных = ИсточникСКД.Имя;
    НаборДанных.Имя = "НаборДанных1";
    URLСхемы = ПоместитьВоВременноеХранилище(СКД, Новый УникальныйИдентификатор());
    КомпоновщикЗапрос.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(URLСхемы));
    КомпоновщикЗапрос.ЗагрузитьНастройки(Компоновщик.ПолучитьНастройки());
    УдалитьИзВременногоХранилища(URLСхемы);
    
    УдалитьЭлементОтбора(КомпоновщикЗапрос.Настройки.Отбор, "РасписаниеПредставление");
            
    ГруппировкаНастроек = КомпоновщикЗапрос.Настройки.Структура[0];
        
    ТЗнБуфер = Новый ТаблицаЗначений;
   
    КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных();
    МакетКомпоновкиДанных = КомпоновщикМакета.Выполнить(
        СКД, 
        КомпоновщикЗапрос.Настройки,,,
        Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений")
    );
    
    ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных();
    ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновкиДанных);
    
    ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений();
    ПроцессорВывода.УстановитьОбъект(ТЗнБуфер);
        
    ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных, Истина);
    
    УстановитьПривилегированныйРежим(Ложь);
    
    Для Каждого ТекСтрока Из ТЗнБуфер Цикл
        
        Если ТекСтрока.Расписание <> Неопределено И ТекСтрока.Расписание <> NULL Тогда
            РасписаниеПредставлениеБуфер = ТекСтрока.Расписание.Получить();
            Если РасписаниеПредставлениеБуфер <> Неопределено Тогда
                ТекСтрока.РасписаниеПредставление = Строка(РасписаниеПредставлениеБуфер);
            Иначе
                ТекСтрока.РасписаниеПредставление = "";
            КонецЕсли;
        Иначе
            ТекСтрока.РасписаниеПредставление = "";
        КонецЕсли;
        
        Если ТекСтрока.ДанныеХранилища <> Неопределено И ТекСтрока.ДанныеХранилища <> NULL  Тогда
            
            ДанныеХранилища = ТекСтрока.ДанныеХранилища.Получить();
            Если ТипЗнч(ДанныеХранилища) = Тип("Структура") И ДанныеХранилища.Свойство("КаталогЭкспортаДампов") Тогда
                ТекСтрока.КаталогЭкспортаДампов = ДанныеХранилища.КаталогЭкспортаДампов;
            КонецЕсли;
                        
        КонецЕсли;
                        
    КонецЦикла;
    
    СписокБуфер = Новый ТаблицаЗначений;
    
    СКД = Новый СхемаКомпоновкиДанных();
    ИсточникСКД = СКД.ИсточникиДанных.Добавить();
    ИсточникСКД.Имя = "ИсточникДанных1";
    ИсточникСКД.ТипИсточникаДанных = "local";
    
    НаборДанных = СКД.НаборыДанных.Добавить(Тип("НаборДанныхОбъектСхемыКомпоновкиДанных"));
    НаборДанных.ИсточникДанных = ИсточникСКД.Имя;
    НаборДанных.Имя = "НаборДанных1";
    НаборДанных.ИмяОбъекта = "НаборДанных1";
    
    Для Каждого Колонка Из ТЗнБуфер.Колонки Цикл
        Поле = НаборДанных.Поля.Добавить(Тип("ПолеНабораДанныхСхемыКомпоновкиДанных"));
        Поле.Поле = Колонка.Имя;
        Поле.ПутьКДанным = Колонка.Имя;
        Поле.Заголовок = Колонка.Заголовок;
        Поле.ТипЗначения = Колонка.ТипЗначения;
    КонецЦикла;
    
    URLСхемы = ПоместитьВоВременноеХранилище(СКД, Новый УникальныйИдентификатор());
    
    КомпоновщикБуфер = Новый КомпоновщикНастроекКомпоновкиДанных;
    КомпоновщикБуфер.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(URLСхемы));
    КомпоновщикБуфер.ЗагрузитьНастройки(Компоновщик.ПолучитьНастройки());
    
    КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных();
    МакетКомпоновкиДанных = КомпоновщикМакета.Выполнить(
        СКД, 
        КомпоновщикБуфер.Настройки,,,
        Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений")
    );
    
    ПроцессорКомпоновкиДанных = Новый ПроцессорКомпоновкиДанных();
    ПроцессорКомпоновкиДанных.Инициализировать(МакетКомпоновкиДанных, Новый Структура("НаборДанных1", ТЗнБуфер));
    
    ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений();
    ПроцессорВывода.УстановитьОбъект(СписокБуфер);
    
    ПроцессорВывода.Вывести(ПроцессорКомпоновкиДанных, Истина);
    ЭтотОбъект.Список.Загрузить(СписокБуфер);
            
КонецПроцедуры

&НаСервере
Функция ТекстЗапроса()
    
    ТекстЗапроса = "
    |ВЫБРАТЬ
    |   НастройкиСборДампов.КонтрольнаяПроцедура КАК КонтрольУстойчивости,
    |   КонтрольныеПроцедуры.Наименование КАК Наименование,
    |   КонтрольныеПроцедуры.Расписание КАК Расписание,
    |   ВЫБОР
    |       КОГДА КонтрольныеПроцедуры.Выполнять И НЕ КонтрольныеПроцедуры.Пауза ТОГДА ""Выполняется""
    |       КОГДА КонтрольныеПроцедуры.Выполнять И КонтрольныеПроцедуры.Пауза ТОГДА ""Пауза""
    |       КОГДА НЕ КонтрольныеПроцедуры.Выполнять И НЕ КонтрольныеПроцедуры.Пауза ТОГДА ""Отключен""
    |   КОНЕЦ КАК Состояние,
    |   ВЫРАЗИТЬ("""" КАК СТРОКА(300)) КАК РасписаниеПредставление,
    |   ВЫРАЗИТЬ("""" КАК СТРОКА(300)) КАК КаталогЭкспортаДампов,
    |   БезопасноеХранилище.ДанныеХранилища КАК ДанныеХранилища,
    |";
    
    Для Каждого ТекПараметр Из ПараметрыЗаписи Цикл
        
        Если ТекПараметр.Параметр <> "РасписаниеПредставление" И ТекПараметр.Параметр <> "Состояние" И ТекПараметр.Параметр <> "КаталогЭкспортаДампов" Тогда
            ТекстЗапроса = ТекстЗапроса + " НастройкиСборДампов." + ТекПараметр.Параметр + ",";
        КонецЕсли;
                
    КонецЦикла;
    
    ТекстЗапроса = Лев(ТекстЗапроса, СтрДлина(ТекстЗапроса) - 1) + "
    |ИЗ
    |   РегистрСведений.НастройкиСборДампов КАК НастройкиСборДампов
    |ВНУТРЕННЕЕ СОЕДИНЕНИЕ
    |   Справочник.КонтрольныеПроцедуры КАК КонтрольныеПроцедуры
    |ПО
    |   КонтрольныеПроцедуры.Ссылка = НастройкиСборДампов.КонтрольнаяПроцедура
    |ЛЕВОЕ СОЕДИНЕНИЕ
    |   РегистрСведений.БезопасноеХранилище КАК БезопасноеХранилище
    |ПО
    |   БезопасноеХранилище.ВладелецХранилища = НастройкиСборДампов.КонтрольнаяПроцедура 
    |";
           
    Возврат ТекстЗапроса;
    
КонецФункции

&НаКлиенте
Процедура РасписаниеЗавершениеВыбора(ВыбранноеРасписание, ДополнительныеПараметры) Экспорт
    
    Если ВыбранноеРасписание <> Неопределено Тогда
        
        Расписание = ВыбранноеРасписание;
        
        ТекДанные = Элементы.ПараметрыЗаписи.ТекущиеДанные;
        ТекДанные.ПараметрЗначение = ВыбранноеРасписание;
        ТекДанные.ПараметрЗначениеПароль = Строка(ВыбранноеРасписание);
        РасписаниеПредставление = ТекДанные.ПараметрЗначениеПароль;
        
        Если ТекДанные.ЕстьОтбор Тогда
            Если ТекДанные.Параметр <> "РасписаниеПредставление" Тогда
                УстановитьОтборПоПараметру(ТекДанные.Параметр, ТекДанные.ПараметрЗначение);
            Иначе
                УстановитьОтборПоПараметру(ТекДанные.Параметр, ТекДанные.ПараметрЗначениеПароль);
            КонецЕсли;
        КонецЕсли;
        
    КонецЕсли;
    
КонецПроцедуры

&НаКлиенте
Процедура КомпоновщикНастройкиОтборЗаверешениеВыбора(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
    
    Если ТипЗнч(РезультатЗакрытия) = Тип("КомпоновщикНастроекКомпоновкиДанных") Тогда
        
        ЗагрузитьНастройкиУстановитьОтбор(РезультатЗакрытия);
                
    КонецЕсли;
        
КонецПроцедуры

#КонецОбласти
