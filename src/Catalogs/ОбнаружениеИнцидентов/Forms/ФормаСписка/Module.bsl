////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ЗапуститьВсе(Команда)
	
	ЗапуститьВсеНаСервере();
	ПоказатьОповещениеПользователя(НСтр("ru = 'Источники запущены'"));
	ОповеститьОбИзменении(Тип("СправочникСсылка.ОбнаружениеИнцидентов"));
	
КонецПроцедуры

&НаКлиенте
Процедура ОстановитьВсе(Команда)
	
	ОстановитьВсеНаСервере();
	ПоказатьОповещениеПользователя(НСтр("ru = 'Источники остановлены'"));
	ОповеститьОбИзменении(Тип("СправочникСсылка.ОбнаружениеИнцидентов"));
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервереБезКонтекста
Процедура ЗапуститьВсеНаСервере()
	
	МаксимальноеКоличествоИсполняющихФоновыхЗаданий = Константы.МаксимальноеКоличествоИсполняющихФоновыхЗаданий.Получить();
	Если МаксимальноеКоличествоИсполняющихФоновыхЗаданий = 0 Тогда
		Константы.МаксимальноеКоличествоИсполняющихФоновыхЗаданий.Установить(1);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Т.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ОбнаружениеИнцидентов КАК Т
		|ГДЕ
		|	Т.ИсполняющееЗадание <> ЗНАЧЕНИЕ(Справочник.ОчередьЗаданий.ПустаяСсылка)";
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ИсточникОбъект = Выборка.Ссылка.ПолучитьОбъект();
		Если ИсточникОбъект <> Неопределено И ИсточникОбъект.ПроверитьКорректностьЗаполнения(Ложь) Тогда
			ОчередьЗаданий.ИзменитьЗадание(ИсточникОбъект.ИсполняющееЗадание, Новый Структура("Использование", Истина));
			Справочники.ОбнаружениеИнцидентов.ЗапуститьОстановитьАссоциированноеОповещение(Выборка.Ссылка, Истина);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОстановитьВсеНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Т.ИсполняющееЗадание,
		|	Т.Ссылка
		|ИЗ
		|	Справочник.ОбнаружениеИнцидентов КАК Т
		|ГДЕ
		|	Т.ИсполняющееЗадание <> ЗНАЧЕНИЕ(Справочник.ОчередьЗаданий.ПустаяСсылка)";
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ОчередьЗаданий.ИзменитьЗадание(Выборка.ИсполняющееЗадание, Новый Структура("Использование", Ложь));
		Справочники.ОбнаружениеИнцидентов.ЗапуститьОстановитьАссоциированноеОповещение(Выборка.Ссылка, Ложь);
	КонецЦикла;
	
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапуститьВыбранное(Команда)
	
	ВыбранныеИсточники = Новый Массив;
	Для Каждого ТекИсточник Из ЭтотОбъект.Элементы.Список.ВыделенныеСтроки Цикл
		Если ТипЗнч(ТекИсточник) = Тип("СправочникСсылка.ОбнаружениеИнцидентов") Тогда
			ВыбранныеИсточники.Добавить(ТекИсточник);
		КонецЕсли;
	КонецЦикла;
	 
	ЗапуститьВыбранноеНаСервере(ВыбранныеИсточники);
	
	ПоказатьОповещениеПользователя(НСтр("ru = 'Выбранные источники запущены'"));
	ОповеститьОбИзменении(Тип("СправочникСсылка.ОбнаружениеИнцидентов"));
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗапуститьВыбранноеНаСервере(Источники)
	Для Каждого ТекИсточник Из Источники Цикл
		ИсточникОбъект = ТекИсточник.ПолучитьОбъект();
		
		Если ИсточникОбъект <> Неопределено И ИсточникОбъект.ПроверитьКорректностьЗаполнения(Ложь) Тогда
			Если ИсточникОбъект.ИсполняющееЗадание <> Справочники.ОчередьЗаданий.ПустаяСсылка() Тогда
				ОчередьЗаданий.ИзменитьЗадание(ИсточникОбъект.ИсполняющееЗадание, Новый Структура("Использование", Истина));
				Справочники.ОбнаружениеИнцидентов.ЗапуститьОстановитьАссоциированноеОповещение(ИсточникОбъект.Ссылка, Истина);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ОстановитьВыбранное(Команда)
	ВыбранныеИсточники = ЭтотОбъект.Элементы.Список.ВыделенныеСтроки;
	ОстановитьВыбранноеНаСервере(ВыбранныеИсточники);
	ПоказатьОповещениеПользователя(НСтр("ru = 'Выбранные источники остановлены'"));
	ОповеститьОбИзменении(Тип("СправочникСсылка.ОбнаружениеИнцидентов"));
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОстановитьВыбранноеНаСервере(Источники)
    Для Каждого ТекИсточник Из Источники Цикл
        
        Если ТипЗнч(ТекИсточник) = Тип("СтрокаГруппировкиДинамическогоСписка") Тогда
            Продолжить;
        КонецЕсли;
                
		ИсточникОбъект = ТекИсточник.ПолучитьОбъект();
		
		Если ИсточникОбъект <> Неопределено И ИсточникОбъект.ПроверитьКорректностьЗаполнения(Ложь) Тогда
			Если ИсточникОбъект.ИсполняющееЗадание <> Справочники.ОчередьЗаданий.ПустаяСсылка() Тогда
				ОчередьЗаданий.ИзменитьЗадание(ИсточникОбъект.ИсполняющееЗадание, Новый Структура("Использование", Ложь));
				Справочники.ОбнаружениеИнцидентов.ЗапуститьОстановитьАссоциированноеОповещение(ИсточникОбъект.Ссылка, Ложь);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "ОбновитьСписокПоказателей" Тогда
		ЭтотОбъект.ОбновитьОтображениеДанных();
	КонецЕсли;
КонецПроцедуры

