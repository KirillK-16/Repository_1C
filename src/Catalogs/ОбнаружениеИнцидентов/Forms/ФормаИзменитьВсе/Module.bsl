
#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
    
    Если НЕ ЗначениеЗаполнено(ФорматнаяСтрокаВсем) Тогда
    	ФорматнаяСтрокаВсем = "[ЧДЦ='2']";
        ФорматнаяСтрокаВсемИзменить = Истина;
    КонецЕсли;
    
    Если НЕ ЗначениеЗаполнено(ФункцияВсем) Тогда
    	ФункцияВсем = ПредопределенноеЗначение("Перечисление.ФункцииОповещений.Максимум");
        ФункцияВсемИзменить = Истина;
    КонецЕсли;
    
    Если ИнтервалВсем = 0 Тогда
    	ИнтервалВсем = 180;
        ИнтервалВсемИзменить = Истина;
    КонецЕсли;
    
    Если НЕ ЗначениеЗаполнено(ВремяВсем) Тогда
    	ВремяВсем = ПредопределенноеЗначение("Перечисление.ЕдиницыВремени.Секунда");
        ВремяВсемИзменить = Истина;
    КонецЕсли;
    
    Если НЕ ЗначениеЗаполнено(СравнениеВсем) Тогда
    	СравнениеВсем = ПредопределенноеЗначение("Перечисление.ВидыСравненияПоказателейОповещения.Больше");
        СравнениеВсемИзменить = Истина;
    КонецЕсли;
    
    Если НЕ ЗначениеЗаполнено(ПорогВПроцентахВсем) Тогда
    	ПорогВПроцентахВсем = Ложь;
    КонецЕсли;
    
    Если НЕ ЗначениеЗаполнено(СмещениеБазыВсем) Тогда
    	СмещениеБазыВсем = ПредопределенноеЗначение("Перечисление.ВариантыСмещенияБазы.Прошлый");
    КонецЕсли;
    
    ИзменитьЗаголовокЗначениеПоказателя();
    
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ФорматнаяСтрокаВсемНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
    
    ФорматнаяСтрокаЗначенияБуфер = ЭтотОбъект.ФорматнаяСтрокаВсем;
	ИндексНачала = СтрНайти(ЭтотОбъект.ФорматнаяСтрокаВсем, "[");
	ИндексОкончания = СтрНайти(ЭтотОбъект.ФорматнаяСтрокаВсем, "]");
	Если ИндексНачала > 0 И ИндексОкончания > 0 Тогда
		ФорматнаяСтрокаЗначенияБуфер = Сред(ФорматнаяСтрокаЗначенияБуфер, ИндексНачала + 1, ИндексОкончания - ИндексНачала - 1);
	КонецЕсли;
	
	КонструкторФорматнойСтроки = Новый КонструкторФорматнойСтроки(ФорматнаяСтрокаЗначенияБуфер);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОкончаниеВводаФорматнойСтроки", ЭтотОбъект);
	КонструкторФорматнойСтроки.Показать(ОписаниеОповещения);
	
	СтандартнаяОбработка = Ложь;
    
КонецПроцедуры

&НаКлиенте
Процедура ФорматнаяСтрокаВсемИзменитьПриИзменении(Элемент)
    УстановитьДоступность();
КонецПроцедуры

&НаКлиенте
Процедура ФункцияВсемИзменитьПриИзменении(Элемент)
    УстановитьДоступность();
КонецПроцедуры

&НаКлиенте
Процедура ИнтервалВсемИзменитьПриИзменении(Элемент)
    УстановитьДоступность();
КонецПроцедуры

&НаКлиенте
Процедура ВремяВсемИзменитьПриИзменении(Элемент)
    УстановитьДоступность();
КонецПроцедуры

&НаКлиенте
Процедура СравнениеВсемИзменитьПриИзменении(Элемент)
    УстановитьДоступность();    
КонецПроцедуры

&НаКлиенте
Процедура СравнениеВсемПриИзменении(Элемент)
    ИзменитьЗаголовокЗначениеПоказателя();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
    
    ПараметрыЗакрытия = Новый Структура;
    
    Если ЭтотОбъект.ФорматнаяСтрокаВсемИзменить Тогда
        ПараметрыЗакрытия.Вставить("ФорматнаяСтрокаВсем", ЭтотОбъект.ФорматнаяСтрокаВсем);
    КонецЕсли;
    
    Если ЭтотОбъект.ФункцияВсемИзменить Тогда
        ПараметрыЗакрытия.Вставить("ФункцияВсем", ЭтотОбъект.ФункцияВсем);
    КонецЕсли;
    
    Если ЭтотОбъект.ИнтервалВсемИзменить Тогда
        ПараметрыЗакрытия.Вставить("ИнтервалВсем", ЭтотОбъект.ИнтервалВсем);
    КонецЕсли;
    
    Если ЭтотОбъект.ВремяВсемИзменить Тогда
        ПараметрыЗакрытия.Вставить("ВремяВсем", ЭтотОбъект.ВремяВсем);
    КонецЕсли;
    
    Если ЭтотОбъект.СравнениеВсемИзменить Тогда
        ПараметрыЗакрытия.Вставить("СравнениеВсем", ЭтотОбъект.СравнениеВсем);
        ПараметрыЗакрытия.Вставить("ПорогВсем", ЭтотОбъект.ПорогВсем);
        ПараметрыЗакрытия.Вставить("ПорогДоВсем", ЭтотОбъект.ПорогДоВсем);
        ПараметрыЗакрытия.Вставить("ПорогВПроцентахВсем", ЭтотОбъект.ПорогВПроцентахВсем);
        ПараметрыЗакрытия.Вставить("СмещениеБазыВсем", ЭтотОбъект.СмещениеБазыВсем);
    КонецЕсли;
    
    ЭтотОбъект.Закрыть(ПараметрыЗакрытия);
    
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьИстинаВсем(Команда)
    
    ЭтотОбъект.ФорматнаяСтрокаВсемИзменить = Истина;
    ЭтотОбъект.ФункцияВсемИзменить = Истина;
    ЭтотОбъект.ИнтервалВсемИзменить = Истина;
    ЭтотОбъект.ВремяВсемИзменить = Истина;
    ЭтотОбъект.СравнениеВсемИзменить = Истина;
        
    УстановитьДоступность();
    
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьЛожьВсем(Команда)
    
    ЭтотОбъект.ФорматнаяСтрокаВсемИзменить = Ложь;
    ЭтотОбъект.ФункцияВсемИзменить = Ложь;
    ЭтотОбъект.ИнтервалВсемИзменить = Ложь;
    ЭтотОбъект.ВремяВсемИзменить = Ложь;
    ЭтотОбъект.СравнениеВсемИзменить = Ложь;
        
    УстановитьДоступность();
    
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОкончаниеВводаФорматнойСтроки(Текст, ДополнительныеПараметры) Экспорт
	
	ИндексНачала = СтрНайти(ЭтотОбъект.ФорматнаяСтрокаВсем, "[");
	ИндексОкончания = СтрНайти(ЭтотОбъект.ФорматнаяСтрокаВсем, "]");
	
	Если ИндексНачала > 0 И ИндексОкончания > 0 Тогда
		ФорматнаяСтрокаЗначенияБуфер = Сред(ЭтотОбъект.ФорматнаяСтрокаВсем, ИндексНачала + 1, ИндексОкончания - ИндексНачала - 1);
		ЭтотОбъект.ФорматнаяСтрокаВсем = СтрЗаменить(ЭтотОбъект.ФорматнаяСтрокаВсем, "[" + ФорматнаяСтрокаЗначенияБуфер + "]", "[" + Текст + "]");
	Иначе
		ЭтотОбъект.ФорматнаяСтрокаВсем = "[" + Текст + "]";
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступность()
    
    ЭтотОбъект.Элементы.ФорматнаяСтрокаВсем.Доступность = ЭтотОбъект.ФорматнаяСтрокаВсемИзменить;
    ЭтотОбъект.Элементы.ФункцияВсем.Доступность = ЭтотОбъект.ФункцияВсемИзменить;
    ЭтотОбъект.Элементы.ИнтервалВсем.Доступность = ЭтотОбъект.ИнтервалВсемИзменить;
    ЭтотОбъект.Элементы.ВремяВсем.Доступность = ЭтотОбъект.ВремяВсемИзменить;
    ЭтотОбъект.Элементы.СравнениеВсем.Доступность = ЭтотОбъект.СравнениеВсемИзменить;
    ЭтотОбъект.Элементы.ПорогВсем.Доступность = ЭтотОбъект.СравнениеВсемИзменить;
    ЭтотОбъект.Элементы.ПорогДоВсем.Доступность = ЭтотОбъект.СравнениеВсемИзменить;
    ЭтотОбъект.Элементы.ПорогВПроцентахВсем.Доступность = ЭтотОбъект.СравнениеВсемИзменить;
    ЭтотОбъект.Элементы.СмещениеБазыВсем.Доступность = ЭтотОбъект.СравнениеВсемИзменить;
    
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьЗаголовокЗначениеПоказателя()
    
    ЭтотОбъект.Элементы.ПорогВПроцентахВсем.СписокВыбора.Очистить();
    
    Если ЭтотОбъект.СравнениеВсем = ПредопределенноеЗначение("Перечисление.ВидыСравненияПоказателейОповещения.Больше") Тогда
        ЭтотОбъект.ПорогВсемЗаголовок = "чем";
        ЭтотОбъект.Элементы.ПорогДоВсем.Видимость = Ложь;
        ЭтотОбъект.Элементы.ПорогВПроцентахВсем.СписокВыбора.Добавить(Ложь, "ед.");
        ЭтотОбъект.Элементы.СмещениеБазыВсем.Видимость = Ложь;
    ИначеЕсли ЭтотОбъект.СравнениеВсем = ПредопределенноеЗначение("Перечисление.ВидыСравненияПоказателейОповещения.Меньше") Тогда
        ЭтотОбъект.ПорогВсемЗаголовок = "чем";
        ЭтотОбъект.Элементы.ПорогДоВсем.Видимость = Ложь;
        ЭтотОбъект.Элементы.ПорогВПроцентахВсем.СписокВыбора.Добавить(Ложь, "ед.");
        ЭтотОбъект.Элементы.СмещениеБазыВсем.Видимость = Ложь;
    ИначеЕсли ЭтотОбъект.СравнениеВсем = ПредопределенноеЗначение("Перечисление.ВидыСравненияПоказателейОповещения.Между") Тогда
        ЭтотОбъект.ПорогВсемЗаголовок = "";
        ЭтотОбъект.Элементы.ПорогВПроцентахВсем.СписокВыбора.Добавить(Ложь, "ед.");
        ЭтотОбъект.Элементы.ПорогДоВсем.Видимость = Истина;
        ЭтотОбъект.Элементы.СмещениеБазыВсем.Видимость = Ложь;
    ИначеЕсли ЭтотОбъект.СравнениеВсем = ПредопределенноеЗначение("Перечисление.ВидыСравненияПоказателейОповещения.Изменился") Тогда
        ЭтотОбъект.ПорогВсемЗаголовок = "на";
        ЭтотОбъект.Элементы.ПорогДоВсем.Видимость = Ложь;
        ЭтотОбъект.Элементы.ПорогВПроцентахВсем.СписокВыбора.Добавить(Ложь, "ед.");
		ЭтотОбъект.Элементы.ПорогВПроцентахВсем.СписокВыбора.Добавить(Истина, "%");
        ЭтотОбъект.Элементы.СмещениеБазыВсем.Видимость = Истина;
    КонецЕсли;
        
КонецПроцедуры

#КонецОбласти
