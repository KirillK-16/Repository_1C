#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Функция ПолучитьМассивСтруктурыПоказателей(ТипПоказателя, ИдентификаторНастройки, МассивСтруктурПоказателей, ТолькоВыбранные) Экспорт
	
	МассивСтруктурыПоказателей = Новый Массив;
	
	ТаблицаПоказателей = Новый ТаблицаЗначений;
	ТаблицаПоказателей.Колонки.Добавить("ПоказательМониторинга", Новый ОписаниеТипов("СправочникСсылка.ПоказателиМониторинга"));
	ТаблицаПоказателей.Колонки.Добавить("ГруппаОбъектовКонтроля", Новый ОписаниеТипов("Булево"));
	
	Для Каждого СтруктураПоказатель Из МассивСтруктурПоказателей Цикл
		
		Если ЗначениеЗаполнено(СтруктураПоказатель.Показатель) И СтруктураПоказатель.ТипПоказателя = "ПоказателиAPDEX" Тогда
			
			НоваяСтрока							= ТаблицаПоказателей.Добавить();
			НоваяСтрока.ПоказательМониторинга	= СтруктураПоказатель.Показатель;
			НоваяСтрока.ГруппаОбъектовКонтроля	= СтруктураПоказатель.ГруппаОбъектовКонтроля;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("ИдентификаторНастройки", ИдентификаторНастройки);
	Запрос.УстановитьПараметр("ТаблицаПоказателей", ТаблицаПоказателей);
	
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ТаблицаПоказателей.ПоказательМониторинга КАК ПоказательМониторинга,
	|	ТаблицаПоказателей.ГруппаОбъектовКонтроля КАК ГруппаОбъектовКонтроля
	|ПОМЕСТИТЬ
	|	ТаблицаПоказателей
	|ИЗ
	|	&ТаблицаПоказателей КАК ТаблицаПоказателей
	|
	|;
	|
	|ВЫБРАТЬ
	|	ТаблицаПоказателей.ПоказательМониторинга.Наименование Как Код,
	|	ТаблицаПоказателей.ПоказательМониторинга КАК ПоказательМониторинга
	|ИЗ
	|	ТаблицаПоказателей КАК ТаблицаПоказателей
	|ГДЕ
	|	ТаблицаПоказателей.ГруппаОбъектовКонтроля
	|
	|УПОРЯДОЧИТЬ ПО
	|	Код
	|";
	
	СтруктураДанныхГруппы = Новый Структура();
	СтруктураДанныхГруппы.Вставить("ПоказательМониторинга", "");
	СтруктураДанныхГруппы.Вставить("ОбъектКонтроля", "ГруппаОбъектовКонтроляРодитель");
	СтруктураДанныхГруппы.Вставить("Код", "Группы APDEX");
	СтруктураДанныхГруппы.Вставить("МассивСтрок", Новый Массив());
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		СтруктураДанных = Новый Структура();
		СтруктураДанных.Вставить("ПоказательМониторинга", Строка(Выборка.ПоказательМониторинга.УникальныйИдентификатор()));
		СтруктураДанных.Вставить("ОбъектКонтроля", "ГруппаОбъектовКонтроля");
		СтруктураДанных.Вставить("Код", Выборка.Код);
		СтруктураДанных.Вставить("МассивСтрок", Новый Массив());
		
		СтруктураДанныхГруппы.МассивСтрок.Добавить(СтруктураДанных);
		
	КонецЦикла;
	
	МассивСтруктурыПоказателей.Добавить(СтруктураДанныхГруппы);
	
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	БазыОперации.ИнформационнаяБаза.Наименование КАК БазаНаименование,
	|	БазыОперации.ИнформационнаяБаза КАК ОбъектКонтроля,
	|	БазыОперации.УникальныйИдентификатор КАК КлючеваяОперация,
	|	БазыОперации.Имя КАК КлючеваяОперацияНаименование,
	|	ЕСТЬNULL(ТаблицаПоказателей.ПоказательМониторинга, Неопределено) КАК ПоказательМониторинга
	|ИЗ
	|	РегистрСведений.ОценкаПроизводительностиКлючевыеОперации КАК БазыОперации
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ПоказателиAPDEX.КлючевыеОперации КАК ПоказателиAPDEX
	|
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаПоказателей КАК ТаблицаПоказателейОтбор
	|		ПО ТаблицаПоказателейОтбор.ПоказательМониторинга.Показатель = ПоказателиAPDEX.Ссылка
	|		И НЕ ТаблицаПоказателейОтбор.ГруппаОбъектовКонтроля
	|
	|	ПО ПоказателиAPDEX.ИнформационнаяБаза = БазыОперации.ИнформационнаяБаза
	|	И ПоказателиAPDEX.ИдентификаторКлючевойОперации = БазыОперации.УникальныйИдентификатор
	|	И ПоказателиAPDEX.Ссылка.ТипСпискаПользователей = ЗНАЧЕНИЕ(Перечисление.ТипыСпискаПользователей.Все)
	|	И ПоказателиAPDEX.Ссылка.ТипПоказателя = ЗНАЧЕНИЕ(Перечисление.ТипыПоказателейПроизводительности.APDEX)
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаПоказателей КАК ТаблицаПоказателей
	|	ПО ТаблицаПоказателей.ПоказательМониторинга.Показатель = ПоказателиAPDEX.Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	БазаНаименование,
	|	КлючеваяОперацияНаименование
	|
	|ИТОГИ 
	|	МАКСИМУМ(ПоказательМониторинга) КАК ПоказательМониторинга
	|ПО
	|	ОбъектКонтроля
	|";
	
	ВыборкаБаза = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "ОбъектКонтроля");
	Пока ВыборкаБаза.Следующий() Цикл
		
		Если ТолькоВыбранные И НЕ ЗначениеЗаполнено(ВыборкаБаза.ПоказательМониторинга) Тогда
			Продолжить;
		КонецЕсли;
		
		СтруктураДанныхБаза = Новый Структура();
		СтруктураДанныхБаза.Вставить("ПоказательМониторинга", "");
		СтруктураДанныхБаза.Вставить("Код", ВыборкаБаза.БазаНаименование);
		СтруктураДанныхБаза.Вставить("ОбъектКонтроля", Строка(ВыборкаБаза.ОбъектКонтроля.УникальныйИдентификатор()));
		СтруктураДанныхБаза.Вставить("МассивСтрок", Новый Массив());
		
		ВыборкаКлючеваяОперация = ВыборкаБаза.Выбрать();
		Пока ВыборкаКлючеваяОперация.Следующий() Цикл
		
			Если ТолькоВыбранные И НЕ ЗначениеЗаполнено(ВыборкаКлючеваяОперация.ПоказательМониторинга) Тогда
				Продолжить;
			КонецЕсли;
			
			СтруктураДанныхКлючеваяОперация = Новый Структура();
			
			Если ЗначениеЗаполнено(ВыборкаКлючеваяОперация.ПоказательМониторинга) Тогда
				СтруктураДанныхКлючеваяОперация.Вставить("ПоказательМониторинга", Строка(ВыборкаКлючеваяОперация.ПоказательМониторинга.УникальныйИдентификатор()));
			Иначе
				СтруктураДанныхКлючеваяОперация.Вставить("ПоказательМониторинга", "");
			КонецЕсли;
			
			СтруктураДанныхКлючеваяОперация.Вставить("Код", ВыборкаКлючеваяОперация.КлючеваяОперацияНаименование);
			СтруктураДанныхКлючеваяОперация.Вставить("ОбъектКонтроля", Строка(ВыборкаКлючеваяОперация.ОбъектКонтроля.УникальныйИдентификатор()) + "/" + Строка(ВыборкаКлючеваяОперация.КлючеваяОперация));
			СтруктураДанныхКлючеваяОперация.Вставить("МассивСтрок", Новый Массив());
			
			СтруктураДанныхБаза.МассивСтрок.Добавить(СтруктураДанныхКлючеваяОперация);
			
		КонецЦикла;
		
		МассивСтруктурыПоказателей.Добавить(СтруктураДанныхБаза);
		
	КонецЦикла;
	
	Возврат МассивСтруктурыПоказателей;
	
КонецФункции

Функция ПоискПоказателей(Знач СтрокаПоиска) Экспорт
	
	СтрокаПоиска = СтрЗаменить(СтрокаПоиска, """", "");
	
	МассивРезультатов = Новый Массив;
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("СтрокаПоиска", "%" + СтрокаПоиска + "%");
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	""APDEX/"" + БазыОперации.ИнформационнаяБаза.Наименование + ""/"" + БазыОперации.Имя КАК Представление,
	|	БазыОперации.ИнформационнаяБаза КАК ОбъектКонтроля,
	|	БазыОперации.УникальныйИдентификатор КАК КлючеваяОперация
	|ИЗ
	|	РегистрСведений.ОценкаПроизводительностиКлючевыеОперации КАК БазыОперации
	|
	|ГДЕ
	|	(БазыОперации.ИнформационнаяБаза.Наименование + ""/"" + БазыОперации.Имя) ПОДОБНО &СтрокаПоиска
	|
	|УПОРЯДОЧИТЬ ПО
	|	Представление
	|";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		СтруктураРезультата = Новый Структура();
		СтруктураРезультата.Вставить("Представление", Выборка.Представление);
		СтруктураРезультата.Вставить("ОбъектКонтроля", Строка(Выборка.ОбъектКонтроля.УникальныйИдентификатор()) + "/" + Строка(Выборка.КлючеваяОперация));
		СтруктураРезультата.Вставить("ТипПоказателя", "ПоказателиAPDEX");
		
		МассивРезультатов.Добавить(СтруктураРезультата);
		
	КонецЦикла;
	
	Возврат МассивРезультатов;
	
КонецФункции

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	Запрос = Новый Запрос;
	
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	Описание
	|ИЗ
	|	Справочник.ПоказателиAPDEX
	|ГДЕ
	|	Ссылка = &Ссылка
	|";
	Запрос.УстановитьПараметр("Ссылка", Данные.Ссылка);
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		
		Представление = Выборка.Описание;
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
КонецПроцедуры

#КонецЕсли