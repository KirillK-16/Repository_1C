#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
    
#Область ПрограммныйИнтерфейс

// Найти по наименованию, в случае отсутсвия создать новый элемент.
//
// Параметры:
//  Наименование - Строка - наименование версии платформы. 
// 
// Возвращаемое значение:
//  СпрвочникСсылка.ВерсииПлатформы - ссылка на найденный или созданный элемент.
//
Функция НайтиСоздатьПоНаименованию(Наименование) Экспорт
    
    ВерсияПлатформыСсылка = Справочники.ВерсииПлатформы.НайтиПоНаименованию(Наименование);
    
    Если НЕ ЗначениеЗаполнено(ВерсияПлатформыСсылка) Тогда
        
        НачатьТранзакцию();
        
        Попытка
            Блокировка = Новый БлокировкаДанных;
            ЭлементБлокировки = Блокировка.Добавить("Справочник.ВерсииПлатформы");
            ЭлементБлокировки.УстановитьЗначение("Наименование", Наименование);
            ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
            Блокировка.Заблокировать();
            
            ВерсияПлатформыСсылка = Справочники.ВерсииПлатформы.НайтиПоНаименованию(Наименование);
            
            Если НЕ ЗначениеЗаполнено(ВерсияПлатформыСсылка) Тогда
                
                ВерсияПлатформы = Справочники.ВерсииПлатформы.СоздатьЭлемент();
                ВерсияПлатформы.Наименование = Наименование;
                ВерсияПлатформы.Записать();
                
                ВерсияПлатформыСсылка = ВерсияПлатформы.Ссылка;
                
            КонецЕсли;
            
            ЗафиксироватьТранзакцию();
            
        Исключение
            
            ОтменитьТранзакцию();
            ВызватьИсключение ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
            
        КонецПопытки;
    КонецЕсли;
        
    Возврат ВерсияПлатформыСсылка;
    
КонецФункции

#КонецОбласти
    
#КонецЕсли