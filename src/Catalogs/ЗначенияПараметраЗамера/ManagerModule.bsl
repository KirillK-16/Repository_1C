#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Функция СсылкаПоНаименованию(Наименование, Владелец) Экспорт
	
	Ссылка = Неопределено;
	
	Ссылка = НайтиСсылкуПоНаименованию(Наименование, Владелец);
	
	Если Ссылка = Неопределено Тогда
		
		НачатьТранзакцию();
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("Справочник.ЗначенияПараметраЗамера");
		ЭлементБлокировки.УстановитьЗначение("Наименование", Наименование);
		ЭлементБлокировки.УстановитьЗначение("Владелец", Владелец);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		
        Попытка
            Блокировка.Заблокировать();
			Ссылка = НайтиСсылкуПоНаименованию(Наименование, Владелец);
			Если Ссылка = Неопределено Тогда
				Ссылка = СоздатьПоНаименованию(Наименование, Владелец);
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
        Исключение
            ОтменитьТранзакцию();
			ВызватьИсключение "Ожидаемая управляемая блокировка";
		КонецПопытки;
			
	КонецЕсли;
	
	Возврат Ссылка;
	
КонецФункции

Функция НайтиСсылкуПоНаименованию(Наименование, Владелец)
	
	Ссылка = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	Ссылка
	|ИЗ
	|	Справочник.ЗначенияПараметраЗамера
	|ГДЕ
	|	Наименование = &Наименование
	|	И Владелец = &Владелец
	|";
	Запрос.УстановитьПараметр("Наименование", Наименование);
	Запрос.УстановитьПараметр("Владелец", Владелец);
	
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		
		Ссылка = Выборка.Ссылка;
	КонецЕсли;
	
	Возврат Ссылка;
	
КонецФункции

Функция СоздатьПоНаименованию(Наименование, Владелец)
	
	Элемент = Справочники.ЗначенияПараметраЗамера.СоздатьЭлемент();
	Элемент.Владелец = Владелец;
	Элемент.Наименование = Наименование;
	Элемент.Записать();
	
	Возврат Элемент.Ссылка;
	
КонецФункции

#КонецЕсли

