#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Функция СоздатьЭлементПоНаименованию(Наименование, Владелец) Экспорт
	Результат = РезультатЗапросаПоНаименованию(Наименование, Владелец);
	Если Результат.Пустой() Тогда
		Попытка
			БлокировкаДанных = Новый БлокировкаДанных;
			ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.ОбъектыБлокировок");
			ЭлементБлокировки.УстановитьЗначение("Объект", "Справочник.ВерсииКонфигурации." + Наименование);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			БлокировкаДанных.Заблокировать();
		Исключение
			ВызватьИсключение "Центр мониторинга. Ожидаемая управляемая блокировка при создании элемента справочника 'ВерсииКонфигурации'";
		КонецПопытки;
		
		Результат = РезультатЗапросаПоНаименованию(Наименование, Владелец);
		Если Результат.Пустой() Тогда
			НовыйЭлемент = Справочники.ВерсииКонфигурации.СоздатьЭлемент();
			НовыйЭлемент.ОбменДанными.Загрузка = Истина;
			НовыйЭлемент.Наименование = Наименование;
			НовыйЭлемент.Владелец = Владелец;
			НовыйЭлемент.Записать();
			
			РезультатВыполнения = НовыйЭлемент.Ссылка;
		Иначе
			Выборка = Результат.Выбрать();
			Выборка.Следующий();
			РезультатВыполнения = Выборка.Ссылка;
		КонецЕсли;
	Иначе
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		РезультатВыполнения = Выборка.Ссылка;
	КонецЕсли;
		
	Возврат РезультатВыполнения;
КонецФункции

Функция РезультатЗапросаПоНаименованию(Наименование, Владелец)
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ВерсииКонфигурации.Ссылка КАК Ссылка
	               |ИЗ
	               |	Справочник.ВерсииКонфигурации КАК ВерсииКонфигурации
	               |ГДЕ
	               |	ВерсииКонфигурации.Наименование = &Наименование
	               |	И ВерсииКонфигурации.Владелец = &Владелец";
	Запрос.УстановитьПараметр("Наименование", Наименование);
	Запрос.УстановитьПараметр("Владелец", Владелец);
	Результат = Запрос.Выполнить();
	
	Возврат Результат;
КонецФункции

#КонецЕсли