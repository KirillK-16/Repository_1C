&НаКлиенте
Перем ОповеститьОЗаписи, ПередЗаписьюВопрос;

&НаСервереБезКонтекста
Функция ЗаполнитьПоШаблонуНаСервере(Ссылка)
	Возврат Справочники.ТехнологическиеЖурналыШаблоны.ПолучитьШаблонПредопределенного(Ссылка);
КонецФункции

&НаКлиенте
Процедура ЗаполнитьПоШаблону(Команда)
	Объект.Шаблон = ЗаполнитьПоШаблонуНаСервере(Объект.Ссылка);
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Объект.Ссылка = Справочники.ТехнологическиеЖурналыШаблоны.ПустаяСсылка() Тогда
		ЭтотОбъект.Элементы.СтраницаТЖ.Видимость = Ложь;
	Иначе
		Запрос = Новый Запрос;
		Запрос.Текст = "
		|ВЫБРАТЬ
		|	ТехнологическиеЖурналыШаблоныТЖ.Ссылка
		|ИЗ
		|	Справочник.ТехнологическиеЖурналы.ШаблоныТЖ КАК ТехнологическиеЖурналыШаблоныТЖ
		|ГДЕ
		|	ТехнологическиеЖурналыШаблоныТЖ.ШаблонТЖВыбранный = &ШаблонТЖВыбранный
		|";
		
		Запрос.УстановитьПараметр("ШаблонТЖВыбранный", Объект.Ссылка);
		
		Результат = Запрос.Выполнить();
		
		Если Результат.Пустой() Тогда
			ЭтотОбъект.Элементы.СтраницаТЖ.Видимость = Ложь;
		Иначе
			Выборка = Результат.Выбрать();
			Пока Выборка.Следующий() Цикл
				НовСтрока = ЭтотОбъект.ТехнологическиеЖурналы.Добавить();
				НовСтрока.Изменить = Ложь;
				НовСтрока.ТехнологическийЖурнал = Выборка.Ссылка;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	ЭтотОбъект.Элементы.ФормаЗаполнитьПоШаблону.Видимость = Объект.Ссылка.Предопределенный;
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Для Каждого ТекДанные Из ЭтотОбъект.ТехнологическиеЖурналы Цикл
		Если ТекДанные.Изменить Тогда
			ТЖОбъект = ТекДанные.ТехнологическийЖурнал.ПолучитьОбъект();
			ТЖОбъект.ИзменитьШаблон(Объект.Ссылка);
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Если ОповеститьОЗаписи Тогда
		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("Ссылка", Объект.Ссылка);
		ПараметрыОповещения.Вставить("Шаблон", Объект.Шаблон);
		Оповестить("ТехнологическийЖурналШаблонЗаписан", ПараметрыОповещения, ЭтотОбъект); 
	КонецЕсли;
	
	Если ПараметрыЗаписи.Свойство("Закрыть") И ПараметрыЗаписи.Закрыть Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	ОповеститьОЗаписи = Ложь;
	
	Если ЭтотОбъект.ТехнологическиеЖурналы.Количество() > 0 Тогда
		ВсегоОтмечено = 0;
		Для Каждого ТекТЖ Из ЭтотОбъект.ТехнологическиеЖурналы Цикл
			Если ТекТЖ.Изменить Тогда
				ВсегоОтмечено = ВсегоОтмечено + 1;
			КонецЕсли;
		КонецЦикла;
		
		Если ВсегоОтмечено = 0 Тогда
			Если ПередЗаписьюВопрос <> Истина Тогда
				ОписаниеОповещения = Новый ОписаниеОповещения("ПередЗаписьюЗавершение", ЭтотОбъект, ПараметрыЗаписи);
				ПоказатьВопрос(ОписаниеОповещения, "Применить шаблон к технологическим журналам?", РежимДиалогаВопрос.ДаНетОтмена);
				Отказ = Истина;
			Иначе
				ПередЗаписьюВопрос = Неопределено;
			КонецЕсли;
		Иначе
			ОповеститьОЗаписи = Истина;
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписьюЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Нет Тогда
		ПередЗаписьюВопрос = Истина;
		Записать(ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	Записать(Новый Структура("Закрыть", Истина));
КонецПроцедуры
