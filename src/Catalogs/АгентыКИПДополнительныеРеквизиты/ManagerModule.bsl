#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ПолучитьСсылкуПоВладельцу(Владелец, ВернутьОбъект = Ложь) Экспорт
    
    Запрос = Новый Запрос;
    
    Запрос.Текст = "
    |ВЫБРАТЬ ПЕРВЫЕ 1
    |   ДополнительныеРеквизиты.Ссылка КАК Ссылка
    |ИЗ
    |   Справочник.АгентыКИПДополнительныеРеквизиты КАК ДополнительныеРеквизиты
    |ГДЕ
    |   ДополнительныеРеквизиты.Владелец = &Владелец
    |УПОРЯДОЧИТЬ ПО
    |   ДополнительныеРеквизиты.Ссылка
    |";
    
    Запрос.УстановитьПараметр("Владелец", Владелец);
    
    Результат = Запрос.Выполнить();
    Если Результат.Пустой() Тогда
        
        СпрОбъект = Справочники.АгентыКИПДополнительныеРеквизиты.СоздатьЭлемент();
        СпрОбъект.УстановитьСсылкуНового(Справочники.АгентыКИПДополнительныеРеквизиты.ПолучитьСсылку());
        СпрОбъект.Заполнить(Неопределено);
        
        Если ВернутьОбъект Тогда
            Ссылка = СпрОбъект;
        Иначе
            Ссылка = СпрОбъект.ПолучитьСсылкуНового();
        КонецЕсли;
                
    Иначе
        
        Выборка = Результат.Выбрать();
        Выборка.Следующий();
        
        Если ВернутьОбъект Тогда
            Ссылка = Выборка.Ссылка.ПолучитьОбъект();
        Иначе
            Ссылка = Выборка.Ссылка;
        КонецЕсли;
                
    КонецЕсли;
    
    Возврат Ссылка;    
        
КонецФункции

Процедура ЗаписатьРеквизит(Владелец, Реквизит, Значение) Экспорт
    
    Запрос = Новый Запрос;
    
    Запрос.Текст = "
    |ВЫБРАТЬ ПЕРВЫЕ 1
    |   Ссылка
    |ИЗ
    |   Справочник.АгентыКИПДополнительныеРеквизиты
    |ГДЕ
    |   Владелец = &Владелец
    |";
    
    Запрос.УстановитьПараметр("Владелец", Владелец);
    
    Результат = Запрос.Выполнить();
    Если НЕ Результат.Пустой() Тогда
        Выборка = Результат.Выбрать();
        Выборка.Следующий();
        СпрОбъект = Выборка.Ссылка.ПолучитьОбъект();
        СпрОбъект[Реквизит] = Значение;
        СпрОбъект.Записать();
    Иначе
        ВызватьИсключение "Не найдены дополнительные реквизиты для " + Владелец +"!";
    КонецЕсли;
        
КонецПроцедуры

// Изменяет "ИнформационныеБазыХеш" у агентов ЦКК, которые контролируют информационную базу.
//
// Параметры:
//  ОбъектКонтроляСсылка - СправочникСсылка.ОбъектыКонтроля - ссылка на информационную базу.
//
Процедура ИзменитьИнформационныеБазыХеш(ОбъектКонтроляСсылка) Экспорт
    
    Запрос = Новый Запрос;
    
    Запрос.Текст = "
    |ВЫБРАТЬ РАЗЛИЧНЫЕ
    |   АгентыКИПДополнительныеРеквизиты.Ссылка
    |ИЗ
    |   Справочник.АгентыКИПДополнительныеРеквизиты.ИнформационныеБазы КАК АгентыКИПДополнительныеРеквизиты
    |ГДЕ
    |   АгентыКИПДополнительныеРеквизиты.ИнформационнаяБаза = &ИнформационнаяБаза
    |";
    
    Запрос.УстановитьПараметр("ИнформационнаяБаза", ОбъектКонтроляСсылка);
    
    Результат = Запрос.Выполнить();
    
    Выборка = Результат.Выбрать();
    Пока Выборка.Следующий() Цикл
        
        АгентКИПДополнительныеРеквизитыОбъект = Выборка.Ссылка.ПолучитьОбъект();
        АгентКИПДополнительныеРеквизитыОбъект.ДополнительныеСвойства.Вставить("ИнформационныеБазыХешБыл", Выборка.Ссылка.ИнформационныеБазыХеш);
        АгентКИПДополнительныеРеквизитыОбъект.Записать();
        
    КонецЦикла;
        
КонецПроцедуры

#КонецОбласти

#КонецЕсли
