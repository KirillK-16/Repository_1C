
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ВыборкаРолейПользователя = УправлениеЗадачами.ВыборкаРолейПользователей(
		Новый Структура("Роль", Объект.Ссылка)
	);
	Пока ВыборкаРолейПользователя.Следующий() Цикл
		НоваяСтрока = Пользователи.Добавить(); 
		НоваяСтрока.ПользовательСсылка = ВыборкаРолейПользователя.Пользователь;
	КонецЦикла;	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Если Пользователи.Количество() = 0 Тогда
		ОбщийКлиентСервер.СгенерироватьСообщениеПользователю(
			"Роль должна содержать хотя бы одного пользователя!", 
			"Пользователи", 
			ЭтотОбъект
		);
		Отказ = Истина;
	Иначе
		НомерСтроки = 0;
		Для Каждого СтрокаПользователя Из Пользователи Цикл
			
			Если СтрокаПользователя.ПользовательСсылка.Пустая() Тогда
				ОбщийКлиентСервер.СгенерироватьСообщениеПользователю(
					"Пользователь не выбран!", 
					"Пользователи[" + НомерСтроки + "].ПользовательСсылка", 
					ЭтотОбъект
				);
				Отказ = Истина;
			КонецЕсли;
			НомерСтроки = НомерСтроки + 1;
		КонецЦикла;
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	НаборСвязей = РегистрыСведений.СвязьПользовательРоль.СоздатьНаборЗаписей();
	НаборСвязей.Отбор.Роль.Установить(ТекущийОбъект.Ссылка);	
	НаборСвязей.Записать();
	
	Для Каждого ПользовательСтрока Из Пользователи Цикл
		УправлениеЗадачами.СвязатьПользователяИРоль(
			ПользовательСтрока.ПользовательСсылка,
			ТекущийОбъект.Ссылка
		);
	КонецЦикла;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦЫ ФОРМЫ "Пользователи"

&НаКлиенте
Процедура ПользователиПользовательСсылкаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ПользовательУжеВыбран(ВыбранноеЗначение) Тогда
		СтандартнаяОбработка = Ложь;
		ПоказатьПредупреждение(,"Пользователь " + Строка(ВыбранноеЗначение) + " уже выбран");
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПользователиПриИзменении(Элемент)
	ЭтотОбъект.Модифицированность = Истина;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Функция ПользовательУжеВыбран(Пользователь)
	Возврат Пользователи.НайтиСтроки(
		Новый Структура("ПользовательСсылка", Пользователь)
	).Количество() > 0;	
КонецФункции	






