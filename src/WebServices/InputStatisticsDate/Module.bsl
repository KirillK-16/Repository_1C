Функция InputData(inDate, inParam)
    
    Попытка
        
        ДанныеДляЗаписи = СтатистикаГруппыСчетчиков.РазобратьДанные(inParam, Истина);
        
        Данные = Новый Массив;
        Для Каждого ТекДанные Из ДанныеДляЗаписи Цикл
            
            ЭлементДанных = Новый Соответствие;
                       
            ИмяСчетчикаМассив = СтрРазделить(ТекДанные.Ключ, "/", Истина);
            ЭлементДанных.Вставить("name", ИмяСчетчикаМассив);
            ЭлементДанных.Вставить("counter", ТекДанные.Значение["ЗначениеКоличество"]);
            ЭлементДанных.Вставить("min", ТекДанные.Значение["ЗначениеСобытия"]);
            ЭлементДанных.Вставить("max", ТекДанные.Значение["ЗначениеСобытия"]);
            ЭлементДанных.Вставить("avg", ТекДанные.Значение["ЗначениеСобытия"]);
            
            Данные.Добавить(ЭлементДанных);
            
        КонецЦикла;
        
        ЗаписьJSON = Новый ЗаписьJSON();
	    ЗаписьJSON.УстановитьСтроку(Новый ПараметрыЗаписиJSON(ПереносСтрокJSON.Нет));
	    ЗаписатьJSON(ЗаписьJSON, Данные);
	    ДанныеJSON = ЗаписьJSON.Закрыть();
                
        ТекДата = ТекущаяДата();
		
		НаборЗаписей = РегистрыСведений.СтатистикаБуферДата.СоздатьНаборЗаписей();
		НовЗапись = НаборЗаписей.Добавить();
		НовЗапись.ДатаЗаписи = inDate;
		НовЗапись.ГУИД = Новый УникальныйИдентификатор();
		НовЗапись.Данные = ДанныеJSON;
		
		НаборЗаписей.ОбменДанными.Загрузка = Истина;
		НаборЗаписей.Записать(Ложь);
		
		Возврат "OK";
        
	Исключение
        
        Комментарий = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЗаписьЖурналаРегистрации("InputData(inParam)", УровеньЖурналаРегистрации.Предупреждение, Метаданные.WebСервисы.InputStatistics,,Комментарий);
		
		Возврат "ERROR: " + Комментарий;
        
    КонецПопытки
    
КонецФункции
