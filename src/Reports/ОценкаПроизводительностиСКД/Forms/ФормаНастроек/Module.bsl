
#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
    ЭтотОбъект.Элементы.КомпоновщикНастроекПользовательскиеНастройкиОсновные.РастягиватьПоВертикали = Истина;
    ЭтотОбъект.Элементы.КомпоновщикНастроекПользовательскиеНастройкиОсновные.РастягиватьПоГоризонтали = Истина;
КонецПроцедуры

&НаСервере
Процедура ПриОбновленииСоставаПользовательскихНастроекНаСервере(СтандартнаяОбработка)
    
    СтандартнаяОбработка = Ложь;
    СоздатьЭлементыФормыПользовательскихНастроек();
    
    ЭлементПериод = ФункцииСКД.НайтиЭлементПользовательскойНастройки(ЭтотОбъект.Элементы.КомпоновщикНастроекПользовательскиеНастройки, Отчет.КомпоновщикНастроек.ПользовательскиеНастройки, Новый ПараметрКомпоновкиДанных("Период"));
    Если ЭлементПериод <> Неопределено Тогда
        ЭлементПериод.УстановитьДействие("ПриИзменении", "ПрограммноПериодПриИзменении");
    КонецЕсли;
    
    ЭлементПериодСравнения = ФункцииСКД.НайтиЭлементПользовательскойНастройки(ЭтотОбъект.Элементы.КомпоновщикНастроекПользовательскиеНастройки, Отчет.КомпоновщикНастроек.ПользовательскиеНастройки, Новый ПараметрКомпоновкиДанных("ПериодСравнения"));
    Если ЭлементПериодСравнения <> Неопределено Тогда
        ЭлементПериодСравнения.УстановитьДействие("НачалоВыбора", "ПрограммноПериодСравненияНачалоВыбора");
	    ЭлементПериодСравнения.УстановитьДействие("ОбработкаВыбора", "ПрограммноПериодСравненияОбработкаВыбора");
    КонецЕсли;
    
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПрограммноПериодПриИзменении(Элемент)
    
    ПараметрНастройки = ЭтотОбъект.Отчет.КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти("Период");
	ПериодПараметр = ЭтотОбъект.Отчет.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Найти(ПараметрНастройки.ИдентификаторПользовательскойНастройки);
    
    ПараметрНастройки = ЭтотОбъект.Отчет.КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти("ПериодСравнения");
	ПериодСравненияПараметр = ЭтотОбъект.Отчет.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Найти(ПараметрНастройки.ИдентификаторПользовательскойНастройки);
    
    ДлительностьПериода = (ПериодПараметр.Значение.ДатаОкончания - ПериодПараметр.Значение.ДатаНачала);
    ПериодСравненияПараметр.Значение.ДатаОкончания = ПериодСравненияПараметр.Значение.ДатаНачала + ДлительностьПериода;
    Если ПериодСравненияПараметр.Значение.ДатаОкончания >= ПериодПараметр.Значение.ДатаНачала Тогда
        ПериодСравненияПараметр.Значение.ДатаОкончания = ПериодПараметр.Значение.ДатаНачала - 1;
        ПериодСравненияПараметр.Значение.ДатаНачала = ПериодСравненияПараметр.Значение.ДатаОкончания - ДлительностьПериода;
    КонецЕсли;
    
КонецПроцедуры

&НаКлиенте
Процедура ПрограммноПериодСравненияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
    
    ПараметрНастройки = ЭтотОбъект.Отчет.КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти("Период");
	ПериодПараметр = ЭтотОбъект.Отчет.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Найти(ПараметрНастройки.ИдентификаторПользовательскойНастройки);
    ДлительностьПериода = (ПериодПараметр.Значение.ДатаОкончания - ПериодПараметр.Значение.ДатаНачала) + 1;
    
    ПараметрНастройки = ЭтотОбъект.Отчет.КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти("ПериодСравнения");
	ПериодСравненияПараметр = ЭтотОбъект.Отчет.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Найти(ПараметрНастройки.ИдентификаторПользовательскойНастройки);
    
    ДопПараметры = Новый Структура;
    ДопПараметры.Вставить("ПериодСравненияПараметр", ПериодСравненияПараметр);
    ДопПараметры.Вставить("ДлительностьПериода", ДлительностьПериода);
    ДопПараметры.Вставить("ТекущийЭлемент", Элемент);
    
    ФормаВыбораЗначения = ПолучитьФорму("Отчет.ОценкаПроизводительностиСКД.Форма.ФормаПериодДляСравнения");
    ФормаВыбораЗначения.ВладелецФормы = Элемент;
    ФормаВыбораЗначения.ЗакрыватьПриЗакрытииВладельца = Истина;
    ФормаВыбораЗначения.ДатаНачала = ПериодСравненияПараметр.Значение.ДатаНачала;
    ФормаВыбораЗначения.ДатаОкончания = ПериодСравненияПараметр.Значение.ДатаОкончания;
    ФормаВыбораЗначения.ДлительностьПериода = ДлительностьПериода;
    ФормаВыбораЗначения.Открыть();
    
    СтандартнаяОбработка = Ложь;
    
КонецПроцедуры

&НаКлиенте
Процедура ПрограммноПериодСравненияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
    
    ПараметрНастройки = ЭтотОбъект.Отчет.КомпоновщикНастроек.Настройки.ПараметрыДанных.Элементы.Найти("ПериодСравнения");
	ПериодСравненияПараметр = ЭтотОбъект.Отчет.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Найти(ПараметрНастройки.ИдентификаторПользовательскойНастройки);
    
    ПериодСравненияПараметр.Значение.ДатаНачала = ВыбранноеЗначение.ДатаНачала;
    ПериодСравненияПараметр.Значение.ДатаОкончания = ВыбранноеЗначение.ДатаОкончания;
    
    СтандартнаяОбработка = Ложь;
КонецПроцедуры

#КонецОбласти
