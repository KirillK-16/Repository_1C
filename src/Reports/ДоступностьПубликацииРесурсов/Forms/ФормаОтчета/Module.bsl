
&НаСервере
Процедура ПриЗагрузкеПользовательскихНастроекНаСервере(Настройки)
	ИнициализироватьНастройки(Отчет.КомпоновщикНастроек.Настройки, Настройки, ЭтотОбъект.НачалоРабочегоДня, ЭтотОбъект.ОкончаниеРабочегоДня);
КонецПроцедуры

&НаСервере
Процедура ПриОбновленииСоставаПользовательскихНастроекНаСервере(СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	СоздатьЭлементыФормыПользовательскихНастроек(ЭтотОбъект.Элементы.КомпоновщикНастроекПользовательскиеНастройки,,2);
	
	Для Каждого ЭлементКолонка Из ЭтотОбъект.Элементы.КомпоновщикНастроекПользовательскиеНастройки.ПодчиненныеЭлементы Цикл
		Для Каждого ЭлементНастройка Из ЭлементКолонка.ПодчиненныеЭлементы Цикл
			Если ЭлементНастройка.Заголовок = "Период анализа" Тогда
				ЭлементНастройка.УстановитьДействие("ПриИзменении", "ПериодАнализаПриИзменении");
			ИначеЕсли ЭлементНастройка.Заголовок = "Дата начала" Тогда
				ЭлементНастройка.ОтображениеПодсказки = ОтображениеПодсказки.Кнопка;
				ЭлементНастройка.Подсказка = "Редактирование возможно только для произвольного периода";
			ИначеЕсли ЭлементНастройка.Заголовок = "Дата окончания" Тогда
				ЭлементНастройка.ОтображениеПодсказки = ОтображениеПодсказки.Кнопка;
				ЭлементНастройка.Подсказка = "Редактирование возможно только для произвольного периода";
			ИначеЕсли ЭлементНастройка.Заголовок = "Начало рабочего дня" Тогда
				ЭлементНастройка.Видимость = Ложь;
			ИначеЕсли ЭлементНастройка.Заголовок = "Окончание рабочего дня" Тогда
				ЭлементНастройка.Видимость = Ложь;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ПериодАнализаПриИзменении(Элемент)
	ОбработатьЗначениеПериодАнализа();	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьЗначениеПериодАнализа()
	ПериодАнализаЗначение = ПолучитьЗначениеПользовательскойНастройки("ПериодАнализа", Отчет.КомпоновщикНастроек.Настройки, Отчет.КомпоновщикНастроек.ПользовательскиеНастройки); 
	ИнициализироватьНастройки(Отчет.КомпоновщикНастроек.Настройки, Отчет.КомпоновщикНастроек.ПользовательскиеНастройки, ЭтотОбъект.НачалоРабочегоДня, ЭтотОбъект.ОкончаниеРабочегоДня);
	
	ЭлементДатаНачала = ПолучитьПолеФормыПользовательскойНастройки("Дата начала");
	ЭлементДатаОкончания = ПолучитьПолеФормыПользовательскойНастройки("Дата окончания");
	Если ПериодАнализаЗначение = "Сутки" Тогда
		ЭлементДатаНачала.ТолькоПросмотр = Истина;
		ЭлементДатаОкончания.ТолькоПросмотр = Истина;
	ИначеЕсли ПериодАнализаЗначение = "Неделя" Тогда
		ЭлементДатаНачала.ТолькоПросмотр = Истина;
		ЭлементДатаОкончания.ТолькоПросмотр = Истина;
	ИначеЕсли ПериодАнализаЗначение = "Месяц" Тогда
		ЭлементДатаНачала.ТолькоПросмотр = Истина;
		ЭлементДатаОкончания.ТолькоПросмотр = Истина;
	Иначе
		ЭлементДатаНачала.ТолькоПросмотр = Ложь;
		ЭлементДатаОкончания.ТолькоПросмотр = Ложь;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Функция ПолучитьПолеФормыПользовательскойНастройки(Заголовок)
	Для Каждого ЭлементКолонка Из ЭтотОбъект.Элементы.КомпоновщикНастроекПользовательскиеНастройки.ПодчиненныеЭлементы Цикл
		Для Каждого ЭлементНастройка Из ЭлементКолонка.ПодчиненныеЭлементы Цикл
			Если ЭлементНастройка.Заголовок = Заголовок Тогда
				Возврат ЭлементНастройка;				
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
КонецФункции

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	Если НЕ ЗначениеЗаполнено(ЭтотОбъект.НачалоРабочегоДня) Тогда
		ЭтотОбъект.НачалоРабочегоДня = Дата(1,1,1,9,0,0);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ЭтотОбъект.ОкончаниеРабочегоДня) Тогда
		ЭтотОбъект.ОкончаниеРабочегоДня = Дата(1,1,1,18,0,0);
	КонецЕсли;
	
	ИнициализироватьНастройки(Отчет.КомпоновщикНастроек.Настройки, Отчет.КомпоновщикНастроек.ПользовательскиеНастройки, ЭтотОбъект.НачалоРабочегоДня, ЭтотОбъект.ОкончаниеРабочегоДня);
	ОбработатьЗначениеПериодАнализа();
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьЗначениеПользовательскойНастройки(ИмяНастройки, Настройки, ПользовательскиеНастройки)
	Параметр = Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных(ИмяНастройки));
	ЭлементНастройки = ПользовательскиеНастройки.Элементы.Найти(Параметр.ИдентификаторПользовательскойНастройки);
	
	Возврат ЭлементНастройки.Значение;
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УставноитьЗначениеПользовательскойНастройки(ИмяНастройки, Значение, Настройки, ПользовательскиеНастройки)
	Параметр = Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных(ИмяНастройки));
	ЭлементНастройки = ПользовательскиеНастройки.Элементы.Найти(Параметр.ИдентификаторПользовательскойНастройки);
	
	ЭлементНастройки.Значение = Значение;
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ИнициализироватьНастройки(Настройки, ПользовательскиеНастройки, НачалоРабочегоДня, ОкончаниеРабочегоДня)
		
	ПараметрПериодАнализа = Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ПериодАнализа"));
	ПараметрДатаНачала = Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ДатаНачала"));
	ПараметрДатаОкончания = Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ДатаОкончания"));
	ПараметрНачалоРабочегоДня = Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("НачалоРабочегоДня"));
	ПараметрОкончаниеРабочегоДня = Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ОкончаниеРабочегоДня"));
	
	ЭлементПериодАнализа = ПользовательскиеНастройки.Элементы.Найти(ПараметрПериодАнализа.ИдентификаторПользовательскойНастройки);
	ЭлементДатаНачала = ПользовательскиеНастройки.Элементы.Найти(ПараметрДатаНачала.ИдентификаторПользовательскойНастройки);
	ЭлементДатаОкончания = ПользовательскиеНастройки.Элементы.Найти(ПараметрДатаОкончания.ИдентификаторПользовательскойНастройки);
	ЭлементНачалоРабочегоДня = ПользовательскиеНастройки.Элементы.Найти(ПараметрНачалоРабочегоДня.ИдентификаторПользовательскойНастройки);
	ЭлементОкончаниеРабочегоДня = ПользовательскиеНастройки.Элементы.Найти(ПараметрОкончаниеРабочегоДня.ИдентификаторПользовательскойНастройки);
	
	ТекДата = ТекущаяДата();
	Если ЭлементПериодАнализа <> Неопределено Тогда
		Если НЕ ЗначениеЗаполнено(ЭлементПериодАнализа.Значение) ИЛИ ТипЗнч(ЭлементПериодАнализа.Значение) <> Тип("Строка") Тогда
			ЭлементПериодАнализа.Значение = "Сутки";	
		КонецЕсли;
		
		Если ЭлементПериодАнализа.Значение = "Сутки" Тогда
			ЭлементДатаОкончания.Значение = ТекДата;
			ЭлементДатаНачала.Значение = ТекДата - 86400;
		ИначеЕсли ЭлементПериодАнализа.Значение = "Неделя" Тогда
			ЭлементДатаОкончания.Значение = ТекДата;
			ЭлементДатаНачала.Значение = ТекДата - 86400*7;
		ИначеЕсли ЭлементПериодАнализа.Значение = "Месяц" Тогда
			ЭлементДатаОкончания.Значение = ТекДата;
			ЭлементДатаНачала.Значение = ТекДата - 86400*30;
		Иначе
		КонецЕсли;
	КонецЕсли;
	
	Если ЭлементНачалоРабочегоДня <> Неопределено Тогда
		ЭлементНачалоРабочегоДня.Значение = НачалоРабочегоДня;
	КонецЕсли;
	
	Если ЭлементОкончаниеРабочегоДня <> Неопределено Тогда
		ЭлементОкончаниеРабочегоДня.Значение = ОкончаниеРабочегоДня;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СформироватьПрограммно(Команда)
	Если АвтообновлениеВключено = Неопределено Тогда
		Автообновление = Ложь;
	КонецЕсли;
		
	ЕстьОшибки = Ложь;
	ОписаниеОшибки = "";
	Если НЕ ЗначениеЗаполнено(ЭтотОбъект.НачалоРабочегоДня) ИЛИ НЕ ЗначениеЗаполнено(ЭтотОбъект.ОкончаниеРабочегоДня) Тогда
		ОписаниеОшибки = ОписаниеОшибки + "Не указаны параметры рабочего времени" + Символы.ПС;
		ЕстьОшибки = Истина;
	КонецЕсли;
	
	Если ЭтотОбъект.НачалоРабочегоДня >= ЭтотОбъект.ОкончаниеРабочегоДня Тогда
		ОписаниеОшибки = ОписаниеОшибки + "Начало рабочего дня больше или равно окончанию" + Символы.ПС;
		ЕстьОшибки = Истина;
	КонецЕсли;
			
	ИнициализироватьНастройки(Отчет.КомпоновщикНастроек.Настройки, Отчет.КомпоновщикНастроек.ПользовательскиеНастройки, ЭтотОбъект.НачалоРабочегоДня, ЭтотОбъект.ОкончаниеРабочегоДня);
	ОбработатьЗначениеПериодАнализа();
	
	ДатаНачала = Дата(ПолучитьЗначениеПользовательскойНастройки("ДатаНачала", Отчет.КомпоновщикНастроек.Настройки, Отчет.КомпоновщикНастроек.ПользовательскиеНастройки));
	ДатаОкончания = Дата(ПолучитьЗначениеПользовательскойНастройки("ДатаОкончания", Отчет.КомпоновщикНастроек.Настройки, Отчет.КомпоновщикНастроек.ПользовательскиеНастройки));
	
	Если НЕ ЗначениеЗаполнено(ДатаНачала) ИЛИ НЕ ЗначениеЗаполнено(ДатаОкончания) Тогда
		ОписаниеОшибки = ОписаниеОшибки + "Не указаны параметры периода отчета" + Символы.ПС;
		ЕстьОшибки = Истина;
	КонецЕсли;
	
	Если ДатаНачала >= ДатаОкончания Тогда
		ОписаниеОшибки = ОписаниеОшибки + "Дата начала больше или равна дате окончания отчета" + Символы.ПС;
		ЕстьОшибки = Истина;
	КонецЕсли;
	
	Если ЕстьОшибки Тогда
		ОписаниеОшибки = Лев(ОписаниеОшибки, СтрДлина(ОписаниеОшибки) - 1);
		ПоказатьПредупреждение(,ОписаниеОшибки, 30, "Ошибка");
		Возврат;
	КонецЕсли;
		
	ЭтотОбъект.СкомпоноватьРезультат(РежимКомпоновкиРезультата.Фоновый);

КонецПроцедуры

&НаКлиенте
Процедура НачалоРабочегоДняПриИзменении(Элемент)
	УставноитьЗначениеПользовательскойНастройки("НачалоРабочегоДня", ЭтотОбъект.НачалоРабочегоДня, Отчет.КомпоновщикНастроек.Настройки, Отчет.КомпоновщикНастроек.ПользовательскиеНастройки);  	
КонецПроцедуры

