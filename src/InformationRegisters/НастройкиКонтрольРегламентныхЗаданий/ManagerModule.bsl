#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
    
#Область ПрограммныйИнтерфейс

Функция ПрочитатьНастройки(КонтрольнаяПроцедура) Экспорт
    
    МенеджерЗаписи = СоздатьМенеджерЗаписи();
    МенеджерЗаписи.КонтрольнаяПроцедура = КонтрольнаяПроцедура;
    МенеджерЗаписи.Прочитать();
    
    Настройки = Новый Структура;
    Для Каждого ТекРесурс Из Метаданные.РегистрыСведений.НастройкиКонтрольРегламентныхЗаданий.Ресурсы Цикл
        Настройки.Вставить(ТекРесурс.Имя, МенеджерЗаписи[ТекРесурс.Имя]);
    КонецЦикла;
    
    Настройки.Вставить("Расписание", МенеджерЗаписи.Расписание.Получить());
    
    Возврат Настройки
    
КонецФункции

// Записать настройки
//
// Параметры:
//  КонтрольнаяПроцедура - СправочникСсылка.КонтрольныеПроцедуры - контрольная процедура, для которой записываются настройки.
//  Настройки            - Структура - структура создания с ключами:
//                                   — КаталогЗапускаКлиента            - Строка.
//                                   — ДопустимаяДлительностьВыполнения - Число, секунды.
//
Процедура ЗаписатьНастройки(КонтрольнаяПроцедура, Настройки) Экспорт
    
    МенеджерЗаписи = СоздатьМенеджерЗаписи();
    МенеджерЗаписи.КонтрольнаяПроцедура = КонтрольнаяПроцедура;
    МенеджерЗаписи.Прочитать();
    
    МенеджерЗаписи.КонтрольнаяПроцедура = КонтрольнаяПроцедура;
    
    Если Настройки.Свойство("ДопустимаяДлительностьВыполнения") Тогда
        МенеджерЗаписи.ДопустимаяДлительностьВыполнения = Настройки.ДопустимаяДлительностьВыполнения;
    КонецЕсли;
    
    Если Настройки.Свойство("КаталогЗапускаКлиента") Тогда
        МенеджерЗаписи.КаталогЗапускаКлиента = Настройки.КаталогЗапускаКлиента;
    КонецЕсли;
    
    Если Настройки.Свойство("Расписание") Тогда
        МенеджерЗаписи.Расписание = Новый ХранилищеЗначения(Настройки.Расписание);
    КонецЕсли;
    
    МенеджерЗаписи.Записать(Истина);    
   
КонецПроцедуры

#КонецОбласти

#КонецЕсли