
&НаКлиенте
Процедура ЗаполнитьПоУмолчанию(Команда)
	ЗаполнитьПоУмолчаниюНаСервере();
	Элементы.Список.Обновить();
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСОтбором(Команда)
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗаполнитьСОтборомПослеЗакрытия", ЭтотОбъект);
	ОткрытьФорму("РегистрСведений.ЗапрошенныеДампы.Форма.ЗаполнениеВариантов",, ЭтаФорма,,,,ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	Элементы.Список.Обновить();
КонецПроцедуры

&НаКлиенте
Процедура ЗапроситьДампы(Команда)
	Состояние(ЗапроситьДампыНаСервере());
	Элементы.Список.Обновить();
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСОтборомПослеЗакрытия(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	Если РезультатЗакрытия = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ЗаполнитьСОтборомНаСервере(РезультатЗакрытия);
	Элементы.Список.Обновить();
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаполнитьСОтборомНаСервере(ПараметрыЗаполнения) 
	ВариантыДампов = ПараметрыЗаполнения.ВариантыДампов;
	ТипыДампов = СтрРазделить(ПараметрыЗаполнения.ТипыДампов, ";");
	
	Настройки = Константы.НастройкиПолученияПолныхДампов.Получить().Получить();
	Если Настройки = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	ДатаОкончания = НачалоДня(ТекущаяДатаСеанса());
	ДатаНачала = ДатаОкончания - Настройки.ГлубинаПолученияИнформационныхБаз * 86400;
	МассивДанных = Новый Массив;
	
	// Запомним ИБ, у которых уже запросили вариант. 
	// Другой вариант дампа у этой ИБ запрашивать нельзя.
	ОбработанныеИБ = Новый Массив;
	
	Запрос = Новый Запрос(ТекстЗапросаСОтбором());
	Если Настройки.Статусы.Количество() > 0 Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "//#СоединениеСтатусы", "ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтатусИнформационнойБазы КАК СтатусИнформационнойБазы
				|		ПО (ДампыИБ.ИнформационнаяБаза = СтатусИнформационнойБазы.ИнформационнаяБаза
				|			И СтатусИнформационнойБазы.Статус В (&Статусы))");
		Запрос.УстановитьПараметр("Статусы", Настройки.Статусы);
	КонецЕсли;
	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", ДатаОкончания);
	Запрос.УстановитьПараметр("ВариантДампа", ВариантыДампов);
	Запрос.УстановитьПараметр("Подсистема", Настройки.СтандартныеПодсистемы);
	Запрос.УстановитьПараметр("ВерсияПодсистемыЧисло", Настройки.ВерсияПодсистемы.ВерсияЧисло);
	
	ВыборкаВарианты = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаВарианты.Следующий() Цикл
		Счетчик = 0;
		Выборка = ВыборкаВарианты.Выбрать();
		Пока Выборка.Следующий() Цикл
			Если ОбработанныеИБ.Найти(Выборка.ИнформационнаяБаза) = Неопределено Тогда
				ОбработанныеИБ.Добавить(Выборка.ИнформационнаяБаза);
			Иначе
				Продолжить;
			КонецЕсли;
			Запись = Новый Структура("ИнформационнаяБаза, ВариантДампа");
			ЗаполнитьЗначенияСвойств(Запись, Выборка);
			МассивДанных.Добавить(Запись);
			Счетчик = Счетчик + 1;
			Если Счетчик >= Настройки.МаксимальноеКоличествоКлиентовДляВариантаДампа Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	ЗаписатьДанные(МассивДанных, ОбработанныеИБ, ТипыДампов);
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаполнитьПоУмолчаниюНаСервере()
	Настройки = Константы.НастройкиПолученияПолныхДампов.Получить().Получить();
	Если Настройки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ДатаОкончания = НачалоДня(ТекущаяДатаСеанса());
	ДатаНачала = ДатаОкончания - Настройки.ГлубинаПолученияИнформационныхБаз * 86400;
	МассивДанных = Новый Массив;
	
	// Запоним ИБ, у которых уже запросили вариант. 
	// Другой вариант дампа у этой ИБ запрашивать нельзя.
	ОбработанныеИБ = Новый Массив;
	
	Запрос = Новый Запрос(ТекстЗапросаПоУмолчанию());
	Если Настройки.Статусы.Количество() > 0 Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "//#СоединениеСтатусы", "ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтатусИнформационнойБазы КАК СтатусИнформационнойБазы
				|		ПО (ДампыИБ.ИнформационнаяБаза = СтатусИнформационнойБазы.ИнформационнаяБаза
				|			И СтатусИнформационнойБазы.Статус В (&Статусы))");
		Запрос.УстановитьПараметр("Статусы", Настройки.Статусы);
	КонецЕсли;
	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", ДатаОкончания);
	Запрос.УстановитьПараметр("Подсистема", Настройки.СтандартныеПодсистемы);
	Запрос.УстановитьПараметр("ВерсияПодсистемыЧисло", Настройки.ВерсияПодсистемы.ВерсияЧисло);
	Запрос.УстановитьПараметр("МинимальноеКоличествоИБ", Настройки.МинимальноеКоличествоКлиентов);
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "10//#КоличествоВерсий", Формат(Настройки.КоличествоВерсийПлатформы, "ЧН=0; ЧГ="));
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "10//#КоличествоВариантовДампов", Формат(Настройки.КоличествоВариантовДампов, "ЧН=0; ЧГ="));
	
	
	ВыборкаВарианты = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаВарианты.Следующий() Цикл
		Счетчик = 0;
		Выборка = ВыборкаВарианты.Выбрать();
		Пока Выборка.Следующий() Цикл
			Если ОбработанныеИБ.Найти(Выборка.ИнформационнаяБаза) = Неопределено Тогда
				ОбработанныеИБ.Добавить(Выборка.ИнформационнаяБаза);
			Иначе
				Продолжить;
			КонецЕсли;
			Запись = Новый Структура("ИнформационнаяБаза, ВариантДампа");
			ЗаполнитьЗначенияСвойств(Запись, Выборка);
			МассивДанных.Добавить(Запись);
			Счетчик = Счетчик + 1;
			Если Счетчик >= Настройки.МаксимальноеКоличествоКлиентовДляВариантаДампа Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	ТипыДампов = СтрРазделить("0;3", ";");
	ЗаписатьДанные(МассивДанных, ОбработанныеИБ, ТипыДампов);
		
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаписатьДанные(Данные, ОбработанныеИБ, ТипыДампов)
	
	ЗапрошеныРанее = Новый Соответствие;
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	ЗапрошенныеДампы.ИнформационнаяБаза КАК ИнформационнаяБаза,
	                      |	ЗапрошенныеДампы.ВариантДампа КАК ВариантДампа
	                      |ИЗ
	                      |	РегистрСведений.ЗапрошенныеДампы КАК ЗапрошенныеДампы
	                      |ГДЕ
	                      |	ЗапрошенныеДампы.ИнформационнаяБаза В(&ИнформационнаяБаза)
	                      |	И НЕ ЗапрошенныеДампы.Получен
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	ЗапрошенныеДампы.ВариантДампа,
	                      |	ЗапрошенныеДампы.ИнформационнаяБаза");
	Запрос.УстановитьПараметр("ИнформационнаяБаза", ОбработанныеИБ);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		ЗапрошеныРанее.Вставить(Выборка.ИнформационнаяБаза, Выборка.ВариантДампа);
	КонецЦикла;
	
	ОписаниеЧисло = Новый ОписаниеТипов("Число");
	
	Для Каждого Строка Из Данные Цикл
		ВариантДампаПрошлый = ЗапрошеныРанее.Получить(Строка.ИнформационнаяБаза);
		
		// Если есть предыдущие запрошенные дампы - удалим.
		Если НЕ ВариантДампаПрошлый = Неопределено Тогда
			НаборЗаписей = РегистрыСведений.ЗапрошенныеДампы.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.ИнформационнаяБаза.Установить(Строка.ИнформационнаяБаза);
			НаборЗаписей.Отбор.ВариантДампа.Установить(ВариантДампаПрошлый);
			НаборЗаписей.Записать();
		КонецЕсли;
		
		НаборЗаписей = РегистрыСведений.ЗапрошенныеДампы.СоздатьНаборЗаписей();
		Для Каждого ТипДампа Из ТипыДампов Цикл
			Запись = НаборЗаписей.Добавить();
			ЗаполнитьЗначенияСвойств(Запись, Строка);
			Запись.ТипДампа = ТипДампа;
		КонецЦикла;
		НаборЗаписей.Записать(Ложь);
			
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ТекстЗапросаПоУмолчанию()
	Возврат "ВЫБРАТЬ ПЕРВЫЕ 10//#КоличествоВерсий
	        |	ВерсииПлатформыЦентрМониторинга.Ссылка КАК Версия,
	        |	ВерсииПлатформыЦентрМониторинга.ВерсияЧисло КАК ВерсияЧисло,
	        |	ВерсииПлатформыЦентрМониторинга.Релизная КАК Релизная,
	        |	ВЫРАЗИТЬ(ВерсииПлатформыЦентрМониторинга.ВерсияЧисло / 10000 - 0.5 КАК ЧИСЛО(12, 0)) КАК ВерсияСборки
	        |ПОМЕСТИТЬ ВерсииПлатформы
	        |ИЗ
	        |	Справочник.ВерсииПлатформыЦентрМониторинга КАК ВерсииПлатформыЦентрМониторинга
	        |ГДЕ
	        |	ВерсииПлатформыЦентрМониторинга.Релизная
	        |
	        |УПОРЯДОЧИТЬ ПО
	        |	ВерсияСборки УБЫВ,
	        |	ВерсияЧисло УБЫВ
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ
	        |	ДампыЦентраМониторинга.ВариантДампа КАК ВариантДампа,
	        |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДампыЦентраМониторинга.ИнформационнаяБаза) КАК КоличествоИБ,
	        |	СУММА(ДампыЦентраМониторинга.Количество) КАК Количество,
	        |	ВерсииПлатформы.ВерсияСборки КАК ВерсияСборки,
	        |	ВерсииПлатформы.ВерсияЧисло КАК ВерсияЧисло,
	        |	ДампыЦентраМониторинга.ВариантДампа.ИмяПроцесса КАК ИмяПроцесса,
	        |	ДампыЦентраМониторинга.ВариантДампа.Смещение КАК Смещение
	        |ПОМЕСТИТЬ ВТ_ВариантыДампов
	        |ИЗ
	        |	ВерсииПлатформы КАК ВерсииПлатформы
	        |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДампыЦентраМониторинга КАК ДампыЦентраМониторинга
	        |		ПО ВерсииПлатформы.Версия = ДампыЦентраМониторинга.ВариантДампа.ВерсияПлатформы
	        |			И (НЕ ДампыЦентраМониторинга.ВариантДампа.НулевоеСмещение)
	        |ГДЕ
	        |	ДампыЦентраМониторинга.Период МЕЖДУ &ДатаНачала И &ДатаОкончания
	        |	И НЕ ДампыЦентраМониторинга.ВариантДампа.Наименование ПОДОБНО ""%ffffffff""
	        |
	        |СГРУППИРОВАТЬ ПО
	        |	ДампыЦентраМониторинга.ВариантДампа,
	        |	ВерсииПлатформы.ВерсияСборки,
	        |	ВерсииПлатформы.ВерсияЧисло,
	        |	ДампыЦентраМониторинга.ВариантДампа.ИмяПроцесса,
	        |	ДампыЦентраМониторинга.ВариантДампа.Смещение
	        |
	        |ИМЕЮЩИЕ
	        |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДампыЦентраМониторинга.ИнформационнаяБаза) >= &МинимальноеКоличествоИБ
	        |
	        |ИНДЕКСИРОВАТЬ ПО
	        |	ИмяПроцесса,
	        |	Смещение,
	        |	ВерсияЧисло
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ
	        |	ВТ_ВариантыДампов.ИмяПроцесса КАК ИмяПроцесса,
	        |	ВТ_ВариантыДампов.Смещение КАК Смещение,
	        |	МАКСИМУМ(ВТ_ВариантыДампов.ВерсияЧисло) КАК ВерсияЧисло
	        |ПОМЕСТИТЬ ПоследниеВерсииПлатформыПоДампам
	        |ИЗ
	        |	ВТ_ВариантыДампов КАК ВТ_ВариантыДампов
	        |
	        |СГРУППИРОВАТЬ ПО
	        |	ВТ_ВариантыДампов.ИмяПроцесса,
	        |	ВТ_ВариантыДампов.Смещение
	        |
	        |ИНДЕКСИРОВАТЬ ПО
	        |	ИмяПроцесса,
	        |	Смещение,
	        |	ВерсияЧисло
			|;
			|ВЫБРАТЬ
			|	ПолученныеДампы.ВариантДампа КАК ВариантДампа
			|ПОМЕСТИТЬ ПолученныеВарианты
			|ИЗ
			|	РегистрСведений.ПолученныеДампы КАК ПолученныеДампы
			|
			|СГРУППИРОВАТЬ ПО
			|	ПолученныеДампы.ВариантДампа
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	ВариантДампа
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ ПЕРВЫЕ 10//#КоличествоВариантовДампов
	        |	ВТ_ВариантыДампов.ВариантДампа КАК ВариантДампа,
	        |	ВТ_ВариантыДампов.КоличествоИБ КАК КоличествоИБ,
	        |	ВТ_ВариантыДампов.Количество КАК Количество,
	        |	ВТ_ВариантыДампов.ВерсияСборки КАК ВерсияСборки,
	        |	ВТ_ВариантыДампов.ВерсияЧисло КАК ВерсияЧисло,
	        |	ВТ_ВариантыДампов.ИмяПроцесса КАК ИмяПроцесса,
	        |	ВТ_ВариантыДампов.Смещение КАК Смещение
	        |ПОМЕСТИТЬ ДампыДляОтбораБаз
	        |ИЗ
	        |	ВТ_ВариантыДампов КАК ВТ_ВариантыДампов
	        |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПоследниеВерсииПлатформыПоДампам КАК ПоследниеВерсииПлатформыПоДампам
	        |		ПО ВТ_ВариантыДампов.ИмяПроцесса = ПоследниеВерсииПлатформыПоДампам.ИмяПроцесса
	        |			И ВТ_ВариантыДампов.Смещение = ПоследниеВерсииПлатформыПоДампам.Смещение
			|			И ВТ_ВариантыДампов.ВерсияЧисло = ПоследниеВерсииПлатформыПоДампам.ВерсияЧисло
			|		ЛЕВОЕ СОЕДИНЕНИЕ ПолученныеВарианты КАК ПолученныеВарианты
			|		ПО (ВТ_ВариантыДампов.ВариантДампа = ПолученныеВарианты.ВариантДампа)
			|ГДЕ 
			|	ПолученныеВарианты.ВариантДампа ЕСТЬ NULL
			|
			|УПОРЯДОЧИТЬ ПО
			|	ВерсияСборки УБЫВ,
	        |	КоличествоИБ УБЫВ
	        |
	        |ИНДЕКСИРОВАТЬ ПО
	        |	ВариантДампа
	        |;
	        |
			|////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ
	        |	ДампыЦентраМониторинга.ВариантДампа КАК ВариантДампа,
	        |	ДампыЦентраМониторинга.ИнформационнаяБаза КАК ИнформационнаяБаза,
	        |	СУММА(ДампыЦентраМониторинга.Количество) КАК Количество
			|Поместить ДампыИБ
	        |ИЗ
	        |	ДампыДляОтбораБаз КАК ДампыДляОтбораБаз
	        |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДампыЦентраМониторинга КАК ДампыЦентраМониторинга
	        |		ПО ДампыДляОтбораБаз.ВариантДампа = ДампыЦентраМониторинга.ВариантДампа
	        |			И (ДампыЦентраМониторинга.Период МЕЖДУ &ДатаНачала И &ДатаОкончания)
	        |
	        |СГРУППИРОВАТЬ ПО
	        |	ДампыЦентраМониторинга.ВариантДампа,
	        |	ДампыЦентраМониторинга.ИнформационнаяБаза
			|Индексировать ПО
			|	ИнформационнаяБаза
	        |	;			
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ
	        |	ДампыИБ.ВариантДампа КАК ВариантДампа,
	        |	ДампыИБ.ИнформационнаяБаза КАК ИнформационнаяБаза,
	        |	СУММА(ДампыИБ.Количество) КАК Количество
	        |ИЗ
	        |	ДампыИБ КАК ДампыИБ
			|		//#СоединениеСтатусы
	        |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ВерсииПодсистемСрезПоследних КАК ВерсииПодсистемСрезПоследних
	        |		ПО ДампыИБ.ИнформационнаяБаза = ВерсииПодсистемСрезПоследних.ИнформационнаяБаза
	        |			И (ВерсииПодсистемСрезПоследних.Подсистема = &Подсистема)
	        |			И (ВерсииПодсистемСрезПоследних.ВерсияПодсистемы.ВерсияЧисло >= &ВерсияПодсистемыЧисло)
	        |СГРУППИРОВАТЬ ПО
	        |	ДампыИБ.ВариантДампа,
	        |	ДампыИБ.ИнформационнаяБаза
	        |
	        |УПОРЯДОЧИТЬ ПО
	        |	Количество УБЫВ
	        |ИТОГИ
	        |	СУММА(Количество)
	        |ПО
	        |	ВариантДампа";
КонецФункции

&НаСервереБезКонтекста
Функция ТекстЗапросаСОтбором() 
	Возврат "ВЫБРАТЬ
	        |	ДампыЦентраМониторинга.ВариантДампа КАК ВариантДампа,
	        |	ДампыЦентраМониторинга.ИнформационнаяБаза КАК ИнформационнаяБаза,
	        |	СУММА(ДампыЦентраМониторинга.Количество) КАК Количество
			|Поместить ДампыИБ
	        |ИЗ
	        |	РегистрСведений.ДампыЦентраМониторинга КАК ДампыЦентраМониторинга
	        |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ВерсииПодсистемСрезПоследних КАК ВерсииПодсистемСрезПоследних
	        |		ПО ДампыЦентраМониторинга.ИнформационнаяБаза = ВерсииПодсистемСрезПоследних.ИнформационнаяБаза
	        |			И (ВерсииПодсистемСрезПоследних.Подсистема = &Подсистема)
	        |			И (ВерсииПодсистемСрезПоследних.ВерсияПодсистемы.ВерсияЧисло >= &ВерсияПодсистемыЧисло)
	        |ГДЕ
	        |	ДампыЦентраМониторинга.ВариантДампа В(&ВариантДампа)
	        |	И ДампыЦентраМониторинга.Период МЕЖДУ &ДатаНачала И &ДатаОкончания
	        |
	        |СГРУППИРОВАТЬ ПО
	        |	ДампыЦентраМониторинга.ВариантДампа,
	        |	ДампыЦентраМониторинга.ИнформационнаяБаза
			|Индексировать ПО
			|	ИнформационнаяБаза
	        |;
			|ВЫБРАТЬ
			|	ДампыИБ.ВариантДампа КАК ВариантДампа,
			|	ДампыИБ.ИнформационнаяБаза КАК ИнформационнаяБаза,
			|	СУММА(ДампыИБ.Количество) КАК Количество
			|ИЗ
			|	ДампыИБ КАК ДампыИБ
			|		//#СоединениеСтатусы
			|		
			|СГРУППИРОВАТЬ ПО
			|	ДампыИБ.ВариантДампа,
			|	ДампыИБ.ИнформационнаяБаза
	        |УПОРЯДОЧИТЬ ПО
	        |	Количество УБЫВ
	        |ИТОГИ
	        |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ИнформационнаяБаза),
	        |	СУММА(Количество)
	        |ПО
	        |	ВариантДампа";
КонецФункции

&НаСервереБезКонтекста
Функция ЗапроситьДампыНаСервере()
	Возврат ЦентрМониторингаПолучениеДампов.ПоместитьИнформациюОНеобходимыхДампах();
КонецФункции

