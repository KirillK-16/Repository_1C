
&НаКлиенте
Процедура ЗаполнитьПоУмолчанию(Команда)
	ВариантыДампов.ЗагрузитьЗначения(МассивВариантов());
КонецПроцедуры

&НаСервереБезКонтекста
Функция МассивВариантов()
	МассивВариантов = Новый Массив;
	Настройки = Константы.НастройкиПолученияПолныхДампов.Получить().Получить();
	Если Настройки = Неопределено Тогда 
		Возврат МассивВариантов;
	КонецЕсли;
	ДатаОкончания = НачалоДня(ТекущаяДатаСеанса());
	ДатаНачала = ДатаОкончания - Настройки.ГлубинаПолученияИнформационныхБаз * 86400;
	
	Запрос = Новый Запрос(ТекстЗапроса());
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "10//#КоличествоВерсий", Формат(Настройки.КоличествоВерсийПлатформы, "ЧН=0; ЧГ="));
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "10//#КоличествоВариантовДампов", Формат(Настройки.КоличествоВариантовДампов, "ЧН=0; ЧГ="));
	Запрос.УстановитьПараметр("ДатаНачала", ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", ДатаОкончания);
	Запрос.УстановитьПараметр("МинимальноеКоличествоИБ", Настройки.МинимальноеКоличествоКлиентов);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		МассивВариантов.Добавить(Выборка.ВариантДампа);
	КонецЦикла;
	
	Возврат МассивВариантов;
	
КонецФункции

&НаСервереБезКонтекста
Функция ТекстЗапроса()
	Возврат "ВЫБРАТЬ ПЕРВЫЕ 10//#КоличествоВерсий
	        |	ВерсииПлатформыЦентрМониторинга.Ссылка КАК Версия,
	        |	ВерсииПлатформыЦентрМониторинга.ВерсияЧисло КАК ВерсияЧисло,
	        |	ВерсииПлатформыЦентрМониторинга.Релизная КАК Релизная,
	        |	ВЫРАЗИТЬ(ВерсииПлатформыЦентрМониторинга.ВерсияЧисло / 10000 - 0.5 КАК ЧИСЛО(12, 0)) КАК ВерсияСборки
	        |ПОМЕСТИТЬ ВерсииПлатформы
	        |ИЗ
	        |	Справочник.ВерсииПлатформыЦентрМониторинга КАК ВерсииПлатформыЦентрМониторинга
	        |ГДЕ
	        |	ВерсииПлатформыЦентрМониторинга.Релизная
	        |
	        |УПОРЯДОЧИТЬ ПО
	        |	ВерсияСборки УБЫВ,
	        |	ВерсияЧисло УБЫВ
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ
	        |	ДампыЦентраМониторинга.ВариантДампа КАК ВариантДампа,
	        |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДампыЦентраМониторинга.ИнформационнаяБаза) КАК КоличествоИБ,
	        |	СУММА(ДампыЦентраМониторинга.Количество) КАК Количество,
	        |	ВерсииПлатформы.ВерсияСборки КАК ВерсияСборки,
	        |	ВерсииПлатформы.ВерсияЧисло КАК ВерсияЧисло,
	        |	ДампыЦентраМониторинга.ВариантДампа.ИмяПроцесса КАК ИмяПроцесса,
	        |	ДампыЦентраМониторинга.ВариантДампа.Смещение КАК Смещение
	        |ПОМЕСТИТЬ ВТ_ВариантыДампов
	        |ИЗ
	        |	ВерсииПлатформы КАК ВерсииПлатформы
	        |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ДампыЦентраМониторинга КАК ДампыЦентраМониторинга
	        |		ПО ВерсииПлатформы.Версия = ДампыЦентраМониторинга.ВариантДампа.ВерсияПлатформы
	        |			И (НЕ ДампыЦентраМониторинга.ВариантДампа.НулевоеСмещение)
	        |ГДЕ
	        |	ДампыЦентраМониторинга.Период МЕЖДУ &ДатаНачала И &ДатаОкончания
	        |	И НЕ ДампыЦентраМониторинга.ВариантДампа.Наименование ПОДОБНО ""%ffffffff""
	        |
	        |СГРУППИРОВАТЬ ПО
	        |	ДампыЦентраМониторинга.ВариантДампа,
	        |	ВерсииПлатформы.ВерсияСборки,
	        |	ВерсииПлатформы.ВерсияЧисло,
	        |	ДампыЦентраМониторинга.ВариантДампа.ИмяПроцесса,
	        |	ДампыЦентраМониторинга.ВариантДампа.Смещение
	        |
	        |ИМЕЮЩИЕ
	        |	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ДампыЦентраМониторинга.ИнформационнаяБаза) >= &МинимальноеКоличествоИБ
	        |
	        |ИНДЕКСИРОВАТЬ ПО
	        |	ИмяПроцесса,
	        |	Смещение,
	        |	ВерсияЧисло
	        |;
	        |
	        |////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ
	        |	ВТ_ВариантыДампов.ИмяПроцесса КАК ИмяПроцесса,
	        |	ВТ_ВариантыДампов.Смещение КАК Смещение,
	        |	МАКСИМУМ(ВТ_ВариантыДампов.ВерсияЧисло) КАК ВерсияЧисло
	        |ПОМЕСТИТЬ ПоследниеВерсииПлатформыПоДампам
	        |ИЗ
	        |	ВТ_ВариантыДампов КАК ВТ_ВариантыДампов
	        |
	        |СГРУППИРОВАТЬ ПО
	        |	ВТ_ВариантыДампов.ИмяПроцесса,
	        |	ВТ_ВариантыДампов.Смещение
	        |
	        |ИНДЕКСИРОВАТЬ ПО
	        |	ИмяПроцесса,
	        |	Смещение,
	        |	ВерсияЧисло
			|;
			|ВЫБРАТЬ
			|	ПолученныеДампы.ВариантДампа КАК ВариантДампа
			|ПОМЕСТИТЬ ПолученныеВарианты
			|ИЗ
			|	РегистрСведений.ПолученныеДампы КАК ПолученныеДампы
			|
			|СГРУППИРОВАТЬ ПО
			|	ПолученныеДампы.ВариантДампа
			|
			|ИНДЕКСИРОВАТЬ ПО
			|	ВариантДампа
			|;
			|
			|////////////////////////////////////////////////////////////////////////////////
	        |ВЫБРАТЬ ПЕРВЫЕ 10//#КоличествоВариантовДампов
	        |	ВТ_ВариантыДампов.ВариантДампа КАК ВариантДампа,
	        |	ВТ_ВариантыДампов.КоличествоИБ КАК КоличествоИБ,
	        |	ВТ_ВариантыДампов.Количество КАК Количество,
	        |	ВТ_ВариантыДампов.ВерсияСборки КАК ВерсияСборки,
	        |	ВТ_ВариантыДампов.ВерсияЧисло КАК ВерсияЧисло,
	        |	ВТ_ВариантыДампов.ИмяПроцесса КАК ИмяПроцесса,
	        |	ВТ_ВариантыДампов.Смещение КАК Смещение
	        |ИЗ
	        |	ВТ_ВариантыДампов КАК ВТ_ВариантыДампов
	        |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ПоследниеВерсииПлатформыПоДампам КАК ПоследниеВерсииПлатформыПоДампам
	        |		ПО ВТ_ВариантыДампов.ИмяПроцесса = ПоследниеВерсииПлатформыПоДампам.ИмяПроцесса
	        |			И ВТ_ВариантыДампов.Смещение = ПоследниеВерсииПлатформыПоДампам.Смещение
			|			И ВТ_ВариантыДампов.ВерсияЧисло = ПоследниеВерсииПлатформыПоДампам.ВерсияЧисло
			|		ЛЕВОЕ СОЕДИНЕНИЕ ПолученныеВарианты КАК ПолученныеВарианты
			|		ПО (ВТ_ВариантыДампов.ВариантДампа = ПолученныеВарианты.ВариантДампа)
			|ГДЕ 
			|	ПолученныеВарианты.ВариантДампа ЕСТЬ NULL
			|
			|УПОРЯДОЧИТЬ ПО
			|	ВерсияСборки УБЫВ,
	        |	КоличествоИБ УБЫВ";
КонецФункции

&НаКлиенте
Процедура ОК(Команда)
	ПараметрыЗакрытия = Новый Структура("ВариантыДампов, ТипыДампов", ВариантыДампов.ВыгрузитьЗначения(), ТипыДампов);
	Закрыть(ПараметрыЗакрытия);
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ТипыДампов = "0;3";
КонецПроцедуры
