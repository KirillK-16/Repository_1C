
&НаКлиенте
Процедура СвязанныеЗаписиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Строка = СвязанныеЗаписи.НайтиПоИдентификатору(ВыбраннаяСтрока);
	ЗначениеКлюча = Новый Структура;
	ЗначениеКлюча.Вставить("Дата", Строка.Дата);
	ЗначениеКлюча.Вставить("Тип", Строка.Тип);
	ЗначениеКлюча.Вставить("Кластер", Строка.Кластер);
	ЗначениеКлюча.Вставить("РабочийСервер", Строка.РабочийСервер);
	ЗначениеКлюча.Вставить("ИнформационнаяБаза", Строка.ИнформационнаяБаза);
	ЗначениеКлюча.Вставить("ФайлЖурнала", Строка.ФайлЖурнала);
	ЗначениеКлюча.Вставить("УникальныйИдентификатор", Строка.УникальныйИдентификатор);
	ЗначениеКлюча.Вставить("КонтрольнаяПроцедура", Строка.КонтрольнаяПроцедура);
	Аргументы = Новый Массив;
	Аргументы.Добавить(ЗначениеКлюча);
	КлючЗаписи = Новый ("РегистрСведенийКлючЗаписи.ТехнологическийЖурнал", Аргументы); 
	ОткрытьФорму("РегистрСведений.ТехнологическийЖурнал.ФормаЗаписи", Новый Структура("Ключ", КлючЗаписи),,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ДубликатОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Аргументы = Новый Массив;
	Аргументы.Добавить(Новый Структура("УникальныйИдентификатор, ФайлЖурнала", Запись.Дубликат, ФайлДубликата));
	Ключ = Новый ("РегистрСведенийКлючЗаписи.ТехнологическийЖурнал", Аргументы); 
	ОткрытьФорму("РегистрСведений.ТехнологическийЖурнал.ФормаЗаписи", Новый Структура("Ключ", Ключ),,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьДубликат();
	ЗаполнитьСвязанныеЗаписи();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСвязанныеЗаписи()
	
	Если ЗначениеЗаполнено(Запись.ГоловнаяЗапись) Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ТехнологическийЖурнал.Контекст,
			|	ТехнологическийЖурнал.ТекстИсключения,
			|	ТехнологическийЖурнал.Тип,
			|	ТехнологическийЖурнал.УникальныйИдентификатор,
			|	ТехнологическийЖурнал.ФайлЖурнала,
			|	ТехнологическийЖурнал.Дата КАК Дата,
			|	ТехнологическийЖурнал.ДробныеСекунды КАК ДробныеСекунды,
			|	ТехнологическийЖурнал.Продолжительность,
			|	ТехнологическийЖурнал.Дата КАК Дата1,
			|	ТехнологическийЖурнал.Кластер,
			|	ТехнологическийЖурнал.РабочийСервер,
			|	ТехнологическийЖурнал.ИнформационнаяБаза,
			|	ТехнологическийЖурнал.КонтрольнаяПроцедура
			|ИЗ
			|	РегистрСведений.ТехнологическийЖурнал КАК ТехнологическийЖурнал
			|ГДЕ
			|	(ТехнологическийЖурнал.ГоловнаяЗапись = &ГоловнаяЗапись
			|			ИЛИ ТехнологическийЖурнал.УникальныйИдентификатор = &ГоловнаяЗапись)
			|
			|УПОРЯДОЧИТЬ ПО
			|	Дата,
			|	ДробныеСекунды";
		Запрос.УстановитьПараметр("ГоловнаяЗапись", Запись.ГоловнаяЗапись);
		Выгрузка = Запрос.Выполнить().Выгрузить();
		Выгрузка.Колонки.Добавить("ДробнаяДата");
		Для Каждого Строка Из Выгрузка Цикл
			
			Строка.ДробнаяДата = Формат(Строка.Дата, "ДФ='yyyy-MM-dd HH:mm:ss'") + "." + Формат(Строка.ДробныеСекунды, "ЧЦ=4; ЧВН=; ЧГ=0");
			
		КонецЦикла;
		СвязанныеЗаписи.Загрузить(Выгрузка);
		
	Иначе
		Элементы.СтраницаСвязанныеЗаписи.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере 
Процедура ЗаполнитьДубликат()
	
	Если ЗначениеЗаполнено(Запись.Дубликат) Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ТехнологическийЖурнал.ФайлЖурнала КАК ФайлЖурнала
			|ИЗ
			|	РегистрСведений.ТехнологическийЖурнал КАК ТехнологическийЖурнал
			|ГДЕ
			|	ТехнологическийЖурнал.УникальныйИдентификатор = &УникальныйИдентификатор";
		Запрос.УстановитьПараметр("УникальныйИдентификатор", Запись.Дубликат);
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			ФайлДубликата = Выборка.ФайлЖурнала;
		Иначе
			Элементы.Дубликат.КнопкаОткрытия = Ложь;
		КонецЕсли;
	Иначе
		Элементы.Дубликат.КнопкаОткрытия = Ложь;
	КонецЕсли;
	
КонецПроцедуры