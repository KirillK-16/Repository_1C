
#Область ПрограммныйИнтерфейс

// Функция - Прочитать настройки.
//
// Параметры:
//  ОбъектКонтроля - СправочникСсылка.ОбъектыКонтроля - Объект контроля для которого необходимы настройки
// 
// Возвращаемое значение:
//   - Соответствие 
//
Функция ПрочитатьНастройки(ОбъектКонтроля) Экспорт
    
    Настройки = Новый Соответствие;
    
    Запрос = Новый Запрос;
    Запрос.Текст = "
    |ВЫБРАТЬ
    |   ИмяРесурса,
    |   Контроль
    |ИЗ
    |   РегистрСведений.КонтрольПараметровОбъектов
    |ГДЕ
    |   ОбъектКонтроля = &ОбъектКонтроля
    |";
    
    Запрос.УстановитьПараметр("ОбъектКонтроля", ОбъектКонтроля);
    
    Результат = Запрос.Выполнить();
    
    Выборка = Результат.Выбрать();
    Пока Выборка.Следующий() Цикл
        Настройки.Вставить(Выборка.ИмяРесурса, Выборка.Контроль);
    КонецЦикла;
    
    Возврат Настройки        
    
КонецФункции

// Процедура - Записать настройки
//
// Параметры:
//  ОбъектКонтроля - СправочникСсылка.ОбъектыКонтроля - Объект контроля для которого записываются настройки.
//  Настройки      - Соответсвие - Настройки для записи.
//
Процедура ЗаписатьНастройки(ОбъектКонтроля, Настройки) Экспорт
    
    НаборЗаписей = СоздатьНаборЗаписей();
    НаборЗаписей.Отбор.ОбъектКонтроля.Установить(ОбъектКонтроля);
    
    Для Каждого Настройка Из Настройки Цикл
        
        НовЗапись = НаборЗаписей.Добавить();
        НовЗапись.ОбъектКонтроля = ОбъектКонтроля;
        НовЗапись.ИмяРесурса = Настройка.Ключ;
        НовЗапись.Контроль = Настройка.Значение;
        
    КонецЦикла;
    
    НаборЗаписей.Записать(Истина);
        
КонецПроцедуры

// Функция - Прочитать настройки объектов
//
// Параметры:
//  ОбъектыКонтроля - Массив - Массив СправочникСсылка.ОбъектыКонтроля.
// 
// Возвращаемое значение:
//   - Соответствие 
//
Функция ПрочитатьНастройкиОбъектов(ОбъектыКонтроля) Экспорт
    
    Результат = Новый Соответствие;
    
    Запрос = Новый Запрос;
    Запрос.Текст = "
    |ВЫБРАТЬ
    |   ОбъектКонтроля,
    |   ИмяРесурса,
    |   Контроль
    |ИЗ
    |   РегистрСведений.КонтрольПараметровОбъектов
    |ГДЕ
    |   ОбъектКонтроля В (&ОбъектыКонтроля)
    |";
    
    Запрос.УстановитьПараметр("ОбъектыКонтроля", ОбъектыКонтроля);
    
    РезультатЗапроса = Запрос.Выполнить();
    
    Выборка = РезультатЗапроса.Выбрать();
    Пока Выборка.Следующий() Цикл
        
        ПараметрыКонтроля = Результат[Выборка.ОбъектКонтроля];
        Если ПараметрыКонтроля = Неопределено Тогда
            ПараметрыКонтроля = Новый Соответствие;
            Результат.Вставить(Выборка.ОбъектКонтроля, ПараметрыКонтроля);
        КонецЕсли;
        
        ПараметрыКонтроля.Вставить(Выборка.ИмяРесурса, Выборка.Контроль);        
        
    КонецЦикла;
    
    Возврат Результат;    
    
КонецФункции

#КонецОбласти