
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
    
#Область ПрограммныйИнтерфейс

Функция ПрочитатьНастройки(ОбъектКонтроля) Экспорт
    
    ТекДата = ТекущаяДата();
    ВремяАктуальностиКеша = Константы.Кластер1СВремяАктуальностиКеша.Получить();
    Если НЕ ЗначениеЗаполнено(ВремяАктуальностиКеша) Тогда
        ВремяАктуальностиКеша = 600;
    КонецЕсли;
        
    МенеджерЗаписи = СоздатьМенеджерЗаписи();
    МенеджерЗаписи.ОбъектКонтроля = ОбъектКонтроля;
    МенеджерЗаписи.Прочитать();
        
    Настройки = Новый Структура("ИдентификаторКластера, СписокИБ, СписокРабочиеСерверы", Неопределено, Неопределено, Неопределено);
    Если МенеджерЗаписи.ДатаЗаписи + ВремяАктуальностиКеша > ТекДата Тогда
        Настройки.ИдентификаторКластера = МенеджерЗаписи.ИдентификаторКластера;
        Настройки.СписокИБ = МенеджерЗаписи.СписокИБ.Получить();
        Настройки.СписокРабочиеСерверы = МенеджерЗаписи.СписокРабочиеСерверы.Получить();
    КонецЕсли;
            
    Возврат Настройки;
    
КонецФункции

// Записать настройки
//
// Параметры:
//  ОбъектКонтроля - СправочникСсылка.ОбъектыКонтроля - кластер 1С для которого записываются настройки.
//  Настройки      - Структура - структура создания:
//
Процедура ЗаписатьНастройки(ОбъектКонтроля, Настройки) Экспорт
    
    ТекДата = ТекущаяДата();
    
    МенеджерЗаписи = СоздатьМенеджерЗаписи();
    МенеджерЗаписи.ОбъектКонтроля = ОбъектКонтроля;
    МенеджерЗаписи.Прочитать();
    
    МенеджерЗаписи.ОбъектКонтроля = ОбъектКонтроля;
    МенеджерЗаписи.ДатаЗаписи = ТекДата;
    
    Если Настройки.Свойство("ИдентификаторКластера") Тогда
        МенеджерЗаписи.ИдентификаторКластера = Настройки.ИдентификаторКластера;
    КонецЕсли;
    
    Если Настройки.Свойство("СписокИБ") Тогда
        МенеджерЗаписи.СписокИБ = Новый ХранилищеЗначения(Настройки.СписокИБ);
    КонецЕсли;
    
    Если Настройки.Свойство("СписокРабочиеСерверы") Тогда
        МенеджерЗаписи.СписокРабочиеСерверы = Новый ХранилищеЗначения(Настройки.СписокРабочиеСерверы);
    КонецЕсли;
    
    МенеджерЗаписи.Записать(Истина);
    
КонецПроцедуры

Процедура ОчиститьКеш(ОбъектКонтроля) Экспорт
    
    МенеджерЗаписи = СоздатьМенеджерЗаписи();
    МенеджерЗаписи.ОбъектКонтроля = ОбъектКонтроля;
    МенеджерЗаписи.Удалить();
    
КонецПроцедуры

#КонецОбласти

#КонецЕсли