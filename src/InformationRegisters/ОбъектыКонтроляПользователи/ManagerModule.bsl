#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Функция ДобавитьПользователя(ОбъектКонтроля, Пользователь) Экспорт
	КлючЗаписи = ПолучитьКлючЗаписи(ОбъектКонтроля, Пользователь);
	
	Если КлючЗаписи = Неопределено Тогда
		НачатьТранзакцию();
		
		БлокировкаДанных = Новый БлокировкаДанных;
		ЭлементБлокировки = БлокировкаДанных.Добавить("РегистрСведений.ОбъектыКонтроляПользователи");
		ЭлементБлокировки.УстановитьЗначение("ОбъектКонтроля", ОбъектКонтроля);
		ЭлементБлокировки.УстановитьЗначение("ПользовательЗамерПроизводительности", Пользователь);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		
		Попытка
			БлокировкаДанных.Заблокировать();
			
			КлючЗаписи = ПолучитьКлючЗаписи(ОбъектКонтроля, Пользователь);
			Если КлючЗаписи = Неопределено Тогда
				ДобавитьЗапись(ОбъектКонтроля, Пользователь);
			КонецЕсли;
			
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ВызватьИсключение;
		КонецПопытки;
	КонецЕсли;
КонецФункции

Функция ПолучитьКлючЗаписи(ОбъектКонтроля, Пользователь)
	КлючЗаписи = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ОбъектКонтроля КАК ОбъектКонтроля,
	|	ПользовательЗамерПроизводительности КАК ПользовательЗамерПроизводительности
	|ИЗ
	|	РегистрСведений.ОбъектыКонтроляПользователи КАК ОбъектыКонтроляПользователи
	|ГДЕ
	|	ОбъектыКонтроляПользователи.ОбъектКонтроля = &ОбъектКонтроля
	|	И ОбъектыКонтроляПользователи.ПользовательЗамерПроизводительности = &ПользовательЗамерПроизводительности
	|";
	
	Запрос.УстановитьПараметр("ОбъектКонтроля", ОбъектКонтроля);
	Запрос.УстановитьПараметр("ПользовательЗамерПроизводительности", Пользователь);
	
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		
		ПараметрыКлюча = Новый Структура("ОбъектКонтроля, ПользовательЗамерПроизводительности", Выборка.ОбъектКонтроля, Выборка.ПользовательЗамерПроизводительности);
		КлючЗаписи = РегистрыСведений.ОбъектыКонтроляПользователи.СоздатьКлючЗаписи(ПараметрыКлюча);
	КонецЕсли;
		
	Возврат КлючЗаписи;
	
КонецФункции

Функция ДобавитьЗапись(ОбъектКонтроля, Пользователь)
	НоваяЗапись = РегистрыСведений.ОбъектыКонтроляПользователи.СоздатьМенеджерЗаписи();
	НоваяЗапись.ОбъектКонтроля = ОбъектКонтроля;
	НоваяЗапись.ПользовательЗамерПроизводительности = Пользователь;
	
	НоваяЗапись.Записать(Ложь);
КонецФункции

#КонецЕсли