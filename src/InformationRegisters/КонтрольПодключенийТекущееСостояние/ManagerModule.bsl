#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Функция ЗаписатьТекущееСостояние(Параметры, РезультатПодключения) Экспорт
	
	Попытка
		
		НачатьТранзакцию();
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.КонтрольПодключенийТекущееСостояние");
		ЭлементБлокировки.УстановитьЗначение("ИнформационнаяБаза", Параметры.ИнформационнаяБаза);
        ЭлементБлокировки.УстановитьЗначение("Источник", Параметры.Источник);
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		
		МенеджерЗаписи = РегистрыСведений.КонтрольПодключенийТекущееСостояние.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.ИнформационнаяБаза = Параметры.ИнформационнаяБаза;
        МенеджерЗаписи.Источник = Параметры.Источник;
		МенеджерЗаписи.Прочитать();
		
		ТекДата = ТекущаяДата();
		Если МенеджерЗаписи.Выбран() Тогда
			Если МенеджерЗаписи.РезультатПодключения = РезультатПодключения Тогда
				МенеджерЗаписи.ДатаОкончания = ТекДата;
			Иначе
				МенеджерЗаписи.РезультатПодключения = РезультатПодключения;
				МенеджерЗаписи.ДатаНачала = ТекДата;
				МенеджерЗаписи.ДатаОкончания = ТекДата;
			КонецЕсли;
		Иначе
			МенеджерЗаписи.ИнформационнаяБаза = Параметры.ИнформационнаяБаза;
            МенеджерЗаписи.Источник = Параметры.Источник;
			МенеджерЗаписи.РезультатПодключения = РезультатПодключения;
			МенеджерЗаписи.ДатаНачала = ТекДата;
			МенеджерЗаписи.ДатаОкончания = ТекДата;
		КонецЕсли;
        
        Если Параметры.Свойство("ТекущийПроцентДоступности") Тогда
            МенеджерЗаписи.ТекущийПроцентДоступности = Параметры.ТекущийПроцентДоступности;
        КонецЕсли;
        
        МенеджерЗаписи.Записать(Истина);
		
		ЗафиксироватьТранзакцию();
	Исключение
		ОтменитьТранзакцию();
		ЗаписьЖурналаРегистрации("КонтрольПодключенийСервер.ЗаписатьТекущееСостояние", УровеньЖурналаРегистрации.Ошибка,,,ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение;
	КонецПопытки;
	
КонецФункции

#КонецЕсли