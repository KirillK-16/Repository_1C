#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
    
#Область ПрограммныйИнтерфейс

Процедура ОткрытьДлительностьИнцидента(Инцидент, ТипИнцидента, УровеньИнцидента, Ответственный) Экспорт
    
    ТекДата = ТекущаяУниверсальнаяДата();
    
    ВыборкаОткрытых = ПолучитьВыборкуОткрытых(Инцидент);
    
    УровеньИнцидентаОткрыть = Истина;
    Пока ВыборкаОткрытых.Следующий() Цикл
        Если ВыборкаОткрытых.УровеньИнцидента <> УровеньИнцидента ИЛИ ВыборкаОткрытых.Ответственный <> Ответственный Тогда
            
            МенеджерЗаписи = РегистрыСведений.ИнцидентыДлительность.СоздатьМенеджерЗаписи();
            МенеджерЗаписи.Период = ВыборкаОткрытых.Период;
            МенеджерЗаписи.Инцидент = Инцидент;
            МенеджерЗаписи.Открыт = Истина;
            МенеджерЗаписи.Прочитать();
            
            МенеджерЗаписи.Открыт = Ложь;
            МенеджерЗаписи.Длительность = ТекДата - МенеджерЗаписи.Период;
            МенеджерЗаписи.Записать(Истина);
        Иначе
            УровеньИнцидентаОткрыть = Ложь;    
        КонецЕсли;
    КонецЦикла;
    
    Если УровеньИнцидентаОткрыть Тогда
        МенеджерЗаписи = РегистрыСведений.ИнцидентыДлительность.СоздатьМенеджерЗаписи();
        МенеджерЗаписи.Период = ТекДата;
        МенеджерЗаписи.Инцидент = Инцидент;
        МенеджерЗаписи.Открыт = Истина;
        МенеджерЗаписи.ТипИнцидента = ТипИнцидента;
        МенеджерЗаписи.УровеньИнцидента = УровеньИнцидента;
        МенеджерЗаписи.Ответственный = Ответственный;
        МенеджерЗаписи.Длительность = 0;
        МенеджерЗаписи.Записать(Ложь);
    КонецЕсли;
            
КонецПроцедуры

Процедура ЗакрытьДлительностьИнцидента(Инцидент) Экспорт
    
    ТекДата = ТекущаяУниверсальнаяДата();
    
    ВыборкаОткрытых = ПолучитьВыборкуОткрытых(Инцидент);
    
    Пока ВыборкаОткрытых.Следующий() Цикл
        МенеджерЗаписи = РегистрыСведений.ИнцидентыДлительность.СоздатьМенеджерЗаписи();
        МенеджерЗаписи.Период = ВыборкаОткрытых.Период;
        МенеджерЗаписи.Инцидент = Инцидент;
        МенеджерЗаписи.Открыт = Истина;
        МенеджерЗаписи.Прочитать();
        
        МенеджерЗаписи.Открыт = Ложь;
        МенеджерЗаписи.Длительность = ТекДата - МенеджерЗаписи.Период;
        МенеджерЗаписи.Записать(Истина);
     КонецЦикла;
    
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьВыборкуОткрытых(Инцидент)
    
    Запрос = Новый Запрос;
    Запрос.Текст = "
    |ВЫБРАТЬ
    |   Период,
    |   УровеньИнцидента,
    |   Ответственный
    |ИЗ
    |   РегистрСведений.ИнцидентыДлительность
    |ГДЕ
    |   Инцидент = &Инцидент
    |   И Открыт = Истина
    |";
    
    Запрос.УстановитьПараметр("Инцидент", Инцидент);
    Результат = Запрос.Выполнить();
    Выборка = Результат.Выбрать();
    
    Возврат Выборка;
    
КонецФункции

#КонецОбласти

#КонецЕсли