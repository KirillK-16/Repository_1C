
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	МассивЗначений = ЗначениеПараметра.ВыгрузитьЗначения();
	ТекущийОбъект.Значение = Новый ХранилищеЗначения(МассивЗначений, Новый СжатиеДанных(9));
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ЗаписьОбъект = РеквизитФормыВЗначение("Запись");
	ЗначениеХранилища = ЗаписьОбъект.Значение.Получить();
	Если НЕ ЗначениеХранилища = Неопределено Тогда
		ЗначениеПараметра.ЗагрузитьЗначения(ЗначениеХранилища);	
	КонецЕсли;
	МассивОтчетов = МассивОтчетов();
	Элементы.ИмяОтчета.СписокВыбора.ЗагрузитьЗначения(МассивОтчетов);
	Если ЗначениеЗаполнено(Запись.ИмяОтчета) Тогда
		МассивПараметров.ЗагрузитьЗначения(МассивПараметров(Запись.ИмяОтчета));
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ИмяОтчетаПриИзменении(Элемент)
	МассивПараметров.ЗагрузитьЗначения(МассивПараметров(Запись.ИмяОтчета));
	УстановитьСписокВыбораИмяПараметра();
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьСписокВыбораИмяПараметра();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьСписокВыбораИмяПараметра()
	Элементы.ИмяПараметра.СписокВыбора.ЗагрузитьЗначения(МассивПараметров.ВыгрузитьЗначения());	
КонецПроцедуры

&НаСервереБезКонтекста
Функция МассивОтчетов()
	МассивОтчетов = Новый Массив;
	Для Каждого Отчет Из Метаданные.Отчеты Цикл
		Если НЕ Отчет.ОсновнаяСхемаКомпоновкиДанных = Неопределено Тогда
			МассивОтчетов.Добавить(Отчет.Имя);
		КонецЕсли;
	КонецЦикла;
	Возврат МассивОтчетов;
КонецФункции


&НаСервереБезКонтекста
Функция МассивПараметров(ИмяОтчета)
	МассивПараметров = Новый Массив;
	Попытка
		СКДОтчета = Отчеты[ИмяОтчета].Создать().СхемаКомпоновкиДанных;
		Для Каждого Параметр Из СКДОтчета.Параметры Цикл
			Если Параметр.ОграничениеИспользования Тогда
				МассивПараметров.Добавить(Параметр.Имя);
			КонецЕсли;
		КонецЦикла;
	Исключение
	КонецПопытки;
	Возврат МассивПараметров;
КонецФункции



#КонецОбласти

