
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
    
    ЭтотОбъект.Заголовок = "Список доступности ресурсов";
    
    ПараметрыСписка = ЭтотОбъект.Список.Параметры;
	ПараметрыСписка.УстановитьЗначениеПараметра("ТипЗначенияПубликация", ТипЗнч(Справочники.Публикации.ПустаяСсылка()));
    ПараметрыСписка.УстановитьЗначениеПараметра("ТипЗначенияОборудование", ТипЗнч(Справочники.Оборудование.ПустаяСсылка()));
    ПараметрыСписка.УстановитьЗначениеПараметра("ТипЗначенияОбъектКонтроля", ТипЗнч(Справочники.ОбъектыКонтроля.ПустаяСсылка()));
    ПараметрыСписка.УстановитьЗначениеПараметра("ТипЗначенияАгентКИП", ТипЗнч(Справочники.АгентыКИП.ПустаяСсылка()));
    ПараметрыСписка.УстановитьЗначениеПараметра("Доступен", "Доступен");
    ПараметрыСписка.УстановитьЗначениеПараметра("НеДоступен", "Недоступен");
    ПараметрыСписка.УстановитьЗначениеПараметра("НетДанныхПоДоступности", "Нет данных по доступности");
    ПараметрыСписка.УстановитьЗначениеПараметра("Публикация", "Публикация");
    ПараметрыСписка.УстановитьЗначениеПараметра("ИнформационнаяБаза", "Информационная база");
    ПараметрыСписка.УстановитьЗначениеПараметра("Оборудование", "Оборудование (ping)");
    ПараметрыСписка.УстановитьЗначениеПараметра("КонтрольПодключений", Справочники.ВидыКонтрольныхПроцедур.КонтрольПодключений());	
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	ЭтотОбъект.Список.Отбор.Элементы.Очистить();
	
	Для Каждого ТекЭлемент Из ВыбранноеЗначение.Элементы Цикл
		НовЭлемент = ЭтотОбъект.Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		
		НовЭлемент.ЛевоеЗначение = ТекЭлемент.ЛевоеЗначение;
		НовЭлемент.Использование = ТекЭлемент.Использование;
		НовЭлемент.ПравоеЗначение = ТекЭлемент.ПравоеЗначение;
	КонецЦикла;
	
	СохранитьОтбор(НастройкаОтбораСписка, ЭтотОбъект.Список.Отбор);	
	
	ЭтотОбъект.ОтборПредставление = Строка(ЭтотОбъект.Список.Отбор);
	
	ЭтотОбъект.Элементы.Список.Обновить();
    
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
    
    Если ЗначениеЗаполнено(НастройкаОтбораСписка) Тогда
		ЭтотОбъект.Список.Отбор.Элементы.Очистить();
		
		НовыйОтбор = ВосстановитьОтбор(НастройкаОтбораСписка);
		Для Каждого ТекЭлемент Из НовыйОтбор.Элементы Цикл
			НовЭлемент = ЭтотОбъект.Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			НовЭлемент.ЛевоеЗначение = ТекЭлемент.ЛевоеЗначение;
			НовЭлемент.Использование = ТекЭлемент.Использование;
			НовЭлемент.ВидСравнения = ТекЭлемент.ВидСравнения;
			НовЭлемент.ПравоеЗначение = ТекЭлемент.ПравоеЗначение;
		КонецЦикла;
		
		ЭтотОбъект.ОтборПредставление = Строка(ЭтотОбъект.Список.Отбор);

    КонецЕсли;
    
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ДобавитьРесурс(Команда)
	ОткрытьФорму("Справочник.Публикации.Форма.ФормаЭлемента");
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьРесурсПинг(Команда)
	ОткрытьФорму("Справочник.Оборудование.Форма.ФормаЭлемента");
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьПубликации(Команда)
	ОткрытьФорму("Справочник.Публикации.ФормаСписка");
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьОборудование(Команда)
	ОткрытьФорму("Справочник.Оборудование.ФормаСписка");
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьИнформационныеБазы(Команда)
	ОткрытьФорму("Справочник.КонтрольныеПроцедуры.Форма.ФормаСпискаКонтрольПодключений");
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьДанные(Команда)
	Если ТипЗнч(ЭтотОбъект.Элементы.Список.ТекущиеДанные.Ресурс) = Тип("СправочникСсылка.ОбъектыКонтроля") Тогда
		ПараметрыОткрытия = Новый Структура("ИнформационнаяБаза", ЭтотОбъект.Элементы.Список.ТекущиеДанные.Ресурс);
		ОткрытьФорму("РегистрСведений.ДоступностьИнформационныхБаз.Форма.ФормаСписка", ПараметрыОткрытия);	
	ИначеЕсли ТипЗнч(ЭтотОбъект.Элементы.Список.ТекущиеДанные.Ресурс) = Тип("СправочникСсылка.Оборудование") Тогда
		ПараметрыОткрытия = Новый Структура("РабочийСервер", ЭтотОбъект.Элементы.Список.ТекущиеДанные.Ресурс);
		ОткрытьФорму("РегистрСведений.ДоступностьОборудования.Форма.ФормаСписка", ПараметрыОткрытия);
	Иначе
		ПараметрыОткрытия = Новый Структура("Ресурс", ЭтотОбъект.Элементы.Список.ТекущиеДанные.Ресурс);
		ОткрытьФорму("РегистрСведений.ДоступностьСайта.Форма.ФормаСписка", ПараметрыОткрытия);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОтборСписка(Команда)
	ОткрытьОтбор();
КонецПроцедуры

&НаКлиенте
Процедура СнятьОтбор(Команда)
	
	ЭтотОбъект.Список.Отбор.Элементы.Очистить();
	НастройкаОтбораСписка = "";
	ЭтотОбъект.ОтборПредставление = Строка(ЭтотОбъект.Список.Отбор);
	ЭтотОбъект.Элементы.Список.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЗначенияВыбранным(Команда)
    
    ДанныеДляРедактирования = Новый Массив;
    
    Для Каждого ТекСтрока Из ЭтотОбъект.Элементы.Список.ВыделенныеСтроки Цикл
        
        ДанныеСтроки = ЭтотОбъект.Элементы.Список.ДанныеСтроки(ТекСтрока);
        ДанныеДляРедактирования.Добавить(ДанныеСтроки.Ресурс);
        
    КонецЦикла;
    
    
    ПараметрыОткрытия = Новый Структура("Ресурсы", ДанныеДляРедактирования);
    
    ТекДанные = ЭтотОбъект.Элементы.Список.ТекущиеДанные;
    ПараметрыОткрытия.Вставить("ВыполнятьКонтроль", ТекДанные.ВыполнятьКонтроль);
    ПараметрыОткрытия.Вставить("Таймаут", ТекДанные.Таймаут);
    ПараметрыОткрытия.Вставить("ПериодичностьКонтроля", ТекДанные.ПериодичностьКонтроля);
    ПараметрыОткрытия.Вставить("ПериодКонтроля", ТекДанные.ПериодКонтроля);
    ПараметрыОткрытия.Вставить("МинимальныйПроцентДоступности", ТекДанные.МинимальныйПроцентДоступности);
    ПараметрыОткрытия.Вставить("ДопустимоНетДанных", ТекДанные.ДопустимоНетДанных);
    
    ОписаниеОповещения = Новый ОписаниеОповещения("УстановитьЗначенияВыбраннымЗакрытие", ЭтотОбъект);
    ОткрытьФорму("РегистрСведений.ДоступностьСайта.Форма.ФормаУстановитьЗначенияВыбранным", ПараметрыОткрытия,,,,,ОписаниеОповещения,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
    
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Если ТипЗнч(Элемент.ДанныеСтроки(ВыбраннаяСтрока).Ресурс) = Тип("СправочникСсылка.ОбъектыКонтроля") Тогда
		КонтрольнаяПроцедураСсылка = ПолучитьКонтрольнуюПроцедуруПодключения(Элемент.ДанныеСтроки(ВыбраннаяСтрока).Ресурс);
		ПараметрыФормы = Новый Структура("Ключ", КонтрольнаяПроцедураСсылка);
        ПараметрыФормы.Вставить("Источник", Элемент.ДанныеСтроки(ВыбраннаяСтрока).Источник);
		ОткрытьФорму("Справочник.КонтрольныеПроцедуры.Форма.ФормаЭлементаКонтрольПодключений", ПараметрыФормы);
		СтандартнаяОбработка = Ложь;
	ИначеЕсли ТипЗнч(Элемент.ДанныеСтроки(ВыбраннаяСтрока).Ресурс) = Тип("СправочникСсылка.Оборудование") Тогда
		ПараметрыФормы = Новый Структура("Ключ", Элемент.ДанныеСтроки(ВыбраннаяСтрока).Ресурс);
		ОткрытьФорму("Справочник.Оборудование.ФормаОбъекта", ПараметрыФормы);
		СтандартнаяОбработка = Ложь;
	Иначе
		ПараметрыФормы = Новый Структура("Ключ", Элемент.ДанныеСтроки(ВыбраннаяСтрока).Ресурс);
		ОткрытьФорму("Справочник.Публикации.Форма.ФормаЭлемента", ПараметрыФормы);
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВыполнитьПроверкуНемедленно(Команда)
	
	ВыполнитьПроверкуНемедленноНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьПроверкуНемедленноНаСервере ()
	
	ПубликацииКонтрольДоступности.ВыполнитьПроверкуПубликаций();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьКонтрольнуюПроцедуруПодключения(ОбъектКонтроляСсылка)
	Возврат Справочники.ОбъектыКонтроля.ПолучитьКонтрольПодключений(ОбъектКонтроляСсылка);
КонецФункции

&НаКлиенте
Процедура ОтборПредставлениеНажатие(Элемент, СтандартнаяОбработка)
	ОткрытьОтбор();
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьОтбор()
	ПараметрыОткрытия = Новый Структура("Отбор", ЭтотОбъект.Список.Отбор);
	ОткрытьФорму("РегистрСведений.ДоступностьСайта.Форма.ФормаБыстрогоОтбора", ПараметрыОткрытия, ЭтотОбъект);
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СохранитьОтбор(НастройкаОтбораСписка, Знач Отбор)
	
	Поток = Новый ЗаписьXML();
	Поток.УстановитьСтроку();
	СериализаторXDTO.ЗаписатьXML(Поток, Отбор);
	НастройкаОтбораСписка = Поток.Закрыть();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ВосстановитьОтбор(НастройкаОтбораСписка)
	
	Поток = Новый ЧтениеXML();
	Поток.УстановитьСтроку(НастройкаОтбораСписка);
	НовыйОтбор = СериализаторXDTO.ПрочитатьXML(Поток);
	Поток.Закрыть();
	
	Возврат НовыйОтбор;
	
КонецФункции

&НаКлиенте
Процедура ДобавитьРесурсИнформационнуюБазу(Команда)
	ОткрытьФорму("Справочник.КонтрольныеПроцедуры.Форма.ФормаЭлементаКонтрольПодключений");
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЗначенияВыбраннымЗакрытие(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
    
    Если РезультатЗакрытия = "Обновить" Тогда
        ЭтотОбъект.Элементы.Список.Обновить();
    КонецЕсли;
        
КонецПроцедуры

#КонецОбласти









