
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
    
    ЭтотОбъект.ВыполнятьКонтроль = ЭтотОбъект.Параметры.ВыполнятьКонтроль;
    
    ЭтотОбъект.Таймаут = ЭтотОбъект.Параметры.Таймаут;
    ЭтотОбъект.ТаймаутУстановить = Истина;
    
    ЭтотОбъект.ПериодКонтроля = ЭтотОбъект.Параметры.ПериодКонтроля;
    ЭтотОбъект.ПериодКонтроляУстановить = Истина;
    
    ЭтотОбъект.ПериодичностьКонтроля = ЭтотОбъект.Параметры.ПериодичностьКонтроля;
    ЭтотОбъект.ПериодичностьКонтроляУстановить = Истина;
    
    ЭтотОбъект.МинимальныйПроцентДоступности = ЭтотОбъект.Параметры.МинимальныйПроцентДоступности;
    ЭтотОбъект.МинимальныйПроцентДоступностиУстановить = Истина;
    
    ЭтотОбъект.ДопустимоНетДанных = ЭтотОбъект.Параметры.ДопустимоНетДанных;
    ЭтотОбъект.ДопустимоНетДанныхУстановить = Истина;
    
    Для Каждого ТекРесурс Из ЭтотОбъект.Параметры.Ресурсы Цикл
        
        НовСтрока = ЭтотОбъект.Ресурсы.Добавить();
        НовСтрока.Ресурс = ТекРесурс;
        
    КонецЦикла;
    
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВсем(Команда)
    
    УстановитьДоступностьВсем(Истина);
    ИзменитьДоступность();
    
КонецПроцедуры

&НаКлиенте
Процедура СнятьВсем(Команда)
    
    УстановитьДоступностьВсем(Ложь);
    ИзменитьДоступность();
    
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Записать(Команда)
    
    ЗаписатьНаСервере();
    ЭтотОбъект.Закрыть("Обновить");
    
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ТаймаутУстановитьПриИзменении(Элемент)
    ИзменитьДоступность();
КонецПроцедуры

&НаКлиенте
Процедура ПериодКонтроляУстановитьПриИзменении(Элемент)
    ИзменитьДоступность();
КонецПроцедуры

&НаКлиенте
Процедура ПериодичностьКонтроляУстановитьПриИзменении(Элемент)
    ИзменитьДоступность();
КонецПроцедуры

&НаКлиенте
Процедура МинимальныйПроцентДоступностиУстановитьПриИзменении(Элемент)
    ИзменитьДоступность();
КонецПроцедуры

&НаКлиенте
Процедура ДопустимоНетДанныхУстановитьПриИзменении(Элемент)
    ИзменитьДоступность();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаписатьНаСервере()
    
    Для Каждого ТекРесурс Из Ресурсы Цикл
        
        Если ТипЗнч(ТекРесурс.Ресурс) = Тип("СправочникСсылка.Публикации") Тогда
            ЗаписатьПубликацию(ТекРесурс.Ресурс);
        ИначеЕсли ТипЗнч(ТекРесурс.Ресурс) = Тип("СправочникСсылка.Оборудование") Тогда
            ЗаписатьРабочийСервер(ТекРесурс.Ресурс);
        ИначеЕсли ТипЗнч(ТекРесурс.Ресурс) = Тип("СправочникСсылка.ОбъектыКонтроля") Тогда
            ЗаписатьОбъектКонтроля(ТекРесурс.Ресурс);
        КонецЕсли;
                
    КонецЦикла;
        
КонецПроцедуры

&НаСервере
Процедура ЗаписатьПубликацию(Публикация)
    
    СпрОбъект = Публикация.ПолучитьОбъект();
    СпрОбъект.ВыполнятьКонтроль = ЭтотОбъект.ВыполнятьКонтроль;
    Если ЭтотОбъект.ТаймаутУстановить Тогда
        СпрОбъект.Таймаут = ЭтотОбъект.Таймаут;
    КонецЕсли;
    Если ЭтотОбъект.ПериодКонтроляУстановить Тогда
        СпрОбъект.ПериодКонтроля = ЭтотОбъект.ПериодКонтроля;
    КонецЕсли;
    Если ЭтотОбъект.ПериодичностьКонтроляУстановить Тогда
        СпрОбъект.ПериодичностьКонтроля = ЭтотОбъект.ПериодичностьКонтроля;
    КонецЕсли;
    Если ЭтотОбъект.МинимальныйПроцентДоступностиУстановить Тогда
        СпрОбъект.МинимальныйПроцентДоступности = ЭтотОбъект.МинимальныйПроцентДоступности;
    КонецЕсли;
    Если ЭтотОбъект.ДопустимоНетДанныхУстановить Тогда
        СпрОбъект.ДопустимоНетДанных = ЭтотОбъект.ДопустимоНетДанных;
    КонецЕсли;
    СпрОбъект.Записать();
    
КонецПроцедуры

&НаСервере
Процедура ЗаписатьРабочийСервер(РабочийСервер)
    
    СпрОбъект = РабочийСервер.ПолучитьОбъект();
    СпрОбъект.ПроверятьДоступность = ЭтотОбъект.ВыполнятьКонтроль;
    
    Если ЭтотОбъект.ПериодКонтроляУстановить Тогда
        СпрОбъект.ПериодКонтроля = ЭтотОбъект.ПериодКонтроля;
    КонецЕсли;
    Если ЭтотОбъект.МинимальныйПроцентДоступностиУстановить Тогда
        СпрОбъект.МинимальныйПроцентДоступности = ЭтотОбъект.МинимальныйПроцентДоступности;
    КонецЕсли;
    Если ЭтотОбъект.ДопустимоНетДанныхУстановить Тогда
        СпрОбъект.ДопустимоНетДанных = ЭтотОбъект.ДопустимоНетДанных;
    КонецЕсли;
    
    СпрОбъект.Записать();
    
КонецПроцедуры

&НаСервере
Процедура ЗаписатьОбъектКонтроля(ОбъектКонтроля)
    
    Запрос = Новый Запрос;
    Запрос.Текст = "
    |ВЫБРАТЬ
    |   КонтрольныеПроцедуры.Ссылка КАК КонтрольнаяПроцедура
    |ИЗ
    |   Справочник.КонтрольныеПроцедуры КАК КонтрольныеПроцедуры
    |ГДЕ
    |   КонтрольныеПроцедуры.ОбъектКонтроля = &ОбъектКонтроля
    |   И КонтрольныеПроцедуры.Владелец = &Владелец
    |";
    
    Запрос.УстановитьПараметр("ОбъектКонтроля", ОбъектКонтроля);
    Запрос.УстановитьПараметр("Владелец", Справочники.ВидыКонтрольныхПроцедур.НайтиПоНаименованию("Контроль подключений"));
    
    Результат = Запрос.Выполнить();
    Если НЕ Результат.Пустой() Тогда
        Выборка = Результат.Выбрать();
        Выборка.Следующий();
        
        КонтрольнаяПроцедура = Выборка.КонтрольнаяПроцедура;
        КонтрольнаяПроцедураОбъект = КонтрольнаяПроцедура.ПолучитьОбъект();
        КонтрольнаяПроцедураОбъект.Выполнять = ЭтотОбъект.ВыполнятьКонтроль;
        КонтрольнаяПроцедураОбъект.Записать();
        
        НастройкиКонтрольнойПроцедуры = РегистрыСведений.НастройкиКонтрольПодключений.СоздатьМенеджерЗаписи();
        НастройкиКонтрольнойПроцедуры.КонтрольнаяПроцедура = КонтрольнаяПроцедура;
        НастройкиКонтрольнойПроцедуры.Прочитать();
        
        НастройкиКонтрольнойПроцедуры.КонтрольнаяПроцедура = КонтрольнаяПроцедура;
        Если ЭтотОбъект.ТаймаутУстановить Тогда
            НастройкиКонтрольнойПроцедуры.Таймаут = ЭтотОбъект.Таймаут;
        КонецЕсли;
        Если ЭтотОбъект.ПериодКонтроляУстановить Тогда
            НастройкиКонтрольнойПроцедуры.ПериодКонтроля = ЭтотОбъект.ПериодКонтроля;
        КонецЕсли;
        Если ЭтотОбъект.МинимальныйПроцентДоступностиУстановить Тогда
            НастройкиКонтрольнойПроцедуры.МинимальныйПроцентДоступности = ЭтотОбъект.МинимальныйПроцентДоступности;
        КонецЕсли;
        Если ЭтотОбъект.ДопустимоНетДанныхУстановить Тогда
            НастройкиКонтрольнойПроцедуры.ДопустимоНетДанных = ЭтотОбъект.ДопустимоНетДанных;
        КонецЕсли;
        НастройкиКонтрольнойПроцедуры.Записать();
    КонецЕсли;
    
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьДоступность()
    
    ЭтотОбъект.Элементы.Таймаут.Доступность = ЭтотОбъект.ТаймаутУстановить;
    ЭтотОбъект.Элементы.ПериодКонтроля.Доступность = ЭтотОбъект.ПериодКонтроляУстановить;
    ЭтотОбъект.Элементы.ПериодичностьКонтроля.Доступность = ЭтотОбъект.ПериодичностьКонтроляУстановить;
    ЭтотОбъект.Элементы.МинимальныйПроцентДоступности.Доступность = ЭтотОбъект.МинимальныйПроцентДоступностиУстановить;
    ЭтотОбъект.Элементы.ДопустимоНетДанных.Доступность = ЭтотОбъект.ДопустимоНетДанныхУстановить;    
    
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступностьВсем(ЗначениеДоступности)
    
    ЭтотОбъект.ТаймаутУстановить = ЗначениеДоступности;
    ЭтотОбъект.ПериодКонтроляУстановить = ЗначениеДоступности;
    ЭтотОбъект.ПериодичностьКонтроляУстановить = ЗначениеДоступности;
    ЭтотОбъект.МинимальныйПроцентДоступностиУстановить = ЗначениеДоступности;
    ЭтотОбъект.ДопустимоНетДанныхУстановить = ЗначениеДоступности;
    
КонецПроцедуры

#КонецОбласти
