////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	ИнформационнаяБаза = Параметры.ИнформационнаяБаза;
	СписокЗапрещенныхОпераций = Параметры.СписокЗапрещенныхОпераций;
	МассивИдентификаторов = Новый Массив;
	Для Каждого Идентификатор Из СписокЗапрещенныхОпераций Цикл
		МассивИдентификаторов.Добавить(Идентификатор.Значение);
	КонецЦикла;
	
	СписокКлючевыхОпераций.Параметры.УстановитьЗначениеПараметра("ИнформационнаяБаза", ИнформационнаяБаза);
	СписокКлючевыхОпераций.Параметры.УстановитьЗначениеПараметра(
		"ЗапрещенныеУникальныеИдентификаторы", МассивИдентификаторов
	);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦЫ ФОРМЫ СписокКлючевыхОпераций

&НаКлиенте
Процедура СписокКлючевыхОперацийВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	РезультатВыбора = Неопределено;
	
	НастройкиБратьИзБазы = Параметры.НастройкиБратьИзБазы;
	ТД = Элементы.СписокКлючевыхОпераций.ТекущиеДанные;
	Если ТД <> Неопределено Тогда
		РезультатВыбора = Новый Структура("УникальныйИдентификатор, Имя, Приоритет, ЦелевоеВремя");
		РезультатВыбора.УникальныйИдентификатор = ТД.УникальныйИдентификатор;
		РезультатВыбора.Имя = ТД.Имя;
		
		РезультатВыбора.Приоритет = ТД["Приоритет" + ?(НастройкиБратьИзБазы, "ВИБ", "")];
		РезультатВыбора.ЦелевоеВремя = ТД["ЦелевоеВремя" + ?(НастройкиБратьИзБазы, "ВИБ", "")];
		
	КонецЕсли;
	
	ЭтотОбъект.Закрыть(РезультатВыбора);
	
КонецПроцедуры
