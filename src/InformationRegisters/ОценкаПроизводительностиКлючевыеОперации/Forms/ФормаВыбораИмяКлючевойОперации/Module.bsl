&НаКлиенте
Процедура ЗаполнитьВыбранныеИмена()
	Если НЕ ЭтотОбъект.МножественныйВыбор Тогда
		ЭтотОбъект.ИменаКлючевыхОперацийВыбранные.Очистить();
	КонецЕсли;
		
	ВыбранныеИмена = Новый Массив;
	Для Каждого ТекСтрока Из ЭтотОбъект.Элементы.СписокИменаКлючевыхОпераций.ВыделенныеСтроки Цикл
		ПараметрыОтбора = Новый Структура("Имя", Элементы.СписокИменаКлючевыхОпераций.ДанныеСтроки(ТекСтрока).Имя); 
		ЭлементыНашел = ИменаКлючевыхОперацийВыбранные.НайтиСтроки(ПараметрыОтбора);
		
		Если ЭлементыНашел.Количество() = 0 Тогда
			НовСтрока = ИменаКлючевыхОперацийВыбранные.Добавить();
			НовСтрока.Имя = Элементы.СписокИменаКлючевыхОпераций.ДанныеСтроки(ТекСтрока).Имя;
		КонецЕсли;
	КонецЦикла;
	
	ЭтотОбъект.Элементы.СписокИменаКлючевыхОпераций.ВыделенныеСтроки.Очистить();
КонецПроцедуры

&НаКлиенте
Процедура СписокИменаКлючевыхОперацийВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	ЗаполнитьВыбранныеИмена();	
	СтандартнаяОбработка = Ложь;
	
	Если НЕ МножественныйВыбор Тогда
		ВыбратьИмена(ЭтотОбъект.Команды.ВыбратьИмена);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьИмена(Команда)
	ВыбранныеИмена = Новый Массив;
	Для Каждого ТекСтрока Из ЭтотОбъект.ИменаКлючевыхОперацийВыбранные Цикл
		ВыбранныеИмена.Добавить(ТекСтрока.Имя);	
	КонецЦикла;
	
	ОповеститьОВыборе(ВыбранныеИмена);
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьИмя(Команда)
	ЗаполнитьВыбранныеИмена();
	ВыбратьИмена(Команда);
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЭтотОбъект.ЦентрКонтроляКачества = Справочники.ОбъектыКонтроля.НайтиПоНаименованию("Центр Контроля Качества");
	ЭтотОбъект.ЦентрМониторинга = Справочники.ОбъектыКонтроля.НайтиПоНаименованию("Центр мониторинга");
	
	Если Константы.ОтображатьВнутренниеЗамерыПроизводительности.Получить() Тогда
		ЭтотОбъект.СписокИменаКлючевыхОпераций.Параметры.УстановитьЗначениеПараметра("ЦентрКонтроляКачества", Неопределено);
		ЭтотОбъект.СписокИменаКлючевыхОпераций.Параметры.УстановитьЗначениеПараметра("ЦентрМониторинга", Неопределено);
	Иначе
		ЭтотОбъект.СписокИменаКлючевыхОпераций.Параметры.УстановитьЗначениеПараметра("ЦентрКонтроляКачества", ЭтотОбъект.ЦентрКонтроляКачества);
		ЭтотОбъект.СписокИменаКлючевыхОпераций.Параметры.УстановитьЗначениеПараметра("ЦентрМониторинга", ЭтотОбъект.ЦентрМониторинга);	
	КонецЕсли;
		
	Если ЭтотОбъект.Параметры.Свойство("Ключ") И ЭтотОбъект.Параметры.Ключ <> Неопределено Тогда
		Если ТипЗнч(ЭтотОбъект.Параметры.Ключ) = Тип("Строка") Тогда
			НовСтрока = ИменаКлючевыхОперацийВыбранные.Добавить();
			НовСтрока.Имя = ЭтотОбъект.Параметры.Ключ;
		ИначеЕсли ТипЗнч(ЭтотОбъект.Параметры.Ключ) = Тип("СписокЗначений") Тогда
			Для Каждого ТекЗначение Из ЭтотОбъект.Параметры.Ключ Цикл
				НовСтрока = ИменаКлючевыхОперацийВыбранные.Добавить();
				НовСтрока.Имя = ТекЗначение;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Если ЭтотОбъект.Параметры.Свойство("МножественныйВыбор") И ЭтотОбъект.Параметры.МножественныйВыбор <> Неопределено Тогда
		Если ЭтотОбъект.Параметры.МножественныйВыбор Тогда
			ЭтотОбъект.Элементы.СписокИменаКлючевыхОпераций.РежимВыделения = РежимВыделенияТаблицы.Множественный;
		Иначе
			ЭтотОбъект.Элементы.СписокИменаКлючевыхОпераций.РежимВыделения = РежимВыделенияТаблицы.Одиночный;
			ЭтотОбъект.Элементы.ГруппаВыбранные.Видимость = Ложь;
			ЭтотОбъект.Элементы.СписокИменаКлючевыхОперацийВыбратьИмя.Видимость = Истина;
			ЭтотОбъект.Элементы.СписокИменаКлючевыхОперацийВыбратьИмя.КнопкаПоУмолчанию = Истина;
			
			ЭтотОбъект.Элементы.СписокИменаКлючевыхОпераций.ТекущаяСтрока = ПолучитьИндексВыбраннойКлючевойОперации(СписокИменаКлючевыхОпераций, "Имя", ЭтотОбъект.Параметры.Ключ);
		КонецЕсли;
		
		ЭтотОбъект.МножественныйВыбор = ЭтотОбъект.Параметры.МножественныйВыбор;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьИндексВыбраннойКлючевойОперации(ДинамическийСписок, ИменаПолей, Значение)
 	ДанныеДинамическогоСписка = ФункцииСКД.ПолучитьТаблицуЗначенийДинамическогоСписка(ДинамическийСписок, ИменаПолей);
	
	ПараметрыОтбора = Новый Структура("Имя", Значение);
	СтрокиНашел = ДанныеДинамическогоСписка.НайтиСтроки(ПараметрыОтбора);
	
	Если СтрокиНашел.Количество() = 1 Тогда
		Возврат ДанныеДинамическогоСписка.Индекс(СтрокиНашел[0]) + 1;
	КонецЕсли;
КонецФункции

&НаКлиенте
Процедура ИменаКлючевыхОперацийВыбранныеПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	ЗаполнитьВыбранныеИмена();
КонецПроцедуры

&НаКлиенте
Процедура ИменаКлючевыхОперацийВыбранныеПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьВнутренниеОперации(Команда)
	
	ЭтотОбъект.СписокИменаКлючевыхОпераций.Параметры.УстановитьЗначениеПараметра("ЦентрКонтроляКачества", Неопределено);
	ЭтотОбъект.СписокИменаКлючевыхОпераций.Параметры.УстановитьЗначениеПараметра("ЦентрМониторинга", Неопределено);
	
	ЭтотОбъект.Элементы.СписокИменаКлючевыхОпераций.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура СкрытьВнутренниеОперации(Команда)
	
	ЭтотОбъект.СписокИменаКлючевыхОпераций.Параметры.УстановитьЗначениеПараметра("ЦентрКонтроляКачества", ЭтотОбъект.ЦентрКонтроляКачества);
	ЭтотОбъект.СписокИменаКлючевыхОпераций.Параметры.УстановитьЗначениеПараметра("ЦентрМониторинга", ЭтотОбъект.ЦентрМониторинга);
	
	ЭтотОбъект.Элементы.СписокИменаКлючевыхОпераций.Обновить();
	
КонецПроцедуры


