#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
    
#Область ПрограммныйИнтерфейс

Функция ПолучитьЗначения(Оборудование) Экспорт
    
    Запрос = Новый Запрос;
    
    Запрос.Текст = "
    |ВЫБРАТЬ
    |   СчетчикПроизводительности,
    |   Значение,
    |   ОписаниеОшибки
    |ИЗ
    |   РегистрСведений.ТекущиеЗначенияПроизводительности КАК РегСвТекЗнач
    |ГДЕ
    |   Сервер = &Оборудование
    |";
    
    Запрос.УстановитьПараметр("Оборудование", Оборудование);
    
    Результат = Запрос.Выполнить();
    
    СчетчикиЗначения = Новый Соответствие;
    
    Выборка = Результат.Выбрать();
    Пока Выборка.Следующий() Цикл
        
        СчетчикиЗначения.Вставить(Выборка.СчетчикПроизводительности, Новый Структура("Значение, ОписаниеОшибки", Выборка.Значение, Выборка.ОписаниеОшибки));
        
    КонецЦикла;
    
    Возврат СчетчикиЗначения;        
    
КонецФункции

Процедура Очистить(Оборудование) Экспорт
    
    Запрос = Новый Запрос;
    
    Запрос.Текст = "
    |ВЫБРАТЬ ПЕРВЫЕ 1
    |   Сервер
    |ИЗ
    |   РегистрСведений.ТекущиеЗначенияПроизводительности КАК РегСвТекЗнач
    |ГДЕ
    |   Сервер = &Оборудование
    |";
    
    Запрос.УстановитьПараметр("Оборудование", Оборудование);
    
    Результат = Запрос.Выполнить();
    
    Если НЕ Результат.Пустой() Тогда
        НаборЗаписей = СоздатьНаборЗаписей();
        НаборЗаписей.Отбор.Сервер.Установить(Оборудование);
        НаборЗаписей.Записать(Истина);
    КонецЕсли;
        
КонецПроцедуры

#КонецОбласти

#КонецЕсли