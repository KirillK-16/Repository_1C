
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ПараметрыФормы = Новый Структура("Ключ", ПолучитьКлючЗаписиНаСервере());
	ОткрытьФорму("РегистрСведений.ПараметрыКластеров.Форма.ФормаЗначенияПоУмолчанию", ПараметрыФормы);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьКлючЗаписиНаСервере()
	
	КластерСерверов1С = Справочники.ВидыОбъектовКонтроля.КластерСерверов1С.Ссылка;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ОбъектКонтроля
	|ИЗ
	|	РегистрСведений.ПараметрыКластеров
	|ГДЕ
	|	ОбъектКонтроля = &КластерСерверов1С
	|";
	Запрос.УстановитьПараметр("КластерСерверов1С", КластерСерверов1С);
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		МенеджерЗаписи = РегистрыСведений.ПараметрыКластеров.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.ОбъектКонтроля = КластерСерверов1С;
		МенеджерЗаписи.Записать(Истина);
	КонецЕсли;
		
	ЗначенияКлюча = Новый Структура;
	ЗначенияКлюча.Вставить("ОбъектКонтроля", Справочники.ВидыОбъектовКонтроля.КластерСерверов1С.Ссылка); 
	
	КлючЗаписи = РегистрыСведений.ПараметрыКластеров.СоздатьКлючЗаписи(ЗначенияКлюча);
	
	Возврат КлючЗаписи;
	
КонецФункции

