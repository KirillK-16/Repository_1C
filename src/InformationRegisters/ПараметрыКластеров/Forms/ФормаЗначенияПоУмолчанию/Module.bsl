
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ЭтотОбъект.АдресСервераАдминистрирования = Запись.АдресСервераАдминистрирования + ":" + Формат(Запись.ПортСервераАдминистрирования, "ЧГ=0");
	ЭтотОбъект.АгентКластера = Запись.АдресКластера + ":" + Формат(Запись.ПортАгентаКластера, "ЧГ=0");
	
	ЗаполнитьОтветственных();
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьОтветственных()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ВидКонтрольнойПроцедуры,
	|	РольПользователя
	|ИЗ
	|	РегистрСведений.ОтветственныеЗаНастройкуПроцедур
	|ГДЕ
	|	ОбъектКонтроля = &ВидОбъектаКонтроля
	|	И ВидКонтрольнойПроцедуры В (&ВидыКонтрольныхПроцедур)
	|";
	
	ВидыКонтрольныхПроцедур = Новый Массив;
	КонтрольПотребленияПамяти = Справочники.ВидыКонтрольныхПроцедур.НайтиПоНаименованию("Контроль потребления памяти");
	ВидыКонтрольныхПроцедур.Добавить(КонтрольПотребленияПамяти);
	
	Запрос.УстановитьПараметр("ВидОбъектаКонтроля", Справочники.ВидыОбъектовКонтроля.КластерСерверов1С);
	Запрос.УстановитьПараметр("ВидыКонтрольныхПроцедур", ВидыКонтрольныхПроцедур);
	
	КонтрольныеПроцедуры = Новый Соответствие;
	КонтрольныеПроцедуры.Вставить(КонтрольПотребленияПамяти, Неопределено);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		КонтрольныеПроцедуры[Выборка.ВидКонтрольнойПроцедуры] = Выборка.РольПользователя;
	КонецЦикла;
	
	Для Каждого ТекКонтрольнаяПроцедура Из КонтрольныеПроцедуры Цикл
		НовСтрока = ЭтотОбъект.ОтветственныеЗаНастройкуКонтрольныхПроцедур.Добавить();
		НовСтрока.ВидКонтрольнойПроцедуры = ТекКонтрольнаяПроцедура.Ключ;
		НовСтрока.РольПользователя = ТекКонтрольнаяПроцедура.Значение;
	КонецЦикла;
			
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	НастройкиСервераАдминистрирования = НастройкиСервераАдминистрирования(ЭтотОбъект.АдресСервераАдминистрирования);
	Запись.АдресСервераАдминистрирования = НастройкиСервераАдминистрирования.Сервер;
	Запись.ПортСервераАдминистрирования =  НастройкиСервераАдминистрирования.Порт;
	
	НастройкиАгентаКластера = НастройкиАгентаКластера(ЭтотОбъект.АгентКластера);
	Запись.АдресКластера = НастройкиАгентаКластера.Сервер;
	Запись.ПортАгентаКластера = НастройкиАгентаКластера.Порт;
	
КонецПроцедуры

&НаКлиенте
Функция НастройкиСервераАдминистрирования(Настройки)
	
	Результат = Новый Структура;
	
	НастройкиМассив = СтрРазделить(Настройки, ":", Истина);
	Если НастройкиМассив.Количество() = 1 Тогда
		Результат.Вставить("Сервер", НастройкиМассив[0]);
		Результат.Вставить("Порт", 1545);
	ИначеЕсли НастройкиМассив.Количество() = 2 Тогда
		Результат.Вставить("Сервер", НастройкиМассив[0]);
		Попытка
			Результат.Вставить("Порт", Число(НастройкиМассив[1]));
		Исключение
			Результат.Вставить("Порт", 0);
		КонецПопытки;
	Иначе
		ВызватьИсключение "Не верные настройки сервера администрирования.";
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Функция НастройкиАгентаКластера(Настройки)
	
	Результат = Новый Структура;
	
	НастройкиМассив = СтрРазделить(Настройки, ":", Истина);
	Если НастройкиМассив.Количество() = 1 Тогда
		Результат.Вставить("Сервер", НастройкиМассив[0]);
		Результат.Вставить("Порт", 1540);
	ИначеЕсли НастройкиМассив.Количество() = 2 Тогда
		Результат.Вставить("Сервер", НастройкиМассив[0]);
		Попытка
			Результат.Вставить("Порт", Число(НастройкиМассив[1]));
		Исключение
			Результат.Вставить("Порт", 0);
		КонецПопытки;
	Иначе
		Результат.Вставить("Сервер", Неопределено);
		Результат.Вставить("Порт", Неопределено);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Для Каждого ТекСтрока Из ЭтотОбъект.ОтветственныеЗаНастройкуКонтрольныхПроцедур Цикл
		МенеджерЗаписи = РегистрыСведений.ОтветственныеЗаНастройкуПроцедур.СоздатьМенеджерЗаписи();
		МенеджерЗаписи.ОбъектКонтроля = Справочники.ВидыОбъектовКонтроля.КластерСерверов1С;
		МенеджерЗаписи.ВидКонтрольнойПроцедуры = ТекСтрока.ВидКонтрольнойПроцедуры;
		МенеджерЗаписи.РольПользователя = ТекСтрока.РольПользователя;
		МенеджерЗаписи.Записать(Истина);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	ПроверяемыеРеквизиты.Очистить();
КонецПроцедуры
