#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
    
#Область ПрограммныйИнтерфейс

Функция ПрочитатьНастройки(Оборудование) Экспорт
    
    Запрос = Новый Запрос;
    
    Запрос.Текст = "
    |ВЫБРАТЬ
    |   ДатаЗаписиUTC,
    |   ОперативныйРежим,
    |   ЯзыкОС
    |ИЗ
    |   РегистрСведений.ОборудованиеОперативныеНастройки
    |ГДЕ
    |   Оборудование = &Оборудование
    |";
    
    Запрос.УстановитьПараметр("Оборудование", Оборудование);
    
    Результат = Запрос.Выполнить();
    
    Настройки = Новый Структура("ДатаЗаписиUTC, ОперативныйРежим, ЯзыкОС", Дата(1,1,1), Ложь, Перечисления.ЯзыкиСистемы.Английский);
    Если НЕ Результат.Пустой() Тогда
        Выборка = Результат.Выбрать();
        Выборка.Следующий();
        ЗаполнитьЗначенияСвойств(Настройки, Выборка);
    КонецЕсли;
        
    Возврат Настройки;
    
КонецФункции

Процедура ЗаписатьНастройки(Оборудование, Настройки) Экспорт
    
    МенеджерЗаписи = СоздатьМенеджерЗаписи();
    МенеджерЗаписи.Оборудование = Оборудование;
    
    ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Настройки);
    
    МенеджерЗаписи.Записать(Истина);
    
КонецПроцедуры

Процедура ОчиститьНастройки(Оборудование) Экспорт
    
    МенеджерЗаписи = СоздатьМенеджерЗаписи();
    МенеджерЗаписи.Оборудование = Оборудование;
    МенеджерЗаписи.Удалить();
    
КонецПроцедуры

#КонецОбласти
    
#КонецЕсли