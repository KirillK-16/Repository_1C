
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
    
#Область ПрограммныйИнтерфейс

Процедура ЗагрузитьЛицензии(Оборудование, Лицензии) Экспорт
    
    ЛицензииОборудования = Новый Массив;
    
    Для Каждого Лицензия Из Лицензии Цикл
        
        ПараметрыЛицензии = Новый Структура("Наименование, ТипЛицензии", Лицензия["DESCRIPTION"], -1);
        ПараметрыЛицензии.Вставить("Описание", Лицензия["DESCRIPTION"]);
        
        Если Лицензия["SUPPLY_KIND"] = 0 Тогда
            ПараметрыЛицензии.Вставить("Поставка", Перечисления.ПоставкаЛицензии.ПРОФ);
        ИначеЕсли Лицензия["SUPPLY_KIND"] = 1 Тогда
            ПараметрыЛицензии.Вставить("Поставка", Перечисления.ПоставкаЛицензии.КОРП);
        ИначеЕсли Лицензия["SUPPLY_KIND"] = 2 Тогда
            ПараметрыЛицензии.Вставить("Поставка", Перечисления.ПоставкаЛицензии.БЕТА);
        ИначеЕсли Лицензия["SUPPLY_KIND"] = -1 Тогда
            ПараметрыЛицензии.Вставить("Поставка", Перечисления.ПоставкаЛицензии.НеИзвестно);
        КонецЕсли;
        
        ПараметрыЛицензии.Вставить("ВидЛицензии", Лицензия["LICENSE_KIND"]);
        ПараметрыЛицензии.Вставить("Активная", Лицензия["VALIDATE"]);
        ПараметрыЛицензии.Вставить("Файл", Лицензия["FILE"]);
        ПараметрыЛицензии.Вставить("ВладелецЛицензии", Лицензия["OWNER"]);
        
        Если ЗначениеЗаполнено(Лицензия["REG_NUMBER"]) Тогда
            
            ЛицензияСсылка = Справочники.Лицензии.СоздатьЛицензию(Лицензия["REG_NUMBER"], ПараметрыЛицензии);
            
        Иначе
            
            ЛицензияСсылка = Лицензия(Оборудование, Лицензия["FILE"]); 
            
            ПараметрыЛицензии.Удалить("Поставка");
            ПараметрыЛицензии.Удалить("ВидЛицензии");
            ПараметрыЛицензии.Удалить("Файл");
            ПараметрыЛицензии.Удалить("ВладелецЛицензии");
            
        КонецЕсли;
        
        Если ЗначениеЗаполнено(ЛицензияСсылка) Тогда
            
            ПараметрыЛицензии.Удалить("Наименование");
            ПараметрыЛицензии.Удалить("ТипЛицензии");
            
            ЛицензииОборудования.Добавить(ЛицензияСсылка);
            Справочники.Лицензии.ИзменитьЛицензию(ЛицензияСсылка, ПараметрыЛицензии);
        КонецЕсли;
        
    КонецЦикла;
    
    РегистрыСведений.ОборудованиеЛицензии1С.УстановитьЛицензии(Оборудование, ЛицензииОборудования);
    
КонецПроцедуры

Функция Лицензия(Оборудование, ИмяФайла) Экспорт
    
    Запрос = Новый Запрос;
    
    Запрос.Текст = "
    |ВЫБРАТЬ
    |   ОборудованиеЛицензии1С.Лицензия КАК Лицензия
    |ИЗ
    |   РегистрСведений.ОборудованиеЛицензии1С КАК ОборудованиеЛицензии1С
    |ВНУТРЕННЕЕ СОЕДИНЕНИЕ
    |   Справочник.Лицензии КАК Лицензии
    |ПО
    |   Лицензии.Ссылка = ОборудованиеЛицензии1С.Лицензия
    |   И Лицензии.Файл = &Файл
    |ГДЕ
    |   ОборудованиеЛицензии1С.Оборудование = &Оборудование
    |";
    
    Запрос.УстановитьПараметр("Оборудование", Оборудование);
    Запрос.УстановитьПараметр("Файл", ИмяФайла);
    
    Результат = Запрос.Выполнить();
    
    Если Результат.Пустой() Тогда
        Возврат Справочники.Лицензии.ПустаяСсылка();
    Иначе
        Выборка = Результат.Выбрать();
        Выборка.Следующий();
        Возврат Выборка.Лицензия;
    КонецЕсли;
        
КонецФункции

Процедура УстановитьЛицензии(Оборудование, Лицензии) Экспорт
    
    Запрос = Новый Запрос;
    
    Запрос.Текст = "
    |ВЫБРАТЬ
    |   КОЛИЧЕСТВО(*) КАК Количество
    |ИЗ
    |   РегистрСведений.ОборудованиеЛицензии1С
    |ГДЕ
    |   Оборудование = &Оборудование
    |   И Лицензия В (&Лицензии)
    |";
    
    Запрос.УстановитьПараметр("Оборудование", Оборудование);
    Запрос.УстановитьПараметр("Лицензии", Лицензии);
    
    Результат = Запрос.Выполнить();
    
    Выборка = Результат.Выбрать();
    Выборка.Следующий();
    КоличествоЛицензий = Выборка.Количество;
    
    Если КоличествоЛицензий <> Лицензии.Количество() Тогда
        
        НаборЗаписей = СоздатьНаборЗаписей();
        НаборЗаписей.Отбор.Оборудование.Установить(Оборудование);
        
        Для Каждого Лицензия Из Лицензии Цикл
            
            НовЗапись = НаборЗаписей.Добавить();
            НовЗапись.Оборудование = Оборудование;
            НовЗапись.Лицензия = Лицензия;
            
        КонецЦикла;
        
        НаборЗаписей.Записать(Истина);
                
    КонецЕсли;
        
КонецПроцедуры

#КонецОбласти
    
#КонецЕсли