
#Область ОбработчикиСобытийФомы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
    
    МассивСтрок = Новый Массив;
    
    Если ЗначениеЗаполнено(Запись.ИнформационнаяБаза) Тогда
        МассивСтрок.Добавить(Запись.ИнформационнаяБаза);
    КонецЕсли;
    
    Если ЗначениеЗаполнено(Запись.Кластер) Тогда
        МассивСтрок.Добавить(Запись.Кластер);
    КонецЕсли;
    
    ЭтотОбъект.СтрокаПодключения = СтрСоединить(МассивСтрок, Символы.ПС);
    
    ЭтотОбъект.Заголовок = Запись.Инцидент.Наименование;
    
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Оповестить("ОбновитьПанельИнцидентов");
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура История(Команда)
	
	ПараметрыФормы = Новый Структура("Отбор", Новый Структура("Инцидент, ТипИнцидента", Запись.Инцидент, Запись.ТипИнцидента));
	ОткрытьФорму("РегистрСведений.ИнцидентыИстория.ФормаСписка", ПараметрыФормы,,Новый УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьОповещения(Команда)
	СписокПодходящихОповещений = ПолучитьСписокОповещений();
	
	Если СписокПодходящихОповещений.Количество()=0 Тогда
		ПоказатьПредупреждение(,"Нет подходящих оповещений для инцидента");
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура("Отбор", Новый Структура("Ссылка", СписокПодходящихОповещений));
	ОткрытьФорму("Справочник.Оповещения.ФормаСписка", ПараметрыФормы,,ЭтотОбъект.УникальныйИдентификатор);
КонецПроцедуры

&НаКлиенте
Процедура СоздатьОповещение(Команда)
	ПараметрыФормы = Новый Структура("ИнцидентДляЗаполненияФильтра, ТипИнцидента", Запись.Инцидент, Запись.ТипИнцидента);
	ОткрытьФорму("Справочник.Оповещения.ФормаОбъекта", ПараметрыФормы);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПолучитьСписокОповещений()
	
	Результат = Новый Массив();
	
	ПотенциальныеОповещения = ИнцидентыСервер.СписокПотенциальныхОповещений(Запись.ТипИнцидента);
	Для Каждого ТекСсылка из ПотенциальныеОповещения Цикл
		
		Если Справочники.Оповещения.ВыполненыУсловияДляИнцидента(ТекСсылка, Запись) Тогда
			Результат.Добавить(ТекСсылка);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

















