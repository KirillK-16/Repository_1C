#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ПолучитьТекстЗапросаИнициализацииСКД_Оперативное() Экспорт
    
    Возврат "
    |ВЫБРАТЬ
    |   РегСвИнциденты.Инцидент КАК Инцидент,
    |   РегСвИнциденты.ТипИнцидента КАК ТипИнцидента,
    |   Инциденты.Наименование КАК НаименованиеИнцидента,
    |   ТипыИнцидентов.Наименование КАК НаименованиеТипаИнцидента,
    |   Инциденты.ПометкаУдаления КАК ПометкаУдаленияИнцидента,
    |   ТипыИнцидентов.ПометкаУдаления КАК ПометкаУдаленияТипаИнцидента,
    |   ЗНАЧЕНИЕ(Перечисление.УровниИнцидентов.Информация) КАК УровеньИнцидента
    |ИЗ
    |   РегистрСведений.Инциденты КАК РегСвИнциденты
    |ВНУТРЕННЕЕ СОЕДИНЕНИЕ
    |   Справочник.Инциденты КАК Инциденты
    |ПО
    |   Инциденты.Ссылка = РегСвИнциденты.Инцидент
    |ВНУТРЕННЕЕ СОЕДИНЕНИЕ
    |   Справочник.ТипыИнцидентов КАК ТипыИнцидентов
    |ПО
    |   ТипыИнцидентов.Ссылка = РегСвИнциденты.ТипИнцидента
    |";
    
КонецФункции

Функция ПолучитьТекстЗапросаИнициализацииСКД() Экспорт
    
    Возврат "
    |ВЫБРАТЬ
    |   СпрИнциденты.Ссылка КАК ИнцидентСсылка,
    |   СпрИнциденты.Наименование КАК КодИнцидента,
    |   РегСвИнциденты.ТипИнцидента КАК ТипИнцидента,
    |   РегСвИнциденты.Статус КАК Статус,
    |   РегСвИнциденты.ПоследнееСрабатывание КАК ПоследнееСрабатывание,
    |   РегСвИнциденты.ДатаЗакрытия КАК ДатаЗакрытия,
    |   РегСвИнциденты.ЧислоСрабатываний КАК ЧислоСрабатываний,
    |   РегСвИнциденты.ИнформационнаяБаза КАК ИнформационнаяБаза,
    |   РегСвИнциденты.Кластер КАК Кластер,
    |   РегСвИнциденты.Ответственный КАК Ответственный,
    |   РегСвИнциденты.ПодробноеСообщение КАК ПодробноеСообщение,
    |   РегСвИнциденты.ДатаРеакции КАК ДатаРеакции,
    |   РегСвИнциденты.НеОповещатьДо КАК НеОповещатьДо,
    |   РегСвИнциденты.ПроигнорироватьДо КАК ПроигнорироватьДо,
    |   РегСвИнциденты.ДатаОткрытияПовторная КАК ДатаОткрытияПовторная,
    |   РегСвИнциденты.ЧислоСрабатыванийПовторное КАК ЧислоСрабатыванийПовторное,
    |   РегСвИнциденты.УровеньИнцидента КАК УровеньИнцидента
    |ИЗ
    |   Справочник.Инциденты КАК СпрИнциденты
    |ВНУТРЕННЕЕ СОЕДИНЕНИЕ
    |   РегистрСведений.Инциденты КАК РегСвИнциденты
    |ПО
    |   РегСвИнциденты.Инцидент = СпрИнциденты.Ссылка
    |";
    
КонецФункции

Функция ПолучитьТекстЗапросаВыбратьЗаписи() Экспорт
    
    Возврат "
    |ВЫБРАТЬ
    |   СпрИнциденты.Ссылка КАК ИнцидентСсылка,
    |   СпрИнциденты.Наименование КАК КодИнцидента,
    |   РегСвИнциденты.ТипИнцидента КАК ТипИнцидента,
    |   РегСвИнциденты.Статус КАК Статус,
    |   РегСвИнциденты.ПоследнееСрабатывание КАК ПоследнееСрабатывание,
    |   РегСвИнциденты.ДатаЗакрытия КАК ДатаЗакрытия,
    |   РегСвИнциденты.ЧислоСрабатываний КАК ЧислоСрабатываний,
    |   РегСвИнциденты.ИнформационнаяБаза КАК ИнформационнаяБаза,
    |   РегСвИнциденты.Кластер КАК Кластер,
    |   РегСвИнциденты.Ответственный КАК Ответственный,
    |   РегСвИнциденты.ПодробноеСообщение КАК ПодробноеСообщение,
    |   РегСвИнциденты.ДатаРеакции КАК ДатаРеакции,
    |   РегСвИнциденты.НеОповещатьДо КАК НеОповещатьДо,
    |   РегСвИнциденты.ПроигнорироватьДо КАК ПроигнорироватьДо,
    |   РегСвИнциденты.ДатаОткрытияПовторная КАК ДатаОткрытияПовторная,
    |   РегСвИнциденты.ЧислоСрабатыванийПовторное КАК ЧислоСрабатыванийПовторное,
    |   РегСвИнциденты.УровеньИнцидента КАК УровеньИнцидента
    |ИЗ
    |   Справочник.Инциденты КАК СпрИнциденты
    |ВНУТРЕННЕЕ СОЕДИНЕНИЕ
    |   РегистрСведений.Инциденты КАК РегСвИнциденты
    |ПО
    |   РегСвИнциденты.Инцидент = СпрИнциденты.Ссылка
    |";
    
КонецФункции

Процедура УдалитьПоТипуИнцидента(ТипИнцидента) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Инцидент,
    |   ТипИнцидента
	|ИЗ
	|	РегистрСведений.Инциденты
	|ГДЕ
	|	ТипИнцидента = &ТипИнцидента
	|";
	
	Запрос.УстановитьПараметр("ТипИнцидента", ТипИнцидента);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		Запись = ПолучитьМенеджерЗаписи(Выборка.Инцидент, Выборка.ТипИнцидента);
		Запись.Удалить();
	КонецЦикла;
		
КонецПроцедуры

Функция ПолучитьМенеджерЗаписи(Знач Инцидент, Знач ТипИнцидента) Экспорт
    
    Если ТипЗнч(Инцидент) = Тип("Строка") Тогда
        Инцидент = Справочники.Инциденты.НайтиСсылкуПоНаименованию(Инцидент);
    КонецЕсли;
       
	МенеджерЗаписи = РегистрыСведений.Инциденты.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Инцидент = Инцидент;
    МенеджерЗаписи.ТипИнцидента = ТипИнцидента;
	МенеджерЗаписи.Прочитать();
		
	Возврат МенеджерЗаписи;
	
КонецФункции

// Функция - Статус
//
// Параметры:
//  ТипИнцидента - СправочникСсылка.ТипИнцидента      - тип инцидента для отбора.
//  Инцидент     - СправочникСсылка.Инциденты, Строка - инцидент для отбора.
// 
// Возвращаемое значение:
//  Статус - ПеречислениеСсылка.СтатусыИнцидентов 
//
Функция Статус(ТипИнцидента, Инцидент) Экспорт
    
    Статус = Неопределено;
    
    Запрос = Новый Запрос;
    
    Если ТипЗнч(Инцидент) = Тип("СправочникСсылка.Инциденты") Тогда
        Запрос.Текст = "
        |ВЫБРАТЬ
        |   Статус
        |ИЗ
        |   РегистрСведений.Инциденты
        |ГДЕ
        |   Инцидент = &Инцидент
        |   И ТипИнцидента = &ТипИнцидента
        |";
    Иначе
        Запрос.Текст = "
        |ВЫБРАТЬ
        |   Рег.Статус
        |ИЗ
        |   Справочник.Инциденты КАК Спр
        |ВНУТРЕННЕЕ СОЕДИНЕНИЕ
        |   РегистрСведений.Инциденты КАК Рег
        |ПО
        |   Рег.Инцидент = Спр.Ссылка
        |   И Рег.ТипИнцидента = &ТипИнцидента
        |ГДЕ
        |   Наименование = &Инцидент
        |";
    КонецЕсли;
    
    Запрос.УстановитьПараметр("Инцидент", Инцидент);
    Запрос.УстановитьПараметр("ТипИнцидента", ТипИнцидента);
    
    Результат = Запрос.Выполнить();
    Если НЕ Результат.Пустой() Тогда
        Выборка = Результат.Выбрать();
        Выборка.Следующий();
        Статус = Выборка.Статус;
    КонецЕсли;
    
    Возврат Статус;
    
КонецФункции

#КонецОбласти

#КонецЕсли