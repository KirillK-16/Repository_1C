
Процедура ПередЗаписью(Отказ, Замещение)
	УстановитьАктуальностьНовыхКоманд = Ложь;
	ВерсияПоставки = "";
	УстанавливаемыйПериод = РаботаСоСценариямиАвтоматизацииСервер.ПолучитьТекущуюДатуСеанса();
	
	Для Каждого Запись Из ЭтотОбъект Цикл
		Запись.ДатаИзмененияАктуальности = УстанавливаемыйПериод;
		Если Запись.Актуальная Тогда
			УстановитьАктуальностьНовыхКоманд = Истина;
			ВерсияПоставки = Запись.Версия;
		КонецЕсли;	
	КонецЦикла;	
		
	Если УстановитьАктуальностьНовыхКоманд Тогда
		// Актуальная версия только одна
		Запрос = Новый Запрос("ВЫБРАТЬ
		                      |	ВерсииПоставки.Версия КАК Версия
		                      |ИЗ
		                      |	РегистрСведений.ВерсииПоставки КАК ВерсииПоставки
		                      |ГДЕ
		                      |	ВерсииПоставки.Версия <> &Версия
		                      |	И ВерсииПоставки.Актуальная");
		Запрос.УстановитьПараметр("Версия", ВерсияПоставки);
		
		ВыборкаВерсийДляАктуализации = Запрос.Выполнить().Выбрать();
		Пока ВыборкаВерсийДляАктуализации.Следующий() Цикл
			НЗ = РегистрыСведений.ВерсииПоставки.СоздатьНаборЗаписей();
			НЗ.Отбор.Версия.Установить(ВыборкаВерсийДляАктуализации.Версия);
			
			НЗ.Прочитать();
			
			Для Каждого Запись Из НЗ Цикл
				Запись.Актуальная = Ложь;
				Запись.ДатаИзмененияАктуальности = УстанавливаемыйПериод;
			КонецЦикла;	
			
			НЗ.Записать();
		КонецЦикла;	
	КонецЕсли;	
КонецПроцедуры
