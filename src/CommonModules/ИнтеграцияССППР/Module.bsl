#Область ПрограммныйИнтерфейс

// Создает прокси веб-сервиса СППР. В случае ошибки при создании вызывается исключение.
//
// Возвращаемое значение:
//	WSПрокси - Прокси веб-сервиса.
//
Функция ПолучитьПрокси(МестоположениеWSDL, ПараметрыАвторизации) Экспорт
	
	Если ЗначениеЗаполнено(МестоположениеWSDL) 
		И (НЕ СтрЗаканчиваетсяНа(МестоположениеWSDL, "/")) И (НЕ СтрЗаканчиваетсяНа(МестоположениеWSDL, "\")) Тогда
		МестоположениеWSDL = МестоположениеWSDL + "/";
	КонецЕсли;
	МестоположениеWSDL = МестоположениеWSDL + "ws/ErrorsExchange?wsdl";
	
	Если НЕ ЗначениеЗаполнено(ПараметрыАвторизации.Логин) Тогда
		ВызватьИсключение
			НСтр("ru='Ошибка подключения: не заполнены параметры подключения.
				|Обратитесь к администратору системы.'");
	КонецЕсли;
	
	Попытка
		Определение = Новый WSОпределения(
			МестоположениеWSDL, 
			ПараметрыАвторизации.Логин,
			ПараметрыАвторизации.Пароль);
		
		Прокси = Новый WSПрокси(
			Определение,
			"http://www.1c.ru/modeling",
			"ErrorsExchange",
			"ErrorsExchangeSoap");
		
	Исключение
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		
		ЗаписьЖурналаРегистрации(НСтр("ru='Интеграция с СППР'", Метаданные.ОсновнойЯзык.КодЯзыка), 
			УровеньЖурналаРегистрации.Ошибка,,, 
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
			
		ВызватьИсключение НСтр("ru='Ошибка подключения. 
			|Возможно не прошла авторизация, указан неверный адрес веб-сервиса или база СППР не опубликована на веб-сервере. 
			|Подробности в журнале регистрации. Обратитесь к администратору системы.'");
		
	КонецПопытки;
	
	Прокси.Пользователь = ПараметрыАвторизации.Логин;
	Прокси.Пароль 		= ПараметрыАвторизации.Пароль;
	
	Возврат Прокси;
	
КонецФункции

// Проверяет результат, возвращенный операцией WEB-сервиса.
// В случае возврата объекта типа Структура с свойством ОшибкаСервиса вызывается исключение.
//
// Параметры:
//	Ответ - Произвольный - возвращаемое значение web-сервиса.
//	Действие - Строка - описание текущей операции web-сервиса.
//
Процедура ПроверитьРезультатОперацииВебСервиса(Ответ, Действие) Экспорт
	
	Если ТипЗнч(Ответ) = Тип("ХранилищеЗначения") Тогда
		
		Значение = Ответ.Получить();
		Если ТипЗнч(Значение) = Тип("Структура") И Значение.Свойство("ОшибкаСервиса") Тогда
			
			ТекстОшибки = Значение.Описание;
			Пока СтрНайти(ТекстОшибки, "}") > 0 Цикл
				ТекстОшибки = Сред(ТекстОшибки, СтрНайти(ТекстОшибки, "}") + 3);
			КонецЦикла;
			
			ПозицияТекстаПоПричине = СтрНайти(ТекстОшибки, Символы.ПС + НСтр("ru='по причине:'"));
			Если ПозицияТекстаПоПричине > 0 Тогда
				ТекстОшибки = Сред(ТекстОшибки, ПозицияТекстаПоПричине + 13);
			КонецЕсли;
			
			Если СтрНайти(ТекстОшибки, "ВызватьИсключение") Тогда
				ТекстОшибки = Сред(ТекстОшибки, 1, СтрНайти(ТекстОшибки, "ВызватьИсключение") - 1);
			КонецЕсли;
			
			ТекстОшибки = Действие + ":" + Символы.ПС + Значение.Заголовок + ":" + Символы.ПС + ТекстОшибки;
			
			ЗаписьЖурналаРегистрации(НСтр("ru='Интеграция с СППР'", Метаданные.ОсновнойЯзык.КодЯзыка), 
				УровеньЖурналаРегистрации.Ошибка,,, 
				ТекстОшибки);
			
			ВызватьИсключение ТекстОшибки;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти