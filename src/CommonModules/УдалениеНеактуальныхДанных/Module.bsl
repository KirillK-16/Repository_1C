
#Область ПрограммныйИнтерфейс

Функция ПроверитьВозможностьУдаленияГруппыСчетчиков(ГруппаСчетчиков) Экспорт 
	
	ВозможноУдалять = Истина;
	
	Выборка = ПолучитьПоказателиСчетчиковПоГруппеСчетчиков(ГруппаСчетчиков);
	
	Пока Выборка.Следующий() И ВозможноУдалять Цикл
		ПоказательСчетчиковОбъект = Выборка.ПоказательСчетчиков.ПолучитьОбъект();
		
		Отбор = Новый Структура("Счетчик", ГруппаСчетчиков);
		СтрокиМассив = ПоказательСчетчиковОбъект.Счетчики.НайтиСтроки(Отбор);
		Если ПоказательСчетчиковОбъект.Счетчики.Количество() = СтрокиМассив.Количество() Тогда
			Если ТипЗнч(Выборка.ПоказательСчетчиков.Владелец) = Тип("СправочникСсылка.ПоказателиМониторинга") Тогда
				ЕстьПрофили = ЕстьПрофилиМониторингаПоПоказателю(Выборка.ПоказательСчетчиков.Владелец);
				Если ЕстьПрофили Тогда 
					ВозможноУдалять = Ложь;
					Прервать;
				КонецЕсли;
			КонецЕсли;		
		КонецЕсли;
	КонецЦикла;	
	
	Возврат ВозможноУдалять;
	
КонецФункции

Функция ПолучитьПоказателиСчетчиковПоГруппеСчетчиков(ГруппаСчетчиков) Экспорт 
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПоказателиСчетчиковСчетчики.Ссылка КАК ПоказательСчетчиков
	|ИЗ
	|	Справочник.ПоказателиСчетчиков.Счетчики КАК ПоказателиСчетчиковСчетчики
	|ГДЕ
	|	ПоказателиСчетчиковСчетчики.Счетчик = &ГруппаСчетчиков
	|";
	
	Запрос.УстановитьПараметр("ГруппаСчетчиков", ГруппаСчетчиков);
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();	
	
	Возврат Выборка;
	
КонецФункции

Процедура УдалитьПоказательСчетчиковИзПоказателиМониторинга(Знач Ссылка) Экспорт
	
	Выборка = ПолучитьПоказательМониторингаПоПоказателю(Ссылка); 
	
	Пока Выборка.Следующий() Цикл
		ПоказательМониторингаОбъект = Выборка.ПоказательМониторинга.ПолучитьОбъект();
		ПоказательМониторингаОбъект.Удалить();
	КонецЦикла;
	
КонецПроцедуры

Функция	ЕстьПрофилиМониторингаПоПоказателю(Показатель) Экспорт  
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ПрофильМониторингаПорядокПоказателей.Ссылка
	|ИЗ
	|	Справочник.ПрофильМониторинга.ПорядокПоказателей КАК ПрофильМониторингаПорядокПоказателей
	|ГДЕ
	|	ПрофильМониторингаПорядокПоказателей.Показатель = &Показатель
	|	И ПрофильМониторингаПорядокПоказателей.Ссылка <> &ПрофильМониторинга";
	Запрос.УстановитьПараметр("Показатель", Показатель);
	Запрос.УстановитьПараметр("ПрофильМониторинга", Показатель.Владелец);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ЕстьПрофили = Не РезультатЗапроса.Пустой(); 
	
	Возврат ЕстьПрофили;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьПоказательМониторингаПоПоказателю(Показатель)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	ПоказателиМониторинга.Ссылка КАК ПоказательМониторинга
	|ИЗ
	|	Справочник.ПоказателиМониторинга КАК ПоказателиМониторинга
	|ГДЕ
	|	ПоказателиМониторинга.Показатель = &Показатель
	|";
	Запрос.УстановитьПараметр("Показатель", Показатель);
	Результат = Запрос.Выполнить();
	
	Выборка = Результат.Выбрать();
	
	Возврат Выборка;
	
КонецФункции

#КонецОбласти
