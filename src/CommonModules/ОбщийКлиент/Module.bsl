
#Область ПрограммныйИнтерфейс

// Процедура - Выбрать каталог
//
// Параметры:
//  ТекущийКаталог						 - Строка - Текущий каталог. 
//  ОписаниеОповещенияЗавершенияВыбора	 - ОписаниеОповещения - Содержит описание процедуры, которая будет вызвана после закрытия диалога выбора
//																со следующими параметрами:
//																* <ВыбранныеФайлы> – массив выбранных имен файлов или Неопределено, если выбор не осуществлен.
//																* <ДополнительныеПараметры> - значение, которое было указано при создании объекта ОписаниеОповещения.
//
Процедура ВыбратьКаталог(ТекущийКаталог = "", ОписаниеОповещенияЗавершенияВыбора) Экспорт

	#Если ВебКлиент Тогда
		ОбработатьРасширениеРаботыСФайлами(ТекущийКаталог, ОписаниеОповещенияЗавершенияВыбора);
	#Иначе
		ВыбратьКаталогЗавершить(ТекущийКаталог, ОписаниеОповещенияЗавершенияВыбора);
	#КонецЕсли

КонецПроцедуры

Процедура ВыбратьФайл(ТекущийКаталог = "", Фильтр = "", ОписаниеОповещенияЗавершенияВыбора) Экспорт
    
    #Если ВебКлиент Тогда
		ОбработатьРасширениеРаботыСФайлами(ТекущийКаталог, ОписаниеОповещенияЗавершенияВыбора);
	#Иначе
		ВыбратьФайлЗавершить(ТекущийКаталог, Фильтр, ОписаниеОповещенияЗавершенияВыбора);
    #КонецЕсли
    
КонецПроцедуры

// Процедура - Диаграмма общая счетчики производительности
//
// Параметры:
//  Ссылки - Массив - массив элементов СправочникСсылка.ГруппыСчетчиков
//  ДатаНачала - Дата - Начальная дата формирования данных.
//  ДатаОкончания - Дата - Конечная дата формирования данных.
//  Уникальность - Произвольный - В данном параметре может быть задан некоторый ключ,
//                                значение которого будет использоваться для поиска уже открытых форм.
//                                Если будет найдена форма с таким же ключом уникальности,
//                                вместо открытия новой формы будет активизирована найденная форма.
//
Процедура ДиаграммаОбщаяСчетчикиПроизводительности(Ссылки, ДатаНачала, ДатаОкончания, Уникальность) Экспорт
    
    ПараметрыОткрытия = Новый Структура;
    ПараметрыОткрытия.Вставить("ФормаВыбораДанных", "Справочник.ГруппыСчетчиков.Форма.ФормаВыбораДляДиаграммаОбщая");
    ПараметрыОткрытия.Вставить("ФункцияПолученияДанных", "Справочники.ГруппыСчетчиков.ДанныеДляДиаграммаОбщая");
    ПараметрыОткрытия.Вставить("Ссылки", Ссылки);
    ПараметрыОткрытия.Вставить("ДатаНачала", ДатаНачала);
    ПараметрыОткрытия.Вставить("ДатаОкончания", ДатаОкончания);
    ПараметрыОткрытия.Вставить("ФорматИмениТочки", "ДФ='дд.ММ.гг ЧЧ:мм'");
    ПараметрыОткрытия.Вставить("ФорматЗначенийВПодписях", "ЧДЦ=5");
    ПараметрыОткрытия.Вставить("ШкалаСерийФорматПодписей", "ЧРГ=' '");
    ПараметрыОткрытия.Вставить("Уникальность", Уникальность);
    
    ДиаграммаОбщаяКлиент.ОткрытьДиаграммуОбщую(ПараметрыОткрытия);
    
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ВыбратьКаталогЗавершить(ТекущийКаталог, ОписаниеОповещенияЗавершенияВыбора)
			
	ДиалогВыбора = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	#Если Не ВебКлиент Тогда
		ДиалогВыбора.Каталог = ТекущийКаталог;
    #КонецЕсли
    ДиалогВыбора.Показать(ОписаниеОповещенияЗавершенияВыбора);

КонецПроцедуры

Процедура ВыбратьФайлЗавершить(ТекущийКаталог, Фильтр, ОписаниеОповещенияЗавершенияВыбора)
    
    ДиалогВыбора = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	#Если Не ВебКлиент Тогда
		ДиалогВыбора.Каталог = ТекущийКаталог;
    #КонецЕсли
    Если ЗначениеЗаполнено(Фильтр) Тогда
        ДиалогВыбора.Фильтр = Фильтр;
    КонецЕсли;
	ДиалогВыбора.Показать(ОписаниеОповещенияЗавершенияВыбора);
    
КонецПроцедуры

// Выполняет попытку подключения расширения работы с файлами
//
// Возвращаемое значение:
//  Булево - 
//  	Истина, расширение было подключено
//  	Ложь, расширение не подключено
//
Процедура ОбработатьРасширениеРаботыСФайлами(ТекущийКаталог, ОписаниеОповещенияЗавершенияВыбора)
	
	ДополнительныеПараметры = Новый Структура("ТекущийКаталог, ОписаниеОповещения", ТекущийКаталог, ОписаниеОповещенияЗавершенияВыбора);
	ОписаниеОповещенияОЗавершении = Новый ОписаниеОповещения("ОбработатьРасширениеРаботыСФайламиЗавершение", ЭтотОбъект, ДополнительныеПараметры); 
	НачатьПодключениеРасширенияРаботыСФайлами(ОписаниеОповещенияОЗавершении);
	
КонецПроцедуры

Процедура ОбработатьРасширениеРаботыСФайламиЗавершение(Подключено, ДополнительныеПараметры) Экспорт
	
	Если Подключено Тогда
		ВыбратьКаталогЗавершить(ДополнительныеПараметры.ТекущийКаталог, ДополнительныеПараметры.ОписаниеОповещения);
	Иначе
		ОписаниеОповещенияОЗавершении = Новый ОписаниеОповещения("ОбработатьРасширениеРаботыСФайламиВопросЗавершение", ЭтотОбъект);
		ПоказатьВопрос(ОписаниеОповещенияОЗавершении, НСтр("ru = 'Расширение работы с файлами не установлено, установить?'"), РежимДиалогаВопрос.ДаНет, 30);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьРасширениеРаботыСФайламиВопросЗавершение(Результат, Параметры) Экспорт
	Если Результат = КодВозвратаДиалога.Да Тогда
		НачатьУстановкуРасширенияРаботыСФайлами();
	КонецЕсли;
КонецПроцедуры

Процедура ПриНачалеРаботыСистемы(ПараметрыОткрытия) Экспорт
	
	ИдетОбновление = Ложь;
	
	Если ПараметрыОткрытия.СохраненнаяВерсия = "" Тогда
		ОписаниеОповещенияОЗакрытии = Новый ОписаниеОповещения("ОбновлениеИнформационнойБазыЗакрытии", ЭтотОбъект, ПараметрыОткрытия);
		ОткрытьФорму("Обработка.ОбновлениеИнформационнойБазы.Форма.Форма",,,,,,ОписаниеОповещенияОЗакрытии,РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
		ИдетОбновление = Истина;
	Иначе
		Если ПараметрыОткрытия.ИдетОбновление Тогда
			ОписаниеОповещенияОЗакрытии = Новый ОписаниеОповещения("ОбновлениеИнформационнойБазыЗакрытии", ЭтотОбъект, ПараметрыОткрытия);
			ОткрытьФорму("Обработка.ОбновлениеИнформационнойБазы.Форма.Форма",,,,,,ОписаниеОповещенияОЗакрытии,РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
			ИдетОбновление = Истина;
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ИдетОбновление Тогда
		ОбновлениеИнформационнойБазыЗакрытии(, ПараметрыОткрытия);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбновлениеИнформационнойБазыЗакрытии(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	Если НЕ ДополнительныеПараметры.НастройкаПараметровОповещенияВыполнена Тогда
		ОткрытьФорму("ОбщаяФорма.НаименованиеЦКК",,,,,,,РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
    Иначе
        Если ДополнительныеПараметры.Свойство("ФормаПослеОбновления") И ЗначениеЗаполнено(ДополнительныеПараметры.ФормаПослеОбновления) Тогда
            ОткрытьФорму(ДополнительныеПараметры.ФормаПослеОбновления);
        КонецЕсли;        
	КонецЕсли;
КонецПроцедуры

#КонецОбласти


