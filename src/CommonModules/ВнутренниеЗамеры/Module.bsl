
#Область ПрограммныйИнтерфейс

Функция НачатьЗамер(ИмяКлючевойОперации) Экспорт
    Возврат ВнутренниеЗамерыКлиентСервер.НовыйЗамер(ТекущаяУниверсальнаяДатаВМиллисекундах(), ИмяКлючевойОперации, Ложь);
КонецФункции

Процедура ЗавершитьЗамер(Замер, ДопПараметры = Неопределено) Экспорт
    
    Замер["ДатаОкончания"] = ТекущаяУниверсальнаяДатаВМиллисекундах();
    
	УстановитьПривилегированныйРежим(Истина);
    
    ПараметрыЗаписиПроизводительности = ОбщийСерверПовтИсп.ПолучитьПараметрыЗаписиПроизводительности(Замер["ИмяКлючевойОперации"], 1);
    ПользовательЗамераПроизводительности = ОбщийСерверПовтИсп.ПолучитьПользователяЗамераПроизводительности(ПользователиИнформационнойБазы.ТекущийПользователь().ПолноеИмя);
    
    Если НЕ ПараметрыЗаписиПроизводительности.ПомеченаНаУдаление Тогда
        
        УникальныйИдентификаторЗаписи = Новый УникальныйИдентификатор();
        
        НовЗапись = РегистрыСведений.ЗамерыПроизводительности.СоздатьМенеджерЗаписи();
        НовЗапись.ОбъектКонтроля = ПараметрыЗаписиПроизводительности.ОбъектКонтроля;
        НовЗапись.ДатаЗамераUTC = Замер["ДатаНачала"];
        НовЗапись.ИдентификаторКлючевойОперации = ПараметрыЗаписиПроизводительности.УникальныйИдентификаторКлючевойОперации;
        НовЗапись.НомерСеанса = НомерСеансаИнформационнойБазы();
        НовЗапись.Значение = (Замер["ДатаОкончания"] - Замер["ДатаНачала"])/1000;
        НовЗапись.ДатаЗаписи = ТекущаяДата();
        НовЗапись.Пользователь = ПользовательЗамераПроизводительности;
        НовЗапись.УникальныйИдентификаторЗаписи = УникальныйИдентификаторЗаписи; 
        НовЗапись.Записать(Ложь);
        
        Если ДопПараметры <> Неопределено Тогда
            
            НаборЗаписей = РегистрыСведений.ЗамерыПроизводительностиДополнительнаяИнформация.СоздатьНаборЗаписей();
            
            Для Каждого ТекПараметр Из ДопПараметры Цикл
                
                ПараметрЗамера = ОбщийСерверПовтИсп.ПолучитьПараметрЗамераПоНаименованию(ТекПараметр.Ключ);
                ЗначениеПараметра = ОбщийСерверПовтИсп.ПолучитьЗначениеПараметраЗамераПоНаименованию(ТекПараметр.Значение, ПараметрЗамера);
                
                НовЗаписьДопИнфо = НаборЗаписей.Добавить();
                НовЗаписьДопИнфо.УникальныйИдентификаторЗаписи = УникальныйИдентификаторЗаписи;
                НовЗаписьДопИнфо.ПараметрыЗамера = ПараметрЗамера;
                НовЗаписьДопИнфо.Значение = ЗначениеПараметра;
                
            КонецЦикла;
            
            НаборЗаписей.Записать(Ложь);
            
        КонецЕсли;
    КонецЕсли;
    	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Процедура УстановитьИмяКлючевойОперации(Замер, ИмяКлючевойОперации) Экспорт
    Замер["ИмяКлючевойОперации"] = ИмяКлючевойОперации;
КонецПроцедуры

#КонецОбласти