
#Область СлужебныеПроцедурыИФункции

Функция ЗаписатьБуферКлиента(Замеры) Экспорт
    
    УстановитьПривилегированныйРежим(Истина);
    
    ПользовательЗамераПроизводительности = ОбщийСерверПовтИсп.ПолучитьПользователяЗамераПроизводительности(ПользователиИнформационнойБазы.ТекущийПользователь().Имя);
    
    НаборЗаписей = РегистрыСведений.ЗамерыПроизводительности.СоздатьНаборЗаписей();
    
    Для Каждого ТекЗамер Из Замеры Цикл
        
        Замер = ТекЗамер.Значение;
        
        ПараметрыЗаписиПроизводительности = ОбщийСерверПовтИсп.ПолучитьПараметрыЗаписиПроизводительности(Замер["ИмяКлючевойОперации"], 1);
        
        НовЗапись = НаборЗаписей.Добавить();
        НовЗапись.ОбъектКонтроля = ПараметрыЗаписиПроизводительности.ОбъектКонтроля;
        НовЗапись.ДатаЗамераUTC = Замер["ДатаНачала"];
        НовЗапись.ИдентификаторКлючевойОперации = ПараметрыЗаписиПроизводительности.УникальныйИдентификаторКлючевойОперации;
        НовЗапись.НомерСеанса = НомерСеансаИнформационнойБазы();
        НовЗапись.Значение = (Замер["ДатаОкончания"] - Замер["ДатаНачала"])/1000;
        НовЗапись.ДатаЗаписи = ТекущаяДата();
        НовЗапись.Пользователь = ПользовательЗамераПроизводительности;
        НовЗапись.УникальныйИдентификаторЗаписи = Новый УникальныйИдентификатор;
        
    КонецЦикла;
    
    НаборЗаписей.Записать(Ложь);
    
    ПериодЗаписи = Константы.ПериодЗаписиВнутреннихЗамеровКлиента.Получить();
    Если НЕ ЗначениеЗаполнено(ПериодЗаписи) Тогда
        ПериодЗаписи = 300;
    КонецЕсли;
        
    УстановитьПривилегированныйРежим(Ложь);
    
    Возврат ПериодЗаписи;
    
КонецФункции

#КонецОбласти