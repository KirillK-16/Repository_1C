
#Область ПрограммныйИнтерфейс

Функция НастройкиКластер1С(Ссылка, ВерсияДанных, ВерсияДанныхПодключение) Экспорт
    
    Настройки = РегистрыСведений.ПараметрыКластеров.ПрочитатьНастройки(Ссылка);
    АктивноеПодключение = Справочники.ПараметрыПодключенияКластер1С.АктивноеПодключение(Ссылка.Подключение);
    Настройки.Вставить("АктивноеПодключение", АктивноеПодключение);
    
    Возврат Настройки;
    
КонецФункции

Функция НастройкиКонтрольПотребленияПамяти(Ссылка, ВерсияДанных) Экспорт
    Возврат РегистрыСведений.НастройкиКонтрольПамяти.ПрочитатьНастройки(Ссылка);
КонецФункции

Функция КонтрольПотребленияПамяти(СсылкаОбъектКонтроля, ВерсияДанных) Экспорт
    Возврат Справочники.КонтрольныеПроцедуры.ПолучитьКонтрольнуюПроцедуруПоОбъектуКонтроля(СсылкаОбъектКонтроля, Справочники.ВидыКонтрольныхПроцедур.КонтрольПотребленияПамяти());
КонецФункции

Функция ПолучитьПараметрыЗаписиПроизводительности(ИмяКлючевойОперации, ЦелевоеВремя) Экспорт
    
    ОбъектКонтроля = Справочники.ОбъектыКонтроля.ПолучитьСсылкуПоНаименованию("Центр контроля качества");
	УникальныйИдентификаторКлючевойОперацииСтруктура = РегистрыСведений.ОценкаПроизводительностиКлючевыеОперации.ПолучитьУникальныйИдентификаторКлючевойОперации(ИмяКлючевойОперации, ОбъектКонтроля, ЦелевоеВремя, Истина);
	ПользовательЗамераПроизводительности = ПолучитьПользователяЗамераПроизводительности();	
	
	ПараметрыЗаписиПроизводительности = Новый Структура();
	ПараметрыЗаписиПроизводительности.Вставить("ОбъектКонтроля", ОбъектКонтроля);
	ПараметрыЗаписиПроизводительности.Вставить("УникальныйИдентификаторКлючевойОперации", УникальныйИдентификаторКлючевойОперацииСтруктура.УникальныйИдентификатор);
    ПараметрыЗаписиПроизводительности.Вставить("ПомеченаНаУдаление", УникальныйИдентификаторКлючевойОперацииСтруктура.ПомеченаНаУдаление);
	ПараметрыЗаписиПроизводительности.Вставить("ПользовательЗамераПроизводительности", ПользовательЗамераПроизводительности);	
		
	Возврат ПараметрыЗаписиПроизводительности;
    
КонецФункции

Функция ПолучитьПользователяЗамераПроизводительности(ИмяТекПользователя = Неопределено) Экспорт
    
    Если ИмяТекПользователя = Неопределено Тогда
	    ИмяТекПользователя = ПользователиИнформационнойБазы.ТекущийПользователь().ПолноеИмя;
    КонецЕсли;
    
	Ссылка = Справочники.ПользователиЗамерПроизводительности.ПустаяСсылка();
	
	Если ЗначениеЗаполнено(ИмяТекПользователя) Тогда
		Ссылка = ПользовательЗамерПроизводительностиПоНаименование(ИмяТекПользователя);
		
		Если Ссылка = Справочники.ПользователиЗамерПроизводительности.ПустаяСсылка() Тогда
			НачатьТранзакцию();
			
			БлокировкаДанных = Новый БлокировкаДанных;
			ЭлементБлокировки = БлокировкаДанных.Добавить("Справочник.ПользователиЗамерПроизводительности");
			ЭлементБлокировки.УстановитьЗначение("Наименование", ИмяТекПользователя);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			
			Попытка
				БлокировкаДанных.Заблокировать();
				
				Ссылка = ПользовательЗамерПроизводительностиПоНаименование(ИмяТекПользователя);
				Если Ссылка = Справочники.ПользователиЗамерПроизводительности.ПустаяСсылка() Тогда
					ОбъектДляЗаписи = Справочники.ПользователиЗамерПроизводительности.СоздатьЭлемент();
					ОбъектДляЗаписи.Наименование = ИмяТекПользователя;
					ОбъектДляЗаписи.Записать();
					Ссылка = ОбъектДляЗаписи.Ссылка;
				КонецЕсли;
				
				ЗафиксироватьТранзакцию();
			Исключение
				ОтменитьТранзакцию();
				ВызватьИсключение(ИнформацияОбОшибке());
			КонецПопытки;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Ссылка;
    
КонецФункции

Функция ИдентификаторЦКК() Экспорт
	Возврат Общий.ИдентификаторЦКК();
КонецФункции

Функция ПолучитьПараметрЗамераПоНаименованию(Наименование) Экспорт
	Возврат Справочники.ПараметрыЗамера.СсылкаПоНаименованию(Наименование);
КонецФункции

Функция ПолучитьЗначениеПараметраЗамераПоНаименованию(Наименование, ПараметрЗамера) Экспорт
	Возврат Справочники.ЗначенияПараметраЗамера.СсылкаПоНаименованию(Наименование, ПараметрЗамера)
КонецФункции

Функция ПолучитьПользователяЗамераПроизводительностиПоНаименованию(Наименование, СоздаватьНового) Экспорт
	Возврат Справочники.ПользователиЗамерПроизводительности.ПолучитьСсылкуПоНаименованию(Наименование, СоздаватьНового);
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПользовательЗамерПроизводительностиПоНаименование(Наименование)
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	Ссылка
	|ИЗ
	|	Справочник.ПользователиЗамерПроизводительности
	|ГДЕ
	|	Наименование = &Наименование
	|";
	
	Запрос.УстановитьПараметр("Наименование", Наименование);
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Ссылка = Справочники.ПользователиЗамерПроизводительности.ПустаяСсылка();
	Иначе
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		
		Ссылка = Выборка.Ссылка;
	КонецЕсли;
	
	Возврат Ссылка;
КонецФункции

#КонецОбласти
