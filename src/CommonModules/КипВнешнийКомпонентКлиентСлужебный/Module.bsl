
// Начать получение объекта инструментов (см. раздел ИНСТРУМЕНТЫ)
//
&НаКлиенте
Процедура НачатьПолучениеИнструментов(ДополнительныеПараметры) Экспорт
    
    ОписаниеОповещения = Новый ОписаниеОповещения("НачатьПолучениеИнструментовЗавершение", ЭтотОбъект, ДополнительныеПараметры);
    НачатьПодключениеВнешнейКомпоненты(ОписаниеОповещения, "ОбщийМакет.КипВнешнийКомпонент", "ETP");
    
КонецПроцедуры

&НаКлиенте
Процедура НачатьПолучениеИнструментовЗавершение(Результат, ДополнительныеПараметры) Экспорт
    
    Если НЕ Результат Тогда
        
        Если НЕ ДополнительныеПараметры.Свойство("БылаПопыткаУстановки") Тогда
            
            ДополнительныеПараметры.Вставить("БылаПопыткаУстановки", Истина);
            ОписаниеОповещения = Новый ОписаниеОповещения("НачатьПолучениеИнструментов", ЭтотОбъект, ДополнительныеПараметры);
            НачатьУстановкуВнешнейКомпоненты(ОписаниеОповещения, "ОбщийМакет.КипВнешнийКомпонент"); 
            
        Иначе
            
            //TODO
            ВызватьИсключение("Ошибка установки ""Addin.ETP.Tools""!");
            
        КонецЕсли;
        
    Иначе
        
        Попытка
            глИнструменты = Новый ("Addin.ETP.Tools");
        Исключение
            //TODO
            ВызватьИсключение("Ошибка создания объекта ""Addin.ETP.Tools""!");
        КонецПопытки;
        
        ВыполнитьМетодАсинхронно(ДополнительныеПараметры.Метод, ДополнительныеПараметры.ОписаниеОповещения, ДополнительныеПараметры.Параметры);    
        
    КонецЕсли;
    
КонецПроцедуры

// Выполнить метод внешнего компонента асинхронно
//
// Параметры:
//  Объект - "Addin.ETP.*", экземпляр класса внешнего компонента
//  Имя - Строка, имя выполняемого метода
//  Параметры - Структура, содержит имена и значения параметров
//	ОписаниеОповещения - описание оповещения
//
&НаКлиенте
Процедура ВыполнитьМетодАсинхронно(Имя, ОписаниеОповещения, ПараметрыВызова = Неопределено) Экспорт
    
    Объект = глИнструменты;
    
    ИменаПараметров = "";
	Если ПараметрыВызова <> Неопределено Тогда
		Для каждого Параметр Из ПараметрыВызова Цикл
			Если Не ПустаяСтрока(ИменаПараметров) Тогда
				ИменаПараметров = ИменаПараметров + ", ";
			КонецЕсли;
			
			ИменаПараметров = ИменаПараметров + "ПараметрыВызова." + Параметр.Ключ;
		КонецЦикла;
	КонецЕсли;
	
	Попытка
		
		Если Не ПустаяСтрока(ИменаПараметров) Тогда
			ИменаПараметров = ", " + ИменаПараметров;
		КонецЕсли;
		
		Результат = Вычислить("Объект." + "НачатьВызов" + Имя + "(" + "ОписаниеОповещения" + ИменаПараметров + ")");
		
	Исключение
		
		ВызватьИсключение;
		
	КонецПопытки;
	
КонецПроцедуры



