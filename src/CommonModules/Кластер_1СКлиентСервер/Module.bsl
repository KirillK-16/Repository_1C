
#Область ПрограммныйИнтерфейс

Функция ПараметрыПодключенияRAS(Администратор, Пароль, СерверRAS, КаталогRAC) Экспорт
    
    СерверМассив = СтрРазделить(СерверRAS, ":", Истина);
    Если СерверМассив.Количество() = 1 Тогда
        Сервер = СерверМассив[0];
        Порт = 1545;
    Иначе
        Сервер = СерверМассив[0];
        Порт = СерверМассив[1];
    КонецЕсли;
        
    Параметры = Новый Структура;
    
    Параметры.Вставить("ТипПодключения", "RAS");
    Параметры.Вставить("ИмяАдминистратораКластера", Администратор);
    Параметры.Вставить("ПарольАдминистратораКластера", Пароль);
    Параметры.Вставить("АдресСервераАдминистрирования", Сервер);
	Параметры.Вставить("ПортСервераАдминистрирования", Порт);
    Параметры.Вставить("КаталогRAC", КаталогRAC);
    
    Возврат Параметры;
   
КонецФункции

Функция ПредставлениеПараметра(Параметр, Значение) Экспорт
    
    Если Параметр = "ЗащищенноеСоединение" Тогда
        Возврат ПредставлениеПараметраЗащищенноеСоединение(Значение);
    ИначеЕсли Параметр = "ПринудительноЗавершатьПроблемныеПроцессы" Тогда
        Если Значение = Неопределено Тогда
            Возврат Ложь;
        Иначе
            Возврат Булево(Значение);
        КонецЕсли;
    ИначеЕсли Параметр = "РежимРаспределенияНагрузки" Тогда
        Возврат ПредставлениеПараметраРежимРаспределенияНагрузки(Значение);
    Иначе
        Возврат Значение;
    КонецЕсли;
    
КонецФункции

Функция ПараметрПоПредставлению(Параметр, Значение) Экспорт
    
    Если Параметр = "ЗащищенноеСоединение" Тогда
        Возврат ПараметрЗащищенноеПредставлениеПоПредставлению(Значение);
    ИначеЕсли Параметр = "РежимРаспределенияНагрузки" Тогда
        Возврат ПараметрРежимРаспределенияНагрузкиПоПредставлению(Значение);
    Иначе
        Возврат Значение;
    КонецЕсли;
    
КонецФункции

Функция РазделитьСерверПорт(Строка, ПортПоУмолчанию = 0) Экспорт
    
    Результат = Новый Структура("Сервер, Порт");
    
    СтрокаМассив = СтрРазделить(Строка, ":", Истина);
    
    Если СтрокаМассив.Количество() = 1 Тогда
        Результат.Сервер = СтрокаМассив[0];
        Результат.Порт = ПортПоУмолчанию;
    Иначе
        Результат.Сервер = СтрокаМассив[0];
        Результат.Порт = Число(СтрокаМассив[1]);
    КонецЕсли;
    
    Возврат Результат;
    
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПредставлениеПараметраЗащищенноеСоединение(Значение)
    
    Если Значение = 0 ИЛИ Значение = АдминистрированиеУровеньБезопасностиСоединений.Незащищенное Тогда
        Возврат "выключено";
    ИначеЕсли Значение = 1 ИЛИ Значение = АдминистрированиеУровеньБезопасностиСоединений.ЗащищенноеПриУстановкеСоединения Тогда
        Возврат "только соединение";
    ИначеЕсли Значение = 2 ИЛИ Значение = АдминистрированиеУровеньБезопасностиСоединений.Защищенное Тогда
        Возврат "постоянно";
    КонецЕсли;
    
КонецФункции

Функция ПараметрЗащищенноеПредставлениеПоПредставлению(Значение)
    
    Если Значение = "выключено" Тогда
        Возврат 0;
    ИначеЕсли Значение = "только соединение" Тогда
        Возврат 1;
    ИначеЕсли Значение = "постоянно" Тогда
        Возврат 2;
    КонецЕсли;
    
КонецФункции

Функция ПредставлениеПараметраРежимРаспределенияНагрузки(Значение)
    
    Если Значение = 0 Тогда
        Возврат "Приоритет по производительности";
    ИначеЕсли Значение = 1 Тогда
        Возврат "Приоритет по памяти";
    КонецЕсли;
    
КонецФункции

Функция ПараметрРежимРаспределенияНагрузкиПоПредставлению(Значение)
    
    Если Значение = "Приоритет по производительности" Тогда
        Возврат 0;
    ИначеЕсли Значение = "Приоритет по памяти" Тогда
        Возврат 1;
    КонецЕсли;
    
КонецФункции

#КонецОбласти