Функция ПолучитьПараметрыЗаписиПроизводительности(ИмяКлючевойОперации) Экспорт
	ПараметрыЗаписиПроизводительности = Новый Структура;
		
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	Ссылка
	|ИЗ
	|	Справочник.ОбъектыКонтроля
	|ГДЕ
	|	Наименование = &Наименование
	|";
	Запрос.УстановитьПараметр("Наименование", "Центр мониторинга");
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		ОбъектКонтроля = Справочники.ОбъектыКонтроля.СоздатьЭлемент();
		ОбъектКонтроля.Владелец = Справочники.ВидыОбъектовКонтроля.ИнформационнаяБаза;
		ОбъектКонтроля.Наименование = "Центр мониторинга";
		ОбъектКонтроля.Записать();
		
		ПараметрыЗаписиПроизводительности.Вставить("ОбъектКонтроля", ОбъектКонтроля.Ссылка);
	Иначе
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		
		ПараметрыЗаписиПроизводительности.Вставить("ОбъектКонтроля", Выборка.Ссылка);
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "
	|ВЫБРАТЬ
	|	УникальныйИдентификатор КАК УникальныйИдентификаторКлючевойОперации,
    |   ПомеченаНаУдаление КАК ПомеченаНаУдаление
	|ИЗ
	|	РегистрСведений.ОценкаПроизводительностиКлючевыеОперации
	|ГДЕ
	|	Имя = &Наименование
	|	И ИнформационнаяБаза = &ОбъектКонтроля
	|";
	Запрос.УстановитьПараметр("Наименование", ИмяКлючевойОперации);
	Запрос.УстановитьПараметр("ОбъектКонтроля", ПараметрыЗаписиПроизводительности.ОбъектКонтроля);
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		НовЗапись = РегистрыСведений.ОценкаПроизводительностиКлючевыеОперации.СоздатьМенеджерЗаписи();
		УникальныйИдентификаторКлючевойОперации = Новый УникальныйИдентификатор();
		НовЗапись.УникальныйИдентификатор = УникальныйИдентификаторКлючевойОперации;
		НовЗапись.ИнформационнаяБаза = ПараметрыЗаписиПроизводительности.ОбъектКонтроля;
		НовЗапись.Приоритет = 1;
		НовЗапись.ЦелевоеВремя = 1;
		НовЗапись.Имя = ИмяКлючевойОперации;
		НовЗапись.ЦелевоеВремяВИБ = 1;
		
		НовЗапись.Записать();
		
		ПараметрыЗаписиПроизводительности.Вставить("УникальныйИдентификаторКлючевойОперации", УникальныйИдентификаторКлючевойОперации);
        ПараметрыЗаписиПроизводительности.Вставить("ПомеченаНаУдаление", Ложь);
	Иначе
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		
		ПараметрыЗаписиПроизводительности.Вставить("УникальныйИдентификаторКлючевойОперации", Выборка.УникальныйИдентификаторКлючевойОперации);
        ПараметрыЗаписиПроизводительности.Вставить("ПомеченаНаУдаление", Выборка.ПомеченаНаУдаление);
	КонецЕсли;
    
    ПараметрыЗаписиПроизводительности.Вставить("ПользовательЗамераПроизводительности", "");
    
	Возврат ПараметрыЗаписиПроизводительности;
    
КонецФункции

Функция ПолучитьКонстантыКаталогов() Экспорт
	НаборКонстант = Константы.СоздатьНабор("
		|КаталогЗагрузкиДанныхЦентрМониторингаИмпорт,
		|КаталогЗагрузкиУспешноЦентрМониторингаИмпорт,
		|КаталогЗагрузкиОшибкаЦентрМониторингаИмпорт");
		
	НаборКонстант.Прочитать();	
		
	Возврат НаборКонстант;
КонецФункции
