
#Область ПрограммныйИнтерфейс

// Зарегистрировать сообщение об ошибке
//
// Параметры:
//  ИнформацияОбОшибке - ИнформацияОбОшибке, подробное описание ошибки
//  ДополнительныйТекст - Строка или Неопределено
// Возвращаемое значение:
//  Строка - текстовое сообщение об ошибке для пользователя
//
Процедура Ошибка(ИнформацияОбОшибке, ДополнительныйТекст = Неопределено) Экспорт
	
	Перем ПодробноеОписание;
	Перем Причина;
	
	ЭтоСтрока = ТипЗнч(ИнформацияОбОшибке) = Тип("Строка");
	
	Если ЭтоСтрока Тогда
		ПодробноеОписание = ИнформацияОбОшибке;
	Иначе
		ПодробноеОписание = ИнформациюОбОшибкеВСтроку(ИнформацияОбОшибке);
	КонецЕсли;
	
	Если Не ДополнительныйТекст = Неопределено Тогда
		ПодробноеОписание = ПодробноеОписание + "
			|
			|" + ДополнительныйТекст;
	КонецЕсли;
	Событие = НСтр("ru = 'Ошибка'", Метаданные.ОсновнойЯзык.КодЯзыка);
	ЗаписьЖурналаРегистрации(Событие, УровеньЖурналаРегистрации.Ошибка,,, ПодробноеОписание);
	
КонецПроцедуры

Процедура Информация(Сообщение) Экспорт
	Событие = НСтр("ru = 'Информация'", Метаданные.ОсновнойЯзык.КодЯзыка);
	ЗаписьЖурналаРегистрации(Событие, УровеньЖурналаРегистрации.Информация,,, Сообщение);
КонецПроцедуры

Процедура ЗаписатьОтладочнуюИнформацию(Событие, Комментарий = "") Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	
	ДатаРегистрацииСобытия = ТекущаяДата();
	Сеанс = НомерСеансаИнформационнойБазы();
	Попытка
		Пользователь = ПараметрыСеанса.ТекущийПользователь;
	Исключение
		//параметр не инициализирован
		Пользователь = Справочники.Пользователи.ПустаяСсылка();
	КонецПопытки;
	РегистрыСведений.Отладка.ЗаписатьВРегистр(Новый Структура("ДатаРегистрацииСобытия, Событие, Сеанс, Пользователь, Комментарий", ДатаРегистрацииСобытия, Событие, Сеанс, Пользователь, Комментарий));
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Функция ПолучитьЗначениеОтладки(Константа) Экспорт 

	Возврат Константы[Константа].Получить();
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Преобразовать объект типа ИнформацияОбОшибке в строку
//
// Параметры:
//  Ошибка - ИнформацияОбОшибке
//  НомерПричины - Число, номер причины ошибки (служебный параметр)
//
// Возвращаемое значение:
//  Строка - Строковое представление ошибки
//
Функция ИнформациюОбОшибкеВСтроку(Ошибка, НомерПричины = 0)	
	ТекстОшибки = "";
	
	Если Не ПустаяСтрока(Ошибка.ИмяМодуля) Тогда
		ТекстОшибки = ТекстОшибки + "{"
			+ Ошибка.ИмяМодуля + "("
			+ Ошибка.НомерСтроки + ")}: ";
	КонецЕсли;
	
	ТекстОшибки = ТекстОшибки
		+ Ошибка.Описание + "
		|" + Ошибка.ИсходнаяСтрока;
	
	Если Ошибка.Причина <> Неопределено Тогда
		ТекстОшибки = ТекстОшибки + "
			|
			|ПРИЧИНА №" + Формат(НомерПричины + 1, "ЧГ=0") + "
			|" + ИнформациюОбОшибкеВСтроку(Ошибка.Причина, НомерПричины + 1);
	КонецЕсли;
	
	Возврат ТекстОшибки;
	
КонецФункции

#КонецОбласти