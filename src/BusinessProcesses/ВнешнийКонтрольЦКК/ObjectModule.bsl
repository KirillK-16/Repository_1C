#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
// Общие настройки
Перем ТекстОшибки;
Перем ЗадачаОшибкаПриАнализе;
Перем ОбязательныеНастройки;
Перем НетПроблем;
Перем ИмяСтраницыСправки;

// Настройки контрольной процедуры
Перем ИмяЦентраКонтроля;
Перем МаксимальнаяДатаПрочитанныхПисем;

//////////////////////
// Общие функции
//////////////////////



Процедура ПроверкаНаНаличиеОшибокВыполнения(ТочкаМаршрутаБизнесПроцесса, Результат)
	Результат = ТекстОшибки <> Неопределено;
КонецПроцедуры

Процедура КонтрольнаяПроцедураЗавершенаПроверкаУсловия(ТочкаМаршрутаБизнесПроцесса, Результат)
	Результат = НЕ КонтрольнаяПроцедура.Выполнять;
КонецПроцедуры

//////////////////
// Разбор Настроек
//////////////////

Процедура ЕстьНезаполненныеНастройкиПроверкаУсловия(ТочкаМаршрутаБизнесПроцесса, Результат)
	Ошибки = Новый Массив;
	Для Каждого Настройка Из ОбязательныеНастройки Цикл		
		Если НЕ ЗначениеЗаполнено(Настройка.Значение) Тогда
			Ошибки.Добавить(Настройка.Ключ);
		КонецЕсли;
	КонецЦикла;	
	Если Ошибки.Количество() > 0 Тогда
		ТекстОшибки = "В контролируемом ЦКК не определены следующие настройки: "; 	
		ТекстОшибки = ТекстОшибки + Символы.ПС + " - " + ОбщийКлиентСервер.ОбъединитьСтроку(Ошибки, Символы.ПС + " - ");
		
		ИмяСтраницыСправки = "НеЗаполненыНастройкиВОбъектеКонтроля";
		
		Результат = Истина;
	Иначе	
		Результат = Ложь;
	КонецЕсли;
		
КонецПроцедуры

Процедура РазобратьНастройкиОбработка(ТочкаМаршрутаБизнесПроцесса)
	ТекстОшибки = Неопределено;
	Попытка
		РазобратьНастройки();
	Исключение
		
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ИмяСтраницыСправки = "НеизвестнаяОшибка";
		Отладка.Ошибка(ТекстОшибки);
		
	КонецПопытки;
КонецПроцедуры

Процедура РазобратьНастройки()
	ОбязательныеНастройки = Новый Соответствие;
			
	ИмяЦентраКонтроля = КонтрольнаяПроцедура.ОбъектКонтроля.Наименование;
	ОбязательныеНастройки.Вставить("Имя Центра Контроля Качества", ИмяЦентраКонтроля);
	
	НастройкиПриватныеСловарь = РегистрыСведений.НастройкиВнешнийКонтрольЦКК.Получить(
		Новый Структура("КонтрольнаяПроцедура", КонтрольнаяПроцедура)
	);		
	МаксимальнаяДатаПрочитанныхПисем = НастройкиПриватныеСловарь.МаксимальнаяДатаПрочитанныхПисемСкрыто;
	
КонецПроцедуры


/////////////////////
// Формирование задач
/////////////////////

Функция СоздатьЗадачуОтветственномуЗаВыполнение(ТекстПоручения)
	
	Возврат Неопределено;
	
КонецФункции	

Процедура СформироватьСписокЗадач(ФормируемыеЗадачи, ЗадачаСсылка, ТочкаМаршрутаБизнесПроцесса)
	ФормируемыеЗадачи.Очистить();		
	
	ЗадачаОбъект = ЗадачаСсылка.ПолучитьОбъект();
	Если ЗадачаОбъект.БизнесПроцесс = Неопределено И ЗадачаОбъект.ТочкаМаршрута = Неопределено Тогда 
		БизнесПроцессСервер.ПривязатьЗадачуКТочкеМаршрута(ЭтотОбъект.Ссылка, ЗадачаОбъект, ТочкаМаршрутаБизнесПроцесса);	
	КонецЕсли;
	ЗадачаОбъект.Выполнена = Ложь;
	ФормируемыеЗадачи.Добавить(ЗадачаОбъект);
	
КонецПроцедуры	

Процедура УстранитьПричиныНевозможностиАнализаПередСозданиемЗадач(ТочкаМаршрутаБизнесПроцесса, ФормируемыеЗадачи, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ЗадачаОшибкаПриАнализе = СоздатьЗадачуОтветственномуЗаВыполнение(ТекстОшибки);
	СформироватьСписокЗадач(ФормируемыеЗадачи, ЗадачаОшибкаПриАнализе, ТочкаМаршрутаБизнесПроцесса);
КонецПроцедуры

Процедура НачатьПроверкуПередСозданиемЗадач(ТочкаМаршрутаБизнесПроцесса, ФормируемыеЗадачи, СтандартнаяОбработка)
    
    СтандартнаяОбработка = Ложь;
	
	//здесь надо переиспользовать задачу
	ЗадачаПоПерезапуску = БизнесПроцессСервер.НайтиЗадачуПерезапуска(ЭтотОбъект.Ссылка);
	СформироватьСписокЗадач(ФормируемыеЗадачи, ЗадачаПоПерезапуску, ТочкаМаршрутаБизнесПроцесса);
    
КонецПроцедуры

////////////////////////
// Анализ
////////////////////////

Процедура ВыполнитьАнализОбработка(ТочкаМаршрутаБизнесПроцесса)
	ТекстОшибки = Неопределено;
	Попытка 
		ВыполнитьАнализ();
	Исключение
		
		НетПроблем = Неопределено;
		ИмяСтраницыСправки = "НеизвестнаяОшибка";
		Сообщение = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		Отладка.Ошибка(Сообщение);
		СоздатьЗадачуОтветственномуЗаВыполнение(Сообщение);
		
	КонецПопытки;

КонецПроцедуры

Процедура ВыполнитьАнализ()
	НетПроблем = Истина;

	Профиль = Новый ИнтернетПочтовыйПрофиль;
	
	Профиль = ИнтернетПочта.СформироватьИнтернетПрофиль(
		ИнтернетПочта.ПолучитьСистемнуюУчетнуюЗапись()	
	);
	
	Почта = Новый ИнтернетПочта;
	Попытка
		Почта.Подключиться(Профиль);
		ЗаголовкиОтчетов = Новый Массив;
		ВсеЗаголовки = Почта.ПолучитьЗаголовки();
	Исключение
		НетПроблем = Неопределено;
		ИмяСтраницыСправки = "ВнешнийКонтрольЦККНеизвестнаяОшибка";
		Сообщение = НСтр("ru = 'Ошибка при получении отчетов: %1'");
		Сообщение = СтрЗаменить(Сообщение, "%1", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		Возврат;
	КонецПопытки;
	Для Каждого Заголовок Из ВсеЗаголовки Цикл
		Если Заголовок.Тема = Оповещение.ПрефиксЗаголовкаОтчетаЦКК() + ИмяЦентраКонтроля Тогда
			ЗаголовкиОтчетов.Добавить(Заголовок);	
		КонецЕсли;
	КонецЦикла;
	
	Если ЗаголовкиОтчетов.Количество() = 0 Тогда
		ТекстПредупреждения = НСтр("ru = 'Восстановить работоспособность ЦКК'");
		НетПроблем = Ложь;
	Иначе
		Почта.Выбрать(Истина, ЗаголовкиОтчетов);	
	КонецЕсли;
	
	Почта.Отключиться();	
	
КонецПроцедуры

#КонецЕсли













